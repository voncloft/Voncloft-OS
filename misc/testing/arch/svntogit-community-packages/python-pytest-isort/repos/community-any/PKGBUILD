# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-isort
pkgver=1.3.0
pkgrel=1
pkgdesc='pytest plugin to perform isort checks (import ordering)'
arch=('any')
license=('BSD')
url='https://github.com/moccu/pytest-isort'
depends=('python-pytest-cache' 'python-isort')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/moccu/pytest-isort/archive/$pkgver.tar.gz")
sha512sums=('b0d1ca6553178161490aed3b11be83909f9d223421e73a1c99dbcc0c4310734ce1850b88fed312cb7f364e170aad098014c5563f19e9c1ac65af8697fff74b30')

build() {
  cd pytest-isort-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-isort-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages:$PYTHONPATH" py.test
}

package() {
  cd pytest-isort-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}

# vim:set ts=2 sw=2 et:
