# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=jalv
pkgver=1.6.6
pkgrel=1
pkgdesc="A simple but fully featured LV2 host for Jack"
arch=('x86_64')
url="https://drobilla.net/software/jalv"
license=('custom:ISC')
groups=('pro-audio')
depends=('glibc')
makedepends=('gtk3' 'gtkmm' 'jack' 'lilv' 'lv2' 'qt5-tools' 'serd' 'sord'
'sratom' 'suil' 'waf')
optdepends=('atk: for jalv.gtkmm and jalv.gtk3'
            'cairo: for jalv.gtkmm and jalv.gtk3'
            'fontconfig: for jalv.gtkmm and jalv.gtk'
            'gcc-libs: for jalv.qt5 and jalv.gtkmm'
            'glib2: for jalv.gtkmm and jalv.gtk3'
            'qt5-base: for jalv.qt5'
            'gtk2: for jalv.gtkmm and jalv.gtk'
            'gtk3: for jalv.gtk3'
            'gtkmm: for jalv.gtkmm'
            'harfbuzz: for jalv.gtkmm and jalv.gtk3'
            'gdk-pixbuf2: for jalv.gtkmm, jalv.gtk and jalv.gtk3'
            'libsigc++: for jalv.gtkmm'
            'libfreetype.so: for jalv.gtkmm and jalv.gtk'
            'pango: for jalv.gtkmm, jalv.gtk and jalv.gtk3'
            'zlib: for jalv.gkt3')
provides=('lv2-host')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2"{,.asc})
sha512sums=('8bb77848a890326c8b39d4d6aea65a86c3cf44d7cb3474f20b0279d5f3a1935b955beb2905d315b304926c856029d5180d1e72f89b736e60def9eeab6e2b0f7c'
            'SKIP')
b2sums=('92e93a48cb0a244cdd3fa9264fe6faaaf9580fe035bb4af1358ecdfc6558766e75ff905592dc6ad43202b3b052331346c8b5e864bf88d2efdcfc6edb427fc715'
        'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

prepare() {
  cd "${pkgname}-${pkgver}"
  # copying custom waf script to separate directory, removing outdated waflib
  # and fixing wscript to use the custom tools location instead.
  mkdir -pv tools
  mv -v waflib/extras/autowaf.py tools/
  rm -rfv waflib
  sed -e 's/waflib.extras/tools/g' \
      -e "s/load('autowaf'/load('autowaf', tooldir='tools'/g" \
      -i wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  waf configure --prefix=/usr
  waf
}

package() {
  depends+=('libjack.so' 'liblilv-0.so' 'libserd-0.so' 'libsord-0.so'
  'libsratom-0.so' 'libsuil-0.so')
  cd "${pkgname}-${pkgver}"
  waf install --destdir="${pkgdir}"
  # license
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {AUTHORS,NEWS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
