# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: SÃ©bastien LEBEAU <sebcbi1 at gmail dot com>
# Contributor: Patrick Griffis <tingpign@fedoraproject.org>

pkgname=transmission-remote-gtk
pkgver=1.4.1
pkgrel=2
pkgdesc="GTK remote control for the Transmission BitTorrent client"
arch=('x86_64')
url="https://github.com/transmission-remote-gtk/transmission-remote-gtk"
license=('GPL2')
depends=('curl' 'gtk3' 'libnotify' 'libproxy' 'geoip')
makedepends=('intltool')
source=("${url}/releases/download/${pkgver}/transmission-remote-gtk-${pkgver}.tar.xz"{,.asc})
sha256sums=('59978381a558fc6d1a11091c702a88ac0be8fc7fa135ca367eec55f572a112ab' 'SKIP')
validpgpkeys=('108BF2212A051F4A72B18448B3C7CE210DE76DFC') # Patrick Griffis <tingping@tingping.se>

build() {
	cd ${pkgname}-${pkgver}
	CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
	./configure --prefix=/usr --disable-desktop-database-update
	make
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install
}
