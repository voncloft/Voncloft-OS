# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-shortcut-overlay
pkgver=1.1.2
pkgrel=1
pkgdesc='A native, OS-wide shortcut overlay'
arch=(x86_64)
url=https://github.com/elementary/shortcut-overlay
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgee
  libgranite.so
)
makedepends=(
  git
  meson
  vala
)
source=(pantheon-shortcut-overlay::git+https://github.com/elementary/shortcut-overlay.git#tag=42bd9a1e67780de3bcd861e7a4a523736018fd40)
sha256sums=(SKIP)

pkgver() {
  cd pantheon-shortcut-overlay

  git describe --tags
}

build() {
  arch-meson pantheon-shortcut-overlay build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
