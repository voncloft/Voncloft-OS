# Maintainer:
# Contributor:  Chris Severance aur.severach aATt spamgourmet dott com
# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Francois Charette <firmicus@gmx.net>

_perlmod='Font-TTF'
_modnamespace='Font'
pkgname="perl-${_perlmod,,}"
pkgver=1.06
pkgrel=4
pkgdesc="${_perlmod//-/::} - Perl module for TrueType font hacking"
arch=(any)
url="https://search.cpan.org/dist/${_perlmod}"
license=('Artistic2.0')
depends=(perl-io-string)
options=(!emptydirs)
_verwatch=("https://www.cpan.org/modules/by-module/${_modnamespace}/" "${_perlmod}-\([0-9\.]*\)\.tar\.gz" 'l')
source=("${_verwatch[0]}${_perlmod}-${pkgver}.tar.gz")
sha256sums=('4b697d444259759ea02d2c442c9bffe5ffe14c9214084a01f743693a944cc293')

build() {
  cd "${_perlmod}-${pkgver}"

  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl 'Makefile.PL' INSTALLDIRS='vendor'
  make
}

check() {
  cd "${_perlmod}-${pkgver}"
  make test
}

package() {
  cd "${_perlmod}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  rm -r "$pkgdir"/usr/lib
}

