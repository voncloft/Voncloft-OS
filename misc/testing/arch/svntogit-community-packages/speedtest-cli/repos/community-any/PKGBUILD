# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: William Grieshaber <me@zee.li>
# Contributor: Victor Aur√©lio <victoraur dot santos at gmail dot com>

pkgname=speedtest-cli
pkgver=2.1.2
pkgrel=3
pkgdesc='Command line interface for testing internet bandwidth using speedtest.net'
url='https://github.com/sivel/speedtest-cli'
arch=('any')
license=('Apache')
depends=('python-setuptools')
source=(https://github.com/sivel/speedtest-cli/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('f2eb125116f42075d3248e1c0590cce4f822f383ff7c54c158504b16de72bf35b35d55a78014413a95a5f5bafa98c71c3ea011f20f480f1db272d6e8800c40bd')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package(){
  cd ${pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 ${pkgname}.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
