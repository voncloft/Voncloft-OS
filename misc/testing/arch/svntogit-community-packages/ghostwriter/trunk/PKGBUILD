# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>
# Contributor: Bastien "neitsab" Traverse <firstname [at] lastname [dot] email>
# Contributor: Icaro Perseo <icaroperseo[at]protonmail[dot]com>
# Contributor: Philipp 'TamCore' B. <philipp [at] tamcore [dot] eu>

pkgname=ghostwriter
pkgver=1.8.1
pkgrel=4
pkgdesc="Aesthetic, distraction-free Markdown editor"
arch=(x86_64)
url="https://wereturtle.github.io/ghostwriter/"
license=(GPL3)
depends=(hunspell qt5-svg qt5-webengine)
makedepends=(qt5-tools)
optdepends=('cmark: processing and rendering CommonMark'
            'cmark-gfm: processing and rendering GitHub Flavored Markdown'
            'discount: processing and rendering Markdown'
            'mathjax: formula rendering in live preview'
            'pandoc: Pandoc supported Markdown flavors and export to various formats')
source=("https://github.com/wereturtle/ghostwriter/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "qt-5.15.patch")
sha256sums=('2b73fed6a95ebcd45333fe4b12acb491eb4171fca8a11eea1af9bbdc73ed4b69'
            'f97077cb0137f034c26f30996c6bc6b6835eeeaa55f96b57155434562cb1830c')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../qt-5.15.patch
  sed -i 's/typeof window.MathJax.typeset !==/typeof window.MathJax !==/' resources/gw.js
  sed -i 's|https://cdn.jsdelivr.net/npm/mathjax@3/es5/tex-mml-chtml.js|file:///usr/share/mathjax/tex-mml-chtml.js|' src/HtmlPreview.cpp
  sed -i 's|DATADIR/appdata|DATADIR/metainfo|
          /INSTALLS +=/ s/ pixmap//' ghostwriter.pro
}

build() {
  cd $pkgname-$pkgver
  qmake PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
