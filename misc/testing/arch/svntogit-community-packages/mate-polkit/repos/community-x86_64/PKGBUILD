# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-polkit
pkgver=1.24.0
pkgrel=1
pkgdesc="PolicyKit integration for the MATE desktop"
url="https://mate-desktop.org"
arch=('x86_64')
license=('LGPL')
groups=('mate')
depends=('gtk3' 'polkit' 'gettext')
makedepends=('accountsservice' 'gobject-introspection')
conflicts=('mate-polkit-gtk3')
replaces=('mate-polkit-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('76bf1133a22d0772bff02c34d6178ef73826a400415294541f9e6f4b3f5ea090')

build() {
    	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--libexecdir=/usr/lib/${pkgname} \
        	--sysconfdir=/etc \
        	--localstatedir=/var \
        	--enable-introspection

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
