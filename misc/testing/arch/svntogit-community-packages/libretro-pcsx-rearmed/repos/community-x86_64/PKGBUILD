# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Johnathan Jenkins <twodopeshaggy@gmail.com>
# Contributor: prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive <almostalive2003@gmail.com>

pkgname=libretro-pcsx-rearmed
pkgver=1179
pkgrel=1
pkgdesc='Sony PlayStation core'
arch=('i686' 'x86_64')
url='https://github.com/libretro/pcsx_rearmed'
license=('GPL2')
groups=('libretro')
depends=('glibc' 'zlib')
makedepends=('git')
source=('libretro-pcsx-rearmed::git+https://github.com/libretro/pcsx_rearmed.git'
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/pcsx_rearmed_libretro.info')
sha256sums=('SKIP'
            'e93b0475d7c6f422ad52af696205409423ddfa449109e76f36a95f17943bfa92')

pkgver() {
  cd libretro-pcsx-rearmed

  git rev-list --count HEAD
}

build() {
  cd libretro-pcsx-rearmed

  make -f Makefile.libretro
}

package() {
  cd libretro-pcsx-rearmed

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 pcsx_rearmed_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../pcsx_rearmed_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
