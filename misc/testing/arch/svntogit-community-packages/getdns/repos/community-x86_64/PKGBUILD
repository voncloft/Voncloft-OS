# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Remi Gacogne <rgacogne@archlinux.org>

pkgname=getdns
pkgver=1.5.2
pkgrel=3
pkgdesc="A modern asynchronous DNS API"
arch=('x86_64')
url="https://getdnsapi.net"
license=('BSD')
depends=('libbsd' 'libev' 'libevent' 'libidn2' 'libuv' 'libyaml' 'unbound')
source=("${url}/dist/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('1826a6a221ea9e9301f2c1f5d25f6f5588e841f08b967645bf50c53b970694c0' 'SKIP')
validpgpkeys=('DC34EE5DB2417BCC151E5100E5F8F8212F77A498') # Willem Toorop <willem@nlnetlabs.nl>

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --disable-rpath \
        --enable-static=no \
        --with-libev \
        --with-libevent \
        --with-libuv \
        --with-trust-anchor=/etc/trusted-key.key \
        --with-current-date=$(date -u --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" "+%Y-%m-%dT%H:%M:%S")
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install

    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
