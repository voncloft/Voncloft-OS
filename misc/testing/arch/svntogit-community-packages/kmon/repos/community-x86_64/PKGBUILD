# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=kmon
pkgver=1.5.3
pkgrel=1
pkgdesc="Linux kernel manager and activity monitor"
arch=('x86_64')
url="https://github.com/orhun/kmon"
license=('GPL3')
depends=('kmod' 'libx11')
makedepends=('cargo' 'python' 'git')
source=("git+${url}.git#tag=v${pkgver}?signed")
sha512sums=('SKIP')
validpgpkeys=('39E678DF63BE20EA5A175156B928720AEC532117') # orhun <orhunparmaksiz@gmail.com>

build() {
    cd "${srcdir}"/${pkgname}

    cargo build --release --locked --all-features
}

check() {
    cd "${srcdir}"/${pkgname}

    cargo test --release --locked
}

package() {
    cd "${srcdir}"/${pkgname}

    install -Dm755 target/release/${pkgname} -t "${pkgdir}"/usr/bin
    install -Dm644 README.md -t "${pkgdir}"/usr/share/doc/${pkgname}
    install -Dm644 man/${pkgname}.8 -t "${pkgdir}"/usr/share/man/man8
}
