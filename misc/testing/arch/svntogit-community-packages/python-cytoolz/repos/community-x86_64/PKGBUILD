# Maintainer: David Runge <dvzrv@archlinux.org>

_name=cytoolz
pkgname=python-cytoolz
pkgver=0.11.0
pkgrel=3
pkgdesc="Cython implementation of Toolz: High performance functional utilities"
arch=('x86_64')
url="https://github.com/pytoolz/cytoolz/"
license=('BSD')
depends=('cython' 'python-toolz')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('d7ab7317a8c9b26136dfa1ef0bbe719a09d5ac290d73ad16596c8624976ad4eb50d826fb02885cd1979279057698ceab9276f14687791b6c4adc33fa4650dd6d')
b2sums=('1df4d2342393ab9e58fcdeae7c48cff31a8c4ff02b928f4a1a73e089408349ae121ce9e7ee2cefe2ec92d183d00959cb913273eee78360da75faa813308a6cc6')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  # build C extension
  python setup.py build_ext --inplace --with-cython
  # build python extension
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  local _pyver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  export PYTHONPATH="build/lib.linux-${CARCH}-${_pyver}/:${PYTHONPATH}"
  echo "$PYTHONPATH"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
