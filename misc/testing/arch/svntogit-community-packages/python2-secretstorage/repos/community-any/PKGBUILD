# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>

pkgname=python2-secretstorage
_pkgname=${pkgname#python2-}
pkgver=3.1.1
pkgrel=1
epoch=1
pkgdesc="Securely store passwords and other private data using the SecretService DBus API"
arch=('any')
url="https://github.com/mitya57/secretstorage"
license=('BSD')
depends=('python2-dbus' 'python2-cryptography')
makedepends=('python2-setuptools')
checkdepends=('gnome-keyring' 'xorg-server-xvfb')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('6ebc8e57ea926be18aa4dca9859e1cc93ac9b285a766fc9fa154bdf45c848d7584fe5ea2c2084297e8feabe038b640370916cad4903bee25b285c55f3c47db2a')

build() {
  cd $_pkgname-$pkgver

  python2 setup.py build
}

check() {
  cd $_pkgname-$pkgver

  dbus-launch xvfb-run -a python2 -m unittest discover -s tests || warning "Tests failed"
}

package() {
  cd $_pkgname-$pkgver

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  # Install the license
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
