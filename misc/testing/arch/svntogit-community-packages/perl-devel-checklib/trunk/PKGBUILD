# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: pzl <alsoelp _at_ gmail>

pkgname=perl-devel-checklib
pkgver=1.14
pkgrel=2
pkgdesc="Module to check if other perl modules are installed"
arch=('any')
url='https://search.cpan.org/dist/Devel-CheckLib/'
license=('PerlArtistic' 'GPL')
depends=('perl')
makedepends=('perl-capture-tiny')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/M/MA/MATTN/Devel-CheckLib-${pkgver}.tar.gz")
sha512sums=('a5a310cd20e7bfa3c28718b0cba997e029ae0a422ed355d70774691521a3f5c2942eb39269dec1b09e706c1d124559313ab79e787430e0bb3ad43d8aa937539c')

build() {
  cd Devel-CheckLib-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Devel-CheckLib-$pkgver
  make test
}

package() {
  cd Devel-CheckLib-$pkgver
  make install DESTDIR="$pkgdir"
}
