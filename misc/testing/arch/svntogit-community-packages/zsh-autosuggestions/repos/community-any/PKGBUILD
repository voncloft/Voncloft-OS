# Maintainer: David Runge <dvzrv@archlinux.org>
pkgname=zsh-autosuggestions
pkgver=0.6.4
pkgrel=1
pkgdesc="Fish-like autosuggestions for zsh "
arch=('any')
url="https://github.com/zsh-users/zsh-autosuggestions"
license=('MIT')
depends=('zsh')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zsh-users/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('226b99048577ecdbe198501e6e4b3156c3d849994f0a2f7e15687cd244d4cac472cbcf746aaa32b2886652f6962852a475777b5f2bfcb3a6d0279be98a6020bb')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644 ${pkgname}{,.plugin}.zsh \
    -t "${pkgdir}/usr/share/zsh/plugins/${pkgname}/"
  # docs
  install -vDm 644 {CHANGELOG,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
