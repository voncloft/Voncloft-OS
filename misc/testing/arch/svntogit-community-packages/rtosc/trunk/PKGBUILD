# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rtosc
pkgver=0.2.0
pkgrel=3
pkgdesc="Realtime safe OSC Messaging"
arch=('x86_64')
url="https://github.com/fundamental/rtosc"
license=('MIT')
depends=('gcc-libs' 'glibc')
makedepends=('cmake' 'doxygen')
provides=('librtosc.so' 'librtosc-cpp.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/fundamental/${pkgname}/archive/v${pkgver}.tar.gz"
        "${pkgname}-fix-pkg-config-file.patch"
        "${pkgname}-include-missing-headers.patch")
sha512sums=('f19aed587c752f33a063849fce2b4f531b734d8ae68d9f3b623da2872260b258b92c24b6160734a01f091ba821a4bd72cae98a138570ef6c741b03e1353d835a'
            '65f6ada68c74b4c7f5e1bf3939dce3f04fbc704a8fd5386e003951201c927dc6776327c520de0b6cca2c9b09445072f931fd9cc08ed4a50c94a500f06cce4244'
            '25db71244ce931919c6c95ca3b90fe3d4f21537a913b1c1056559b28fe59abbe6643113d46d5fda02ff3c11de4c4e51ac0f03b1a9b550176f3d547f698dccfe4')
b2sums=('723f4876c21b1e5ca1196bf726ad21d68083b11d56d19ca32c4b68325d59026055b1cb5a2eb3f4eeffab99f74230947e560ac2c12a4c2123f84629ae9b68a6d1'
        '48013cf03a906a5311b816185da1195690e772c867d4bdaf962ff4d8c0a81de9af421bd0ed73e89fe3ab38de42987677777e643c345771d1d8fb638068655652'
        '615ec60d8bc6abb3af4df32b02be57c7948308f269dbb78aac6af9019f7b12763e6fae186c13b0aff563ac2a53612630c9bf84ab257391c463300053346ec812')

prepare() {
  cd "$pkgname-$pkgver"
  # add missing headers: https://github.com/fundamental/rtosc/issues/41
  patch -Np1 -i ../"${pkgname}-include-missing-headers.patch"
  # fix libdir in pkg-config file:
  # https://github.com/fundamental/rtosc/issues/42
  patch -Np1 -i ../"${pkgname}-fix-pkg-config-file.patch"
  sed -e 's/STATIC/SHARED/g' -i CMakeLists.txt
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DPERF_TEST=ON \
        -W no-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
  doxygen build/Doxyfile
}

check() {
  cd "$pkgname-$pkgver"
  make -k test -C build
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install -C build
  install -vDm 644 html/search/*.{js,png,html} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/html/search"
  install -vDm 644 html/*.{css,js,png,html} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/html"
  install -vDm 644 README.adoc \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
