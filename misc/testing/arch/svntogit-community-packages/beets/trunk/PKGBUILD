# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgname=beets
pkgver=1.4.9.r1031.cbc045f1c
pkgrel=1
pkgdesc='Flexible music library manager and tagger'
arch=(any)
url=https://beets.io/
license=(MIT)
depends=(
  python-confuse
  python-jellyfish
  python-mediafile
  python-munkres
  python-musicbrainzngs
  python-setuptools
  python-six
  python-unidecode
  python-yaml
)
makedepends=(
  git
  python-setuptools
  python-sphinx
)
checkdepends=(
  bash-completion
  imagemagick
  python-discogs-client
  python-flask
  python-mock
  python-mpd2
  python-nose
  python-nose-exclude
  python-pylast
  python-reflink
  python-requests-oauthlib
  python-responses
  python-xdg
)
optdepends=(
  'bash-completion: Bash completion'
  'bs1770gain: ReplayGain plugin'
  'chromaprint: Chromaprint/Acoustid plugin'
  'ffmpeg: Convert, ReplayGain plugins'
  'gst-plugins-bad: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-plugins-good: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-plugins-ugly: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-libav: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'gst-python: Chromaprint/Acoustid, BPD, ReplayGain plugins'
  'imagemagick: Thumbnails plugin'
  'python-beautifulsoup4: Lyrics plugin'
  'python-flask: Web plugin'
  'python-gobject: ReplayGain plugin'
  'python-mpd2: MPDStats plugin'
  'python-pyacoustid: Chromaprint/Acoustid plugin'
  'python-pylast: LastGenre plugin'
  'python-requests: Chromaprint/Acoustid, BPD, FetchArt plugins'
  'python-xdg: Thumbnails plugin'
)
_commit=cbc045f1c8f898acd8798ff134a25557f1769525
source=(
  git+https://github.com/beetbox/beets.git#commit=${_commit}
  beet
)
b2sums=('SKIP'
        '1b36ba07d85f0fbcae0a3d8253ce2e5d7ecf8a17c6f0bb946579663de546d53cd4fe5da84ee8d880d7f95f48e66ef6010232a995d86d0c52b52218cd991a7782')

pkgver() {
  cd beets
  git describe --tags | sed 's/^v//; s/-/.r/; s/-g/./'
}

build() {
  cd beets
  python setup.py build sdist
}

check() {
  cd beets
  python -m nose \
    --exclude-test=test.test_ui.CompletionTest \
    --exclude-test=test.test_zero.ZeroPluginTest
}

package() {
  cd beets
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm 644 ../beet -t "${pkgdir}"/usr/share/bash-completion/completions/
  install -Dm 644 extra/_beet -t "${pkgdir}"/usr/share/zsh/site-functions/
  install -Dm 644 man/beet.1 -t "${pkgdir}"/usr/share/man/man1/
  install -Dm 644 man/beetsconfig.5 -t "${pkgdir}"/usr/share/man/man5/
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/beets/
}

# vim: ts=2 sw=2 et:
