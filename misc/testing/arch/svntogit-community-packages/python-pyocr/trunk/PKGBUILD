# Maintainer: Balló György <ballogyor+arch at gmail dot com>

_pkgname=pyocr
pkgname=python-pyocr
pkgver=0.8
pkgrel=1
pkgdesc="Python wrapper for Tesseract and Cuneiform"
arch=(any)
url="https://gitlab.gnome.org/World/OpenPaperwork/pyocr"
license=(GPL3)
depends=(python-pillow python-six)
makedepends=(git python-setuptools-scm)
optdepends=('cuneiform: OCR backend'
            'tesseract: OCR backend')
_commit=f9f93bbd83ab5814b1c56c3ffe41addc9ec622dc
source=("git+https://gitlab.gnome.org/World/OpenPaperwork/pyocr.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  sed -e '/setuptools_scm_git_archive/d' -i $_pkgname/setup.py # Remove unused makedepends
}

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  python3 setup.py install --root="$pkgdir" --optimize=1
}
