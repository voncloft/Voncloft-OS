# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Alexander Fehr <pizzapunk gmail com>

pkgname=parcellite
pkgver=1.2.1
pkgrel=4
pkgdesc="Lightweight GTK+ clipboard manager"
arch=('x86_64')
url="http://parcellite.sourceforge.net/"
license=('GPL3')
depends=('gtk2' 'xdotool')
makedepends=('intltool')
optdepends=('xdotool')
source=(https://downloads.sourceforge.net/parcellite/parcellite-$pkgver.tar.gz
	fix-crash.patch)
sha256sums=('aba1c7c56c7607e219ce9855723eefa552c5376982891aefbfb5a2d3297ef046'
            '8fe1e86958d371c9fa9474fe64b203ed483f636f3f98152237402ea84b2d2cbd')

prepare() {
  cd "$srcdir/parcellite-$pkgver"
  patch -Np1 <"$srcdir"/fix-crash.patch
}

build() {
  cd "$srcdir/parcellite-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/parcellite-$pkgver"
  make DESTDIR="$pkgdir" install
}
