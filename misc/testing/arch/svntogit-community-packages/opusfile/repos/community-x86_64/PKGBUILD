# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: DrZaius <lou@fakeoutdoorsman.com>

pkgname=opusfile
pkgver=0.12
pkgrel=1
pkgdesc='Library for opening, seeking, and decoding .opus files'
arch=(x86_64)
url='https://opus-codec.org/'
license=(BSD)
depends=(libogg openssl opus)
source=("https://downloads.xiph.org/releases/opus/$pkgname-$pkgver.tar.gz")
sha256sums=('118d8601c12dd6a44f52423e68ca9083cc9f2bfe72da7a8c1acb22a80ae3550b')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/opusfile/LICENSE"
}

# getver: github.com/xiph/opusfile/tags
# vim: ts=2 sw=2 et:
