# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>

pkgname=kcov
pkgver=38
pkgrel=1
pkgdesc="Code coverage tester for compiled programs, Python scripts and shell scripts"
arch=('x86_64')
url="http://simonkagstrom.github.com/kcov/index.html"
license=('GPL2')
depends=('elfutils' 'curl')
makedepends=('cmake' 'python')
source=("https://github.com/SimonKagstrom/kcov/archive/v${pkgver}.tar.gz")
sha512sums=('ceb62379b56470838eb7ae4594433b1f8b64bef0c08fe92372d4616213d029c1563c1b37c4dde54636dd875609b942a4fd428b35109c07609f8257018297198e')

prepare() {
  cd $pkgname-$pkgver

  rm -rf build
  mkdir -p build
}

build() {
  cd $pkgname-$pkgver/build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir/" install
  make clean
}

# vim:set ts=2 sw=2 et:
