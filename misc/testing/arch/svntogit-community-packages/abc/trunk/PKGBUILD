# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=abc
pkgver=0.0.0.20200911
_commit=a2c3c21031d0e83f15c8182924a908f54bbb5ab7
pkgrel=1
pkgdesc="System for Sequential Logic Synthesis and Formal Verification"
arch=("x86_64")
license=("MIT")
url="https://github.com/berkeley-abc/abc"
depends=('readline')
source=(https://github.com/berkeley-abc/abc/archive/$_commit/abc-$_commit.tar.gz)
sha512sums=('6c3b066bd325402c3623b0b8392114da282471b22f769b257566f095455631095034c6fb8d7a530954e5d0ca682b896666b0e6a7979cb8cd487019ac35086d6e')

prepare() {
  cd $pkgname-$_commit
  # - Make abc link to libabc, so the result is 50% smaller
  # - Fix LDFLAGS for shared library
  sed -i -e 's|-o \$@ \$\^ \$(LDFLAGS)|-o $@ src/base/main/main.o $(LDFLAGS) -L. -labc|;s|\$(PROG):|$(PROG): lib$(PROG).so|' \
         -e 's|-o \$@ \$\^ $(LIBS)|-o $@ $^ $(LIBS) $(LDFLAGS)|' \
            Makefile
}

build() {
  cd $pkgname-$_commit
  make ABC_USE_PIC=1 LDFLAGS="$LDFLAGS" libabc.so abc
}

package() {
  cd $pkgname-$_commit
  install -Dm755 libabc.so -t "$pkgdir"/usr/lib/
  install -Dm755 abc -t "$pkgdir"/usr/bin/
  install -Dm644 copyright.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
