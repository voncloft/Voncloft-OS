# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Streetwalrus <streetwalrus@codewalr.us>

pkgname=shotgun
pkgver=2.2.0
pkgrel=2
pkgdesc='Minimal X screenshot utility'
arch=('x86_64')
url=https://github.com/neXromancers/shotgun
license=('MPL2')
depends=('gcc-libs' 'libxrandr')
makedepends=('git' 'rust')
optdepends=('hacksaw: for area/window selection'
            'slop: for area/window selection')
source=("shotgun-$pkgver.tar.gz::https://static.crates.io/crates/shotgun/shotgun-$pkgver.crate")
sha512sums=('671c20268573a52f655d3db484e9387f019487332415359029e6b5de7bd683954c131007bbfa6d2efa035508db09bcf6b5c04c1c9dcff4e29de47618d4f04a1c')

build() {
  cd shotgun-$pkgver
  cargo build --release --locked
}

package() {
  cd shotgun-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/shotgun
}

# vim:set ts=2 sw=2 et:
