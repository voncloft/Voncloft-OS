# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Daniel Isenmann <daniel.isenmann [at] gmx [dot] de>

pkgname=metapixel
pkgver=1.0.2
pkgrel=10
pkgdesc='A program for generating photomosaics.'
arch=('x86_64')
url='https://www.complang.tuwien.ac.at/schani/metapixel/'
license=('GPL')
depends=('giflib' 'libjpeg' 'libpng' 'perl' 'glib2')
makedepends=('git' 'docbook-xsl')
source=("git://github.com/schani/metapixel.git#commit=4ad7be9e82"
        "fix-manpage-path.patch"
        "metapixel-giflib-5.1.0-compat.patch")
md5sums=('SKIP'
         '166d31ddce400bcc4f7d16a8fc399c0a'
         'b959c6d1b14064aee3a73ae568a82555')

prepare() {
  cd "${srcdir}/${pkgname}"

  git submodule init
  git submodule update

  # Fix man page installation path (FS#44543)
  patch -p1 -i ../fix-manpage-path.patch

  cd rwimg
  patch -p1 -i ../../metapixel-giflib-5.1.0-compat.patch
}

build() {
  cd "${srcdir}/${pkgname}"

  # Build librwimg first, make dependencies seem to be bugged.
  make librwimg
  make MANPAGE_XSL="/usr/share/xml/docbook/xsl-stylesheets-*/manpages/docbook.xsl"
}

package() {
  cd "${srcdir}/${pkgname}"
  
  install -dm0755 "${pkgdir}/usr/share/man/man1"

  make PREFIX="${pkgdir}/usr" install
}
