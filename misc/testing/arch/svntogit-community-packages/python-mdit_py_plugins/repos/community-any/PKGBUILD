# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=mdit-py-plugins
pkgname=python-mdit_py_plugins
pkgver=0.2.4
pkgrel=1
pkgdesc='Collection of core plugins for markdown-it-py'
arch=('any')
url='https://github.com/executablebooks/mdit-py-plugins'
license=('MIT')
depends=('python-markdown-it-py')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-regressions')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('e496557804f926d38bd11a04cdb04b7d31be0a3546da7e4e7f52c0c00f94d178231f95b4b2e55e64309590ae7b99bbdbc71f73a3e7eb14976f1b4ea91db2f038')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python -m pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
