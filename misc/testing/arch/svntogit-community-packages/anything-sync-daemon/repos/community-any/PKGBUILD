# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>

pkgname=anything-sync-daemon
pkgver=5.85
pkgrel=3
pkgdesc="Symlinks and syncs user specified dirs to RAM"
arch=('any')
url="https://github.com/graysky2/anything-sync-daemon"
license=('MIT')
depends=('bash' 'findutils' 'procps-ng' 'rsync' 'systemd')
optdepends=('bash-completion: for completion when using bash'
            'zsh-completions: for completion when using zhs')
install="${pkgname}.install"
backup=('etc/asd.conf')
source=("$pkgname-$pkgver.tar.gz::https://github.com/graysky2/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('85b2d7458efe4686bf3564ad9242bac7b17080f4504ca89ea32102d34a730424ea209ec88bdbfca4c16964fe4840c2e33c8600edda46dc9f164c5be022eb9445')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install-systemd-all
  install -vDm 644 MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
