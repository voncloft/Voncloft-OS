# Maintainer: Ivy Foster <iff@archlinux.org>
# Contributor: Alad Wenter <alad@mailbox.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daniel Wallace < danielwallace at gtmanfred dot com>
# Contributor: Trizen <echo dHJpemVueEBnbWFpbC5jb20K | base64 -d>

pkgname=youtube-viewer
pkgdesc='Command line utility for viewing YouTube videos'
url='https://github.com/trizen/youtube-viewer'
license=(GPL3)
epoch=1
pkgver=3.8.1
pkgrel=1
depends=(perl perl-data-dump perl-file-sharedir perl-json perl-lwp-protocol-https perl-libwww)
makedepends=(perl-module-build)
optdepends=(
	'gtk2-perl: GTK2 GUI'
	'mpv: play videos with MPV'
	'perl-file-sharedir: GTK GUI'
	'perl-gtk3: GTK3 GUI'
	'perl-json-xs: faster JSON to HASH conversion'
	'perl-term-readline-gnu: better STDIN support'
	'perl-text-charwidth: print results in a fixed-width format'
	'vlc: play videos with VLC'
	'wget: download videos with wget'
	'youtube-dl: play videos with encrypted signatures'
)
arch=(any)
source=(
	"$pkgname-$pkgver.tar.gz::https://github.com/trizen/youtube-viewer/archive/$pkgver.tar.gz"
	"https://github.com/trizen/youtube-viewer/releases/download/$pkgver/youtube-viewer-$pkgver.tar.gz.asc"
)
install=youtube-viewer.install
validpgpkeys=(ABB8BBAA2E279767774149B7D0A443C703A3A056)
# sums provided by packager makepkg -g
sha512sums=('a5446359c18632a7f0c8717d026e1ad3bf0cf44ebe4db012904b8ad6834fb9dba8f25b4709fff436b47c74bd34bdfaffef1c222f3504c53f9614e4f78b7b3a2a'
            'SKIP')

build() {
	cd "youtube-viewer-$pkgver"
	perl Build.PL --installdirs vendor --gtk
}

check() {
	cd "youtube-viewer-$pkgver"
	./Build test
}

package() {
	cd "youtube-viewer-$pkgver"

	./Build install --destdir "$pkgdir"
	rm -r "$pkgdir/usr/lib"

	install -D -m 644 README.md "$pkgdir/usr/share/doc/youtube-viewer/README.md"

	mkdir -p "$pkgdir/usr/share/applications" "$pkgdir/usr/share/pixmaps"
	mv "$pkgdir/usr/share/perl5/vendor_perl/auto/share/dist/WWW-YoutubeViewer/gtk-youtube-viewer.desktop" \
		"$pkgdir/usr/share/applications/gtk-youtube-viewer.desktop"
	cp "$pkgdir/usr/share/perl5/vendor_perl/auto/share/dist/WWW-YoutubeViewer/icons/gtk-youtube-viewer.png" \
		"$pkgdir/usr/share/pixmaps/gtk-youtube-viewer.png"
}
