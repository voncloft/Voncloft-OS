# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: sputnick <gilles DOT quenot AT gmail DOT com>

pkgname=xwax
pkgver=1.7
pkgrel=2
pkgdesc='Open-source vinyl emulation software for Linux.'
arch=('x86_64')
url='https://www.xwax.co.uk/'
license=('GPL2')
depends=('alsa-lib' 'glibc' 'libjack.so' 'sdl' 'sdl_ttf' 'ttf-dejavu')
optdepends=('cdparanoia: for direct CD import'
            'faad: for AAC import'
            'ffmpeg: for various audio and video import'
            'flac: for FLAC import'
            'mpg123: for MP3 import'
            'sox: for live audio import'
            'vorbis-tools: for OGG import')
source=("https://www.xwax.co.uk/releases/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ab0fcaf32f1fd1d6b38323095524f1932a067a3d04d40e4b043993e360411fed0c4b78bb71662581b438cb1e3005bc21659db4521a6ec704994b2e8e1b7e62d4')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix /usr \
              --enable-alsa \
              --enable-jack
  make EXECDIR="/usr/lib/${pkgname}"
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" EXECDIR="/usr/lib/${pkgname}" install
  install -vDm 644 {CHANGES,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
