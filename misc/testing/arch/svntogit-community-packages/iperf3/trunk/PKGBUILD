# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Jim Ferguson <ferguson@ncsa.uiuc.edu>
# Contributor: Xi0N <jonandermonleon@gmail.com>
# Contributor: goll <adrian.goll+aur[at]gmail>

pkgname=iperf3
pkgver=3.9
pkgrel=1
pkgdesc='TCP, UDP, and SCTP network bandwidth measurement tool'
url='https://github.com/esnet/iperf'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'openssl')
provides=('libiperf.so')
source=(http://downloads.es.net/pub/iperf/iperf-${pkgver}.tar.gz)
sha512sums=('4a2af8a6028b0f6bded9b3ac51c5463bc7595c9a2d2ac13f28b0e2e8dee0ac0a299e7817125e23e409f12e8e80862ed28d5541eef3cda7cd9f7104bb720165ce')
b2sums=('b9b07590e0220b965f25ede95e71b2b8ae1ccea7a75dabc917356eb70405feab858c1e034af72cd87013f89eb098599938e7ee79e6dc58d356bcc68e03551be0')

build() {
  cd iperf-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd iperf-${pkgver}
  make check
}

package() {
  cd iperf-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 examples/*{.am,.in,.c} -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim: ts=2 sw=2 et:
