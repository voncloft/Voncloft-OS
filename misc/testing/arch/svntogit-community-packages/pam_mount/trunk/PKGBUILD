# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sirius Bakke <sirius\at/nonline.org>

pkgname=pam_mount
pkgver=2.18
pkgrel=1
pkgdesc="A PAM module that can mount volumes for a user session"
arch=(x86_64)
url="http://pam-mount.sourceforge.net/"
license=(GPL)
depends=(pcre2 util-linux libhx libxml2 cryptsetup)
backup=('etc/security/pam_mount.conf.xml')
options=(!emptydirs)
source=("https://downloads.sourceforge.net/project/pam-mount/$pkgname/$pkgname-$pkgver.tar"{.xz,.asc})
sha256sums=('0af77a6e9867bb483c1d3e056a0289a7954efd84a6bf65f9d3f1268705b46220'
            'SKIP')
validpgpkeys=(2F8BFE96D14F8B560FBC1F1DA28BDFEEB5A2B2FF  # Jan Engelhardt <jengelh@inai.de>
              23686C10A45691BE7A425109D6388181F35A0938)              

build() {
  cd $pkgname-$pkgver
#  aclocal
#  libtoolize
#  automake --add-missing
#  autoreconf
  ./configure \
	--prefix=/usr \
	--with-ssbindir=/usr/bin \
	--sbindir=/usr/bin \
	--with-slibdir=/usr/lib \
	--sysconfdir=/etc \
	--localstatedir=/var
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
