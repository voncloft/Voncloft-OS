# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=perl-gtk3-imageview
_perl_namespace=Gtk3
_perl_module=ImageView
pkgver=6
pkgrel=1
pkgdesc="Image viewer widget for Gtk3"
arch=('any')
url="https://metacpan.org/release/${_perl_namespace}-${_perl_module}"
license=('LGPL')
depends=('perl-gtk3' 'imagemagick')
checkdepends=('perl-test-exception' 'perl-test-memory-cycle' 'perl-try-tiny'
              'perl-readonly' 'perl-test-mockobject' 'perl-test-differences'
              'perl-carp-always' 'xorg-server-xvfb')
source=("https://cpan.metacpan.org/authors/id/R/RA/RATCLIFFE/${_perl_namespace}-${_perl_module}-${pkgver}.tar.gz")
sha512sums=('68fc3371fe3d62163523d81732f0716fe15623a53139e8050e3bddc12c694a9ae2e89855b9926e14b18803e9c7a586b925b613e1cf364cb449b850fe70b571ab')
options=('!emptydirs')

build() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  xvfb-run -a -s "-extension GLX -screen 0 1280x1024x24" make test
}

package() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make pure_install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  # Delete unuseful files
  find "${pkgdir}" -name '.packlist' -delete
}
