# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jack_mixer
pkgver=14
pkgrel=3
pkgdesc="Jack desktop mixer application"
arch=('x86_64')
url="https://github.com/jack-mixer/jack_mixer"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'python-cairo'
'python-gobject' 'python-xdg')
makedepends=('glib2' 'jack')
optdepends=('new-session-manager: for session management')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jack-mixer/${pkgname}/archive/release-${pkgver}.tar.gz")
sha512sums=('19487f0d09a5bb1924190601c1bd2086c9ba3f456213b4b54aed82469a5e6f7def3b1dfe39fa3f9c7f0123d666c5bf119286caae1e8172cf957812285d739258')
b2sums=('8675813782331f7abc59d0769059eabe250a57cc6bc5411c09349fa0fcce1f44af0348507b3d8dd0fc2cedc97e28910ad1a03413c5844983a8b73e77183048c3')

prepare() {
  mv -v "$pkgname-release-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # keep autotools happy by creating README
  touch README
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  depends+=('libglib-2.0.so' 'libjack.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
