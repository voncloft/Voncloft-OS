# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=dfrs
pkgver=0.0.6
pkgrel=1
pkgdesc='Display file system space usage using graphs and colors'
url='https://github.com/anthraxx/dfrs'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'gcc-libs')
makedepends=('cargo' 'scdoc')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.sig)
options=('!makeflags')
sha512sums=('5d98d4cc4e2be30f11f5c9921173e04cbccfa2c5b10650f4a4fef67e06dba2b660a986db3c3d65ca9f89328af7196b08b4a560bf92b35a115cfc1bba095bfd8a'
            'SKIP')
b2sums=('424fcceb2a3d530b1f81698a7270423b41f78bc2ae4f09747b3ace4d2d0545b733033354e612a33c9e2677dedab8cc8af37db4a901cf245438a894a6dd48c7a8'
        'SKIP')
validpgpkeys=(
  'E240B57E2C4630BA768E2F26FC1B547C8D8172C8'  # Levente Polyak
)

build() {
  cd ${pkgname}-${pkgver}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
