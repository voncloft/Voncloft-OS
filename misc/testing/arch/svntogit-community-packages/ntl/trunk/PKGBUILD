# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Brad Conte <brad AT bradconte.com>

pkgname=ntl
pkgver=11.4.3
pkgrel=1
pkgdesc="A Library for doing Number Theory"
arch=(x86_64)
url="https://www.shoup.net/ntl/"
license=(LGPL)
depends=(gf2x gmp)
makedepends=()
source=("https://www.shoup.net/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('b7c1ccdc64840e6a24351eb4a1e68887d29974f03073a1941c906562c0b83ad2')

prepare() {
  cd $pkgname-$pkgver/src
  rm -r libtool-origin
}

build() {
  cd $pkgname-$pkgver/src
  ./configure DEF_PREFIX=/usr SHARED=on NTL_GF2X_LIB=on NATIVE=off TUNE=x86 \
    LIBTOOL=libtool CXXFLAGS="${CXXFLAGS}" LDFLAGS="${LDFLAGS}"
  make
}

check() {
  cd $pkgname-$pkgver/src
  make -k check
}

package() {
  cd $pkgname-$pkgver/src
  make PREFIX="$pkgdir/usr" install
}
