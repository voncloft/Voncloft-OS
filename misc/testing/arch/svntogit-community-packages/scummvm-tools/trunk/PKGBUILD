# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Jérémie Dimino <jeremie@dimino.org>
# Contributor: Sebastian Hase <sebastian_hase@gmx.de>
# Contributor: Joe Davison <joe@thosedamnpeanuts.com>
pkgname=scummvm-tools
pkgver=2.2.0
pkgrel=1
pkgdesc="A set of tools that enable you to convert, compress and decode game datafiles for ScummVM"
url="http://www.scummvm.org/"
license=('GPL')
depends=('libpng' 'wxgtk3' 'flac' 'libvorbis')
arch=('x86_64')
source=("https://www.scummvm.org/frs/scummvm-tools/${pkgver}/scummvm-tools-${pkgver}.tar.xz")
sha512sums=('97d23a94772809b6fe6dcfc4d1bde9e1a12c124cb49b0a465456adbbd56358a1612b9797abb9577dc350e1150fb4a70d4b7451e794c436c8e91692b4a83deb89')

prepare() {
  cd ${pkgname}-${pkgver}

  # Build with wxgtk3
  sed -i 's/^_wxconfig=wx-config$/_wxconfig=wx-config-gtk3/' configure
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}"/usr/bin

  make DESTDIR="${pkgdir}" install

  install -Dm 644 README "${pkgdir}"/usr/share/doc/scummvm-tools/README
  install -Dm 644 convert_dxa.sh "${pkgdir}"/usr/share/doc/scummvm-tools/samples/convert_dxa.sh
}
#vim: sw=2 ts=2 et:
