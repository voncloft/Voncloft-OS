# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: neodarz <neodarz at neodarz dot net>

_name=flake8-isort
pkgname=python-flake8-isort
pkgver=4.0.0
pkgrel=3
pkgdesc='Flake8 plugin to validate annotations complexity'
arch=('any')
url=https://github.com/gforcada/flake8-isort
license=('GPL3')
depends=('flake8' 'python-isort' 'python-setuptools' 'python-testfixtures')
checkdepends=('python-pytest' 'python-toml')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('2b91300f4f1926b396c2c90185844eb1a3d5ec39ea6138832d119da0a208f4d9')
b2sums=('b4acdd9729643a6c55e50afa2b44c76913285aa498aeefdfa561f090c0eb16926825aede9c24c0e35af0fc1f2f88b0f826b36c50010b588f2d678dbe78922f62')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  pytest
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
