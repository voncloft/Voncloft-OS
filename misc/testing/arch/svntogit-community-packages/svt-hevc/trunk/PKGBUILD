# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Contributor: Luis Garcia <luigi311.lg@gmail.com>

pkgname=svt-hevc
pkgver=1.5.0
pkgrel=1
pkgdesc='Scalable Video Technology HEVC encoder'
arch=(x86_64)
url='https://github.com/OpenVisualCloud/SVT-HEVC'
license=(BSD)
makedepends=(
  cmake
  git
  nasm
)
source=(git+https://github.com/OpenVisualCloud/SVT-HEVC.git#tag=909a9f38f07709991d640bf65a25834acfad6505)
md5sums=(SKIP)

pkgver() {
  cd SVT-HEVC

  git describe --tags | sed 's/^v//'
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  sed '/CMAKE_BUILD_TYPE Release/d' -i SVT-HEVC/CMakeLists.txt
}

build() {
  cd build

  export LDFLAGS="$LDFLAGS -Wl,-z,noexecstack"

  cmake ../SVT-HEVC \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DNATIVE=OFF
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
  install -Dm 644 ../SVT-HEVC/LICENSE.md -t "${pkgdir}"/usr/share/licenses/svt-hevc/
}

# vim: ts=2 sw=2 et:
