# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vapoursynth-plugin-deblock
pkgver=6.1
pkgrel=1
pkgdesc='Deblock plugin for VapourSynth'
arch=(x86_64)
url=https://github.com/HomeOfVapourSynthEvolution/VapourSynth-Deblock
license=(GPL2)
depends=(
  gcc-libs
  glibc
  vapoursynth
)
makedepends=(
  git
  meson
)
_tag=5ec9c9ba349fd1d34d7ceb5300a3837c99d214e7
source=(git+https://github.com/HomeOfVapourSynthEvolution/VapourSynth-Deblock.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd VapourSynth-Deblock

  git describe --tags | sed 's/^r//'
}

build() {
  arch-meson VapourSynth-Deblock build
  ninja -C build
}

package(){
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
