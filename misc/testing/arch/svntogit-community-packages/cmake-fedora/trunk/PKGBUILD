# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cmake-fedora
pkgver=2.9.3
pkgrel=3
pkgdesc='CMake helper modules for fedora developers'
arch=('any')
license=('custom:BSD')
url='https://pagure.io/cmake-fedora'
depends=('cmake')
makedepends=('git')
source=("git+https://pagure.io/cmake-fedora.git#tag=$pkgver")
sha512sums=('SKIP')

build() {
  cd cmake-fedora
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd cmake-fedora
  make DESTDIR="$pkgdir" install

  mv "$pkgdir"/usr/share/cmake{,-3.19}

  install -d -m755 "$pkgdir"/usr/share/licenses/cmake-fedora
  ln -s ../../doc/cmake-fedora/COPYING "$pkgdir"/usr/share/licenses/cmake-fedora/COPYING
}
