# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-requests-mock
pkgver=1.8.0
pkgrel=3
pkgdesc="A mock of useful classes and functions to be used with python-requests."
arch=('any')
url="https://github.com/jamielennox/requests-mock"
license=('Apache')
depends=('python-requests' 'python-six')
makedepends=('python-pbr')
checkdepends=('python-mock' 'python-purl' 'python-pytest' 'python-testrepository')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jamielennox/requests-mock/archive/$pkgver.tar.gz")
sha512sums=('bdcca2b751a77ae05922a6e8949494365cffd2dd3b3dc26258a87c77eb9fe956439fbd6259c133b0a847b1666d4b17db9641dcb9e1ddc9647d760b9e2f9e5ebf')

export PBR_VERSION=$pkgver

build() {
  cd requests-mock-$pkgver
  python setup.py build
}

check() {
  cd requests-mock-$pkgver
  python -m pytest tests/pytest
  python setup.py testr
}

package() {
  cd requests-mock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
