# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: ivh

pkgname=ext3grep
pkgver=0.10.2
pkgrel=6
pkgdesc="A tool to investigate an ext3 file system for deleted content and possibly recover it"
arch=('x86_64')
url="https://code.google.com/archive/p/ext3grep/"
license=('GPL')
makedepends=('e2fsprogs' 'patch')
changelog=$pkgname.changelog
source=(https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/$pkgname/$pkgname-$pkgver.tar.gz
        $pkgname-build.patch ext3grep-e2fsprogs-1.44.1.patch)
sha256sums=('586f3e939d2ffd782f6f006b81a3df2dc477c6da03f21edb8860c5c4f7198f3e'
            '6f57d5012fcfb063012ade80b25c356defc2d22411bbb9086ea89c0de10f3117'
            'f85913a797ddca5ee63481e0982536cf7b0dc062f58f3dae1121113380c30a21')

prepare() {
  cd ${pkgname}-${pkgver}

#http://code.google.com/p/ext3grep/issues/detail?id=34
  patch -Np1 -i "${srcdir}"/${pkgname}-build.patch

  patch -p1 -i ../ext3grep-e2fsprogs-1.44.1.patch # Fix build with e2fsprogs 1.44.1
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
