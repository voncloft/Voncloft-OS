# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=e3
pkgver=2.82
pkgrel=4
pkgdesc='Tiny editor without dependencies, written in assembly'
arch=(x86_64)
url='https://sites.google.com/site/e3editor/'
license=(GPL2)
makedepends=(yasm)
options=(!strip)
source=("https://sites.google.com/site/e3editor/Home/$pkgname-$pkgver.tgz")
sha256sums=('53c51d656801fd3ae0179a5b27f028e07eaba328e80c8c55050268359b9a2924')

build() {
  make -C $pkgname-$pkgver clean
  make -C $pkgname-$pkgver NASM_OR_YASM=yasm 64
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 e3 "$pkgdir/usr/bin/e3"
  for f in e3ws e3em e3pi e3vi e3ne; do
    ln -sf /usr/bin/e3 "$pkgdir/usr/bin/$f"
  done
  install -Dm644 e3.man "$pkgdir/usr/share/man/man1/e3.1"
}

# vim: ts=2 sw=2 et:
