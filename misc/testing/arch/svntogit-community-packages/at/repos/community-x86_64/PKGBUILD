# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Nathan Baum <n@p12a.org.uk>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Todd Musall <tmusall@comcast.net>

pkgname=at
pkgver=3.2.1
pkgrel=3
pkgdesc='AT and batch delayed command scheduling utility and daemon'
arch=('x86_64')
url='http://blog.calhariz.com/index.php/tag/at'
license=('GPL')
depends=('pam' 'flex')
makedepends=('smtp-forwarder' 'git' 'systemd')
backup=('etc/at.deny'
        'etc/pam.d/atd'
        'var/spool/atd/.SEQ')
options=('!makeflags')
validpgpkeys=('464BC7CD439FEE5E8B4098A0348A778D6885EF8F') # Jose M Calhariz (Técnico) <jose.calhariz@tecnico.ulisboa.pt>
source=("http://software.calhariz.com/$pkgname/${pkgname}_${pkgver}.orig.tar.gz"{,.asc}
        '80-atd.hook'
        'pam.conf')
sha256sums=('aabe6e5cb6dd19fe9fb25c2747492f2db38762b95ea41b86f949609c39fb55c4'
            'SKIP'
            'a7048d6dbd8aa4d881979716829d16053b5007c2d83eb2167edd5221f9e48a33'
            'e51630c26765e88bc9c3046c62bf12c56d9863f22bcf04eb00ccd5eb421c7e18')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --with-jobdir=/var/spool/atd \
    --with-atspool=/var/spool/atd \
    --with-systemdsystemunitdir=/usr/lib/systemd/system

  CFLAGS="$CFLAGS -w" make
}

package() {
  make -C "$pkgname-$pkgver" IROOT="$pkgdir" docdir=/usr/share/doc install

  install -D -m0644 pam.conf "$pkgdir"/etc/pam.d/atd
  install -D -m0644 80-atd.hook "$pkgdir"/usr/share/libalpm/hooks/80-atd.hook
}

# vim:set ts=2 sw=2 et:
