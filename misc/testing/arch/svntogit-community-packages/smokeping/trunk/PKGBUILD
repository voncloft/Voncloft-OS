# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Christos Nouskas nous <nous at archlinux.us>

pkgname=smokeping
pkgver=2.7.3
pkgrel=3
pkgdesc="A tool to keep track of your network latency"
arch=('any')
url="https://oss.oetiker.ch/smokeping/"
license=('GPL2')
depends=('perl-config-grammar' 'perl-digest-hmac' 'perl-libwww' 'rrdtool' 'perl-fcgi' 'perl-cgi-fast'
         'perl-io-tty' 'perl-net-dns' 'perl-net-snmp' 'perl-net-telnet' 'perl-ldap' 'perl-net-openssh')
source=("${pkgname}_release-$pkgver.tar.gz::https://github.com/oetiker/SmokePing/releases/download/$pkgver/smokeping-$pkgver.tar.gz"
        smokeping.sysusers
        smokeping.tmpfiles
        service)
sha256sums=('430fd58030a494068a897f772c052fc0979ebdd38d1859a03820e098628d36b4'
            'ab3b399f58118fc31fac5f627814bbb3b1d7752dd3b4fed2d6b7ee58f24fbd2e'
            'afd3196804a120866e8e5f207f177c96fd64b36c974888d8287fba3c216b1583'
            '4691650aed6c7341bf3a898f63c8ad03f2453d4ce9874e83f24ced5ee21317d9')

build() {
  cd "$srcdir"/smokeping-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc/smokeping --with-htdocs-dir=/srv/http/smokeping

  # prevent the build system from packaging CPAN deps
  rm -rf thirdparty/CPAN
  mkdir thirdparty/CPAN
  touch thirdparty/touch thirdparty/CPAN/touch

  make
}

package() {
  cd "$srcdir"/smokeping-${pkgver}

  # We package CPAN modules separately
  rm -rf thirdparty/lib/perl5

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/smokeping.service"
  install -Dm644 "$srcdir/smokeping.sysusers" "$pkgdir/usr/lib/sysusers.d/smokeping.conf"
  install -Dm644 "$srcdir/smokeping.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/smokeping.conf"
  chown -R 181:181 "$pkgdir"/{etc/smokeping,srv/http/smokeping}
  chmod 700 "$pkgdir/etc/smokeping"
}
