# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mcqd
pkgver=1.0.0
pkgrel=2
pkgdesc="Maximum Clique Algorithm for finding a maximum clique in an undirected graph"
arch=(x86_64)
url="http://www.sicmm.org/~konc/maxclique/"
license=(GPL3)
depends=(gcc-libs)
source=($pkgname-$pkgver.tar.gz::"https://gitlab.com/janezkonc/mcqd/repository/v$pkgver/archive.tar.gz")
sha256sums=('292931d9c302099d4f00e8cac1af9197f1fb8807cdef922fede525a6e8a49184')

build() {
  cd $pkgname-v$pkgver-*

  g++ ${CXXFLAGS} ${LDFLAGS} mcqd.cpp -o mcqd
}

package() {
  cd $pkgname-v$pkgver-*

  install -Dm755 mcqd "$pkgdir"/usr/bin/mcqd
  install -Dm644 mcqd.h "$pkgdir"/usr/include/mcqd.h
  install -D COPYING "$pkgdir"/usr/share/licenses/mcqd/COPYING
}

