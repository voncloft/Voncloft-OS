# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: cth451 <cth451@gmail.com>

pkgname=materia-gtk-theme
pkgver=20200916
pkgrel=1
pkgdesc="A Material Design theme for GNOME/GTK+ based desktop environments"
arch=(any)
url="https://github.com/nana-4/materia-theme"
license=(GPL)
depends=(gtk3 gnome-themes-extra)
makedepends=(meson sassc)
optdepends=('gtk-engine-murrine: for gtk2 theme')
provides=(materia-theme)
conflicts=(materia-theme)
replaces=(materia-theme)
source=(https://github.com/nana-4/materia-theme/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('5c524a0a80fc1a7b66ca8dd414b8023d5859bd6657827dceef1c93d2d9ef7dff')

build() {
    arch-meson ${pkgname/gtk-/}-${pkgver} build
    meson compile -C build
}

package() {
    DESTDIR="$pkgdir" meson install -C build
}
