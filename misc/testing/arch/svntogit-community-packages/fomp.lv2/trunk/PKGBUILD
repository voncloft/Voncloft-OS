# Maintainer: David Runge <dave@sleepmap.de>
pkgname=fomp.lv2
pkgver=1.0.0
pkgrel=5
pkgdesc="An LV2 port of the MCP, VCO, FIL, and WAH plugins by Fons Adriaensen."
arch=('x86_64')
url="https://git.drobilla.net/cgit.cgi/fomp.lv2.git/about/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs')
makedepends=('lv2' 'waf')
source=("https://git.drobilla.net/cgit.cgi/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.bz2"
        "wscript::https://git.drobilla.net/cgit.cgi/fomp.lv2.git/plain/wscript?id=6547441f898cc55923878022960c79f33959c7b3"
        "autowaf.py::https://git.drobilla.net/cgit.cgi/autowaf/plain/extras/autowaf.py?id=8280f9de69f93624896b8875caf039066cac0314"
        "lv2.py::https://git.drobilla.net/cgit.cgi/autowaf/plain/extras/lv2.py?id=29d4d293d23759b39e152241ab3669583afbdeca")
sha512sums=('3b4a89ac676f475a67c9f4c21433f20e179c7f411e31bf9ce2a8a7fc65055659e28752803eeae9f4e8454ccd83177f40c2a363281db484289c0b9cf7d31b3835'
            '5cfdea9022946636895462d767185f88cdf77e268daabd6368f91dd853e4bb7bd977ee68cc72948569443f89a624aec46ce034ef50819d33c86d5033f37290fb'
            '8b657954fc435b1c8768c877e9f5ffb5a8ef3cbfa1b63ad841f1ae1442da97b10557740b73f736bb57c05b0e85592b3f866ca1b456c66fbc9092e23bdcd58756'
            '3824eb314ccb5dd8ea9bb4fb6dfabf033b2ccb97593000d1968ee5ff7bc7654ab682c45f5d89f535eb68ccfd0da8026fa7928b4a6b56228b6365d4592d40d040')

prepare() {
  cd "$pkgname-$pkgver"
  # copy more current versions of custom waf scripts in place
  mkdir -pv tools
  cp -av ../{autowaf,lv2}.py tools/
  touch __init__.py
  # copy more current version of wscript in place
  cp -av ../wscript .
  # modify wscript to use current scripts and fix version
  sed -e 's/waflib.extras/tools/' \
      -e "s/load('lv2'/load('lv2', tooldir='tools'/" \
      -e "s/load('autowaf'/load('autowaf', tooldir='tools'/" -i wscript
  sed -E "s|(FOMP_VERSION = ) .+|\1 '${pkgver}'|" -i wscript
  # removing not yet implemented stuff
  sed -e '85,89d' -i wscript
}

build() {
  cd "$pkgname-$pkgver"
  waf configure --prefix=/usr
  waf
}

package() {
  cd "$pkgname-$pkgver"
  waf install --destdir="$pkgdir/"
  install -vDm 644 {AUTHORS,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
