# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=nimble
pkgver=0.12.0
pkgrel=1
epoch=1
pkgdesc='Package manager for the Nim programming language'
url='https://github.com/nim-lang/nimble'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'openssl')
makedepends=('nim')
source=(https://github.com/nim-lang/nimble/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('cb25f57c925af14138efa9f9229f054bd559400effe2c118dda18d7e220d9094380481befb46b2913f22831a70b3958eecb23ec085a5e3a298a4136337b96c2b')
b2sums=('2ae855402746b167aa237feafe59ae7e4decab33a63afb8b1422710cae884a0b0a5c08aa6efef8150d96e51ad468a77147026b6129ee9c2de21a7ceee6e2b5fc')

build() {
  cd ${pkgname}-${pkgver}
  nim c -d:release src/nimble
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 755 src/nimble -t "${pkgdir}/usr/bin"
  install -Dm 644 license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"

  # Nimble looks for nimscriptapi.nim in /usr/bin/nimblepkg/, of all places.
  cp -r src/nimblepkg "${pkgdir}/usr/share/${pkgname}"
  ln -s /usr/share/${pkgname} "${pkgdir}/usr/bin/nimblepkg"

  # completions
  install -Dm 644 nimble.bash-completion "${pkgdir}/usr/share/bash-completion/completions/nimble"
  install -Dm 644 nimble.zsh-completion "${pkgdir}/usr/share/zsh/site-functions/_nimble"
}

# vim: ts=2 sw=2 et:
