# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=packagekit-qt5
pkgver=1.0.2
pkgrel=1
pkgdesc='Qt5 bindings for PackageKit'
arch=('x86_64')
url='http://www.packagekit.org/'
license=('LGPL')
depends=('packagekit' 'qt5-base')
makedepends=('cmake')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17'  # Richard Hughes <richard@hughsie.com>
              'D33A3F0CA16B0ACC51A60738494C8A5FBF4DECEB'  # Matthias Klumpp <matthias@tenstral.net>
              '70C26659D184ABC01FFAF45228DDEDC6E4480AD0') # Daniel Nicoletti <dantti12@gmail.com>
source=("https://github.com/hughsie/PackageKit-Qt/releases/download/v$pkgver/PackageKit-Qt-$pkgver.tar.xz"
        "https://github.com/hughsie/PackageKit-Qt/releases/download/v$pkgver/PackageKit-Qt-$pkgver.tar.xz.asc")
sha256sums=('9e908478ebb85231f9fa1249c9183ccbaa16dc29e774d73f06d4cc0666058a87'
            'SKIP')

build() {
  	mkdir -p PackageKit-Qt-${pkgver}/build
  	cd PackageKit-Qt-${pkgver}/build

  	cmake .. \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
  	cd PackageKit-Qt-${pkgver}/build

	make DESTDIR="${pkgdir}" install
}
