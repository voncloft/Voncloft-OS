# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sebikul <sebikul@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary
pkgver=3.38.1
pkgrel=1
epoch=1
pkgdesc='A lightweight email client for the GNOME desktop'
arch=(x86_64)
url=https://wiki.gnome.org/Apps/Geary
license=(GPL3)
depends=(
  cairo
  enchant
  folks
  gcr
  gdk-pixbuf2
  glib2
  gmime3
  gnome-online-accounts
  gsound
  gspell
  gtk3
  iso-codes
  libcanberra
  libgee
  libhandy
  libnotify
  libpeas
  libsecret
  libsoup
  libxml2
  libytnef
  org.freedesktop.secrets
  pango
  sqlite
  webkit2gtk
)
makedepends=(
  appstream-glib
  cmake
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
_tag=c8ca5eb1b4c9d69ceb02ce6f4b7da9abf2a91c24
source=(git+https://gitlab.gnome.org/GNOME/geary.git?signed#tag=${_tag})
validpgpkeys=(3A2EF7F138557A145F2866E99FAB18747A8FC649) # Michael James Gratton <mike@vee.net>
sha256sums=(SKIP)

pkgver() {
  cd geary
  git describe --tags | sed 's/^gnome-//g'
}

build() {
  arch-meson geary build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
