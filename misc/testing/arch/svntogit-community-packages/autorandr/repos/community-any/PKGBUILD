# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=autorandr
pkgver=1.11
pkgrel=2
pkgdesc="Auto-detect connected display hardware and load appropiate X11 setup using xrandr"
arch=('any')
url="https://github.com/phillipberndt/autorandr"
license=('GPL3')
depends=('python' 'xorg-xdpyinfo' 'xorg-xrandr')
makedepends=('bash-completion' 'systemd')
optdepends=('bash-completion: auto-completion for autorandr in Bash'
            'zsh-completions: auto-completion for autorandr in ZSH')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/phillipberndt/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('93de0461653aa5145956a8aafdc9fb257491495ca335e4213e7b6f1f313ebfd2332c38615ac648204494026439d77861a7eefd97e80cd49a5fe3d9b75db54ec1')
b2sums=('57203b15eebdecab943c706745701e8569eb0a66dab69ae4429abfa863da736a5ed8b5333f6d948bcf8cae30f48c68eb1b35f2e84080526c1507ab78dba02efb')

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  # zsh completion
  install -vDm 644 "contrib/zsh_completion/_${pkgname}" \
    -t "${pkgdir}/usr/share/zsh/site-functions/"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
