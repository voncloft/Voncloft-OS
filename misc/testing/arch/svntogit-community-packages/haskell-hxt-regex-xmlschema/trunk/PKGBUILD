# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hxt-regex-xmlschema
pkgname=haskell-hxt-regex-xmlschema 
pkgver=9.2.0.3
pkgrel=24
pkgdesc="A regular expression library for W3C XML Schema regular expressions"
url="https://www.haskell.org/haskellwiki/Regular_expressions_for_XML_Schema"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hxt-charproperties')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('707ad7a4b5c32785fd89131279dd89a871f134a26ce1f98fd4ac8fec196c8960c2d39d18674967379b40508748fca3a34b9289500173075c54c1d6726191bbd8')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-profile
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
