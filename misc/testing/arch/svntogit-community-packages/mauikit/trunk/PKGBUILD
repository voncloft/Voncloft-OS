# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mauikit
pkgver=1.2.0
pkgrel=3
pkgdesc="Templated convergent controls and multi-platform utilities for Maui applications"
url="https://mauikit.org/"
arch=(x86_64)
license=(LGPL)
depends=(libappletdecoration syntax-highlighting kirigami2)
makedepends=(extra-cmake-modules)
source=(https://invent.kde.org/maui/mauikit/-/archive/v$pkgver/mauikit-v$pkgver.tar.gz)
sha256sums=('d241331ecf8c87448c044f129a486911b7e2e9cfda08717c636d0d5afa4f0f47')

build() {
  cmake -B build -S $pkgname-v$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/usr/lib/qt/qml/org/kde/appletdecoration # Provided by libappletdecoration
}
