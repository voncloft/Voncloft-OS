# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Cedric Girard <girard.cedric@gmail.com>

pkgname=proxychains-ng
pkgver=4.14
pkgrel=2
pkgdesc="A hook preloader that allows to redirect TCP traffic of existing dynamically linked programs through one or more SOCKS or HTTP proxies"
arch=('x86_64')
url="https://github.com/rofl0r/proxychains"
license=('GPL')
provides=('proxychains')
replaces=('proxychains')
conflicts=('proxychains')
depends=('glibc')
backup=('etc/proxychains.conf')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rofl0r/proxychains-ng/archive/v$pkgver.tar.gz")
sha512sums=('8388b3f7c2964bf49429b7dfbe8b6f79ce1967f2bcd93bf2b2c16d1a8d0a45bf7c7d1b57f83ca73319104f53545ef61852bf9d39ee512c90a96ca49e286a4006')

build() {
  cd proxychains-ng-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd proxychains-ng-$pkgver
  make DESTDIR="$pkgdir/" install
  make DESTDIR="$pkgdir/" install-config
  ln -s proxychains4 "$pkgdir/usr/bin/proxychains"
}

# vim:set ts=2 sw=2 et:
