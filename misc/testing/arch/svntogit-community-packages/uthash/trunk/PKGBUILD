# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Michael Frey <mail@mfrey.net>
pkgname=uthash
pkgver=2.1.0
pkgrel=2
pkgdesc="C preprocessor implementations of a hash table and a linked list"
arch=('any')
url="https://troydhanson.github.io/uthash/"
license=('BSD')
source=("$pkgname-$pkgver.tar.gz::https://github.com/troydhanson/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('152ccd8e64d0f495377232e3964d06c7ec8bb8c3fbd3217f8a5702614f9a669e')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
 
  # create directory for header files 
  install -dm755 "${pkgdir}"/usr/include/

  # install header files in /usr/include
  for h in *.h; do
      install -m 644 ${h} "${pkgdir}"/usr/include/
  done

  # install license file
  install -D -m644 ../LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
