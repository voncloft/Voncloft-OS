# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=kompose
pkgver=1.22.0
pkgrel=2
pkgdesc="Docker compose to Kubernetes transformation tool"
arch=('x86_64')
url="https://github.com/kubernetes/kompose"
license=('Apache')
makedepends=('go' 'git' 'jq') # git binary is needed for test scripts
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kubernetes/kompose/archive/v${pkgver}.tar.gz")
sha512sums=('09c2520b759dcc452f6d55b171d9c002ccdab96cf01cd9b67be33070c8b3faff0a189ca30731376386d214002e2c44ffe27715aa74039a8adbae946d2b03d1d0')

prepare() {
  export GOPATH="${srcdir}"
  mkdir -p src/github.com/kubernetes "${GOPATH}/bin"
  mv "${pkgname}-${pkgver}" "src/github.com/kubernetes/${pkgname}"
  export PACKAGE_ROOT="${GOPATH}/src/github.com/kubernetes/${pkgname}"
}

build() {
  cd "${PACKAGE_ROOT}"
  export CGO_ENABLED=1
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -v .
  chmod +x ./kompose
}

# tests are failing right now due to a bug in script/cmd/cmd_test.go:28
#check() {
#  cd "${PACKAGE_ROOT}"
#  go test -v ./...
#}

package() {
  cd "${PACKAGE_ROOT}"
  install -Dsm755 ./kompose "${pkgdir}/usr/bin/kompose"
}
