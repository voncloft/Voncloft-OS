# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-basictracer
pkgver=3.1.0
pkgrel=2
pkgdesc='The Python implementation of the "BasicTracer" reference implementation'
url="https://github.com/opentracing/basictracer"
license=('MIT')
arch=('any')
depends=('python-opentracing' 'python-protobuf' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest')
source=("https://github.com/opentracing/basictracer-python/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c744e8a64b59e688c5048493aa8739da35d3c3580c65bb561d94c0ce0d27fb26d5e99b12d09a8f7d3ca63bc1a98ebcac6e1c6fec7c2361c0734311d623047157')

build() {
  cd basictracer-python-$pkgver
  python setup.py build
}

check() {
  cd basictracer-python-$pkgver
  pytest
}

package() {
  cd basictracer-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
