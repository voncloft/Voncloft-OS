# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=5.2.0.87
pkgrel=1
epoch=1
pkgdesc='Deepin File Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
# startdde: for com.deepin.SessionManager
depends=('deepin-qt5integration' 'deepin-anything' 'disomaster' 'file' 'gio-qt' 'libmediainfo'
         'lucene++' 'avfs' 'polkit-qt5' 'poppler' 'ffmpegthumbnailer' 'jemalloc' 'kcodecs'
         'startdde' 'taglib' 'htmlcxx' 'libgsf' 'mimetic' 'boost-libs')
makedepends=('boost' 'qt5-tools' 'deepin-dock' 'deepin-movie' 'deepin-gettext-tools')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display'
            'deepin-screensaver: for screensaver chooser'
            'deepin-movie: for video preview'
            'deepin-terminal: for opening in terminal'
            'deepin-compressor: for compress/decompress')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-file-manager/archive/$pkgver.tar.gz")
sha512sums=('b1c35beb363127bb96aefb70b3992fa6a7d217d110102ca54407d04ef1806aa520c36d491d92e5311b56082d6f5289dbb4f38763555d152ba7ea2e1bec13a9fb')

prepare() {
  cd dde-file-manager-$pkgver
  sed -i '/#include <QException>/a #include <QPainterPath>' dialogs/dfmtaskwidget.cpp
  sed -i '/#include <QTimer>/a #include <QPainterPath>' dde-file-manager-lib/interfaces/dfmglobal.cpp
  sed -i '/#include <QPainter>/a #include <QPainterPath>' dde-file-manager-lib/interfaces/{dlistitemdelegate,dfmstyleditemdelegate,diconitemdelegate}.cpp dde-file-manager-lib/dialogs/openwithdialog.cpp
  sed -e 's|systembusconf.path = /etc/dbus-1/system.d|systembusconf.path = /usr/share/dbus-1/system.d|' \
      -e 's|systemd_service.path = /lib/systemd/system|systemd_service.path = /usr/lib/systemd/system|' \
      -i dde-file-manager-daemon/dde-file-manager-daemon.pro
}

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr filemanager.pro
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
