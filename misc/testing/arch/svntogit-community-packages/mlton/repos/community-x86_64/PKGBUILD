# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Michael Koloberdin <koloberdin@gmail.com>
# Contributor: tochiro@no.spam.mail.berlios.de
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>
# Contributor: Brian De Wolf <arch@bldewolf.com>

pkgname=mlton
pkgver=20200817
pkgrel=1
pkgdesc='Whole-program, optimizing Standard ML compiler'
arch=(x86_64)
url='http://mlton.org/'
license=(BSD MIT LGPL)
depends=(gmp)
makedepends=(git mlton)
options=(staticlibs)
source=("git+https://github.com/MLton/mlton#tag=on-$pkgver-release")
md5sums=('SKIP')

# Make dependencies for bootstrapping mlton on x86_64:
#makedepends_x86_64=('smlnj' 'setconf')

prepare() {
  cd $pkgname

  sed 's/_BSD_SOURCE/_DEFAULT_SOURCE/g' -i runtime/cenv.h

  # For bootstrapping mlton on x86_64
  #if [ $CARCH = 'x86_64' ]; then
  #  find . -name Makefile -type f -exec setconf {} CFLAGS "$CFLAGS -fPIC" \;
  #fi
}

build() {
  export CFLAGS="$CFLAGS -fPIC"
  export COMPILE_ARGS="-codegen c -cc-opt '-fPIC'"

  # For bootstrapping mlton on x86_64
  #if [ "$CARCH" = 'x86_64' ]; then
  #  # Bootstrap with SML-NJ on x86_64
  #  make -C "$pkgname" -j1 smlnj-mlton all-no-docs
  #else
  #  make -C "$pkgname" -j1 all-no-docs
  #fi

  make -C $pkgname -j1
}

package() {
  make -C $pkgname PREFIX=/usr DESTDIR="$pkgdir" install
  for f in $pkgname/doc/license/*-LICENSE; do
    install -Dm644 "$f" "$pkgdir/usr/share/licenses/$pkgname/$f"
  done
}

# vim: ts=2 sw=2 et:
