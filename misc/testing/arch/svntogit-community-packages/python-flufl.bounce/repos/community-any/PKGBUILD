# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.bounce
pkgname=python-flufl.bounce
pkgver=3.0.1
pkgrel=3
pkgdesc="Email bounce detectors"
arch=('any')
url="https://fluflbounce.readthedocs.io/en/latest/"
license=('Apache')
depends=('python-atpublic' 'python-zope-interface')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
provides=('python-flufl-bounce')
replaces=('python-flufl-bounce')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('9a8d0dec98f8699fd3dff62f5dd831ab5754f11d246b750cfed8ae7ebb771c3c7c1abd4fcceb0f8aa052653802c6e6e5a7bd4711e63b9095ede238817a3cd146')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
