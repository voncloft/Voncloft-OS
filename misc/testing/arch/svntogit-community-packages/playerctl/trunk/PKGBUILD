# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Maintainer : Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Ian D. Scott <ian@perebruin.com>
# Contributor: Tony Crisci <tony@dubstepdish.com>
pkgname=playerctl
pkgver=2.3.1
pkgrel=1
pkgdesc="mpris media player controller and lib for spotify, vlc, audacious, bmp, xmms2, and others."
arch=('x86_64')
url="https://github.com/acrisci/playerctl"
license=('LGPL3')
depends=('glib2')
makedepends=('gobject-introspection' 'gtk-doc' 'meson')
source=("playerctl-${pkgver}.tar.gz::https://github.com/acrisci/playerctl/archive/v${pkgver}.tar.gz")
sha512sums=('06a10d193c83f60a9a9c50dddfb0a74e00be8cde34ddaa9d75582b7ce487ac564d1ff0675f6cb0c27d968e7d435b6a427818ac14e0cdd94b86af8f5d89661a93')
validpgpkeys=('8CDFCC2D6611ACDE63D2C348564F0717F9D84E49')

prepare() {
    cd "${srcdir}/playerctl-${pkgver}"
}

build() {
    cd "${srcdir}/playerctl-${pkgver}"
    arch-meson build -D bash-completions=true -D zsh-completions=true
    ninja -C build
}

package() {
    cd "${srcdir}/playerctl-${pkgver}"
    DESTDIR="${pkgdir}" ninja -C build install
}
