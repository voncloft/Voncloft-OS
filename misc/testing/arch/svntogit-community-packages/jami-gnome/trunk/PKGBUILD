# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=jami-gnome
pkgver=20201217
pkgrel=1
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (GNOME client)"
arch=(x86_64)
url="https://jami.net"
license=(GPL3)
groups=(jami)
depends=(libjamiclient jami-daemon
         gtk3 dconf clutter clutter-gtk webkit2gtk qrencode qt5-base libnotify libcanberra libnm)
makedepends=(git cmake)
_commit=e84cb03f1ccfa180a93ebc85ef6a4e72c9ef0619
source=(git+https://git.jami.net/savoirfairelinux/ring-client-gnome.git#commit=${_commit})
md5sums=(SKIP)

prepare() {
  cd ring-client-gnome
  # Fix build system looking for lrc sources
  rm -r web
  ln -s /usr/include/libringclient/web-chatview/ web
}

build() {
  cmake -B build -S ring-client-gnome \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
