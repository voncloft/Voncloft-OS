# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nikola MilinkoviÄ‡ <nikmil@gmail.com>
# Submitter: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_pkgbase=regex
pkgname=python-regex
pkgver=2020.11.13
pkgrel=2
pkgdesc="Alternative python regular expression module."
arch=('x86_64')
url="https://bitbucket.org/mrabarnett/mrab-regex"
license=('Python' 'Apache')
depends=('python')
makedepends=('python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/r/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
sha256sums=('83d6b356e116ca119db8e7c6fc2983289d87b27b3fac238cfe5dca529d884562')
b2sums=('2ab9db519ae6569ee9f8a3b1d52c6ecf23b5d216a70e24f1357af88dbf93ca3d6d4eef17e267311faec47bbae6ea4041c6e5526c7cfa0ffeed3509d9c9336410')

build() {
  cd "regex-${pkgver}"

  python setup.py build
}

check() {
  cd "regex-${pkgver}"

  cd build/lib.linux-${CARCH}-3*/
  python -m unittest regex/test_regex.py
}

package() {
  cd "regex-${pkgver}"

  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
