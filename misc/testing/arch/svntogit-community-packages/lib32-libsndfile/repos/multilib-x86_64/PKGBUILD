# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_pkgbasename=libsndfile
pkgname=lib32-$_pkgbasename
pkgver=1.0.28
pkgrel=3
pkgdesc="A C library for reading and writing files containing sampled sound (32-bit)"
arch=('x86_64')
url="http://www.mega-nerd.com/libsndfile"
license=('LGPL')
depends=('lib32-alsa-lib' 'lib32-flac' 'lib32-libvorbis' $_pkgbasename)
makedepends=('gcc-multilib' 'libtool-multilib')
provides=('libsndfile.so')
source=(http://www.mega-nerd.com/libsndfile/files/${_pkgbasename}-${pkgver}.tar.gz{,.asc})
sha1sums=('85aa967e19f6b9bf975601d79669025e5f8bc77d'
          'SKIP')
validpgpkeys=('73571E85C19F4281D8C97AA86CA41A7743B8D6C8'
              '6A91A5CF22C24C99A35E013FCFDCF91FB242ACED')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${_pkgbasename}-${pkgver}

  export GETCONF="getconf -v POSIX_V7_ILP32_OFFBIG"
  export GETCONF_DIR="/usr/lib32/getconf"

  ./configure --prefix=/usr --disable-sqlite --libdir=/usr/lib32
  make -C src
}

check() {
  cd ${_pkgbasename}-${pkgver}
  make check
}

package() {
  cd ${_pkgbasename}-${pkgver}
  make -C src DESTDIR="${pkgdir}" install
  make DESTDIR="$pkgdir" install-pkgconfigDATA

  rm -rf "$pkgdir/usr/include"
}
