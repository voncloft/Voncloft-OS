# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Mikko Sysikaski <mikko.sysikaski@gmail.com>

pkgname=elfkickers
pkgver=3.1.a
pkgrel=1
pkgdesc='Collection of ELF utilities (includes sstrip)'
arch=(x86_64)
url='https://www.muppetlabs.com/~breadbox/software/elfkickers.html'
license=(GPL)
makedepends=(setconf)
source=("https://www.muppetlabs.com/~breadbox/pub/software/ELFkickers-${pkgver/.a/a}.tar.gz")
sha256sums=('06430880aaf4919c5f99fc629da7000347421668c2cf32bced2d401aac276508')

prepare() {
  for tool in elftoc objres; do
    setconf ELFkickers-${pkgver/.a/a}/$tool/Makefile CFLAGS "-O2 -I ../elfrw $CFLAGS -Wl,-z,relro,-z,now"
    setconf ELFkickers-${pkgver/.a/a}/$tool/Makefile LDFLAGS "$LDFLAGS -z,relro,-z,now"
  done
}

build() {
  make -C ELFkickers-${pkgver/.a/a} -j1
}

package() {
  install -d "$pkgdir/usr/"{bin,share/man/man1}
  make -C ELFkickers-${pkgver/.a/a} prefix="$pkgdir/usr" install -j1
}

# vim: ts=2 sw=2 et:
