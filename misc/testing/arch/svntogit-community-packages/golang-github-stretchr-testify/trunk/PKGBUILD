# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-stretchr-testify
pkgver=1.6.1
pkgrel=2
pkgdesc='A toolkit with common assertions and mocks that plays nicely with the standard library'
arch=('any')
url="https://github.com/stretchr/testify"
license=('MIT')
depends=('go' 'golang-github-davecgh-go-spew' 'golang-github-pmezard-go-difflib'
         'golang-github-stretchr-objx')
source=("$pkgname-$pkgver.tar.gz::https://github.com/stretchr/testify/archive/v$pkgver.tar.gz")
sha512sums=('f1f08ff9d6fd1b04838155a538da2b4cc25fb4adba798e57755b2e456365bdf169d2f84df586f083429c8e716e22f9b1ac7527d5e00fee6254b1721b9f765bdc')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/stretchr
  cp -a "$srcdir"/testify-$pkgver "$srcdir"/build/src/github.com/stretchr/testify
  cd "$srcdir"/build/src/github.com/stretchr/testify
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/stretchr
  cp -a "$srcdir"/testify-$pkgver "$pkgdir"/usr/share/gocode/src/github.com/stretchr/testify

  install -Dm644 testify-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
