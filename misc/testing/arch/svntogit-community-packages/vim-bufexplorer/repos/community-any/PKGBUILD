# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=vim-bufexplorer
pkgver=7.4.19
pkgrel=2
pkgdesc='Simple buffer list/switcher for vim'
url='https://github.com/jlanzarotta/bufexplorer'
arch=('any')
license=('BSD')
depends=('vim>=7.0')
groups=('vim-plugins')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/jlanzarotta/bufexplorer/archive/v${pkgver}.tar.gz)
sha512sums=('d38bcb52a50cfcf6d0b66ca6f1290a6be53d5cee98b3d2d7a93e01b5ef904af53c6cc8256e667bdeeb23a58344f5d69838bb7b5d71d225a83d1e173b99344888')

package() {
  cd bufexplorer-${pkgver}
  installpath="${pkgdir}/usr/share/vim/vimfiles"
  install -Dm 644 doc/* -t "${installpath}/doc"
  install -Dm 644 plugin/* -t "${installpath}/plugin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
