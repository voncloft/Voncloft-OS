# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-sys
pkgver=0.0.20191229
_commit=04cbcbbfeed8aa0b2b010b2ddab558e9f717fd3e
pkgrel=2
pkgdesc='Go packages for low-level interaction with the operating system'
arch=('any')
url="https://github.com/golang/sys"
license=('BSD')
depends=('go')
source=("$pkgname-$_commit.tar.gz::https://github.com/golang/sys/archive/$_commit.tar.gz")
sha512sums=('9ba7cf4061321ec535b44fc378bb72b166ab670d311cae658be2ae280f4a8f87fa23d46efc1c7e5d34c2be12b57631f89624ba3e3601118ea92859faf53faa1c')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/sys-$_commit "$srcdir"/build/src/golang.org/x/sys
  cd "$srcdir"/build/src/golang.org/x/sys
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/sys-$_commit "$pkgdir"/usr/share/gocode/src/golang.org/x/sys

  install -Dm644 sys-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
