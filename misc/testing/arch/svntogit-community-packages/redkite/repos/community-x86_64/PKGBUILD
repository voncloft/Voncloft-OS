# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=redkite
pkgver=1.3.0
pkgrel=1
pkgdesc="A small free software and cross-platform GUI toolkit"
arch=('x86_64')
url="https://gitlab.com/iurie-sw/redkite"
license=('GPL3')
depends=('glibc' 'cairo')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/iurie-sw/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('965d56256f21b235663d53a2b0881ae4a6c629edac037644952cfd0b42e4de4c494b3b9708a72c3207a2eeda780b0b0607d55bb9b1c016b418a1d0bc7396057f')
b2sums=('82b67243ebb394bb9294aab66d0b2f52d9f4cab5720f3b4fb0e78319924d3f449ce9846e5ca45a4fb7629b0127361da549942ba2b281630ba5f2d8bc7b72a559')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install -C build
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
