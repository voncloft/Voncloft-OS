# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=pylev
pkgname=python-pylev
pkgver=1.3.0
# PyPI does not have testsuite, Github does not have version tags
_commit=72e3d490515c3188e2acac9c15ea1b466f9ff938
pkgrel=6
pkgdesc="A pure Python Levenshtein implementation that's not freaking GPL'd"
arch=('any')
url="https://github.com/toastdriven/${_pkgname}"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
source=("${_pkgname}-${_commit}.tar.gz::${url}/archive/${_commit}.tar.gz")
sha256sums=('f05a9f4eb31355f0536c323df6855a033fd88f4c92bcf23a1802fea4f379aeeb')
b2sums=('03793e831c8034e31638d9c469fd386d3acac1dae75f2ef2a0af5c38ee5f11e6a7a20d423078cc42c647edec258db79486c01179e4284d93e64cca0bf99badca')

build(){
    cd "${srcdir}"/${_pkgname}-${_commit}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${_commit}

    python -m unittest tests
}

package() {
    cd "${srcdir}"/${_pkgname}-${_commit}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
