# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Simon Parzer <simon.parzer@gmail.com>

pkgname=grafx2
pkgver=2.7
pkgrel=3
# TODO: place recoil (retro computer image library) in a separate package
_recoil=5.0.0
pkgdesc='Pixelart-oriented painting program'
arch=(x86_64)
url='http://grafx2.chez.com/'
license=(GPL2)
depends=(fontconfig hicolor-icon-theme lua sdl2_image sdl2_ttf)
makedepends=(git)
options=(!emptydirs)
# tag: v2.7
source=("git+https://gitlab.com/GrafX2/grafX2.git#commit=c6460cf93999b2e6648ffd9cf9a7ef169b15104d"
        "https://sourceforge.net/projects/recoil/files/recoil/$_recoil/recoil-$_recoil.tar.gz"
        "fix-appstream.patch")
sha256sums=('SKIP'
            'caa60b380c7e0a762d62459bc9ca31d16b22b0f85c972e6315e3744ae1f2ffb5'
            'cb0135bb4fd1d17c6e8d6e3d8c0e32a6d0139fd9198bf93d744f655c26d59f16')

prepare() {
  mv grafX2 $pkgname
  sed 's/-liconv//g' -i "$pkgname/src/Makefile"
  mkdir -p grafx2/3rdparty/archives
  cp -uv recoil-$_recoil.tar.gz grafx2/3rdparty/archives/recoil-$_recoil.tar.gz

  cd $pkgname
  # Fix AppStream metadata
  # https://gitlab.com/GrafX2/grafX2/-/merge_requests/288
  patch -Np1 -i ../fix-appstream.patch
}

build() {
  CFLAGS="$CFLAGS -w" make -C $pkgname/src PREFIX=/usr API=sdl2
}

package() {
  install -d "$pkgdir/usr/share/grafx2/scripts/samples_2.4/picture/"{thomson/lib,others-8bit/lib}
  CFLAGS="$CFLAGS -w" make -C $pkgname/src PREFIX=/usr DESTDIR="$pkgdir" API=sdl2 install
  mv "$pkgdir/usr/bin/grafx2-sdl2" "$pkgdir/usr/bin/grafx2"
  install -Dm644 "$pkgname/doc/README.txt" \
    "$pkgdir/usr/share/doc/$pkgname/README"
}

# getver: gitlab.com/GrafX2/grafX2/-/tags
# vim: ts=2 sw=2 et:
