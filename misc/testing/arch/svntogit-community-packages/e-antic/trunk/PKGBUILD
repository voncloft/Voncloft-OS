# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=e-antic
pkgver=0.1.8
pkgrel=2
pkgdesc="Embedded algebraic number fields (on top of antic)"
arch=(x86_64)
url="https://github.com/videlec/e-antic"
license=(LGPL)
depends=(arb)
makedepends=()
source=(https://www.labri.fr/perso/vdelecro/e-antic/$pkgname-$pkgver.tar.gz
        e-antic-flint-2.7.patch)
sha256sums=('d8b6c18107756db86c7c4ca1593364e0ba9b4ad125910b45afd7ad5823d7100c'
            'bae50e5ff9d64257573d2e1771251d1b6ff779ee567caae7ee88d93a91c9d30d')

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../e-antic-flint-2.7.patch # Fix build with flint 2.7
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
