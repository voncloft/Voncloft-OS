# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: rtfreedman  <rob dot til dot freedman at gmail dot com>
# Contributor: speps <speps at aur dot archlinux dot org> 

pkgname=rtmidi
pkgver=4.0.0
pkgrel=2
pkgdesc="A set of C++ classes that provides a common API for realtime MIDI input/output."
arch=('x86_64')
url="https://www.music.mcgill.ca/~gary/rtmidi/"
license=('custom:RtMidi')
depends=('alsa-lib' 'gcc-libs' 'glibc' 'libjack.so')
makedepends=('doxygen')
provides=('librtmidi.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/thestk/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('39383f121320c9471b31f8b9d283167bfadf4c7328b6664d1d54a4c52e3dd9b76362875258d90363c6044e87fcee31ccce80e19435dc620c88e6d60fc82d0f9d')

prepare() {
  cd "${pkgname}-${pkgver}"
  # license in README.md:
  # https://github.com/thestk/rtmidi/issues/198
  tail -n 25 README.md | sed 's/^\ \ \ \ //g' > LICENSE
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --with-alsa \
              --with-jack
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # docs
  install -vDm 644 {README.md,doc/release.txt} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  # html
  install -vDm 644 doc/html/* \
    -t "${pkgdir}/usr/share/doc/${pkgname}/html"
  install -vDm 644 doc/images/* \
    -t "${pkgdir}/usr/share/doc/${pkgname}/images"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
