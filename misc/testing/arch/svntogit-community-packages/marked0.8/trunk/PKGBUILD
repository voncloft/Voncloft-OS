# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=marked0.8
_pkgname=marked
pkgver=0.8.2
pkgrel=1
pkgdesc="A markdown parser built for speed (Legacy 0.8 version)"
arch=('any')
url='https://github.com/chjj/marked'
license=('MIT')
provides=("marked=$pkgver")
conflicts=('marked')
depends=('nodejs')
makedepends=('npm')
source=(https://registry.npmjs.org/$_pkgname/-/$_pkgname-$pkgver.tgz)
noextract=($_pkgname-$pkgver.tgz)
sha512sums=('106c3311e09c2e78855f9d43853a664e89be75203f306fce0540e39d6b476c49e3007d7cd15cd4780c3ea04e3e66ff82a18056c9195839347437c48ef51d4f57')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$_pkgname-$pkgver.tgz

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"

  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s ../../../lib/node_modules/$pkgname/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
