# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jaraco.envs
pkgver=2.1.0
pkgrel=1
pkgdesc="Classes for orchestrating Python (virtual) environments"
url="https://github.com/jaraco/jaraco.envs"
license=('MIT')
arch=('any')
depends=('python-jaraco' 'python-path' 'python-virtualenv' 'python-tox')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-black' 'python-pytest-checkdocs' 'python-pytest-cov'
              'python-pytest-enabler' 'python-pytest-flake8' 'python-pytest-mypy')
source=("https://github.com/jaraco/jaraco.envs/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('4aaebe54df1a7f117ded41c7faed13924b29bb0d7b780581cdf7aa4c596265f590f8e782a50629389d30cde288384f98e8a4194f1f023f5580a8a1967d10d9b3')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd jaraco.envs-$pkgver
  python setup.py build
}

check() {
  cd jaraco.envs-$pkgver
  python -m pytest
}

package() {
  cd jaraco.envs-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  rm "$pkgdir"/usr/lib/python3.9/site-packages/jaraco/__init__.py \
     "$pkgdir"/usr/lib/python3.9/site-packages/jaraco/__pycache__/__init__.cpython-39.pyc

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
