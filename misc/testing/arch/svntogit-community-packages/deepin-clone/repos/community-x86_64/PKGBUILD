# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clone
pkgver=5.0.3
pkgrel=1
pkgdesc='Disk and partition backup/restore tool'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-clone"
license=('GPL3')
depends=('deepin-qt5integration' 'partclone' 'e2fsprogs' 'jfsutils' 'xfsprogs')
makedepends=('qt5-tools' 'deepin-gettext-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-clone/archive/$pkgver.tar.gz"
        dtk5.patch)
sha512sums=('fda6f351c4723ed4eda88c25baae0a3809cb5a2ec6389c325f2ed7567a5656c0db5baa787cdedc69b0c1c4ea7c5e3c8df621b474bd56532ba3398ed59238d49b'
            'eec931bf07057aa68a7ec5721254c54772647f7ed9b20cfde12b8732c7bf09a2abd14d6e55d5b7a18b39e3917f479261ca374078a95b3ffc8c3856cae2a15aea')

prepare() {
  cd deepin-clone-$pkgver
  patch -p1 -i ../dtk5.patch
  sed -i 's|/usr/sbin|/usr/bin|' app/{deepin-clone-app.pro,deepin-clone-ionice,deepin-clone-pkexec,com.deepin.pkexec.deepin-clone.policy.tmp}
}

build() {
  cd deepin-clone-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-clone-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
