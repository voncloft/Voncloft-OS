# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Wilson Pinto Júnior (N3RD3X) <n3rd3x@linuxmail.org>
# Contributor: Attila Bukor <r1pp3rj4ck@w4it.eu>

pkgname=guake
pkgver=3.7.0
pkgrel=2
pkgdesc='Drop-down terminal for GNOME'
arch=('any')
url="http://guake-project.org/"
license=('GPL')
depends=('libkeybinder3' 'libnotify' 'libutempter' 'libwnck3' 'python-cairo'
         'python-dbus' 'python-gobject' 'python-pbr' 'vte3')
makedepends=('git')
_commit=903491aa1d019ba1c8f17179a208eef2434ee949  # tags/3.7.0
source=("git+https://github.com/Guake/guake#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  sed -i '/-m pip install -r requirements.txt/d' Makefile
  make DESTDIR="$pkgdir" PREFIX='/usr' COMPILE_SCHEMA=0 install
}
