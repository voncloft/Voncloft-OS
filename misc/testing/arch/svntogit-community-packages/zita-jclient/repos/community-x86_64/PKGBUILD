# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zita-jclient
pkgver=0.4.2
pkgrel=2
pkgdesc="Create a Jack client with a few lines of C++"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/downloads/index.html"
license=('GPL3')
depends=('jack')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/$pkgname-$pkgver.tar.bz2")
sha512sums=('c785e4fe01561dde7fb6bca985de0c5735fdad1666f6c37ff2a783360b5c6fbe30837088715fe20e21c81cd9396e237fca25c91e8bd18d401032a8ca6a8f06e5')

prepare() {
  cd "$pkgname-$pkgver"
  # remove optimization
  # call ldconfig in local context
  sed -e '/march=native/d' \
      -e 's/ldconfig/& -N $(DESTDIR)\/$(LIBDIR)/' \
      -i source/Makefile
}

build() {
  cd "$pkgname-$pkgver/source"
  make
}

package() {
  cd "$pkgname-$pkgver/source"
  make DESTDIR="$pkgdir/" PREFIX=/usr SUFFIX="" install
  install -vDm 644 ../{AUTHORS,README} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
