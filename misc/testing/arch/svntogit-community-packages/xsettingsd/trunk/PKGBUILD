# Maintainer: Alad Wenter <alad@archlinux.org>
pkgname=xsettingsd
pkgver=1.0.0
pkgrel=3
pkgdesc="Provides settings to X11 applications via the XSETTINGS specification"
arch=('x86_64')
url="https://github.com/derat/xsettingsd"
license=('custom:BSD')
depends=('libx11' 'gcc-libs')
makedepends=('git' 'scons')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
md5sums=('7a1747dd1716cfa697f515e36308e8d4')

build() {
    cd "$pkgname-$pkgver"
    env CPPFLAGS="$CXXFLAGS" scons xsettingsd dump_xsettings
}

package() {
    cd "$pkgname-$pkgver"

    install -Dm755 xsettingsd dump_xsettings -t "$pkgdir/usr/bin/"
    install -Dm644 xsettingsd.1 dump_xsettings.1 -t "$pkgdir/usr/share/man/man1/"
    install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}
