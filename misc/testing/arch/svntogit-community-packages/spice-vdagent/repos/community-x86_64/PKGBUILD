# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: mutantmonkey <aur@mutantmonkey.in>

pkgname=spice-vdagent
pkgver=0.20.0+6+g8adf50d
pkgrel=1
pkgdesc="Spice agent for Linux guests"
arch=('x86_64')
url="https://www.spice-space.org/"
license=('GPL')
depends=('alsa-lib' 'dbus' 'glib2' 'gtk3' 'libdrm' 'libpciaccess' 'libxfixes' 'libxinerama' 'libxrandr' 'systemd')
makedepends=('spice-protocol' 'git')
checkdepends=('procps-ng')
backup=('etc/conf.d/spice-vdagentd')
_commit=8adf50de9ae6714e7a994aba7f6efa9f9528471e  # master
source=("git+https://gitlab.freedesktop.org/spice/linux/vd_agent.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd vd_agent
  git describe --tags | sed 's/^spice-vdagent-//;s/-/+/g'
}

prepare() {
  cd vd_agent
  sed -i 's|/etc/sysconfig/spice-vdagentd|/etc/conf.d/spice-vdagentd|
          s|/usr/sbin/spice-vdagentd|/usr/bin/spice-vdagentd|' data/spice-vdagentd.service
  sed -i 's|/etc/sysconfig/spice-vdagentd|/etc/conf.d/spice-vdagentd|' data/spice-vdagentd.1.in
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd vd_agent
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --sbindir=/usr/bin \
              --with-session-info=systemd --with-init-script=systemd
  make
}

check() {
  make -C vd_agent check
}

package() {
  cd vd_agent
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/var"
  install -dm755 "$pkgdir/etc/conf.d/"
  echo 'SPICE_VDAGENTD_EXTRA_ARGS=""' > "$pkgdir/etc/conf.d/spice-vdagentd"
}
