# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python2-soupsieve
pkgver=1.9.6
pkgrel=2
pkgdesc="A CSS4 selector implementation for Beautiful Soup"
url="https://github.com/facelessuser/soupsieve"
license=('MIT')
arch=('any')
depends=('python2-backports.functools_lru_cache')
makedepends=('python2-setuptools')
checkdepends=('python2-pytest' 'python2-beautifulsoup4' 'python2-html5lib' 'python2-lxml')
source=("https://github.com/facelessuser/soupsieve/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('44954cdedc16a51c794ebe6770caeb8f179167ad2f0a0478cb1efd5878d6cc49fffd6119125964b19184afa6d0d7dedadb81dee020a48c5128ccf56e36a78f61')

build() {
  cd soupsieve-$pkgver
  python2 setup.py build
}

check() {
  cd soupsieve-$pkgver
  pytest2
}

package() {
  cd soupsieve-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
