# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-cangjie
pkgver=0.0.0.20201030
_commit=d0f6f521ec049166ada29085434126be175f6439
pkgrel=1
pkgdesc="Cangjie input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-cangjie"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-luna-pinyin')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-cangjie/archive/$_commit/rime-cangjie-$_commit.tar.gz")
sha512sums=('1cc2a98fd5d9dc506221679423442c548a5085ece22c0f33473e97558f1c3c943f5400d50c4c62598a3f608a7b65508f2d4855dd3ad5fe1735da1468c8f0ef42')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
