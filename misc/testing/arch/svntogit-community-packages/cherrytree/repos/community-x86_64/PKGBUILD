# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Contributor: Morgenstern <charles [at] charlesbwise [dot] com>

pkgname=cherrytree
pkgver=0.99.30
pkgrel=1
pkgdesc="Hierarchical note-taking application"
arch=('x86_64')
url="https://www.giuspen.com/cherrytree/"
license=('GPL3')
depends=('fmt' 'gspell' 'gtksourceviewmm' 'libxml++2.6' 'uchardet')

makedepends=('cmake' 'python' 'spdlog')
source=("${pkgname}-${pkgver}.tar.xz::https://www.giuspen.com/software/${pkgname}_${pkgver}.tar.xz"
        "${pkgname}-${pkgver}.tar.xz.asc::https://www.giuspen.com/software/${pkgname}_${pkgver}.tar.xz.asc")
sha512sums=('949308a5a1eb2ee722d5f4cc530735b597d6caf1f04c4858903260a8c5eedbdc8431e1988516bb5b4fd425c171bbaa332293d741f0a8e594f6c0276468d2ee22'
            'SKIP')
validpgpkeys=('C7BF38CE0BD442C2369AA984049128A20CE0648D') # Giuseppe Penone <giuspen [at] gmail [dot] com>

build() {
  cmake \
	-B "${pkgname}_${pkgver}/build" \
	-S "${pkgname}_${pkgver}" \
  -DINSTALL_GTEST:BOOL='OFF' \
	-DAUTO_RUN_TESTING:BOOL='OFF' \
	-Wno-dev
  make -C "${pkgname}_${pkgver}/build"
}

# NOTE: In order to run tests in a clean chroot, you must allow it access your X server:
# xhost +local:
# https://wiki.archlinux.org/index.php/chroot#Run_graphical_applications_from_chroot
check() {
  export DISPLAY=:0
  cd "${pkgname}_${pkgver}"
  ./build/run_tests
}

package() {
  make -C "${pkgname}_${pkgver}/build" DESTDIR="${pkgdir}" install
}
