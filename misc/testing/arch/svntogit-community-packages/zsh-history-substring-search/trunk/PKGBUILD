# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zsh-history-substring-search
pkgver=1.0.2
pkgrel=1
pkgdesc="ZSH port of Fish history search (up arrow)"
arch=('any')
url="https://github.com/zsh-users/zsh-history-substring-search"
license=('BSD')
depends=('zsh')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zsh-users/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('9547cdd74f6bd5b102ca29b7b455cd4ce7450fe86864b5a149365de4c37d3da887edb403b195e80c142fe61861a3e4c063a5d8ba08ec0532b27a1207c80a023d')

prepare() {
  cd "$pkgname-$pkgver"
  sed -ne 4,37p ${pkgname}.zsh > LICENSE
  sed -e 's/^# //g' -e 's/^#//g' -i LICENSE
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644 "${pkgname}.zsh" \
    -t "${pkgdir}/usr/share/zsh/plugins/${pkgname}/"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
