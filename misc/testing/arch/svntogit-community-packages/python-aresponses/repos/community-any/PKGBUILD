# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aresponses
pkgver=2.1.4
pkgrel=1
pkgdesc="Asyncio http mocking. Similar to the responses library used for 'requests'"
url="https://github.com/circleup/aresponses"
license=('MIT')
arch=('any')
depends=('python-aiohttp' 'python-pytest-asyncio')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov')
source=("https://github.com/circleup/aresponses/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('67a6bdfbb66a708eeb42e68bf617373072a9cbdc8d105c06ce098598704950cc6b8a8c2a9b9cd609efbd409996e172f56477e22e1896907d8f3a3a44310a9d9d')

build() {
  cd aresponses-$pkgver
  python setup.py build
}

check() {
  cd aresponses-$pkgver
  python -m pytest
}

package() {
  cd aresponses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
