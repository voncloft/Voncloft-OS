# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=lib32-acl
pkgver=2.2.53
pkgrel=1
pkgdesc='Access control list libraries (32-bit)'
arch=('x86_64')
url='http://savannah.nongnu.org/projects/acl'
license=('LGPL')
depends=('lib32-attr>=2.4.46' 'acl')
makedepends=('gcc-multilib')
validpgpkeys=('600CD204FBCEA418BD2CA74F154343260542DF34'  # Brandon Philips <brandon@ifup.co>
              'B902B5271325F892AC251AD441633B9FE837F581') # Frysinger <vapier@gentoo.org>
source=("https://download.savannah.gnu.org/releases/acl/acl-${pkgver}.tar.gz"{,.sig})
sha256sums=('06be9865c6f418d851ff4494e12406568353b891ffe1f596b34693c387af26c7'
            'SKIP')

build() {
  cd "${srcdir}/acl-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32
  make 
}

package() {
  cd "${srcdir}/acl-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
