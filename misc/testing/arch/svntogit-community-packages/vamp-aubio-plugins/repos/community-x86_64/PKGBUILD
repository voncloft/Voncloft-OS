# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=vamp-aubio-plugins
pkgver=0.5.1
pkgrel=5
pkgdesc="Onset detection, pitch tracking, note tracking and tempo tracking plugins using aubio"
arch=('x86_64')
url="https://aubio.org/vamp-aubio-plugins/"
license=('GPL3')
groups=('pro-audio')
depends=('aubio' 'vamp-plugin-sdk')
makedepends=('waf')
source=("https://aubio.org/pub/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('ebbd8466ec3907d55e24824d7a725586df38212994f1398b75974dfac8e282dd8b2cb2a9381ad7e3da1a130b77936f109b36909b23591cc58ff176d6eb22625e')

prepare() {
  cd "${pkgname}-${pkgver}"
  # don't care aboute outdated waflib
  rm -rv waflib
}

build() {
  cd "${pkgname}-${pkgver}"
  waf configure --prefix=/usr
  waf build -v
}

package() {
  cd "${pkgname}-${pkgver}"
  waf install --destdir="${pkgdir}"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
