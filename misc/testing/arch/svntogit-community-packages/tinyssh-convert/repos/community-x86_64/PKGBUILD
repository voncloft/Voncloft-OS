# Maintainer: Giancarlo Razzolini <grazzolini@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=tinyssh-convert
pkgver=0.2
pkgrel=6
pkgdesc="Convert Ed25519 keys from OpenSSH to TinySSH format"
arch=(x86_64)
url="https://github.com/ansemjo/tinyssh-convert"
license=(MIT)
depends=(glibc)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=(3634d678aeac0f5afebc85b99260ef5eb9f25084bc0c12022fa92784f240841672074ba15036c2b09915b8c47a9ef8386f588b06ef92851c50037f8a00b8b3a7)

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's:CFLAGS =:CFLAGS +=:' Makefile.am
}

build() {
  cd ${pkgname}-${pkgver}
  autoreconf -vfi
  ./configure
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 ${pkgname} -t "${pkgdir}"/usr/bin/
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
