# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>

pkgname=unhide
pkgver=20130526
pkgrel=3
pkgdesc='A forensic tool to find processes hidden by rootkits, LKMs or by other techniques.'
arch=('x86_64')
url='https://sourceforge.net/projects/unhide/'
license=('GPL3')
depends=('iproute2' 'lsof' 'psmisc' 'net-tools' 'procps-ng')
source=("https://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver.tgz"
        "Makefile")
md5sums=('f3d68ed8eb239614abb0ff5ace22cf85'
         '68ec8dcd2f25d4d5dad089a8535d462f')

prepare() {
  cp -f Makefile $pkgname-$pkgver/
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" install
  ln -s unhide-linux "$pkgdir/usr/bin/unhide"
}
