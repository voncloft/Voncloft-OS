# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-subtests
pkgver=0.4.0
pkgrel=1
pkgdesc='unittest subTest() support and subtests fixture'
arch=('any')
license=('MIT')
url='https://github.com/pytest-dev/pytest-subtests'
depends=('python-pytest')
makedepends=('python-setuptools_scm')
source=("https://github.com/pytest-dev/pytest-subtests/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('00302f47d1699e053314b6b4eee7c84a04fe0f51f287e338374bbd518c7d245d839ea0f1ccf50d4f35c86138cf6ea36da26e67bf41295205fb38323f2ad89f89')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-subtests-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-subtests-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-subtests-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
