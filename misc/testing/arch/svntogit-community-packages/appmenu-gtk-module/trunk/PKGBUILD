# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: rilian-la-te <ria.freelander@gmail.com>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>

pkgname=appmenu-gtk-module
pkgver=0.7.6
pkgrel=1
pkgdesc="Application Menu GTK+ Module"
depends=()
makedepends=(meson gtk3 gtk2)
optdepends=('gtk3: GTK3 module' 'gtk2: GTK2 module')
url="https://gitlab.com/vala-panel-project/vala-panel-appmenu/"
arch=(x86_64)
license=(LGPL3)
source=("https://gitlab.com/vala-panel-project/vala-panel-appmenu/-/archive/$pkgver/vala-panel-appmenu-$pkgver.tar.gz"
        80-appmenu-gtk-module.sh)
sha256sums=('6ade911726c5cfa6e49416b7480c286cd5e098a7a1105f7f027827e3c3a4e119'
            'e8aff88851145085e24989cc99da9f631c593b506d0224225f3eb347d16c6455')

build() {
  meson build vala-panel-appmenu-$pkgver/subprojects/$pkgname \
    --prefix=/usr
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja install -C build
  install -Dm755 80-appmenu-gtk-module.sh -t "$pkgdir"/etc/X11/xinit/xinitrc.d/
}
