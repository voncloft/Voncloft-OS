# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-applications
pkgver=2.1.7
pkgrel=1
pkgdesc='Switchboard Applications Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-applications
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgee
  libgranite.so
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
source=(git+https://github.com/elementary/switchboard-plug-applications.git#tag=efdf7b468d1be2250c4dcd6a468974c4151f6edb)
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-applications

  git describe --tags
}

build() {
  arch-meson switchboard-plug-applications build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
