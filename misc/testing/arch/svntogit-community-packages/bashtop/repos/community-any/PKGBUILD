# Maintainer: SÃ©bastien Luttringer

pkgname=bashtop
pkgver=0.9.25
pkgrel=1
pkgdesc='Linux resource monitor'
arch=('any')
url="https://github.com/aristocratos/bashtop"
license=('Apache')
depends=('bash' 'coreutils' 'grep' 'sed' 'gawk' 'procps-ng')
makedepends=('git' 'sed')
optdepends=(
  'curl: themes download'
  'lm_sensors: cpu temperatures'
  'sysstat: disk read/write stats'
  'python-psutil: use python for data collection'
)
source=("git+https://github.com/aristocratos/bashtop.git#tag=v$pkgver")
sha256sums=('SKIP')


prepare() {
  cd $pkgname
  # disable update check
  sed -ri 's,^update_check="true"$,update_check="false",' bashtop
}

package() {
	cd $pkgname
	install -Dm755 bashtop -t "$pkgdir/usr/bin"
}

# vim:set ts=2 sw=2 et:
