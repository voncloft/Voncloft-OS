# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Stephan Windm√ºller <arch at freewarepoint dot de>

pkgname=osm-gps-map
pkgver=1.1.0
pkgrel=5
pkgdesc="GTK+ 3 library for showing OSM tiles"
arch=('x86_64')
url="https://nzjrs.github.com/osm-gps-map/"
license=('GPL2')
depends=('gtk3' 'libsoup')
makedepends=('gnome-common' 'gtk-doc' 'gobject-introspection')
provides=('osm-gps-map-gtk3')
conflicts=('osm-gps-map-gtk3')
replaces=('osm-gps-map-gtk3')
source=("https://github.com/nzjrs/osm-gps-map/archive/$pkgver.tar.gz")
sha256sums=('027004459f2f29232d4ca0c914f835e6c06b720aef1daa9b470d9d5e1b71159b')

prepare() {
  cd "$srcdir"/osm-gps-map-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$srcdir"/osm-gps-map-$pkgver
  ./configure --prefix=/usr --enable-gtk-doc
  make
}

package() {
  cd "$srcdir"/osm-gps-map-$pkgver
  make DESTDIR="$pkgdir" install
}
