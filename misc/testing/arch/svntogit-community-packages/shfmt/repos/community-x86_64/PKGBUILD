# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Daniel Mart√≠ <mvdan@mvdan.cc>

pkgname=shfmt
pkgver=3.2.1
pkgrel=1
pkgdesc="Format shell programs"
url="https://github.com/mvdan/sh"
license=('BSD')
arch=('x86_64')
makedepends=('git' 'go-pie')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mvdan/sh/archive/v${pkgver}.tar.gz")
sha512sums=('dcc8d256a3220d4216291c929bc462a910ffb43f28f9e89777138d1df5c40f8d6ba017341f73b955950c4ba3f39da5902459985ad02cdb938b9a9f44ad1c3147')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
  cd "${srcdir}/sh-${pkgver}/cmd/shfmt/"
  go build .
}

check() {
  cd "${srcdir}/sh-${pkgver}/"
  go test ./...

}

package() {
  cd "${srcdir}/sh-${pkgver}/"
  install -Dm755 "cmd/shfmt/shfmt" "${pkgdir}/usr/bin/shfmt"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
