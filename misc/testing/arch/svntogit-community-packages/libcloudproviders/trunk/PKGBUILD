# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Fina Wilke <fina@felinira.net>

pkgname=libcloudproviders
pkgver=0.3.1
pkgrel=1
pkgdesc='DBus API that allows cloud storage sync clients to expose their services'
arch=(x86_64)
url=https://gitlab.gnome.org/World/libcloudproviders
license=(LGPL3)
depends=(
  glib2
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
)
_tag=4672d39320f4ef6091a4977b8e6a4ac079296c26
source=(git+https://gitlab.gnome.org/World/libcloudproviders.git#tag=${_tag})
md5sums=(SKIP)

pkgver() {
  cd libcloudproviders

  git describe --tags
}

build() {
  arch-meson libcloudproviders build \
    --prefix /usr
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
