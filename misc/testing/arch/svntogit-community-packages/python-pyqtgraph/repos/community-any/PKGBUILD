# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Windel Bouwman <windel at windel dot nl>
# Contributor: Jakub Klinkovský <kuba.klinkovsky@gmail.com>

_pkgname=pyqtgraph
pkgname=python-$_pkgname
pkgver=0.11.1
pkgrel=1
pkgdesc='Scientific Graphics and GUI Library for Python'
arch=('any')
url='http://www.pyqtgraph.org'
license=('MIT')
makedepends=('python-setuptools')
depends=('python' 'qt5-python-bindings' 'python-numpy')
optdepends=('python-opengl: 3d graphics')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/pyqtgraph/pyqtgraph/archive/pyqtgraph-$pkgver.tar.gz")
sha512sums=('36abf6e8112ab8af51555617107d81e65ec385c7cb7a8371268b2e363eb9dc81b94db59e12acf78839d91939e036f48fffad61a486261b32e8403c31be9fb3f8')

build() {
  cd $_pkgname-$_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
