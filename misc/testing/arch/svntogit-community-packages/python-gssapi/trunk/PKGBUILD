# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Samuel Laur√©n <samuel.lauren@iki.fi>

pkgname=python-gssapi
pkgver=1.6.12
pkgrel=2
pkgdesc='Provides both low-level and high level wrappers around the GSSAPI C libraries'
url='https://github.com/pythongssapi/python-gssapi'
arch=('x86_64')
license=('ISC')
depends=('python' 'python-decorator' 'krb5')
makedepends=('python-setuptools' 'cython')
checkdepends=('python-parameterized' 'python-k5test')
source=("https://github.com/pythongssapi/python-gssapi/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('df28677b7ea0c3f9ee9809310f60becba7095ac2614b61a6611662f118cbdcc5522a9be3f8195e04590b8ed7c5ae6bbc695e20902c8e975871fd55fb1997805d')
b2sums=('2d2798d3e9c249abd67c9dc7426b917b4b54595494aa7331a1f9be6007985bdbc9fc05c624252443a357d5445ed7f93cb2cfb6918ca77feba75ff4e0ad0b56c1')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  python setup.py test
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
