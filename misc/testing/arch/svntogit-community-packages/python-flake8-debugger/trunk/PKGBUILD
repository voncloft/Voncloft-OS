# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flake8-debugger
pkgver=3.2.1
pkgrel=3
pkgdesc='ipdb/pdb statement checker plugin for flake8'
arch=('any')
license=('MIT')
url='https://github.com/JBKahn/flake8-debugger'
depends=('flake8')
makedepends=('python-setuptools' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/JBKahn/flake8-debugger/archive/$pkgver.tar.gz")
sha512sums=('1f5cce02e6a67df021a6c9d5f4bb74fc68fae12d57465497488bd2a7e78b8f6981979a1c433f3d8f83add6a22097f5f145328daf5c298a3e118f88f7677bcbeb')

build() {
  cd "$srcdir"/flake8-debugger-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/flake8-debugger-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/flake8-debugger-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
