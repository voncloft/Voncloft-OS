# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Doug Newgard <scimmia at archlinux dot info>
# Contributor: Matias De la Puente <mfpuente.ar@gmail.com>

pkgname=moserial
pkgver=3.0.12
pkgrel=3
pkgdesc="Clean, friendly GTK+-based serial terminal for the GNOME desktop"
arch=('x86_64')
url="https://wiki.gnome.org/moserial"
license=('GPL3')
depends=('gsettings-desktop-schemas' 'gtk3')
makedepends=('intltool' 'itstool')
optdepends=('lrzsz: for xmodem, ymodem and zmodem file transfer protocols')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('de2448cd37bb64c43f5533913739cbfc2fe3261f6c6a5ea289f9c200f1e7fcc9')


prepare() {
	cd $pkgname-$pkgver

	# Don't use legacy path for AppStream metainfo file 
	# https://gitlab.gnome.org/GNOME/moserial/merge_requests/1
	sed -i 's|$(datarootdir)/appdata|$(datarootdir)/metainfo|' data/Makefile.{am,in}
}

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
}
