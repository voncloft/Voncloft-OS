# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gst-plugins-espeak
pkgver=0.5.0
pkgrel=3
pkgdesc="Use espeak as a sound source for gstreamer"
arch=('x86_64')
url="https://wiki.sugarlabs.org/go/Activity_Team/gst-plugins-espeak"
license=('LGPL')
depends=('gst-plugins-base-libs' 'espeak')
source=(https://download.sugarlabs.org/sources/honey/$pkgname/$pkgname-$pkgver.tar.gz
        fix-build.patch)
sha256sums=('9696278924ef3ba6c4d8b579bd349c81ed42879554d45f0e2be230cf0a893704'
            'aea0d1df4753584925e864450bce2d6ce8a7f1d7108e164057140bc5d7bb63fc')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fix-build.patch
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
