# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: svoufff <svoufff at gmail dot com>

pkgname=jsampler
_name=JSampler
pkgver=0.9
pkgrel=9
pkgdesc="LinuxSampler Java GUI"
arch=('any')
url="http://jsampler.sourceforge.net/"
license=('LGPL2.1')
groups=('pro-audio')
depends=('java-runtime=8' 'linuxsampler')
makedepends=('gendesk' 'java-environment=8' 'apache-ant')
source=("https://downloads.sourceforge.net/${pkgname}/${_name}-${pkgver}.tar.bz2"
        "${pkgname}.sh")
sha512sums=('cbb1d476b1f3882592cc0fb7a61348802c0c399dfc3fcc35f4a8ec3396e8417754cafc3e1a14a90b0014bebd29d28b9d3771dcbb772df40d241eaadedad5299f'
            '2f0799de020eb81620e9e4b55ee7a9c752a599627798b874f081e2ee4dd692059883174f401e03475f7583bc07bba8ad038e7b49684af4e2face64e9d527cb55')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  declare -A comments=(
    ["${pkgname}"]="Fantasia Java GUI"
    ["${pkgname}-classic"]="Classic Java GUI"
  )
  declare -A exec_settings=(
    ["${pkgname}"]="/usr/bin/${pkgname} fantasia"
    ["${pkgname}-classic"]="/usr/bin/${pkgname} classic"
  )
  declare -A names=(
    ["${pkgname}"]="Fantasia"
    ["${pkgname}-classic"]="Classic"
  )
  for _app in "${pkgname}" "${pkgname}-classic"; do
    gendesk -n \
            --pkgname "${_app}" \
            --name "${_name} ${names[${_app}]}" \
            --exec "${exec_settings[${_app}]}" \
            --pkgdesc "${comments[${_app}]} for LinuxSampler" \
            --genericname "Audio Sampler" \
            --categories "AudioVideo;Audio"
  done
}

build() {
  cd "${pkgname}-${pkgver}"
  ant build-fantasia
  ant build-jsclassic
}

package() {
  cd "${pkgname}-${pkgver}"
  # java files
  install -vDm 644 dist/Fantasia-${pkgver}.jar \
    "${pkgdir}/usr/share/java/${pkgname}/fantasia.jar"
  install -vDm 644 dist/JS_Classic-${pkgver}.jar \
    "${pkgdir}/usr/share/java/${pkgname}/classic.jar"
  # start script
  install -vDm 755 "${srcdir}/${pkgname}.sh" \
    "${pkgdir}/usr/bin/${pkgname}"
  # freedesktop.org compatibility
  install -vDm 644 res/fantasia/icons/app_icon.png \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -vDm 644 res/js-classic/icons/app-icon.png \
    "${pkgdir}/usr/share/pixmaps/${pkgname}-classic.png"
  install -vDm 644 *.desktop -t "${pkgdir}/usr/share/applications/"
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
# vim:set ts=2 sw=2 et:
