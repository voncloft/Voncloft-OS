# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Dwayne Bent <dbb.1@liqd.org>
# Contributor: Paul-Sebastian Manole <brokenthorn@gmail.com>

pkgbase=luasec
pkgname=(lua-sec lua51-sec lua52-sec lua53-sec)
epoch=2
pkgver=0.9
pkgrel=2
pkgdesc='Lua bindings for OpenSSL library to provide TLS/SSL communication.'
arch=('x86_64')
url='https://github.com/brunoos/luasec/wiki'
license=('MIT')
makedepends=('openssl'
             'lua'
             'lua-socket'
             'lua51'
             'lua51-socket'
             'lua52'
             'lua52-socket'
             'lua53'
             'lua53-socket')
depends=('openssl')
source=("$pkgbase-v$pkgver.tar.gz"::"https://github.com/brunoos/luasec/archive/v$pkgver.tar.gz")
sha512sums=('ea601016328232ca0ff12be72897eeedac88a78834a63e1c36d2828a3187039665a2c60597b5af1fa6917b6fe87cd292fbcdfabcd67bab968f587e038d52a933')

prepare() {
  cp -a luasec-$pkgver luasec-$pkgver-51
  cp -a luasec-$pkgver luasec-$pkgver-52
  cp -a luasec-$pkgver luasec-$pkgver-53
}

build() {
  cd "$srcdir"/luasec-$pkgver-51
  make linux INC_PATH=-I/usr/include/lua5.1

  cd "$srcdir"/luasec-$pkgver-52
  make linux INC_PATH=-I/usr/include/lua5.2

  cd "$srcdir"/luasec-$pkgver-53
  make linux INC_PATH=-I/usr/include/lua5.3

  cd "$srcdir"/luasec-$pkgver
  make linux
}

package_lua51-sec() {
  depends+=('lua51-socket')
  conflicts=('luasec')
  replaces=('luasec')

  cd "luasec-$pkgver-51"
  make LUACPATH="$pkgdir/usr/lib/lua/5.1" LUAPATH="$pkgdir/usr/share/lua/5.1" install
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua52-sec() {
  depends+=('lua52-socket')

  cd "luasec-$pkgver-52"
  make LUACPATH="$pkgdir/usr/lib/lua/5.2" LUAPATH="$pkgdir/usr/share/lua/5.2" install
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua53-sec() {
  depends+=('lua53-socket')

  cd "luasec-$pkgver-53"
  make LUACPATH="$pkgdir/usr/lib/lua/5.3" LUAPATH="$pkgdir/usr/share/lua/5.3" install
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua-sec() {
  depends+=('lua-socket')

  cd "luasec-$pkgver"
  make LUACPATH="$pkgdir/usr/lib/lua/5.4" LUAPATH="$pkgdir/usr/share/lua/5.4" install
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
