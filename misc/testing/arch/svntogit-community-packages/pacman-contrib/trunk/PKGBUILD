# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=pacman-contrib
pkgver=1.4.0
pkgrel=1

pkgdesc="Contributed scripts and tools for pacman systems"
url="https://git.archlinux.org/pacman-contrib.git/about/"
arch=('x86_64')
license=('GPL')

depends=('fakeroot' 'pacman' 'perl')
makedepends=('asciidoc')
optdepends=('findutils: for pacdiff --find'
            'mlocate: for pacdiff --locate'
            'sudo: privilege elevation for several scripts'
            'vim: default merge program for pacdiff')

source=("https://git.archlinux.org/pacman-contrib.git/snapshot/pacman-contrib-$pkgver.tar".{gz,asc})
sha256sums=('8746f1352aaad990fe633be34f925d5ab8bd8a19a5f1274e72dbde426deb86bd'
            'SKIP')
validpgpkeys=('5134EF9EAF65F95B6BB1608E50FB9B273A9D0BB5'  # Johannes Löthberg <johannes@kyriasis.com>
              '04DC3FB1445FECA813C27EFAEA4F7B321A906AD9') # Daniel M. Capella <polyzen@archlinux.org>

prepare() {
  cd pacman-contrib-$pkgver

  ./autogen.sh
}

build() {
  cd pacman-contrib-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

check() {
  make -C pacman-contrib-$pkgver check
}

package() {
  cd pacman-contrib-$pkgver

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
