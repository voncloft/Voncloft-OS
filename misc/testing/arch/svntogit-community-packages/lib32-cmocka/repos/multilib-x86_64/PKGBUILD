# Maintainer: David Runge <dvzrv@archlinux.org>

_name=cmocka
pkgname=lib32-cmocka
pkgver=1.1.5
pkgrel=1
pkgdesc='Elegant unit testing framework for C with support for mock objects'
url='https://cmocka.org/'
arch=('x86_64')
license=('Apache')
depends=('cmocka' 'lib32-glibc')
makedepends=('cmake')
provides=('libcmocka.so')
source=("https://cmocka.org/files/1.1/${_name}-${pkgver}.tar.xz"{,.asc})
sha512sums=('cad7f04757183d004f6eaad39036fc0e24c5e0e987f80e85bc43bc66dba22389cb02b08e25531cc28a541d0a24a86b29be134a2d6fc339128e87d66952f502bd'
            'SKIP')
validpgpkeys=('8DFF53E18F2ABC8D8F3C92237EE0FC4DCC014E3D') # Andreas Schneider <asn@cryptomilk.org>

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib32 \
        -DCMAKE_BUILD_TYPE=Release \
        -DUNIT_TESTING=ON \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 test -C build
}

package() {
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}" -C build
  rm -rvf "${pkgdir}/usr/include"
}
# vim: ts=2 sw=2 et:
