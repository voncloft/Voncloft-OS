# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: damir <damir@archlinux.org>

_pkg=netcdf
_mpi=openmpi
pkgname=${_pkg}-${_mpi}
pkgver=4.7.4
pkgrel=1
pkgdesc="network Common Data Form interface for array-oriented data access and corresponding library with parallel support (${_mpi} version)"
arch=(x86_64)
url="https://www.unidata.ucar.edu/software/netcdf/"
license=(custom)
depends=("hdf5-${_mpi}" "pnetcdf-${_mpi}" curl)
makedepends=(cmake)
optdepends=('netcdf-fortran: fortran bindings' 'netcdf-cxx: c++ bindings')
checkdepends=(inetutils)
provides=("${_pkg}")
conflicts=("${_pkg}")
options=(!makeflags)
source=(https://github.com/Unidata/netcdf-c/archive/v${pkgver}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('99930ad7b3c4c1a8e8831fb061cb02b2170fc8e5ccaeda733bd99c3b9d31666b')

build() {
    export CC=mpicc
    cmake -B build -S ${_pkg}-c-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_CDF5=ON \
        -DENABLE_PNETCDF=ON \
        -DENABLE_DAP_LONG_TESTS=ON \
        -DENABLE_EXAMPLE_TESTS=ON \
        -DENABLE_EXTRA_TESTS=ON \
        -DENABLE_FAILING_TESTS=ON \
        -DENABLE_FILTER_TESTING=ON \
        -DENABLE_LARGE_FILE_TESTS=ON
    make -C build
}


check() {
    # This is required starting with OpenMPI 3.0 when trying to run more
    # processes than the number of available cores
    export OMPI_MCA_rmaps_base_oversubscribe=yes

    make -C build test
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -Dm644 ${_pkg}-c-${pkgver}/COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${_pkg}/
}
