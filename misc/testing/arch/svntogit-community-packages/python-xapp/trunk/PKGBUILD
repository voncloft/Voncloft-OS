# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

_pkgname=python3-xapp
pkgname=python-xapp
pkgver=2.0.2
pkgrel=2
pkgdesc="Python Xapp Library"
arch=('any')
url="https://github.com/linuxmint/${_pkgname}"
license=('LGPL')
depends=('python' 'python-psutil')
makedepends=('python-setuptools')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('2b5d2a259e55384426940e6284ec9f964f4f0220b8cb364606c938ef6e1717d40ddbc3730732dddc86d460f624ac6161fe5ab55c276aedba9187a79d9d53a60e')

build(){
  cd "${srcdir}"/${_pkgname}-${pkgver}

  python setup.py build
}

package() {
  cd "${srcdir}"/${_pkgname}-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
