# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=btfs
pkgver=2.23
pkgrel=1
pkgdesc="A bittorrent filesystem based on FUSE"
arch=('x86_64')
url="https://github.com/johang/btfs"
license=('GPL')
depends=('fuse2' 'libtorrent-rasterbar' 'curl')
makedepends=('boost')
optdepends=('python: for btplay')
source=("$pkgname-$pkgver.tar.gz::https://github.com/johang/btfs/archive/v$pkgver.tar.gz")
sha512sums=('f911b334367e619621677793342fe36247dc63a8bff6fcab65f0b64e1645bab2ede261a7e7c03c76e18ad75b9b485d8c422e99ffcbd6069ceeb5b5e590bf5f63')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -i
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
