# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-funcparserlib
pkgver=0.3.6
pkgrel=5
pkgdesc="Recursive descent parsing library based on functional combinators"
url="https://github.com/vlasovskikh/funcparserlib"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/vlasovskikh/funcparserlib/archive/$pkgver.tar.gz")
sha512sums=('0fe23dc471f2de4588204d09379ef1400231bbe370e71045ddfad82c0f3a7bd13350954dde7210a370084c7bb7eb8c02ebdb04fb3727bee3e768cebe54e5ed99')

build() {
  cd funcparserlib-$pkgver
  python setup.py build
}

check() {
  cd funcparserlib-$pkgver
  python setup.py nosetests
}

package() {
  cd funcparserlib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
