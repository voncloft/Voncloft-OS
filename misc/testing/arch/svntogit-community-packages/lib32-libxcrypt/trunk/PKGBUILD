# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=lib32-libxcrypt
# Neither tarballs nor tags are signed, but commits are.
_commit='6b110bcd4f4caa61fc39c7339d30adc20a7dd177' # git rev-parse v${pkgver}
pkgver=4.4.17
pkgrel=1
pkgdesc='Modern library for one-way hashing of passwords (32-bit)'
arch=('x86_64')
url='https://github.com/besser82/libxcrypt/'
license=('GPL')
depends=('lib32-glibc' 'libxcrypt')
makedepends=('git' 'gcc-multilib')
validpgpkeys=('678CE3FEE430311596DB8C16F52E98007594C21D') # Bj√∂rn 'besser82' Esser
source=("git+https://github.com/besser82/libxcrypt.git#commit=${_commit}?signed")
sha256sums=('SKIP')

prepare() {
  cd "${srcdir}/libxcrypt"

  autoreconf -fi
}

build() {
  cd "${srcdir}/libxcrypt"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    --disable-static \
    --enable-hashes=strong,glibc \
    --enable-obsolete-api=no \
    --disable-failure-tokens
  make 
}

package() {
  cd "${srcdir}/libxcrypt"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
