# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=lib32-libxcrypt
pkgver=4.4.17
pkgrel=1
pkgdesc='Modern library for one-way hashing of passwords (32-bit)'
arch=('x86_64')
url='https://github.com/besser82/libxcrypt/'
license=('GPL')
depends=('lib32-glibc' 'libxcrypt')
makedepends=('gcc-multilib')
source=($pkgname-$pkgver.tar.gz::$url/archive/v${pkgver}.tar.gz)
sha256sums=('7665168d0409574a03f7b484682e68334764c29c21ca5df438955a381384ca07')

prepare() {
  cd "${srcdir}/libxcrypt-${pkgver}"

  autoreconf -fi
}

build() {
  cd "${srcdir}/libxcrypt-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    --disable-static \
    --enable-hashes=strong,glibc \
    --enable-obsolete-api=no \
    --disable-failure-tokens
  make 
}

package() {
  cd "${srcdir}/libxcrypt-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
