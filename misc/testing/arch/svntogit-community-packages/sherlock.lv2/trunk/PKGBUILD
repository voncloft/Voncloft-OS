# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sherlock.lv2
pkgver=0.26.0
pkgrel=1
pkgdesc="An investigative plugin bundle"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/sherlock/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'libglvnd' 'libx11' 'lv2-host')
makedepends=('flex' 'glew' 'glu' 'lv2' 'meson' 'serd' 'sord' 'sratom')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/$pkgname/snapshot/$pkgname-$pkgver.tar.xz"{,.asc})
sha512sums=('2d9129c2b9186729c57137e3a6efc7cdf8bb466882a9a222a57a65478bd0f1c67a57c5336d11f64a7a8114674aaccb20b73f533ac73046b09d01448be5381114'
            'SKIP')
b2sums=('c2413a0e7c5629ffc23a3914ddf370f91225ec776433151a732883e33e62387a5a80fa8c950e7e2d3d9f1a55d6105ccac70813dcfd32e5972366f59d8e1e4355'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr \
        --libdir=lib \
        --libexecdir lib \
        --sbindir bin \
        --buildtype plain \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  _plugin_names=('atom_inspector' 'midi_inspector' 'osc_inspector')
  for _plugin in ${_plugin_names[@]}; do
    lv2lint -Mpack -I "build/" "http://open-music-kontrollers.ch/lv2/sherlock#${_plugin}"
  done
}

package() {
  depends+=('libGLEW.so' 'libserd-0.so' 'libsratom-0.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
