# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: sh0 <mee@sh0.org>
# Contributor: Maxwel <msg.maxwel@gmail.com>

pkgname=pax-utils
pkgver=1.2.5
pkgrel=1
pkgdesc='ELF utils that can check files for security relevant properties'
url='https://wiki.gentoo.org/wiki/Hardened/PaX_Utilities'
arch=('x86_64')
license=('GPL2')
depends=('bash' 'libcap.so' 'libseccomp.so' 'python' 'python-pyelftools')
makedepends=('libcap' 'libseccomp')
source=(https://dev.gentoo.org/~slyfox/distfiles/${pkgname}-${pkgver}.tar.xz)
sha512sums=('bf2f47393a66b4218eba7981100fc6936c7c9d6d98970f5337e7b3097ce6f7b2ca13de486ff7450e4395b74ba2308d37105508234f323230376ec5814cfe54f9')
b2sums=('348c6ab52a5e950f346115802d596fd33142fe2d93a83d686fbec917d3f74c53c332f2fcbd197c5bdd38b545f48568c7b461143fa328433ce3ee931901012601')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -E 's|(  paths =) \[\]|\1 ["/usr/lib"]|g' -i lddtree.py
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --with-caps \
    --with-seccomp \
    --with-python
  make
}

check() {
  cd ${pkgname}-${pkgver}
  LD_LIBRARY_PATH=/usr/lib make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
