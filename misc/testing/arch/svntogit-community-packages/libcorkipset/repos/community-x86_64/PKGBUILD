# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libcorkipset
pkgver=1.1.1+20150311
_debver=7
pkgrel=3
pkgdesc="A small C helper library for storing sets of IPv4 and IPv6 addresses"
url="https://github.com/rogers0/libcorkipset"
arch=('x86_64')
license=('BSD')
depends=('libcork')
makedepends=('cmake' 'check' 'git')
checkdepends=('python')
source=("git+https://github.com/rogers0/libcorkipset#tag=debian/$pkgver-$_debver")
sha512sums=('SKIP')

prepare() {
  mkdir build

  cd libcorkipset
  for p in debian/patches/*.patch; do
    patch -p1 -i "$p"
  done

  sed -e 's%#include <ipset%#include <libcorkipset%' \
      -e 's%#include "ipset%#include "libcorkipset%' \
      -i include/ipset/*.h */*/*/*.c */*/*/*.c.in */*/*.c */*.c
  mv include/{,libcork}ipset
}

build() {
  cd build
  cmake ../libcorkipset -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install

  install -Dm644 "$srcdir"/$pkgname/LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
