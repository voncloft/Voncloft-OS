# Maintainer: Konstantin Gizdov <arch at kge dot pw>
_pkgname='csexp'
pkgname="ocaml-${_pkgname}"
pkgver=1.3.2
pkgrel=1
pkgdesc="OCaml canonical S-expressions"
arch=(x86_64)
url="https://github.com/ocaml-dune/csexp"
license=('MIT')
makedepends=(dune)
depends=(glibc "ocaml>=4.11.0" ocaml-findlib ocaml-result)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('61b239a0283792c218b8b71582255c5c8ffc882195245e292077064fc9b54ec4402887fc26b74413fca670fb7c01dc42ca998aa2ed40982798b580e6f3b747ee')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    dune build -p csexp
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    DESTDIR="${pkgdir}" dune install --prefix "/usr" --libdir "lib/ocaml"

    install -dm755 "${pkgdir}/usr/share/"
    mv "${pkgdir}/usr/doc" "${pkgdir}/usr/share/"

    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -s /usr/share/doc/csexp/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
