# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=data-accessor-template
pkgname=haskell-data-accessor-template
pkgver=0.2.1.16
pkgrel=16
pkgdesc="Automate generation of Accessor's of the data-accessor package by Template Haskell functions."
url="https://www.haskell.org/haskellwiki/Record_access"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' "haskell-data-accessor" "haskell-utility-ht")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('428b4bc9703f63e5457654c500aefb5c257ec030c0554fc31bd5b629bd281d7e4adda320e3bf22bca4a32b440d6eb3697503eafd92532791580a89cf1c196696')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *2.15/<3/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
