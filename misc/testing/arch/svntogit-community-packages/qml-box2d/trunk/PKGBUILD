# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=qml-box2d
pkgver=20160102
_commit=1b37be7d9dfb44ec6d520595a4e4f45f63717822
pkgrel=3
pkgdesc="Box2D QML plugin"
url="https://github.com/qml-box2d/qml-box2d"
arch=('x86_64')
license=('BSD')
depends=('qt5-declarative')
makedepends=('git')
source=("git+https://github.com/qml-box2d/qml-box2d.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git log -1 --date=format:%Y%m%d --pretty=format:%cd
}

build() {
  cd $pkgname
  qmake
  make
}

package() {
  cd $pkgname
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
