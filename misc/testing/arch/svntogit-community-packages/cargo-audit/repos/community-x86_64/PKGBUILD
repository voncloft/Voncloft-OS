# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-audit
pkgver=0.12.0
pkgrel=1
pkgdesc="Audit Cargo.lock for crates with security vulnerabilities"
url="https://github.com/RustSec/cargo-audit"
depends=('cargo')
arch=('x86_64')
license=('MIT' 'APACHE')
source=("https://github.com/RustSec/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('5c30e38d592dbc135123d3ca69fd1c778782ef9a9064d8b7cfbd93db5d636d75')
b2sums=('f52bc30c3be63468377528808c0d66b47c9a3e10dcee767277cdd3b876da893b959ff6977987761e93bfc3b4d39e930fb3a8e19a7160c62b4f45d439f7019820')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
