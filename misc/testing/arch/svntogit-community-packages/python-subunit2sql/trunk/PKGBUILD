# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-subunit2sql
pkgver=1.10.0
pkgrel=8
pkgdesc="Command to Read a subunit file or stream and put the data in a SQL DB"
arch=('any')
url="https://pypi.python.org/pypi/subunit2sql"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-alembic' 'python-stevedore' 'python-sqlalchemy'
         'python-oslo-config' 'python-dateutil' 'python-subunit' 'python-oslo-db')
makedepends=('python-setuptools')
checkdepends=('python-stestr' 'python-mock' 'python-testresources' 'python-testscenarios'
              'python-oslo-concurrency' 'pifpaf' 'percona-server' 'mariadb-clients' 'postgresql')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack-infra/subunit2sql/archive/$pkgver.tar.gz"
        fix-migrations-test.patch)
sha512sums=('8efe7f53606a4927f1bb97a9329e1d25c16baad32a64ea7df59cc4958af9111c5227e29c205f5bbf8d5fbd1babc0a6242fd067e00bfa1e06e32aeca17c065363'
            '0891880f0ff329a4236a73f88a90ffc4522c123b52e08675609816649934b20fc7ccbd9d5141341bc7a96c6acd32e40b227e61634fbd803e8b7c659e42ea0668')

export PBR_VERSION=$pkgver

prepare() {
  patch -Np1 -d subunit2sql-$pkgver <fix-migrations-test.patch
  touch subunit2sql-$pkgver/subunit2sql/migrations/versions/__init__.py
}

build() {
  cd subunit2sql-$pkgver
  python setup.py build
}

check() {
  cd subunit2sql-$pkgver
  pifpaf run mysql pifpaf run postgresql stestr run || echo "https://storyboard.openstack.org/#!/story/2008348"
}

package() {
  cd subunit2sql-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
