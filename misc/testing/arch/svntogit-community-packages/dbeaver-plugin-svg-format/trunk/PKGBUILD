# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-svg-format
pkgver=1.0.83.202012201616
pkgrel=1
pkgdesc='DBeaver plugin to save diagrams in SVG format'
arch=('any')
url='https://dbeaver.io/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver>=4.2.5' 'dbeaver-plugin-batik')
source=("https://dbeaver.io/update/ce/latest/plugins/org.jkiss.dbeaver.ext.ui.svg_${pkgver}.jar"
        "https://dbeaver.io/update/ce/latest/features/org.jkiss.dbeaver.ext.ui.svg.feature_${pkgver}.jar"
        "${pkgname}.info")
sha256sums=('1d0cf30630cae9c98fb5c68d505f6dbe7cd702e4499564ce90ffab7e4defa386'
            '6e229f67cc32c3ce27b572d43658c5a97cf4c9d2c1b249e017368422e50803d2'
            'c76a26329e151ef782b37930f884eee3e8e95a650d4b24f5593be1d6c17bc6b8')
noextract=("org.jkiss.dbeaver.ext.ui.svg_${pkgver}.jar"
           "ui.svg.feature_${pkgver}.jar")

package() {
  # Install plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins" \
    "org.jkiss.dbeaver.ext.ui.svg_${pkgver}.jar"

  # Extract feature
  install -m 755 -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.ui.svg.feature_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.dbeaver.ext.ui.svg.feature_${pkgver}.jar" \
        -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.ui.svg.feature_${pkgver}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
