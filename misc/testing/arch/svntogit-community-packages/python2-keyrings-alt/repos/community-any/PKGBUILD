# $Id$
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python2-keyrings-alt
_pkgname=keyrings.alt
pkgver=3.1
pkgrel=1
pkgdesc="Alternate keyring backend implementations for use with the keyring package."
arch=('any')
url="https://github.com/jaraco/keyrings.alt"
license=('MIT')
depends=('python2-six')
makedepends=('python2-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://pypi.io/packages/source/${_pkgname:0:1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha256sums=('b59c86b67b9027a86e841a49efc41025bcc3b1b0308629617b66b7011e52db5a')

build() {
  cd $_pkgname-$pkgver

  python2 setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  # Install license
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

