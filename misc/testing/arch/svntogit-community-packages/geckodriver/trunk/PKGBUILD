# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: kevku <kevku@gmx.com>

firefoxver=72.0.1
pkgname=geckodriver
pkgver=0.26.0
pkgrel=1
pkgdesc="Proxy for using W3C WebDriver-compatible clients to interact with Gecko-based browsers."
arch=("x86_64")
url="https://github.com/mozilla/geckodriver"
license=("MPL")
makedepends=("rust" "cargo" "gcc-libs")
source=(https://archive.mozilla.org/pub/firefox/releases/$firefoxver/source/firefox-$firefoxver.source.tar.xz{,.asc})
sha256sums=('1fa59aedc8469c3e6ffb12449ab7de2f93776f7679eedebfb74aa309b694956f'
            'SKIP')
validpgpkeys=('14F26682D0916CDD81E37B6D61B7B526D98F0353') # Mozilla Software Releases <release@mozilla.com>

build() {
  cd firefox-$firefoxver
  cargo build --bin geckodriver --release --locked
}

package() {
  cd firefox-$firefoxver
  install -Dm755 target/release/$pkgname "$pkgdir"/usr/bin/$pkgname
  ln -sf /usr/bin/$pkgname "$pkgdir"/usr/bin/wires
}
