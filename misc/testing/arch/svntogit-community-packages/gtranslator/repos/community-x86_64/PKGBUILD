# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Christer Solskogen (solskogen@carebears.mine.nu)

pkgname=gtranslator
pkgver=3.38.0
pkgrel=1
pkgdesc='An enhanced gettext po file editor for the GNOME desktop environment'
arch=('x86_64')
url='https://projects.gnome.org/gtranslator/'
license=('GPL3')
depends=('gtksourceview4' 'gspell' 'gettext' 'libgda' 'iso-codes' 'hicolor-icon-theme' 'libdazzle')
makedepends=('meson' 'gobject-introspection' 'gtk-doc' 'yelp-tools' 'patch')
source=(https://ftp.acc.umu.se/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        gtk-doc-build-fix.patch::https://gitlab.gnome.org/GNOME/$pkgname/-/merge_requests/70/diffs.patch)
sha256sums=('dbcda9b81a22d9233be18e99fd5c448f6ab05759d1e94c10580bb831ca2d7635'
            '841194ff549d8bb1fc4b1b85056c64313102d2ff042d3ccdd08101a90ac19839')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
  patch -Np1 -i "${srcdir}"/gtk-doc-build-fix.patch
}

build() {
  arch-meson $pkgname-$pkgver build -Dgtk_doc=true
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
