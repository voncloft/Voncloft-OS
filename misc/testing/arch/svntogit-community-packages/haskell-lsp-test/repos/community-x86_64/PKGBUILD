# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=lsp-test
pkgname=haskell-lsp-test
pkgver=0.11.0.7
pkgrel=86
pkgdesc="Functional test framework for LSP servers"
url="https://github.com/bubba/lsp-test#readme"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-diff' 'haskell-glob' 'haskell-aeson' 'haskell-aeson-pretty'
         'haskell-ansi-terminal' 'haskell-async' 'haskell-conduit' 'haskell-conduit-parse'
         'haskell-data-default' 'haskell-lsp' 'haskell-lens' 'haskell-parser-combinators'
         'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-hspec')
# Hackage source is missing test data
source=("https://github.com/bubba/lsp-test/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('bc42e9d5a31a81501b324f5c5c91d00512f85e6a1c867ca17f619565c732dacdd7a058b265819f6f045b84534450e785d88d038a91304ab9310680e46e0bf0ea')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
  uusi -u haskell-lsp $_hkgname.cabal

  sed -i 's|dist-newstyle|dist/build/dummy-server|' test/Test.hs

  # haskell-lsp 0.24.0.0
  sed -i 's/"deleteThis" Nothing))/"deleteThis" Nothing)) Nothing Nothing/' test/dummy-server/Main.hs
}

build() {
  cd $_hkgname-$pkgver    

  # We need to enable dummy-server here for tests, but it shouldn't be installed...
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -fdummyserver

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE

  # Remove dummy-server
  rm -r "$pkgdir"/usr/libexec
}
