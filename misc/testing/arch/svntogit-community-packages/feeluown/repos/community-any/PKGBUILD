# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown
pkgver=3.7.3
pkgrel=1
pkgdesc="FeelUOwn Music Player"
arch=('any')
url="https://github.com/feeluown/FeelUOwn"
license=('GPL3')
depends=('python-qasync' 'python-pyqt5' 'mpv' 'python-opengl' 'python-janus' 'python-requests'
         'python-tomlkit' 'python-pydantic')
makedepends=('python-setuptools' 'python-pip')
optdepends=('feeluown-local' 'feeluown-netease' 'feeluown-kuwo' 'feeluown-xiami' 'feeluown-qqmusic')
groups=('feeluown-full')
source=("https://pypi.io/packages/source/f/feeluown/feeluown-$pkgver.tar.gz"
        "feeluown.desktop")
sha512sums=('847bdc3dfc589ca758f75344cdd5f2e9f0b7b7acab9a115232ed63a89aad19a61765dc4e0cc787eae4dc7b32f355e828a232e4893dce1751524539c328df26ab'
            '48882f7469c22e5db332663bc1aa8b398b0a10a0c929d4d7e3d7b8b91205d7d3070c5fa295cb9a14f8c352bff57978bfaad167343e0ddc51c92417eda07c8087')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm644 feeluown/icons/feeluown.png -t "$pkgdir"/usr/share/icons/hicolor/512x512/apps/
  install -Dm644 "$srcdir"/feeluown.desktop -t "$pkgdir"/usr/share/applications/
}
