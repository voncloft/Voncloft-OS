# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=auditwheel
pkgname=python-$_pkgname
pkgver=3.3.1
pkgrel=1
pkgdesc='Auditing and relabeling cross-distribution Linux wheels'
arch=('any')
url='https://github.com/pypa/auditwheel'
license=('MIT' 'BSD')
depends=('python' 'python-wheel' 'python-pyelftools' 'unzip' 'patchelf')
makedepends=('python-setuptools' 'python-pbr')
#checkdepends=('python-tox-current-env' 'python-pip' 'python-pytest' 'python-jsonschema' 'python-pretend' 'python-docker')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('213063e3fa846b53f802499e71f30a53410e18a8e6a787019f07f79a9fd9687f436110c5765407f6a2ac40e4532f292c8c334264436a22322e90939f77e5231a')

export PBR_VERSION=$pkgver

prepare() {
  cd $_pkgname-$pkgver
#  find $_pkgname-$pkgver/tests -type f -exec sed -i 's|subprocess.run(\["auditwheel"|subprocess.run(\["python", "-m", "auditwheel"|' '{}' \+

  # replace the vendored wheel module with the one on the system
  find -type f -name '*.py' -exec \
      sed -i 's|\._vendor\.wheel|wheel|' '{}' \+

  # remove vendored wheel module
  rm -rf auditwheel/_vendor
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

#check() {
#  cd $_pkgname-$pkgver
#
#  tox -e py39 --current-env
#}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm 644 auditwheel/policy/policy.json "$pkgdir"/usr/lib/python3.9/site-packages/auditwheel/policy/policy.json

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
