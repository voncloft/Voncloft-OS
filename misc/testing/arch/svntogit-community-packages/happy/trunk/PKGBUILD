# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=happy
pkgver=1.20.0
pkgrel=2
pkgdesc="The Parser Generator for Haskell"
url="https://hackage.haskell.org/package/happy"
arch=('x86_64')
license=("custom:BSD2")
depends=('ghc-libs')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('2c19e6e13032ead435495aab232192cf0ad9fe5e3e04c79b40b586272fb0208284815cf5c15d609f75387e7fe91ca0932b675bcaa95552f62ad08a12701649ce')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --ghc-option='-dynload=deploy' --prefix=/usr --datasubdir="$pkgname"
  runhaskell Setup build $MAKEFLAGS
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  runhaskell Setup copy --destdir="${pkgdir}"
  rm -r "$pkgdir/usr/share/doc"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
