# Maintainer: Roman Kyrylych <Roman.Kyrylych@gmail.com>

pkgname=gdmap
pkgver=0.8.1
pkgrel=6
pkgdesc="Tool to inspect the used space of folders."
arch=('x86_64')
url="http://gdmap.sourceforge.net"
license=('GPL')
depends=('gtk2' 'glib2' 'libxml2')
makedepends=('gettext' 'intltool')
source=(https://downloads.sourceforge.net/sourceforge/gdmap/$pkgname-$pkgver.tar.gz \
	gtk-fix.patch)
sha512sums=('3aaf4da393aaafc350703962c05bdc2ecd8f244a9a1d614011d2073dbbd3c6bd01b77e3794068f5fc0ce2e7adb39c75a8856f2df070c7bbb685c03b976df0cf5'
            'e36f6e0affe1bc6268a1902fe3c465e086cc76f51b76814a350df3a3965c261df8931ba9f30edc8b0eda8125256c4b2f7c5278aff241aeaf844f3f09a34e6664')

build() {
  cd ${srcdir}/gdmap-$pkgver

  cd src
  # fix deprecated gtk macros call
  patch -Np0 -i ../../gtk-fix.patch
  # fix linking
  sed -i -e 's/@LIBXML_LIBS@/@LIBXML_LIBS@ -lm/g' Makefile.in
  cd ..

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/gdmap-$pkgver

  make DESTDIR=${pkgdir} install
}
