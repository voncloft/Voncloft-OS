# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=kxstudio-lv2-extensions
pkgver=2020.08.08
pkgrel=1
pkgdesc="Custom LV2 Extensions used in several KXStudio projects"
arch=('any')
url="https://github.com/KXStudio/LV2-Extensions"
license=('ISC')
makedepends=('git')
source=("${pkgname}::git+https://github.com/KXStudio/LV2-Extensions#tag=${pkgver//./-}?signed")
md5sums=('SKIP')
validpgpkeys=('62B11043D2F6EB6672D93103CDBAA37ABC74FBA0') # falkTX <falktx@falktx.com>

package() {
  local _names=('kx-external-ui.lv2' 'kx-meta' 'kx-programs.lv2' 'kx-rtmempool.lv2')
  local _name
  cd "$pkgname"
  for _name in "${_names[@]}"; do
    install -vDm 644 "${_name}/"*.* -t "${pkgdir}/usr/lib/lv2/${_name}/"
  done
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
