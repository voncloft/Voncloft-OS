# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-jaraco
pkgver=2020.12.29
pkgrel=1
pkgdesc='A gaggle of idiosyncratic and questionable wrappers for the stdlib.'
arch=('any')
url='https://github.com/jaraco?page=1&tab=repositories&utf8=%E2%9C%93&q=jaraco'
license=('MIT')
depends=('python-six' 'python-pytz' 'python-more-itertools')
makedepends=('python-setuptools-scm')
provides=('python-jaraco-text'
          'python-jaraco-functools'
          'python-jaraco-collections'
          'python-jaraco-classes'
          'python-jaraco-stream'
          'python-jaraco-logging'
          'python-jaraco-itertools')

# These should not exist and do not deserve so many packages.
# Furthermore, try to make a replacement for python-irc so we can nuke it all.
# And now cherrypy depends on this too :-(
_text=3.4.0
_functools=3.1.0
_collections=3.1.0
_classes=3.1.0
_stream=3.0.0
_logging=3.1.0
_itertools=5.0.0
_pyhost="https://files.pythonhosted.org/packages/source"

source=("$_pyhost/j/jaraco.text/jaraco.text-$_text.tar.gz"
        "$_pyhost/j/jaraco.functools/jaraco.functools-$_functools.tar.gz"
        "$_pyhost/j/jaraco.collections/jaraco.collections-$_collections.tar.gz"
        "$_pyhost/j/jaraco.classes/jaraco.classes-$_classes.tar.gz"
        "$_pyhost/j/jaraco.stream/jaraco.stream-$_stream.tar.gz"
        "$_pyhost/j/jaraco.logging/jaraco.logging-$_logging.tar.gz"
        "$_pyhost/j/jaraco.itertools/jaraco.itertools-$_itertools.tar.gz")
sha512sums=('074b963c9ccf020409a28d7ef0053d5b221f9a3b16003ca9eac663f70b1a909b4523f9e5aba606f77108723157f237602f7c9f5079a782c8bfa9fdbbb553cf28'
            '81a56d3325106e65889f90137d2f3ab57198e2b5905dbe067a056057dee98ca8a715fc46384f9ab15a081d5d7fd309d69ffb77be9c2b24a9a8eda817fbc0b1e3'
            '9a3e4e60cd497ee2ed4a092bd823ecffe0d20293dd9ba491fd2c4e5df2481042efa8ba52503b001e11908982608852a0588cd70b4957d0551c066be97389652c'
            '697d892a01c4e8ad83f57e69dcd744f976cea67fa3efcd55b10eda02f6bf1916162c0fa952d0d711e62f62fdff62740ee55a93be9fc46ebd4ab22416092f0ff6'
            'f2ce7e65d9a3bcd81985f45d03f7cd56b2f2939f13a423b8b65714f19eb249493d89cad774460dd08c1345124d3d10f85ce6875b5f7049705f993c0a3146e5d9'
            'ce9a9cbad6d6bb448615eac986d361f9c6ff477b1209e85433f12a49055a9e30125cd49bebcf915a08b8efe0732439a8e1d336e63f6454369b3e846a9a1383ba'
            '38ae2cbdd436b277e3d3c252fadeb5239e2d32b2291686b2a3dbaca69328392eb1db4318b2144954b17302e5b15b94ad89f1ea8a1b2e354deec7d4516bd7ec85')

prepare() {
  # remove inflect dep
  cd "$srcdir/jaraco.itertools-$_itertools"
  sed -i "s/'inflect',//" setup.py
  sed -i "s/inflect//"    jaraco.itertools.egg-info/requires.txt
  sed -i "s/inflect//"    setup.cfg
  sed -i 's/import inflect//' jaraco/itertools.py
  sed -i 's/ord = .*$/ord = str(self.sliceArgs[2])+"th"/' jaraco/itertools.py
}

package() {
  cd "$srcdir/jaraco.text-$_text"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.functools-$_functools"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.collections-$_collections"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.classes-$_classes"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.stream-$_stream"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.logging-$_logging"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.itertools-$_itertools"
  python3 setup.py install --root="$pkgdir/" --optimize=0

  #rm "$pkgdir/usr/bin/calc-prorate"
  #rmdir "$pkgdir/usr/bin"
  # None of these packages have a license file?  Fix later.
  #install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
