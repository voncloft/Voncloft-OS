# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=5.3.99.20210113
_commit=e478c8ac3213c6d9659a1ec27d066e323a69b96c
pkgrel=1
pkgdesc='New control center for linux deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'dtkwidget' 'deepin-account-faces' 'deepin-api' 'geoip' 'libpwquality'
         'geoip-database' 'geoip-database-extra' 'deepin-daemon' 'startdde' 'networkmanager-qt'
         'deepin-qt-dbus-factory' 'deepin-qt5integration' 'deepin-network-utils')
makedepends=('cmake' 'deepin-dock' 'gtest' 'ninja' 'qt5-tools')
optdepends=('redshift: automatic color temperature support'
            'networkmanager-openconnect: for OpenConnect support'
            'networkmanager-openvpn: for OpenVPN support'
            'networkmanager-pptp: for PPTP support'
            'networkmanager-strongswan: for StrongSwan support'
            'networkmanager-vpnc: for VPNC support'
            'network-manager-sstp: for SSTP support')
# Not packaged: network-manager-l2tp
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("https://github.com/linuxdeepin/dde-control-center/archive/$_commit/$pkgname-$_commit.tar.gz"
        $pkgname-systeminfo-deepin-icon.patch
        $pkgname-no-user-experience.patch)
sha512sums=('be23a36873c1971c55f7f3c093068d4d2883408c71e45b60ad3013e0293a9d4caf3e2391f5406058945bda800b7600330c74ab356d5b4a2f506fe0e030af4217'
            '74fd63391e923ca37f4559f30da967ba7f33d4426b60d58d1ece8cd9a154578e8184b1a376a8d7ff3ef81ffce530915f79d0845a2612ae4e06522b96855ab3dd'
            '6828c137af055c564df55e043c6a827a843c834906e2a66a59f9b2c8da80ca348ad98e24d2142100a198cd7fdb363a71e150e9392e93c09728c3d3ffb474748f')

prepare() {
  rm dde-control-center-$_commit/src/frame/window/icons/icons/dcc_nav_systeminfo_{42,84}px.svg
  patch -d dde-control-center-$_commit -Np1 < $pkgname-systeminfo-deepin-icon.patch
  patch -d dde-control-center-$_commit -Np1 < $pkgname-no-user-experience.patch
  mkdir -p build

  cd dde-control-center-$_commit
  # remove after they obey -DDISABLE_SYS_UPDATE properly
  sed -i '/new UpdateModule/d' src/frame/window/mainwindow.cpp
}

build() {
  cd build
  cmake -GNinja -DDISABLE_SYS_UPDATE=YES -DDISABLE_RECOVERY=YES -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    ../dde-control-center-$_commit
  ninja
}

check() {
  cd build
  ninja test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
