# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-bouncycastle
pkgver=1.64.0.v20191109.0815
pkgrel=2
pkgdesc='Bouncy Castle library for DBeaver plugin for SSHv2 tunnels'
arch=('any')
url='https://dbeaver.io/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver>=4.2.5')
source=("http://download.eclipse.org/releases/2020-03/202003181000/plugins/org.bouncycastle.bcpg_${pkgver%.*}-${pkgver##*.}.jar"
        "http://download.eclipse.org/releases/2020-03/202003181000/plugins/org.bouncycastle.bcpkix_${pkgver%.*}-${pkgver##*.}.jar"
        "http://download.eclipse.org/releases/2020-03/202003181000/plugins/org.bouncycastle.bcprov_${pkgver%.*}-${pkgver##*.}.jar"
        "${pkgname}.info")
sha256sums=('8341346edd575832349e9eb63b3a5e5ff9d34fdd1ba9ae109507d893d4a525c8'
            '288473159284efc0b24d65b61f20b77ec72c59b9ea08afed338d44f7923d0ea4'
            'dc68a98748bf7fb0e6268fdebf0eba01e20c0e01340878cbef6722abd52c290b'
            '571e6aab0c8e9789fc1f8ddc4222eb67b0b6d74dcce30299a08b7240b7c7b2de')
noextract=("org.bouncycastle.bcpg_${pkgver%.*}-${pkgver##*.}.jar"
           "org.bouncycastle.bcpkix_${pkgver%.*}-${pkgver##*.}.jar"
           "org.bouncycastle.bcprov_${pkgver}.jar")

package() {
  # Copy bundles
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins/" \
    org.bouncycastle.bcpg_${pkgver%.*}-${pkgver##*.}.jar \
    org.bouncycastle.bcpkix_${pkgver%.*}-${pkgver##*.}.jar \
    org.bouncycastle.bcprov_${pkgver%.*}-${pkgver##*.}.jar
  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
