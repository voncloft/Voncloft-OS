# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: astaroth <astaroth_@web.de>

pkgname=jad
pkgver=1.5.8e
pkgrel=8
pkgdesc='Java decompiler'
license=('custom')
arch=('x86_64')
url='https://varaneckas.com/jad'
optdepends=('vim-jad: open class files directly in vim and see the decompiled source code')
source=("https://varaneckas.com/$pkgname/${pkgname}${pkgver//./}.linux.static.zip"
        'l.html::https://varaneckas.com/jad')
sha256sums=('2878e19fc1fdd725b516f538a57b02aaec1b2d1e4b106d550230381ffa9c0c81'
            'f6453f88f287d7f3ad48ab5ad8afcd0c3d53b01fdbc439401040b7a6305bbd5c')

prepare() {
  grep '<blockquote>' -A18 l.html | cut -d'>' -f2 | cut -d'<' -f1 > LICENSE
}

package() {
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 Readme.txt "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
