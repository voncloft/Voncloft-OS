# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Florian Walch <florian+aur@fwalch.com>

pkgname=libvterm
_git=65dbda
pkgver=0.1.3.git1.${_git}
pkgrel=1
pkgdesc='Abstract library implementation of a VT220/xterm/ECMA-48 terminal emulator'
arch=('x86_64')
url='https://github.com/neovim/libvterm'
license=('MIT')
depends=('glibc')
makedepends=('git')
source=("git+https://github.com/neovim/libvterm.git#commit=${_git}")
sha256sums=('SKIP')

build() {
  cd "${pkgname}"
  make PREFIX=/usr
}

package() {
  cd "${pkgname}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
