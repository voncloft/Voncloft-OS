# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Xentec <xentec at aix0 dot eu>

pkgname=glbinding
pkgver=3.1.0
pkgrel=3
pkgdesc="A C++ binding for the OpenGL API, generated using the gl.xml specification"
arch=('x86_64')
url="http://www.glbinding.org"
license=('MIT')
depends=('libgl' 'glfw')
makedepends=('cmake' 'mesa')
checkdepends=('python')
source=($pkgname-$pkgver.tar.gz::"https://github.com/cginternals/glbinding/archive/v$pkgver.tar.gz")
sha256sums=('6729b260787108462ec6d8954f32a3f11f959ada7eebf1a2a33173b68762849e')

prepare() {
  cd $pkgname-$pkgver

  mkdir build
}

build() {
  cd $pkgname-$pkgver/build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DOPTION_BUILD_TESTS=ON
  make
}

check() {
  cd $pkgname-$pkgver/build

  make test
}

package() {
  cd $pkgname-$pkgver

  make -C build DESTDIR="$pkgdir/" install

  mv "$pkgdir"/usr/bin/glinfo "$pkgdir"/usr/bin/glinfo-glbinding
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -d "$pkgdir"/usr/share/doc/$pkgname
  install -m644 README.md AUTHORS "$pkgdir"/usr/share/doc/$pkgname
  rm "$pkgdir"/usr/include/KHR/khrplatform.h
}
