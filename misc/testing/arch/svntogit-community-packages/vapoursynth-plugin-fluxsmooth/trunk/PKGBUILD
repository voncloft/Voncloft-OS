# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vapoursynth-plugin-fluxsmooth
pkgver=2
pkgrel=3
pkgdesc='FluxSmooth plugin for VapourSynth'
arch=('x86_64')
url='https://github.com/dubhater/vapoursynth-fluxsmooth'
license=('None')
depends=('vapoursynth')
[[ $CARCH == 'x86_64' ]] && makedepends=('yasm')
source=("vapoursynth-plugin-fluxsmooth-${pkgver}.tar.gz::https://github.com/dubhater/vapoursynth-fluxsmooth/archive/v${pkgver}.tar.gz")
sha256sums=('975bdf16ec52c9d3ebfd487db10799c0ccb8f8c5e0e70d48df0f8067c5f24fcb')

build() {
  cd vapoursynth-fluxsmooth-${pkgver}

  export LDFLAGS="$LDFLAGS,-z,noexecstack"

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib/vapoursynth'
  make
}

package(){
  cd vapoursynth-fluxsmooth-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -dm 755 "${pkgdir}"/usr/share/doc/vapoursynth/plugins/FluxSmooth
  install -m 644 readme.rst "${pkgdir}"/usr/share/doc/vapoursynth/plugins/FluxSmooth/README
}

#vim: ts=2 sw=2 et:
