# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-settings-daemon
pkgver=3.0.3
pkgrel=1
pkgdesc="Daemon handling the UKUI session settings"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-settings-daemon"
groups=('ukui')
depends=('geoclue' 'gnome-desktop' 'gsettings-qt' 'gtk3' 'kconfig' 'imlib2' 'libmatekbd'
         'libmatemixer' 'libnotify' 'libxklavier' 'mate-desktop' 'qt5-sensors' 'qt5-svg' 'qt5-x11extras')
makedepends=('qt5-tools' 'xorg-server-devel')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-settings-daemon/archive/v$pkgver.tar.gz")
sha512sums=('42e543492ed1230077d330cd2badf221ddc1ac6d80a7f69dc4073a743f50ac79cfe11fe3967f49453fdfb953d9698ebe149510d2b58a2c3d326746f4745747a1')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|udev.path = /lib/udev/rules.d/|udev.path = /usr/lib/udev/rules.d/|' plugins/mouse/mouse.pro
}

build() {
  cd $pkgname-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
