# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=vm.lv2
pkgver=0.12.0
pkgrel=1
pkgdesc="A virtual machine LV2 plugin bundle"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/vm/#"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'libglvnd' 'libx11' 'lv2-host')
makedepends=('glew' 'glu' 'lv2' 'meson')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2//$pkgname/snapshot/$pkgname-$pkgver.tar.xz"{,.asc})
sha512sums=('21398ca6cdf266beb1eeccf70f401fed5c2b6472a5d64d962b3930e8b9d17b7c830e3a0a40a015adbd1b14a319f5bdd5a9ae966dd0ca36f37222ebd42c89bf6a'
            'SKIP')
b2sums=('529516d02f21bdf9789dc5a8de1c979c3d6fe1b99163361afa875f43e085f303bd3f190ce7111c1536af3d71a654f33130952089a8b22250757ff57824a6b480'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr \
        --libdir=lib \
        --libexecdir lib \
        --sbindir bin \
        --buildtype plain \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  _plugin_names=('atom' 'audio' 'control' 'cv' 'midi')
  for _plugin in ${_plugin_names[@]}; do
    lv2lint -Mpack -I "build/" "http://open-music-kontrollers.ch/lv2/vm#${_plugin}"
  done
}

package() {
  depends+=('libGLEW.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
