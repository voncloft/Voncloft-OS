# Maintainer: David Runge <dvzrv@archlinux.org>

_name=calmjs.parse
pkgname=python-calmjs.parse
pkgver=1.2.5
pkgrel=5
pkgdesc="Various parsers for ECMA standards"
arch=('any')
url="https://github.com/calmjs/calmjs.parse"
license=('MIT')
depends=('python-ply=3.11')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.zip")
sha512sums=('3614c1f495fa1ca746a8dd0813629da14115f4ef401176e389515239700849770b7c408d2ebdf55c8286dbde0b8f1b711f7ec4e4f3858f0477b39455a50bbe5a')
b2sums=('9e3f81a99cf346b4ff61fa8f97bc3fa4c667d7dece38c47c512931508d5592b82143b0507e8e5190f2dd07f3596b6d146a90e59bad1a1c44d8f398a51ff0fb48')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # generate symbol tables required for lex and yacc
  # this requires version pinning towards python-ply!
  PYTHONPATH="src:${PYTHONPATH}" \
  python -m calmjs.parse.parsers.optimize
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  PYTHONDONTWRITEBYTECODE=1 pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 {CHANGES,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
