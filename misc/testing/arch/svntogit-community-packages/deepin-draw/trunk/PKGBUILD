# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-draw
pkgver=5.8.0.20
pkgrel=2
pkgdesc="A lightweight drawing tool for Linux Deepin"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-draw"
license=('GPL3')
depends=('deepin-picker' 'deepin-qt5integration' 'freeimage' 'libexif' 'qt5-imageformats')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-draw/archive/$pkgver.tar.gz"
         deepin-draw-dwidget-include.patch)
sha512sums=('60b643b8b699eec01b6cf3fb09f68c3d4107e503f4a88dfa9ad080d4d00f80e72f20253ea36dd5f5f20e74d9258c7aecb1e92ae111e816b96e37789d169095a8'
            'a512608d7812b4389f788c810a8f7aa1ecd7eaca448664766b54eca9002af5fba1683694f9a8225ab0013b4925d1b3c062f14728187e4a3d3d243b314934104e')

prepare() {
  cd deepin-draw-$pkgver
  patch -p1 -i ../deepin-draw-dwidget-include.patch # Fix missing includes
  sed -i '/include <DGraphicsView>/i #include <QFileDevice>' frame/cgraphicsview.h
  sed -i '/include <QPainter>/a #include <QMouseEvent>' widgets/ciconbutton.cpp

}

build() {
  cd deepin-draw-$pkgver

  qmake-qt5
  make
}

package() {
  cd deepin-draw-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
