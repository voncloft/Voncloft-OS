# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Nelson Menon <nelsonmenon@gmail.com insanatorium.wordpress.com>

pkgname=freecol
pkgver=0.11.6
pkgrel=4
pkgdesc="A turn-based strategy game based on Colonization"
arch=('any')
url="http://www.freecol.org/"
license=('GPL')
depends=('java-runtime>=8')
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.zip
        fc.desktop
        fc.sh)
sha512sums=('956ef37f80fcb911c2b6770d12c143599cfe516d6c7583912a834120fe41dbc2e7f0ca75213b4807c78ce8b8591d46edd07a9356682d47545f1fac65b40e41b6'
            '9abd209dfbfdf87c5d90145493f021df4c5b80cf30f6b224116242adf778518907c149a3bb3e1288e8afb0447261f348cc228bdc6eca525ea6e55203b911c453'
            'dc98d4e527eaee0b0b17f3d0dccd5268d9eab32c1e0044ffcaf40337771efcee4c99f6b056374e761ce7a9aeaae968ea888d90ed2d5a1e7be0a1162fdecbb9b7')

build() {
  cd "$srcdir/$pkgname"
}

package() {
  cd "$srcdir/$pkgname"

  install -Dm644 FreeCol.jar "$pkgdir/usr/share/java/$pkgname/FreeCol.jar"
  install -Dm644 FreeCol.pdf "$pkgdir/usr/share/doc/$pkgname/FreeCol.pdf"
  install -Dm644 $pkgname.xpm "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
  install -Dm644 ../fc.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm755 ../fc.sh "$pkgdir/usr/bin/$pkgname"

  cp -r data jars "$pkgdir/usr/share/java/$pkgname/"
  find "$pkgdir/usr/share/java/$pkgname" -type d -exec chmod 755 '{}' \;
  find "$pkgdir/usr/share/java/$pkgname" -type f -exec chmod 644 '{}' \;
}
