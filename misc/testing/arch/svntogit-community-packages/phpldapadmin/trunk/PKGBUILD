# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=phpldapadmin
pkgver=1.2.6.2
pkgrel=2
pkgdesc="A PHP and hence web-based tool to administrate LDAP over the WWW"
arch=('any')
url="https://github.com/leenooks/phpLDAPadmin"
license=('GPL')
depends=('libldap' 'php7')
makedepends=('optipng')
backup=("etc/webapps/phpldapadmin/config.php")
source=($pkgname-$pkgver.tar.gz::https://github.com/leenooks/phpLDAPadmin/archive/$pkgver.tar.gz
        disable-mcrypt.patch
        phpldapadmin-1.2.3-php7_4.patch
        password-unescape.patch
        sort-in-templates.patch)
sha256sums=('49b14019bf611267720b4e9de33f2502b44c526809f17225a4a76ee8296b8d64'
            'b31774abab4dd603ec9acbeda49adb188890173ee93042a811b5724e01edd837'
            'd6e9b6361549be4a4e5a05ffac0917307973fe35717041f7cdde4171271ad168'
            '11e673b4ebbd922e5f6769837fef659303c48247f414dcd80f742bb01b6162b7'
            'fc2d4bdf4d43a90aa541fe20eeed494caa339eacf3f08b06539efcdbacc3578f')

prepare() {
  cd "$srcdir"/phpLDAPadmin-${pkgver}/
  find -type f -name '*.png' -exec optipng -quiet -force -fix {} \;
  patch -p1 -i "$srcdir"/disable-mcrypt.patch
#  patch -p1 -i "$srcdir"/phpldapadmin-1.2.3-php7_4.patch
  patch -p1 -i "$srcdir"/sort-in-templates.patch
  patch -p1 -i "$srcdir"/password-unescape.patch
  find -type f -name '*.orig' -exec rm -f {} \;
}

package() {
  _instdir="$pkgdir"/usr/share/webapps/phpldapadmin
  mkdir -p ${_instdir} "$pkgdir"/etc/webapps/phpldapadmin
  cd ${_instdir}

  cp -ra "$srcdir"/phpLDAPadmin-${pkgver}/* .

  ln -s /etc/webapps/phpldapadmin/config.php ${_instdir}/config/config.php
  cp ${_instdir}/config/config.php.example "$pkgdir"/etc/webapps/phpldapadmin/config.php
}
