# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=th-lift
pkgname=haskell-th-lift
pkgver=0.8.2
pkgrel=6
pkgdesc="Derive Template Haskell's Lift class for datatypes."
url="https://github.com/RyanGlScott/th-lift"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-th-abstraction')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('bda1fd661274d911898eb163427a83db81bf5cc6d7cdd2936a84cb314268be2d38c5753727c1e87e83f8edb61e8bbbd0976001f680830b3bc2d4eb96178505ec')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "BSD3" "${pkgdir}/usr/share/licenses/${pkgname}/BSD3"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/BSD3"
}
