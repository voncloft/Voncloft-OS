# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Python Shell <pythonshell@yeah.net>
# Contributor: paraxor <paraxor5344@gmail.com>
# Contributor: Pranay Kanwar <pranay.kanwar@gmail.com>

pkgname=flawfinder
pkgver=2.0.11
pkgrel=1
pkgdesc='Searches through source code for potential security flaws'
url='https://www.dwheeler.com/flawfinder'
arch=('any')
license=('GPL2')
depends=('python')
makedepends=('python-setuptools')
source=(https://www.dwheeler.com/flawfinder/flawfinder-${pkgver}.tar.gz)
sha512sums=('4236da49914fa6317eb2fae1b3fd7ba08aa96a34bc5245bfd15b6532049193cbd0367bb3620e32252ce71cdd1128bec4b20c0e88b02c55d0ede1bb5bb091ddeb')
b2sums=('e2aa109a0cc435da7a8ba5f40b5dc6474e27ec340785c0070fce4a648fb5a186f6f0f5ab4e53c8e06c91fb1cb15179ecdb605a7f7477968e4d5595fc46cbd7c8')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|env python.*|env python|' -i flawfinder
  sed -E 's|(diff_file = open\(diffhitlist_filename)\)|\1, "rb")|' -i flawfinder
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" prefix=/usr install
  install -Dm 644 README.md ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
