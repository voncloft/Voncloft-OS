# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-sshj-lib
pkgver=0.27.3
pkgrel=2
pkgdesc='SSHv2 library for Java for DBeaver plugin for SSHv2 tunnels'
arch=('any')
url='https://dbeaver.io/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver-plugin-bouncycastle')
source=("https://dbeaver.io/update/ce/latest/plugins/org.jkiss.bundle.sshj_${pkgver}.jar"
        "${pkgname}.info")
sha256sums=('d483cd42a053c7c7229f9f0004f009ecd7d33405a2a0e93918d254be625b2258'
            'a5a3c20235fb2816dbfd40c7fd4cd54f15d41a2ee1b5789845263c4deff15b59')
noextract=("org.jkiss.bundle.sshj_${pkgver}.jar")

package() {
  # Extract bundle
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins/org.jkiss.bundle.sshj_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.bundle.sshj_${pkgver}.jar" \
        -d "${pkgdir}/usr/lib/dbeaver/plugins/org.jkiss.bundle.sshj_${pkgver}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
