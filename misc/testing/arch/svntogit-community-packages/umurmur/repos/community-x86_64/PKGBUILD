# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Robert Knauer <robert@privatdemail.net>
# Contributor: xav <xav at ethertricks dot net>

pkgname=umurmur
pkgver=0.2.18
pkgrel=1
pkgdesc='Minimalistic Mumble server'
url="https://github.com/umurmur/umurmur"
arch=('x86_64')
license=('BSD')
depends=('glibc' 'openssl')
makedepends=('cmake' 'libconfig' 'protobuf-c')
backup=('etc/umurmur/umurmur.conf')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/umurmur/umurmur/archive/${pkgver}.tar.gz
        "${pkgname}-0.2.18-cmake.patch"
        umurmur.sysusers
        umurmur.service
        umurmur.tmpfiles)
sha512sums=('bd1cd7149684dbe42b9804c9a5539cdb2becf6b721d74bd88d154e9037d3289ab57ee816c0592a0167ddd302da68d94017c86deb96348d272ec9bd21e9628656'
            'b3f0a6c7d7cfe94e6ffceed832b8bcdda256e27f350abf80697d81ba154cd529a8b54fb8dac05273886e75d137ebcd71b4c9c06fdb7d0f45f1345a7cf9418b3f'
            'd84950a32ab8a2e84f5fe333cd2894e52aba624531644d106c982aa4ff04271d318543398fa7f48c719f26338679fa971bb5332472e9040ac9aa8a9b4a1f2832'
            '1e4c7c41fdcc37aa681080ee6f0bb617e7d7245d23e07b586807b2fcb3c04f4d5109e6fad50ec43738007f57e9585b5622f112be3b0def155b5ac144f88930a7'
            '825b50448231b5d791e87d7c4c471fdfe2e9a1560dad6fc90c2f4f8d0c5ed682291bf20b147a6a8c7ae361aeb8b1a11c24c6d41ffc17f06fb0f5ccd8208a899a')
b2sums=('45a6e247dee604861e70698350b7b0ee28fd7ee82a94f684eae8ff5ab7daa0c3446b32c4aa28b39e64588944b8b81c4e2a11db79d0bde9e4a2012e67b4125be2'
        'ff64c6179ebd6a21e3d51acef36c23955a4a1fcc1a9794686f8a0a447ec36f7c8b490c0ba553971bb76fbf77bda0600ddb4acf0163fa492d6e1dc75d29ba059d'
        '549dda6277c3758d221a259d08d3f91658d7615b0c06ebf2af6f3966fd798ce6228ff9ccb653daeb1d2b592e029e96e756df779ad0d4a809e224f2071e5d76cc'
        'a4be46591c2e5315826708587a8e9f9416e8ce91580457b0a9fc36dc3749eeb5737a9e1ebc47387c160e1de897ab940c3badaeb03f06f542c4f76536df1d5590'
        '355eb00fc390ff200c96ef179f2f8cab4b4a5aeca3db0781556b567de44996562f12f7cc69225159e8b1cdb26b0bacf38c7776cdc553bbe0745eb0228219df4c')

prepare() {
  cd ${pkgname}-${pkgver}
  # fix config install directory
  # https://github.com/umurmur/umurmur/pull/164
  patch -Np1 -i "../${pkgname}-0.2.18-cmake.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  export CFLAGS+=" ${CPPFLAGS}"
  export CXXFLAGS+=" ${CPPFLAGS}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libconfig.so' 'libprotobuf-c.so')
  cd ${pkgname}-${pkgver}
  make VERBOSE=1 DESTDIR="${pkgdir}" install -C build
  install -vDm 644 "${srcdir}/umurmur.service" -t "${pkgdir}/usr/lib/systemd/system"
  install -vDm 644 "${srcdir}/umurmur.sysusers" "${pkgdir}/usr/lib/sysusers.d/umurmur.conf"
  install -vDm 644 "${srcdir}/umurmur.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/umurmur.conf"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {AUTHORS,ChangeLog,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
