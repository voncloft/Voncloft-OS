# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=1.3.0
pkgrel=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=('x86_64')
url="https://github.com/jacktrip/jacktrip"
license=('MIT')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'qt5-base')
makedepends=('jack')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('050203521a53a8f944a5365c4f6106ae6db37dd2a8dc0fd74edd98cc6d5306efde2c29bce2bb22a6abcca14e44545abe32c2d4066774ca8bc71e5848c1409d9a')
b2sums=('2ca3f7bdf3fc808dd7919e77e2c25bfc287e576dc05d20e5a070057cd6498f33351fe83c32200d5ef9eec202954bde4b53c02265f4cc9405a27540c336e88fcd')

build() {
  cd "${pkgname}-${pkgver}/src"
  qmake-qt5 -spec linux-g++ "${pkgname}.pro"
  make release
}

package() {
  depends+=('libjack.so')
  cd "${pkgname}-${pkgver}"
  install -vDm 755 "src/${pkgname}" -t "${pkgdir}/usr/bin/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {CHANGESLOG.txt,README.md,TODO.txt} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
