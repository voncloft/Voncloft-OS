# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=flintqs
pkgver=1.0
pkgrel=3
epoch=1
pkgdesc="Highly optimized multi-polynomial quadratic sieve for integer factorization"
arch=(x86_64)
url="https://github.com/sagemath/FlintQS"
license=(GPL)
depends=(gmp)
source=($pkgname-$pkgver.tar.gz::"https://github.com/sagemath/FlintQS/archive/v$pkgver.tar.gz")
sha256sums=('b983052f267988192c9ec59c9d2754470e5f2db74b45e364cf7ad62f349bc9eb')

prepare() {
  cd FlintQS-$pkgver
  touch ChangeLog
  autoreconf -vi
}

build() {
  cd FlintQS-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd FlintQS-$pkgver
  make DESTDIR="$pkgdir" install
}
