# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=fluxctl
_pkgname=flux
pkgver=1.21.1
pkgrel=1
pkgdesc="Controller for the GitOps Kubernetes operator flux"
arch=('x86_64')
url="https://github.com/fluxcd/flux"
license=('Apache')
makedepends=('go' 'git')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/fluxcd/flux/archive/${pkgver}.tar.gz")
sha512sums=('bd850951abe72b78fff7c4b57bae2f5b0dc55a45d95d75c1feff8695043393e2547ac1c37562db8f62dc897e6f9fc4920c0b55a69ca386f3ff181aee43734e6b')

build() {
  cd "${_pkgname}-${pkgver}/cmd/fluxctl"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags "-w -X main.version="${pkgver}" -linkmode=external" .
  chmod +x ./fluxctl
}

check() {
  cd "${_pkgname}-${pkgver}/cmd/fluxctl"
  go test ./...
}

package() {
  cd "${_pkgname}-${pkgver}/cmd/fluxctl"
  install -Dsm755 ./fluxctl "${pkgdir}/usr/bin/fluxctl"
}
