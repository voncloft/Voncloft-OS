# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=muse
pkgver=3.1.1
pkgrel=2
pkgdesc="A digital audio workstation with support for both Audio and MIDI"
arch=('x86_64')
url="https://github.com/muse-sequencer/muse"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'gtkmm' 'python-pyqt5' 'python-pyro' 'qt5-base' 'qt5-svg')
makedepends=('alsa-lib' 'cmake' 'dssi' 'fluidsynth' 'glib2' 'gtk2' 'jack'
'ladspa' 'liblo' 'liblrdf' 'libsamplerate' 'libsndfile' 'lilv' 'lv2'
'qt5-tools' 'rtaudio' 'rubberband' 'serd')
provides=('dssi-host' 'ladspa-host' 'lv2-host' 'vst-host')
source=("$pkgname-$pkgver.tar.gz::https://github.com/muse-sequencer/muse/archive/${pkgname}_${pkgver//./_}.tar.gz")
sha512sums=('a452bef94fb722f9a02c9450e9eb82f5667f4df0593a4f6b7f490c7ed77d7d6d1fde35d1e81e5493838fde0d7cdfaff523bd747ed16999b32d2fdef724f0b16c')
b2sums=('e3fd34be7c2b5ec7685fcd0b6fd4c0457042468465fc4d0ca7ce396a556d24112c1a20c4e5195c1152953d0db89ef84afadf1028ca795c3feca4bfc0a29897bf')

prepare() {
  mv -v "${pkgname}-${pkgname}_${pkgver//./_}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # rename MusE-INSTALL_NAME to muse, because this reflects the package name
  sed -e 's/muse-3.1/muse/g' -i "${pkgname}3/CMakeLists.txt"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -D CMAKE_INSTALL_PREFIX=/usr \
        -D CMAKE_BUILD_TYPE='None' \
        -D ENABLE_PYTHON=ON \
        -Wno-dev \
        -B build \
        -S muse3
  make VERBOSE=1 -C build
}

package() {
  depends+=('libasound.so' 'libfluidsynth.so' 'libgdk-x11-2.0.so'
  'libglib-2.0.so' 'libgobject-2.0.so' 'libgtk-x11-2.0.so'
  'libinstpatch-1.0.so' 'libjack.so' 'liblilv-0.so' 'liblo.so' 'liblrdf.so'
  'librtaudio.so' 'librubberband.so' 'libsamplerate.so' 'libsndfile.so'
  'libserd-0.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install -C build
}
