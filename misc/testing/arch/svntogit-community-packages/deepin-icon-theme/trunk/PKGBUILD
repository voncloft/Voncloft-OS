# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-icon-theme
pkgver=2020.09.25
pkgrel=2
pkgdesc="Deepin Icons"
arch=('any')
url="https://github.com/linuxdeepin/deepin-icon-theme"
license=('GPL3')
depends=('papirus-icon-theme')
replaces=('deepin-cursor-theme')
conflicts=('deepin-cursor-theme')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-icon-theme/archive/$pkgver.tar.gz"
        $pkgname-fix-installation.patch::https://github.com/linuxdeepin/deepin-icon-theme/pull/23.patch)
sha512sums=('f0986492ac2cd910cd04a7362a1090b228b1c6eeb28246a57255acc6df1e3be076467d22446adb932c80430b2eae030a4dc9b713c59c9717a3046812d6b90f9a'
            'de3365adc47d824ae8f527db7b68f568d623bf5f96ab3f511146594aee57db8fd587dc6d2c5639283f620f6f19fa8dd5b789c703dfa7eaf571bd766e397cba23')

prepare() {
  patch -d $pkgname-$pkgver -p1 < $pkgname-fix-installation.patch
  sed -i 's/deepin/bloom/g' $pkgname-$pkgver/tools/hicolor.links

  # Broken filenames are not dealt since reported in June. Let's clean them up ourselves.
  find $pkgname-$pkgver -name "* 2.svg" -delete
}

build() {
  cd $pkgname-$pkgver
  make hicolor-links
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cp -a ./Sea ./usr/share/icons/hicolor "$pkgdir"/usr/share/icons/
}
