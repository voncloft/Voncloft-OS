# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

_pkg=netcdf-fortran
_mpi=openmpi
pkgname=${_pkg}-${_mpi}
pkgver=4.5.3
pkgrel=1
pkgdesc="NetCDF fortran bindings with parallel support (${_mpi} version)"
arch=(x86_64)
url="https://github.com/Unidata/netcdf-fortran"
license=(custom)
depends=(gcc-libs netcdf-openmpi curl)
makedepends=(cmake gcc-fortran doxygen)
checkdepends=(valgrind)
provides=("${_pkg}")
conflicts=("${_pkg}")
options=(!makeflags staticlibs)
source=(${url}/archive/v${pkgver}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('c6da30c2fe7e4e614c1dff4124e857afbd45355c6798353eccfa60c0702b495a')

build() {
    # Failure with GCC 10+
    export FCFLAGS="-fallow-argument-mismatch"
    export FFLAGS="-fallow-argument-mismatch"
    export CC=mpicc FC=mpifort
    cmake -B build -S ${_pkg}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_FILTER_TEST=ON \
        -DENABLE_LARGE_FILE_TESTS=ON \
        -DENABLE_PARALLEL_TESTS=ON \
        -DTEST_WITH_VALGRIND=ON
    make -C build
}

check() {
    # This is required starting with OpenMPI 3.0 when trying to run more
    # processes than the number of available cores
    export OMPI_MCA_rmaps_base_oversubscribe=yes

    # https://github.com/Unidata/netcdf-fortran/issues/109
    make -C build test || warning "Tests failed"
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -Dm644 ${_pkg}-${pkgver}/COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${_pkg}/
    rmdir "${pkgdir}"/usr/include/CMakeFiles/{netcdff.dir,}
}

