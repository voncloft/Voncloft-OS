# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=cpptoml
pkgver=0.1.1
pkgrel=2
pkgdesc="A header-only library for parsing TOML "
arch=('any')
url="https://github.com/skystrife/cpptoml"
license=('MIT')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/skystrife/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('14edce576514d53a7e13562d7f8d2b66ea2b95f44038396c0e26232ec81783042ebecec31ee272a99afef96d5c8582a8e81ea5167a787844b98de6ee6f545cc5')
b2sums=('c0ce036bec4731c4dfe96d01076b9ca18a1c4108180d5dbc839dfc44b37eb0c7af85f685a6356ef92fa684219e2883dd341e64a2012505dacc3df73dfb9e28ca')

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build\
        -S .
  make VERBOSE=1 -C build
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" VERBOSE=1 install -C build
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
