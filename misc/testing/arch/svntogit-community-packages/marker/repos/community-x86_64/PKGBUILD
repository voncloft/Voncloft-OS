# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=marker
pkgver=2020.04.04.2
pkgrel=3
pkgdesc='Simple yet robust Markdown editor made with GTK'
arch=(x86_64)
url='https://fabiocolacio.github.io/Marker/'
license=(GPL3)
depends=(gtksourceview3 gtkspell3 webkit2gtk)
makedepends=(git meson)
optdepends=('mathjax2: alternative backend for rendering formulas'
            'pandoc: export to RTF, ODT, DOCX')
_commit=90f2667611a5017840dced736fd1ea801930a45f  # tags/2020.04.04.2^0
source=(git+https://github.com/fabiocolacio/Marker.git#commit=$_commit
        git+https://github.com/Mandarancio/scidown.git
        git+https://github.com/Mandarancio/charter.git
        git+https://github.com/codeplea/tinyexpr.git
        mathjax.patch
        elf_files_in_lib.patch
        pdf-export.patch
        optional-pandoc.patch)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '00bed5a7b888a2872933b25c0d12c46c38b47f641308e7b87f876ffa34542be2'
            '09c3394f7cdcd21aa184c42ad837c2b1cacc5326772e10f92f3b3585d5b443c2'
            '9d80931b473a7c36d8ac116999484315f3f332d39b26199e87e62cb62edc3f81'
            'ac8ecbdd9d49e14045c7508184aeaf267a5ec24aeb05a186486c1ebac20173fb')

pkgver() {
  cd Marker
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd Marker

  # ELF file must be in lib
  patch -Np1 -i ../elf_files_in_lib.patch

  # Unbundle mathjax
  rm -r data/scripts/mathjax
  patch -Np1 -i ../mathjax.patch

  # Fix PDF export
  patch -Np1 -i ../pdf-export.patch

  # Make pandoc optional
  patch -Np1 -i ../optional-pandoc.patch

  git submodule init
  git submodule set-url src/scidown "$srcdir/scidown"
  git submodule update

  cd src/scidown
  git submodule init
  git submodule set-url src/charter "$srcdir/charter"
  git submodule update

  cd src/charter
  git submodule init
  git submodule set-url src/tinyexpr "$srcdir/tinyexpr"
  git submodule update
}

build() {
  arch-meson Marker build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
