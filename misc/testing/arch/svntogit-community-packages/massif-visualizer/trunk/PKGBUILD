# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Niels Martign√®ne <niels.martignene@gmail.com>
# Contributor: Matz Radloff <matzradloff@googlemail.com>

pkgname=massif-visualizer
pkgver=0.7.0
pkgrel=3
pkgdesc="Visualizer for Valgrind Massif data files"
arch=(x86_64)
url="https://www.linux-apps.com/p/1127160/"
license=(GPL)
depends=(kparts kdiagram hicolor-icon-theme)
makedepends=(extra-cmake-modules kgraphviewer qt5-xmlpatterns)
optdepends=('kgraphviewer: call graph tab')
source=("https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('f8a4cc23c80a259a9edac989e957c48ed308cf9da9caeef19eec3ffb52361f6d'
            'SKIP')
validpgpkeys=(2D1D5B0588357787DE9EE225EC94D18F7F05997E) # Jonathan Riddell <jr@jriddell.org>

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
