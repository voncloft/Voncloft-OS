# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python2-pytest-xdist
pkgver=1.34.0
pkgrel=2
pkgdesc='py.test xdist plugin for distributed testing and loop-on-failing modes'
arch=('any')
license=('MIT')
url='https://bitbucket.org/pytest-dev/pytest-xdist'
depends=('python2-execnet' 'python2-pytest-forked')
makedepends=('python2-setuptools_scm')
checkdepends=('python2-filelock')
source=("https://github.com/pytest-dev/pytest-xdist/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('636fe25d41ae2ba606760ec3fae3a7ae96b1c68251ecbf0df4ec50f03e44ded06038e73bb6955b750cc4b676e7f892bc8d79ae92102fadfb23d64bc5e54efdf6')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-xdist-$pkgver
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-xdist-$pkgver
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" pytest2
}

package() {
  cd pytest-xdist-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
