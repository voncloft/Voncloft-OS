# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=open-isns
pkgver=0.101
pkgrel=1
pkgdesc='iSNS server and client for Linux'
arch=('x86_64')
depends=('openssl')
url='https://github.com/gonzoleeman/open-isns'
license=('GPL')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gonzoleeman/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('f672ec86b6c9e984843a7a28d76f07e26393499c486f86034b8b18caa8deb556')

build() {
	cd "$srcdir"/${pkgname}-${pkgver}

	./configure \
		--prefix=/usr \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--enable-shared
	make
}

package() {
	cd "$srcdir"/${pkgname}-${pkgver}

	make DESTDIR="${pkgdir}" install install_lib install_hdrs
}
