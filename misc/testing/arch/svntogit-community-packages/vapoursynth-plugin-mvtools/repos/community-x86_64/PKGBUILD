# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=vapoursynth-plugin-mvtools
pkgver=23
pkgrel=1
pkgdesc='MVTools plugin for VapourSynth'
arch=(x86_64)
url=https://github.com/dubhater/vapoursynth-mvtools
license=(GPL2)
depends=(
  fftw
  vapoursynth
)
makedepends=(
  git
  meson
  nasm
)
_tag=6d805e232397cb08074dbb58cc02849f63b96714
source=(git+https://github.com/dubhater/vapoursynth-mvtools.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd vapoursynth-mvtools

  git describe --tags | sed 's/^v//'
}

build() {
  arch-meson vapoursynth-mvtools build \
    --libdir=/usr/lib/vapoursynth
  ninja -C build
}

package(){
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
