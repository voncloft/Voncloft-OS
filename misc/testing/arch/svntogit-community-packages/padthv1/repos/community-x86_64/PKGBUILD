# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=padthv1
pkgver=0.9.19
pkgrel=1
pkgdesc="An old-school polyphonic additive synthesizer"
arch=('x86_64')
url="https://padthv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('alsa-lib' 'cmake' 'fftw' 'jack' 'liblo' 'lv2' 'qt5-tools')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'liblo: for standalone application'
            'new-session-manager: for session management using the standalone application')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('c8862a3db0699b7f65d2d634bf35cc052f4a1c64efc2ea768a16701a78a99c49b3c6ec654bf595fea1ae2f8ba0138083734ed4e4fe193a40e836ce0cb3278d94')
b2sums=('bfa3fc1590af4c11ad15674ad630727758db60e4464c2aff3aef5c11ff3200b90a468a5d556ad1f6efaa1bf49902ec2e61013ae8d18e18a6973ca97c35b42592')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libfftw3.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
