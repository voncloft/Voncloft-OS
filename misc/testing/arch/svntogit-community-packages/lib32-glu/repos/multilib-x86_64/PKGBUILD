# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=lib32-glu
pkgver=9.0.1
pkgrel=1
pkgdesc="Mesa OpenGL utility library (32 bits)"
arch=('x86_64')
url="https://mesa.freedesktop.org/"
license=('LGPL')
depends=('lib32-libgl' 'glu')
makedepends=('gcc-multilib' 'lib32-mesa')
source=(ftp://ftp.freedesktop.org/pub/mesa/glu/glu-$pkgver.tar.xz{,.sig})
sha256sums=('fb5a4c2dd6ba6d1c21ab7c05129b0769544e1d68e1e3b0ffecb18e73c93055bc'
            'SKIP')
validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A') # "Matt Turner <mattst88@gmail.com>"

build() {
  cd ${srcdir}/glu-$pkgver

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --disable-static \
    --libdir=/usr/lib32 
  make
}

check() {
  cd "$srcdir/glu-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  make -k check
}

package() {
  cd ${srcdir}/glu-$pkgver

  make DESTDIR=${pkgdir} install
  
  rm -rf ${pkgdir}/usr/include
}
