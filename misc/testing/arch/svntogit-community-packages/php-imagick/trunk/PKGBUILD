# Maintainer: David Runge <dvzrv@archlinux.org>

_name=imagick
pkgbase=php-imagick
pkgname=('php-imagick' 'php7-imagick')
_commit="448c1cd0d58ba2838b9b6dff71c9b7e70a401b90"
pkgver=3.4.4.r66.g448c1cd
pkgrel=2
pkgdesc="PHP extension to create and modify images using the ImageMagick library"
arch=('x86_64')
url="https://github.com/imagick/imagick"
license=('PHP')
depends=('imagemagick' 'ttf-font')
makedepends=('git' 'php' 'php7')
checkdepends=('librsvg' 'ttf-dejavu')
source=("${pkgbase}::git+https://github.com/${_name}/${_name}#commit=${_commit}"
        "${pkgbase}-3.4.4-single-thread.patch::https://github.com/Imagick/imagick/pull/348/commits/f054a66254a71d9840fb9504abeedb4ba25a2b7a.patch"
)
sha512sums=('SKIP'
            '75c873d823b7bee1a2eab00c3f409746c8f0978e2c5822d8f2d91db5618666d411bc6d18f45b9fa98b86a8d5f9773d914ba816aaa3cfd6cd3f5879d53c581637')
b2sums=('SKIP'
        '67d1790bd6bb80338a1d2241852d748dd922e4d27bc982eef1eddb9390f2f32a1118dc1f8a8bce1a907c6eb23bcbeaaa1ec45f8e59cf264a3eef482dec224dd1')

pkgver() {
  cd "$pkgbase"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  # setting package version: https://bugs.archlinux.org/task/64185
  sed -e "s/@PACKAGE_VERSION@/${pkgver}/" \
      -i "${pkgbase}/php_imagick.h" "${pkgbase}/package.xml"
  echo ";extension=${_name}" > "${pkgbase}/${_name}.ini"

  (
    cd "${pkgbase}"
    patch -Np1 -i "../${pkgbase}-3.4.4-single-thread.patch"
  )

  cp -av "${pkgbase}" "${pkgname[1]}"
  (
    cd "$pkgname"
    phpize
  )
  (
    cd "${pkgname[1]}"
    phpize7
  )
}

build() {
  (
    cd "$pkgname"
    ./configure --prefix=/usr
    make
  )
  (
    cd "${pkgname[1]}"
    ./configure --prefix=/usr
    make
  )
}

check() {
  (
    cd "$pkgname"
    export NO_INTERACTION="true"
    make -k test
  )
  (
    cd "${pkgname[1]}"
    export NO_INTERACTION="true"
    make -k test
  )
}

package_php-imagick() {
  depends+=('php')
  backup=("etc/php/conf.d/${_name}.ini")

  cd "$pkgname"
  make INSTALL_ROOT="$pkgdir/" install
  install -vDm 644 "${_name}.ini" -t "${pkgdir}/etc/php/conf.d/"
  install -vDm 644 {ChangeLog,CREDITS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 examples/*.php \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

package_php7-imagick() {
  depends+=('php7')
  backup=("etc/php7/conf.d/${_name}.ini")

  cd "$pkgname"
  make INSTALL_ROOT="$pkgdir/" install
  install -vDm 644 "${_name}.ini" -t "${pkgdir}/etc/php7/conf.d/"
  install -vDm 644 {ChangeLog,CREDITS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 examples/*.php \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}
