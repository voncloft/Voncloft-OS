# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Kwpolska <kwpolska@kwpolska.tk>

pkgname=python-unidecode
pkgver=1.1.2
pkgrel=1
pkgdesc='ASCII transliterations of Unicode text'
arch=(any)
url=https://github.com/avian2/unidecode
license=(GPL)
depends=(python)
makedepends=(
  git
  python-setuptools
)
_tag=fdf8aa731c53154de04a019eaf91405fc5ff15a5
source=(git+https://github.com/avian2/unidecode.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd unidecode

  git describe --tags | sed 's/^unidecode-//'
}

build() {
  cd unidecode

  python setup.py build
}

package() {
  cd unidecode

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
