# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=m2r
pkgname=(m2r python2-m2r)
pkgver=0.2.1
pkgrel=6
pkgdesc="Markdown to reStructuredText converter"
arch=('any')
url="https://github.com/miyakogi/m2r"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-mistune' 'python2-mistune'
             'python-docutils' 'python2-docutils')
checkdepends=('python2-mock' 'python-pygments' 'python2-pygments' 'python-pytest-runner'
              'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/miyakogi/m2r/archive/v$pkgver.tar.gz")
sha512sums=('847f04538ee52f0b0a88cee9474ed889e8114a3a7b1fb221cdaa867ab70ca52f517250f2c8bbf795ae598c7b6614e6d03d9f6f05b7e3d56fb8087d63eb52f1bc')

prepare() {
  cp -a m2r-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/m2r-$pkgver
  python setup.py build

  cd "$srcdir"/m2r-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/m2r-$pkgver
  python setup.py pytest

  cd "$srcdir"/m2r-$pkgver-py2
  python2 setup.py pytest
}

package_m2r() {
  depends=('python-mistune' 'python-docutils')

  cd "$srcdir"/m2r-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-m2r() {
  depends=('python2-mistune' 'python2-docutils' 'python2-setuptools')

  cd "$srcdir"/m2r-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  mv "$pkgdir"/usr/bin/m2r{,-python2}
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
