# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=phodav
pkgver=2.5
pkgrel=1
pkgdesc="WebDav server implementation using libsoup (RFC 4918)"
arch=('x86_64')
url="https://wiki.gnome.org/phodav"
license=('LGPL2.1')
depends=('avahi' 'libsoup' 'systemd')
makedepends=('asciidoc' 'gtk-doc' 'meson' 'xmlto')
backup=('etc/conf.d/spice-webdavd')
source=("https://download.gnome.org/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('71f0a9cd70afd4dd1412a0298331dbb8ac71c0377f52117afc15eb88dc6fb910')

prepare() {
	cd $pkgname-$pkgver
	sed -i 's|/etc/sysconfig/spice-webdavd|/etc/conf.d/spice-webdavd|
	        s|/usr/sbin/spice-webdavd|/usr/bin/spice-webdavd|' data/spice-webdavd.service
}

build() {
	arch-meson $pkgname-$pkgver build
	meson compile -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build

	install -dm755 "$pkgdir/etc/conf.d/"
	echo 'SPICE_WEBDAVD_EXTRA_ARGS=""' > "$pkgdir/etc/conf.d/spice-webdavd"
}
