# Maintainer: Konstantin Gizdov <arch@kge.pw>
# Contributor: Alad Wenter <alad@archlinux.org>
# Contributor: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: J0k3r <moebius282 e4a t gmail D0_T com>
# Contributor: Alois Nespor <alois.nespor@gmail.com>
# Contributor: JerichoKru   <jerichokru(AT)hotmail.com>

pkgname=pragha
pkgver=1.3.4
pkgrel=2
pkgdesc="A lightweight GTK+ music manager - fork of Consonance Music Manager."
arch=('x86_64')
url="https://pragha-music-player.github.io/"
license=('GPL3')
depends=('libnotify' 'libcdio-paranoia' 'libclastfm' 'gst-plugins-base'
         'taglib' 'hicolor-icon-theme' 'dbus-glib' 'desktop-file-utils'
         'libpeas' 'libgudev' 'glyr')
optdepends=('gst-plugins-good: A collection of gstreamer codecs'
            'gst-plugins-bad: A collection of gstreamer codecs'
            'gst-plugins-ugly: A collection of gstreamer codecs')
makedepends=('intltool' 'xfce4-dev-tools' 'gobject-introspection')
replaces=('dissonance')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pragha-music-player/pragha/archive/v$pkgver.tar.gz")
sha256sums=('8be35959f526d079cc19221653cb691fb66251e6819f994062e0b02e08b1809b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./autogen.sh

  CPPFLAGS="${CPPFLAGS} -DHAVE_PARANOIA_NEW_INCLUDES" \
  LIBS="${LIBS} -ldbus-glib-1" \
    ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  install -m 644 "data/${pkgname}.desktop" "${pkgdir}/usr/share/applications"
  install -d "${pkgdir}/usr/share/pixmaps"
  install -m 644 "data/${pkgname}.1" "${pkgdir}/usr/share/man/man1/"
}

# vim:ts=2:sw=2:expandtab
