# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=soundconverter
pkgver=3.0.2
pkgrel=3
pkgdesc="A simple sound converter application for GNOME"
arch=('any')
license=('GPL3')
url="https://soundconverter.org/"
depends=('gst-plugins-base' 'gst-plugins-good' 'gtk3' 'libnotify' 'python-gobject')
makedepends=('intltool')
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-libav: Extra media codecs')
source=("https://launchpad.net/soundconverter/trunk/$pkgver/+download/soundconverter-$pkgver.tar.xz")
sha512sums=('0746395de412a4d845e3df4d6cce0dbcba266c9efc8db71663dd1c751d2a5de5b0dc266f24bbfe6c00f69428801ce5f9558fc8e5e2f5d5e6be8562085cd7fe92')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
