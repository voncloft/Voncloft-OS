# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Lubomir Krajcovic <lubomir.krajcovic(AT)gmail(DOT)com>
# Contributor: sh0 <mee(AT)sh0(DOT)org>
# Contributor: leoarcher <carvalho.inacio(AT)gmail(DOT)com>

pkgname=paxtest
pkgver=0.9.15
pkgrel=3
pkgdesc='PaX regression test suite'
url='https://pax.grsecurity.net'
arch=('x86_64')
license=('GPL')
depends=(sh)
source=("https://grsecurity.net/~spender/${pkgname}-${pkgver}.tar.gz"
        "https://grsecurity.net/~spender/${pkgname}-${pkgver}.tar.gz.sig")
sha512sums=('e152f5ada943f69b3745b51ac6d80f35a4c1aff777e93360287ef18b117d07861216d9d0d2075d2511d33e2cb4ea07168bb9ff2f00e79de6060e546b6806c487'
            'SKIP')
validpgpkeys=(DE9452CE46F42094907F108B44D1C0F82525FE49)

_make="make -f Makefile.psm BINDIR=/usr/bin RUNDIR=/usr/lib/$pkgname"

build() {
  cd $pkgname-$pkgver
  $_make
}

package() {
  cd $pkgname-$pkgver
  $_make DESTDIR="$pkgdir" install
}
