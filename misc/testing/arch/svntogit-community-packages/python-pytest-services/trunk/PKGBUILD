# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pytest-services
pkgname=python-pytest-services
pkgver=2.2.1
pkgrel=3
pkgdesc="Services plugin for pytest testing framework"
arch=('any')
url="https://github.com/pytest-dev/pytest-services"
license=('MIT')
depends=('python-setuptools' 'python-pytest' 'python-requests' 'python-psutil'
'python-zc.lockfile')
checkdepends=('memcached' 'python-astroid' 'python-coverage' 'python-mock'
'python-mysqlclient' 'python-pylibmc' 'xorg-server-xvfb')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('b711c08482938d23fad9996741367281a0550552d83a2b2d2482f9a38919a8c277e33a856f612bd11c8d432465e47c4233ad50004b0defb174dc31425e5fd226')
b2sums=('5a8ff780045e314a4128012aa1d9d8dfa3a4c864dd4dcb694cccc4568edd9ddb94dcc6bcbfceaf8f4d96fd821b4252dcac622c4b84fcb62c0655673d088cb444')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  local python_stdlib_basepath="$(python -c "from sysconfig import get_path; print(get_path('stdlib'))")"
  # Hack entry points by installing it
  python setup.py install --root="${PWD}/tmp_install" --optimize=1
  export PYTHONPATH="${PWD}/tmp_install/${python_stdlib_basepath/\//}/site-packages:${PYTHONPATH}:${PWD}/tests"
  pytest -v -k 'not test_mysql'
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {AUTHORS,CHANGES,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
