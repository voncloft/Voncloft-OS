# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=nzbget
pkgver=21.0
pkgrel=2
pkgdesc="Download from Usenet using .nzb files"
arch=('x86_64')
url="https://nzbget.net/"
license=('GPL')
depends=('libxml2')
optdepends=('python: to run included scripts'
            'unrar: unpacking archives'
            'p7zip: unpacking archives')
changelog=$pkgname.changelog
source=(https://github.com/nzbget/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver-src.tar.gz)
sha256sums=('65a5d58eb8f301e62cf086b72212cbf91de72316ffc19182ae45119ddd058d53')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix='/usr' --sbindir='/usr/bin' --enable-parcheck --with-tlslib=OpenSSL
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/$pkgname
  install -m644 -t "${pkgdir}"/usr/share/$pkgname ChangeLog README
}
