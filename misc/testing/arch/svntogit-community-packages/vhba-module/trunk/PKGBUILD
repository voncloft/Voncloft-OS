# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgbase=vhba-module
pkgname=(vhba-module vhba-module-dkms)
pkgver=20200106
pkgrel=108
pkgdesc="Kernel module that emulates SCSI devices"
url="https://cdemu.sourceforge.io/"
arch=(x86_64)
license=(GPL)
makedepends=('linux-headers')
source=("https://downloads.sourceforge.net/cdemu/$pkgbase-$pkgver.tar.bz2"
        60-vhba.rules dkms.conf)
sha256sums=('59a3208a7b8fcf1bb03bd4d352ec89d06a8b6b84db325e31b0863b209dde3483'
            '3052cb1cadbdf4bfb0b588bb8ed80691940d8dd63dc5502943d597eaf9f40c3b'
            '8cab0ebb4fee72069d63616b0983f105b98d1261e72e9bef5509a6e60bc382a7')

prepare() {
  cd $pkgbase-$pkgver
  sed -i 's/20190302/20190410/' Makefile  # Fixup VHBA_VERSION
}

build() {
  cd $pkgbase-$pkgver
  make KERNELRELEASE="$(</usr/src/linux/version)"
}

package_vhba-module() {
  depends=(linux)

  local extradir="/usr/lib/modules/$(</usr/src/linux/version)/extramodules"
  cd $pkgbase-$pkgver
  install -Dt "$pkgdir$extradir" -m644 *.ko
  install -Dt "$pkgdir/usr/lib/udev/rules.d" -m644 ../60-vhba.rules
  echo 'g cdemu - -' | install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/cdemu.conf"

  find "$pkgdir" -name '*.ko' -exec strip --strip-debug {} +
  find "$pkgdir" -name '*.ko' -exec xz {} +
}

package_vhba-module-dkms() {
  depends=(dkms)
  provides=("vhba-module=$pkgver-$pkgrel")
  conflicts=(vhba-module)

  cd $pkgbase-$pkgver
  install -Dt "$pkgdir/usr/src/$pkgbase-$pkgver" -m644 Makefile vhba.c ../dkms.conf
  install -Dt "$pkgdir/usr/lib/udev/rules.d" -m644 ../60-vhba.rules
  echo 'g cdemu - -' | install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/cdemu.conf"
}

# vim:set ts=2 sw=2 et:
