# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python2-importlib_resources
pkgver=1.0.2
pkgrel=2
pkgdesc='Read resources from Python packages'
arch=('any')
url='https://pypi.python.org/pypi/importlib_resources'
license=('Apache')
depends=('python2-typing' 'python2-pathlib2' 'python2-setuptools' 'python2-wheel')
makedepends=('python2-setuptools')
source=("https://pypi.io/packages/source/i/importlib_resources/importlib_resources-$pkgver.tar.gz")
sha512sums=('50bfc5130a2c9c9354efef1cd7132e805ed0f13467ba67172f83e11d907212bef3957aeef51fd904b73996c8280008d99c918637956a470448dfd67ef4807f82')

build() {
  cd importlib_resources-$pkgver
  python2 setup.py build
}

check() {
  cd importlib_resources-$pkgver
  python2 -m unittest discover
}

package() {
  cd importlib_resources-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim: ts=2 sw=2 et:
