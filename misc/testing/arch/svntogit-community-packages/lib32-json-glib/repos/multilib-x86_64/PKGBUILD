# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=lib32-json-glib
pkgver=1.6.0
pkgrel=1
pkgdesc='JSON library built on GLib'
arch=(x86_64)
url=https://wiki.gnome.org/Projects/JsonGlib
license=(GPL)
depends=(
  json-glib
  lib32-glib2
)
makedepends=(
  git
  meson
)
_tag=760a2ef2f5a505c576a6bb7a88a0a109f8108a49
source=(git+https://gitlab.gnome.org/GNOME/json-glib.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd json-glib

  git describe --tags
}

build(){
  export CC='gcc -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  arch-meson json-glib build \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    -D gtk_doc=disabled \
    -D introspection=disabled \
    -D man=false \
    -D tests=false
  ninja -C build
}

package(){
  DESTDIR="${pkgdir}" ninja -C build install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
