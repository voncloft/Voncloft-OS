# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: TingPing <tingping@tingping.se>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=lib32-brotli
pkgver=1.0.9
pkgrel=1
pkgdesc='Brotli compression library'
arch=(x86_64)
license=(MIT)
url=https://github.com/google/brotli
depends=(
  brotli
  lib32-gcc-libs
)
makedepends=(
  cmake
  git
)
_tag=e61745a6b7add50d380cfd7d3883dd6c62fc2c71
source=(git+https://github.com/google/brotli.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd brotli

  git describe --tags | sed 's/^v//'
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  export CC='gcc -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  cmake ../brotli \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib32
  make
}

check() {
  cd build

  make test
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/usr/{bin,include}
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s brotli "${pkgdir}"/usr/share/licenses/lib32-brotli
}

# vim: ts=2 sw=2 et:
