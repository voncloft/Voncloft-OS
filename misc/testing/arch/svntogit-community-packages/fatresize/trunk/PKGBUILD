# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: ValHue <vhuelamo at gmail dot com>
# Contributor: Alexander Blinne <alexander[at]blinne[dot]net>

pkgname=fatresize
pkgver=1.1.0
pkgrel=1
pkgdesc='A utility to resize FAT filesystems using libparted'
url='https://sourceforge.net/projects/fatresize/'
arch=(x86_64)
license=(GPL3)
depends=(parted)
source=($pkgname-$pkgver.tar.gz::"https://github.com/ya-mouse/fatresize/archive/v$pkgver.tar.gz")
sha256sums=('9232bc354b6c49a9e695e071bfd2d62ec79cdf4bc84928fcf1967fa39b75c33e')

build() {
  cd $pkgname-$pkgver
  autoreconf -fisv
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
