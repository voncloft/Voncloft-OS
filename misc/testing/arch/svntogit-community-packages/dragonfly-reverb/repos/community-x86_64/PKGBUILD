# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=dragonfly-reverb
_name=DragonflyReverb
pkgver=3.2.3
pkgrel=1
pkgdesc="A concert hall reverb and a room reverb"
arch=('x86_64')
url="https://michaelwillis.github.io/dragonfly-reverb/"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('gcc-libs' 'glibc' 'libglvnd' 'libx11')
makedepends=('freeverb3' 'gendesk' 'jack' 'lv2')
checkdepends=('lv2lint')
optdepends=('jack: for standalone applications'
            'lv2-host: for LV2 plugins'
            'vst-host: for VST plugins')
source=("$pkgname-$pkgver.tar.gz::https://github.com/michaelwillis/${pkgname}/releases/download/${pkgver}/${_name}-Source-v${pkgver}.tar.gz")
sha512sums=('04ca36db8ba5a58c264a5b885d2f84a4ea4595145a1883e103d25e638eeaa0ee85a2abdd86e37f55875c8f432bdb6671c6f455babdd55496ea8fbe4f680481aa')
b2sums=('43a75bc1aba4c8a6d4be6d4f01fd05024c1d58f8d4dc9421330ab5f12c46400df9803315bbca0a72fe210fed8e16bd7ee56bd14b5fcdfe5cee36bdb7b8f828d1')
_names=('DragonflyEarlyReflections' 'DragonflyHallReverb' 'DragonflyPlateReverb' 'DragonflyRoomReverb')

prepare() {
  declare -A _description=(
    ["DragonflyEarlyReflections"]="A free stereo-to-stereo early reflections reverb effect"
    ["DragonflyHallReverb"]="A free stereo-to-stereo concert hall reverb effect"
    ["DragonflyPlateReverb"]="A free stereo-to-stereo plate reverb effect"
    ["DragonflyRoomReverb"]="A free stereo-to-stereo room reverb effect"
  )
  declare -A _generic=(
    ["DragonflyEarlyReflections"]="Early Reflections Reverb"
    ["DragonflyHallReverb"]="Concert Hall Reverb"
    ["DragonflyPlateReverb"]="Plate Reverb"
    ["DragonflyRoomReverb"]="Room Reverb"
  )
  mv -v "DragonflyReverb-Source-v${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"

  for name in "${_names[@]}"; do
    gendesk -n \
            --pkgname "io.github.michaelwillis.${name}" \
            --name "${name}" \
            --exec "${name}" \
            --pkgdesc "${comments[$name]}" \
            --genericname "${generic[$name]}" \
            --categories "AudioVideo;Audio"
  done
}

build() {
  cd "$pkgname-$pkgver"
  export SYSTEM_FREEVERB3="true"
  make
}

check() {
  cd "$pkgname-$pkgver"
  # TODO: add the other plugins via -I flag as soon as lv2lint supports providing
  # it multiple times
  lv2lint -I "${PWD}/bin/${_names[1]}.lv2/" \
          "https://github.com/michaelwillis/dragonfly-reverb" \
          || echo "Known to fail: https://github.com/michaelwillis/dragonfly-reverb/issues/65"
}

package() {
  depends+=('libfreeverb3.so')
  cd "$pkgname-$pkgver"
  for name in "${_names[@]}"; do
    install -vDm 755 "bin/${name}" -t "${pkgdir}/usr/bin"
    install -vDm 755 "bin/${name}-vst.so" -t "${pkgdir}/usr/lib/vst"
    install -vDm 755 "bin/${name}.lv2/"*.so \
      -t "${pkgdir}/usr/lib/lv2/${name}.lv2/"
    install -vDm 644 "bin/${name}.lv2/"*.ttl \
      -t "${pkgdir}/usr/lib/lv2/${name}.lv2/"
  done
  install -vDm 644 *".desktop" -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
