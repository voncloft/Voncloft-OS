# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=pytest-datadir
pkgname=python-$_pkgname
pkgver=1.3.1
pkgrel=3
pkgdesc='pytest plugin for test data directories and files'
arch=('any')
url='https://github.com/gabrielcnr/pytest-datadir'
license=('MIT')
depends=('python-pytest')
makedepends=('python-setuptools-scm')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('cfd5134b42ba74d6deaf0797e514969f418ee3ee01e1b559003e7f30d713fa78a79ab392f09494ba057bbd4b2caf1c2c2fafe8b9cf9eeecac3aa18c1180ab182')


build() {
  cd $_pkgname-$pkgver

  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  rm "$pkgdir"/usr/LICENSE
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
