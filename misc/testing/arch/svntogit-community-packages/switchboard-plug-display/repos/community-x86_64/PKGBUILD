# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-display
pkgver=2.2.2
pkgrel=1
pkgdesc='Switchboard Display Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-display
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgee
  libgranite.so
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  switchboard
  vala
)
source=(git+https://github.com/elementary/switchboard-plug-display.git#tag=dae3110fa4d662e8524c505b0c673ab7819332ab)
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-display

  git describe --tags
}

build() {
  arch-meson switchboard-plug-display build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
