# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.lock
pkgname=python-flufl-lock
pkgver=5.0.4
pkgrel=1
pkgdesc="NFS-safe file locking with timeouts for POSIX systems for Python"
arch=('any')
url="https://gitlab.com/warsaw/flufl.lock"
license=('Apache')
depends=('python-atpublic' 'python-psutil')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-sybil')
replaces=('python-flufl.lock')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('c08d8115c836f7523887d86539e9f11a2007913936fd8a4a116a610b648adf289fb086e8904b2163a6af7449e92a6edf9fad09fb770a34473768ffc4d8eb146e')
b2sums=('3fc6c3bfdac3c877c82f9f56e739342604be9f855a42a99bd06e5fb89fe8b3bc4cabd2e357e8e0992f7aa3f5ec7ea4a18e1f59a05beeeed7074a72440d3f6b9b')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we don't use coverage with pytest
  sed -s '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
