# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=planarity
pkgver=3.0.1.0
pkgrel=1
pkgdesc="A library for implementing graph algorithms"
arch=(x86_64)
url="https://github.com/graph-algorithms/edge-addition-planarity-suite"
license=(BSD)
depends=(glibc)
makedepends=()
source=($pkgname-$pkgver.tar.gz::"https://github.com/graph-algorithms/edge-addition-planarity-suite/archive/Version_$pkgver.tar.gz")
sha256sums=('f5489e64ae8d83a4ffb29a4cdfdb2dd7eb4ba33acf23fa54cba08058c8ee085d')

build() {
  cd edge-addition-planarity-suite-Version_$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd edge-addition-planarity-suite-Version_$pkgver
  make install DESTDIR="$pkgdir"

  install -Dm644 LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
