# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Tom K <tomk@runbox.com>
# Contributor: Todd Musall <tmusall@gmail.com>

pkgname=gnubiff-gtk
pkgver=2.2.15
pkgrel=3
pkgdesc="Mail notification program that checks for mail and displays headers when new mail has arrived. GTK version."
arch=('x86_64')
url="http://gnubiff.sourceforge.net/"
license=("GPL")
depends=('libglade' 'gtk3' 'openssl' 'popt')
makedepends=('intltool')
conflicts=('gnubiff')
source=(http://downloads.sourceforge.net/sourceforge/gnubiff/gnubiff-$pkgver.tar.gz
	mutex.patch)
md5sums=('c705e1135b0bd4c8d8f7ce72ca7b5443'
         '76de63fbc4a1487560cfae210fc1d6e4')

prepare() {
  cd $srcdir/gnubiff-$pkgver
  patch -p1 <$srcdir/mutex.patch
}

build() {
  cd $srcdir/gnubiff-$pkgver
  export LDFLAGS="-lgthread-2.0 -lX11"
  ./configure --prefix=/usr --disable-gnome --disable-fam --with-password --with-password-string="65423957eb027b94c9e661611c0e4271"
  make
}

package() {
  cd $srcdir/gnubiff-$pkgver
  make DESTDIR=$pkgdir install
  rm -rf $pkgdir/usr/lib
  rm -rf $pkgdir/usr/share/gnome-2.0
  rm -rf ${pkgdir}/usr/share/info/dir
}
