# Maintainer: David Runge <dvzrv@archlinux.org>

_name=json
pkgname=ruby-json
pkgver=2.3.1
pkgrel=1
pkgdesc="JSON implementation for Ruby"
arch=('x86_64')
url="https://github.com/flori/json"
license=('BSD')
depends=('ruby')
makedepends=('ragel' 'ruby-rake' 'ruby-rdoc')
checkdepends=('git' 'ruby-bundler' 'ruby-test-unit')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/flori/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('4996afd3e90ec6632546ad1e5e04e971c1ef27c3a17066820ed17a45eda448f2a3308039a3d5d5215801a2719a7733df927b63cee530b7c48b7a31045bd02be7')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i *.gemspec Gemfile Rakefile
}

build() {
  cd "${pkgname}-${pkgver}"
  gem build "${_name}.gemspec"
}

check(){
  cd "${pkgname}-${pkgver}"
  rake test
}

package() {
  cd "${pkgname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              ${_name}-${pkgver}.gem
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so,*.gemspec} \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
