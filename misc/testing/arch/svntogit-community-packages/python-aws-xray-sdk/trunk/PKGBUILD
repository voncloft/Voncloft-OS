# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-aws-xray-sdk
_pkgname=aws-xray-sdk-python
pkgver=2.6.0
pkgrel=3
pkgdesc='AWS X-Ray SDK for Python'
arch=(any)
url='https://github.com/aws/aws-xray-sdk-python'
license=(Apache)
makedepends=(python-setuptools)
depends=(python python-botocore python-future python-jsonpickle python-wrapt)
checkdepends=(python-flask-sqlalchemy python-pynamodb python-pytest python-aiohttp
              python-pytest-aiohttp python-testing.postgresql python-psycopg2
              python-pg8000 python-requests python-aiobotocore python-bottle
              python-webtest python-django-fake-model python-mock)
# Not using PyPI source tarball as it does not include tests
source=("https://github.com/aws/aws-xray-sdk-python/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('3db4147eb9fae56a2b414611e8ce84fec9b00b2a0fbb9dee18da22e58ed18a6e')

prepare() {
  cd $_pkgname-$pkgver
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  # See setenv= in upstream tox.ini
  export DJANGO_SETTINGS_MODULE=tests.ext.django.app.settings
  export AWS_SECRET_ACCESS_KEY=fake_key
  export AWS_ACCESS_KEY_ID=fake_id

  # the test suite for pymysql uses testing.mysqld, which is not
  # compatible with MariaDB
  # https://github.com/tk0miya/testing.mysqld/issues/3
  pytest -v tests --ignore tests/ext/pymysql
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
