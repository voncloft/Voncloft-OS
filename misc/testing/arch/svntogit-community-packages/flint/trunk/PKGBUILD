# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>
# Contributor: Alessandro "jakedust" Andrioni <jakedust@gmail.com>

pkgname=flint
pkgver=2.7.1
pkgrel=1
pkgdesc="A C library for doing number theory"
arch=(x86_64)
url="http://www.flintlib.org"
license=(GPL)
depends=(mpfr ntl cblas)
makedepends=(cmake python)
source=("http://www.flintlib.org/flint-$pkgver.tar.gz")
sha256sums=('186e2fd9ab67df8a05b122fb018269b382e4babcdb17353c4be1fe364dca481e')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_NTL=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
