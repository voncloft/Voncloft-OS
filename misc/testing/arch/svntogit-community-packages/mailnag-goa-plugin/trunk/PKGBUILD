# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Patrick Ulbrich <zulu99 at gmx . net>

pkgname=mailnag-goa-plugin
pkgver=2.0.0
pkgrel=2
pkgdesc="Mailnag GNOME Online Accounts plugin"
arch=('any')
url="https://github.com/pulb/mailnag-goa-plugin"
license=('GPL')
depends=('gnome-online-accounts' 'mailnag')
makedepends=(git)
_commit=67596108fa648d47c1a8eb968784809696c08a73  # tags/v2.0.0
source=("git+https://github.com/pulb/mailnag-goa-plugin.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd $pkgname
  python setup.py build
}

package() {
  cd $pkgname
  python setup.py install --root="$pkgdir" --optimize=1
}
