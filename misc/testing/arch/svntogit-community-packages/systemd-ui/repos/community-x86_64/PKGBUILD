# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=systemd-ui
pkgver=3
pkgrel=5
pkgdesc="Graphical front-end for systemd"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/systemd"
license=('GPL')
depends=('gtk3' 'libgee' 'libnotify')
makedepends=('vala' 'docbook-xsl' 'libxslt')
source=(https://www.freedesktop.org/software/systemd/$pkgname-$pkgver.tar.xz
        systemadm.appdata.xml)
sha256sums=('455e65000abf39bbccd937786b5f0d7c76ea631b6f28d7142f007fa141338dad'
            '2d335848d3810e34baa1efada6bb48c459c75e7007db3c7065a9e0d7325adefb')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../systemadm.appdata.xml "$pkgdir/usr/share/metainfo/systemadm.appdata.xml"
}
