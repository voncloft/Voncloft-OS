# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>

pkgname=python-pytest-tap
_pyname=${pkgname#python-}
pkgver=3.2
pkgrel=1
pkgdesc="Test Anything Protocol (TAP) reporting plugin for pytest."
arch=(any)
url="https://github.com/python-tap/pytest-tap"
license=('BSD')
depends=('python-pytest' 'python-tappy')
makedepends=('python-setuptools')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/python-tap/pytest-tap/archive/v${pkgver}.tar.gz)
sha256sums=('d5040ba2e1ed5855babfd50bffeb5e877912750ab33d5c07b20c00f2f2626fed')

build() {
	cd "${srcdir}/${_pyname}-${pkgver}"
	python setup.py build

	# need to compile translation files when using the tarball from GitHub
	python setup.py -v compile_catalog
}

check() {
	cd "${srcdir}/${_pyname}-${pkgver}"
	local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
	python setup.py install --root="${srcdir}/tmp_install" --optimize=1
	PYTHONPATH="${srcdir}/tmp_install/usr/lib/python${python_version}/site-packages" pytest
}

package() {
	cd "${srcdir}/${_pyname}-${pkgver}"
	python setup.py install --root="${pkgdir}" --prefix=/usr --optimize=1
}
