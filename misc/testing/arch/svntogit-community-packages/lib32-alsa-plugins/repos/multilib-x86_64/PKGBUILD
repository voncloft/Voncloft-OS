# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-alsa-plugins
pkgver=1.2.2
pkgrel=1
pkgdesc="Additional ALSA plugins (32-bit)"
arch=(x86_64)
url="https://www.alsa-project.org"
license=(GPL)
depends=('lib32-glibc' 'lib32-alsa-lib' 'alsa-plugins')
makedepends=('lib32-dbus' 'lib32-jack' 'lib32-libavtp' 'lib32-libpulse'
'lib32-libsamplerate' 'lib32-speexdsp')
optdepends=('lib32-dbus: for maemo plugin'
            #'lib32-ffmpeg: for pcm_a52 and rate_lavrate plugins'
            'lib32-jack: for pcm_jack plugin'
            'lib32-libavtp: for pcm_aaf plugin'
            'lib32-libsamplerate: for rate_samplerate plugin'
            'lib32-libpulse: for conf_pulse, ctl_pulse and pcm_pulse plugins'
            'lib32-speexdsp: for pcm_speex and rate_speexrate plugins')
source=("https://www.alsa-project.org/files/pub/plugins/alsa-plugins-$pkgver.tar.bz2")
sha256sums=('1c0f06450c928d711719686c9dbece2d480184f36fab11b8f0534cb7b41e337d')

prepare() {
  cd alsa-plugins-$pkgver
  autoreconf -fvi
}

build() {
  cd alsa-plugins-$pkgver

  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --enable-maemo-plugin
  make
}

package() {
  depends+=(libasound.so)
  cd alsa-plugins-$pkgver
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/{etc,usr/share}
}
# vim:set sw=2 et:
