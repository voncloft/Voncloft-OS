# Maintainer: Daurnimator <quae@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Laszlo Papp <djszapi at archlinux us>
# Contributor: Donald Ephraim Curtis <dcurtis@gmail.com>

pkgbase=penlight
pkgname=('lua-penlight' 'lua53-penlight' 'lua52-penlight' 'lua51-penlight')
pkgver=1.9.2
pkgrel=1
pkgdesc='Lua libraries focusing on input data handling'
url='https://github.com/lunarmodules/Penlight'
arch=('any')
license=('MIT')
checkdepends=(
  'lua' 'lua-filesystem'
  'lua53' 'lua53-filesystem'
  'lua52' 'lua52-filesystem'
  'lua51' 'lua51-filesystem'
)
source=("https://github.com/lunarmodules/Penlight/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('1094368bd95f84428ce1ce814028f8a73ee6a952e18dfffc5fa05d9ee1f0e486')

check() {
  cd Penlight-${pkgver}
  local _version
  for _version in 5.4 5.3 5.2 5.1; do
    export LUA_PATH="${PWD}/lua/?/init.lua;${PWD}/lua/?.lua;;"
    lua$_version run.lua
  done
}

_package() {
  lib_version="$1"

  cd Penlight-${pkgver}
  install -Dm 644 lua/pl/* -t "${pkgdir}/usr/share/lua/${lib_version}/pl"
  install -Dm 644 CONTRIBUTING.md CHANGELOG.md README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/manual/* -t "${pkgdir}/usr/share/doc/${pkgname}/manual"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_lua53-penlight() {
  depends=("lua53-filesystem")

  _package 5.3
}

package_lua52-penlight() {
  depends=("lua52-filesystem")

  _package 5.2
}

package_lua51-penlight() {
  depends=("lua51-filesystem")

  _package 5.1
}

package_lua-penlight() {
  depends=("lua-filesystem")

  _package 5.4
}

# vim: ts=2 sw=2 et:
