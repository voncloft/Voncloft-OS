# Maintainer: Robin Broda <coderobe @ archlinux.org>
# Contributor: Brian Bidulock <bidulock@openss7.org>
# Contributor: Patryk Kowalczyk < patryk at kowalczyk dot ws>

pkgname=hivex
pkgver=1.3.19
pkgrel=1
pkgdesc="System for extracting the contents of Windows Registry."
arch=("x86_64")
url="http://libguestfs.org"
license=("LGPL2.1")
depends=("libxml2" "perl")
makedepends=("python2" "ruby" "ruby-rake" "ruby-rdoc" "perl-io-stringy" "perl-test-simple" "ocaml-findlib" "ocaml" "chrpath")
optdepends=("python2: python bindings"
	    "ruby: ruby bindings"
	    "ocaml: ocaml bindings")
source=("http://libguestfs.org/download/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig})
sha512sums=('dc271349c6efa7b55ba144617e57fe4e7ce855ec1f4ef9f84ee86eeefd3a34cb6b26078786e1568f3008b922a31b758ff2c2734e599b67e0e210aa72e9f41177'
            'SKIP')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0') # Richard W.M. Jones <rjones@redhat.com>

build() {
    cd "$pkgname-$pkgver"

    ./configure \
	--bindir=/usr/bin \
	--libdir=/usr/lib \
	--prefix=/usr \
	--disable-rpath \
	--disable-static PYTHON=python2

    make
    chrpath -d perl/blib/arch/auto/Win/Hivex/Hivex.so
}

_perl_depends() {
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    _perl_depends
}
