# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>

pkgname=perl-datetime-format-iso8601
pkgver=0.08
pkgrel=5
pkgdesc="Parses ISO8601 formats"
arch=('any')
url="https://search.cpan.org/dist/DateTime-Format-ISO8601/"
license=('PerlArtistic')
depends=('perl-datetime-format-builder' 'perl-datetime')
makedepends=()
optdepends=('perl-file-find-rule'
            'perl-test-pod')
options=(!emptydirs)
source=("https://search.cpan.org/CPAN/authors/id/J/JH/JHOBLITT/DateTime-Format-ISO8601-$pkgver.tar.gz")
md5sums=('b7c0a4b5e767a8d3b87f3dc678d8c30c')

build() {
  cd "$srcdir"/DateTime-Format-ISO8601-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/DateTime-Format-ISO8601-$pkgver
  make install DESTDIR="${pkgdir}"
  find "$pkgdir" -name perllocal.pod -delete
  find "$pkgdir" -name .packlist -delete
}
