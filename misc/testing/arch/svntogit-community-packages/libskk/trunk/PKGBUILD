# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: eagletmt <eagletmt@gmail.com>
# Contributor: kusanaginoturugi <kusanaginoturugi@gmail.com>

pkgname=libskk
pkgver=1.0.5
pkgrel=2
pkgdesc='GObject-based library to deal with Japanese kana-to-kanji conversion method'
arch=('x86_64')
url='https://github.com/ueno/libskk'
license=('GPL3')
depends=('libgee' 'libxkbcommon' 'json-glib')
makedepends=('intltool' 'gnome-common' 'gobject-introspection' 'vala')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ueno/libskk/archive/$pkgver.tar.gz")
sha512sums=('50b07b244e1fbec13764ca00b90ede2584c53dd5c16783c9bb9114f69f43dbebd352653f335f1c3584b8c42ef4184e891e0ca7427c38afc99131196721e1c1c1')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
