# Maintainer: Christian Rebischke <Chris.Rebischke[at]archlinux[dot]org>
# Contributor: Florian Dejonckheere <florian at floriandejonckheere dot be>
# Contributor:  jyantis <yantis@yantis.net>

_pkgname=powerlevel9k
pkgname=zsh-theme-"${_pkgname}"
pkgver=0.6.7
pkgrel=3
pkgdesc='Powerlevel9k theme for zsh'
arch=('any')
url='https://github.com/bhilburn/powerlevel9k'
license=('mit')
depends=('zsh')
conflicts=('zsh-theme-powerlevel10k')
optdepends=(
  'powerline-fonts: powerline fonts integration'
  'awesome-terminal-fonts: icon package'
  'acpi: battery monitoring'
  'git: status of repository'
  'mercurial: status of repository'
  'systemd: virtualization detection'
  'openssh: ssh detection')

source=("${pkgname}-${pkgver}.tar.gz::https://github.com/bhilburn/powerlevel9k/archive/v${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.asc::https://github.com/bhilburn/powerlevel9k/releases/download/v${pkgver}/powerlevel9k-${pkgver}.tar.gz.asc")
sha512sums=('592e43be2550ca76c2479b1d93ee76d46370c019958b55f41c3d760af81663a2bb29c759b59569d98d54ec30a03b9ae7a052ae1ddf9e16dc8f4769b30e1c3528'
            'SKIP')
validpgpkeys=('9EA809E41C334416D156E166FA6DB201CA61083A')

package(){
  cd "${srcdir}/${_pkgname}-${pkgver}"

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Install Documentation
  install -D -m644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"

  # Install the theme
  install -D -m644 powerlevel9k.zsh-theme "${pkgdir}/usr/share/zsh-theme-${_pkgname}/${_pkgname}.zsh-theme"

  # Install the utilities
  for FILE in functions/*.zsh; do
    install -D -m644 "${FILE}" "${pkgdir}/usr/share/zsh-theme-${_pkgname}/functions/$(basename ${FILE})"
  done
}

# vim:set et sw=2 ts=2 tw=79:
