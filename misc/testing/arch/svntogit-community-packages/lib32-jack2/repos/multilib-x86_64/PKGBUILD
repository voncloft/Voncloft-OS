# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

_name=jack2
pkgname=lib32-jack2
pkgver=1.9.17
pkgrel=1
pkgdesc="The JACK low-latency audio server (32 bit)"
arch=('x86_64')
url="https://github.com/jackaudio/jack2"
license=('GPL2' 'LGPL2.1')
depends=('lib32-gcc-libs' 'lib32-glibc' 'lib32-opus' "jack2=${pkgver}")
makedepends=('git' 'lib32-celt' 'lib32-dbus' 'lib32-expat' 'lib32-readline'
'lib32-libsamplerate' 'lib32-libsndfile' 'waf')
provides=('lib32-jack' 'libjack.so')
conflicts=('lib32-jack')
source=("git+https://github.com/jackaudio/${_name}.git#tag=v${pkgver}?signed")
sha512sums=('SKIP')
validpgpkeys=('62B11043D2F6EB6672D93103CDBAA37ABC74FBA0') # falkTX <falktx@falktx.com>

prepare() {
  cd "${_name}"
  # copying relevant custom waf scripts and loading them specifically using
  # wscript
  touch __init__.py
  mkdir -vp tools
  cp -v waflib/extras/xcode*.py tools
  rm -rv waflib
  sed -e "s/load('xcode'/load('xcode', tooldir='tools'/g" \
      -e "s/load('xcode6'/load('xcode6', tooldir='tools'/g" \
      -i wscript
}

build() {
  cd "${_name}"
  export PYTHONPATH="${PWD}:${PYTHONPATH}"
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CXXFLAGS+=' -m32'
  export CFLAGS+=' -m32'
  waf configure --prefix=/usr \
                --libdir=/usr/lib32 \
                --classic \
                --dbus
  waf build
}

package() {
  depends+=('libcelt0.so')
  cd "${_name}"
  export PYTHONPATH="${PWD}:${PYTHONPATH}"
  waf install --destdir="${pkgdir}"
  # remove all non-32bit and those provided by jack2
  rm -rf "${pkgdir}"/usr/{bin,lib,include,share}
  rm -rf "${pkgdir}/usr/lib32/jack/"
  rm -rf "${pkgdir}/usr/lib32/libjack"{net,server}*
}
# vim:set ts=2 sw=2 et:
