# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=cage
pkgver=0.1.2.1
pkgrel=2
pkgdesc="A kiosk compositor for Wayland"
arch=('x86_64')
url="https://www.hjdskes.nl/projects/cage/"
license=('MIT')
depends=('glibc' 'polkit' 'wayland')
makedepends=('git' 'libxkbcommon' 'meson' 'pixman' 'scdoc' 'wayland-protocols'
'wlroots' 'xorg-server-xwayland')
optdepends=('xorg-server-xwayland: X11 support')
source=("${pkgname}::git+https://github.com/Hjdskes/${pkgname}.git#tag=v${pkgver}?signed")
sha512sums=('SKIP')
validpgpkeys=('13B6A35E9FD677D02CD68DBE37C445296EBC43B1') # Jente Hidskes <dev@hjdskes.nl>

build() {
  cd "$pkgname"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             -Dxwayland=true \
             build
  ninja -C build
}

package() {
  depends+=('libwlroots.so' 'libxkbcommon.so' 'libpixman-1.so')
  cd "$pkgname"
  DESTDIR="${pkgdir}" meson install -C build
  #docs
  install -vDm 644 README.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
