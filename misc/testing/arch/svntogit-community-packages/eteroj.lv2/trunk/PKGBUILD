# Maintainer: David Runge <dave@sleepmap.de>

pkgname=eteroj.lv2
pkgver=0.8.0
pkgrel=1
pkgdesc="Open Sound Control for LV2"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/eteroj/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc')
makedepends=('lv2' 'meson' 'sord')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz")
sha512sums=('cbe00897cc442c250fcb621a854127b9f83e06069ad1176075f14bc391b8dcde3ad0cdf93f4e9cd17e7396ea451b64ac5dd1c473609565272287ebd17c7551f8')
b2sums=('4083ee379ab85259a0d4b6e241da91ca66c5315ec525d991ac254c5b02207220802eb6964f713990f7ba14edb91faebb1c0dca1f5264b4def15ec3562f02e110')

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             build
  ninja -C build
}

check() {
  local _name
  local _names=('cloak' 'io' 'ninja' 'pack' 'query')
  cd "$pkgname-$pkgver"
  meson test -C build
  for _name in "${_names[@]}"; do
    lv2lint -Mpack -I build/ "http://open-music-kontrollers.ch/lv2/eteroj#${_name}"
  done
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
