# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=42.5
pkgrel=1
pkgdesc="Simple backup tool, that hides the complexity of backing up the Right Way and uses duplicity as the backend"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/DejaDup"
license=(GPL)
depends=(duplicity gvfs libhandy libsecret org.freedesktop.secrets python-gobject python-pydrive)
makedepends=(appstream-glib itstool meson vala)
source=(https://gitlab.gnome.org/World/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz
        libhandy1.patch)
validpgpkeys=('A3A5C2FC56AE7341D308D8571B50ECA373F3F233') # Michael Terry
sha256sums=('b06b95f9a4503143971332f304fde24b45a6b8000184f91a9648fadf9e561909'
            '66b3204425b9353e0f8cb97e7d4d50d58f00be753eb64096cae1d1932b0a31fa')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../libhandy1.patch
}

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
