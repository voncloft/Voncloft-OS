# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=widelands
pkgver=21
_realver=build${pkgver}
pkgrel=4
pkgdesc="A realtime strategy game with emphasis on economy and transport"
arch=('x86_64')
url="https://widelands.org/"
license=('GPL')
depends=('sdl2_mixer' 'sdl2_image' 'sdl2_net' 'sdl2_ttf' 'sdl2_gfx' 'lua' 'glew' 'python' 'boost-libs' 'dbus')
makedepends=('cmake' 'boost' 'mesa' 'ninja' 'git')
replaces=('widelands-data')
source=("git+https://github.com/widelands/widelands.git#tag=build$pkgver"
        $pkgname.desktop
        $pkgname.png)
sha512sums=('SKIP'
            'ef7e54ffba11a2fe54d2cafc0a12fcbe5028e861b9b980f549317b159c8532ee62423af8724b05cfd344017097e022215fa750f4ca9afffa57703d434017d727'
            'f7db9d7584ec8431f2f1857baddeb32cd3ae182343e7f0369e61fb444514bd9c2e6a3931ae34f24cc994840dffbbe4451f33845798f3d582f41b31f451b261ac')

build() {
  cd $pkgname

  cmake . \
      -G Ninja . \
      -Bbuild \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DWL_INSTALL_BASEDIR=/usr \
      -DWL_INSTALL_DATADIR=/usr/share/widelands
  ninja -C build
}

package() {
  cd $pkgname

  DESTDIR="$pkgdir" ninja -C build install

  mkdir -p "$pkgdir"/usr/bin
  mv "$pkgdir"/usr/{widelands,wl_map_object_info,wl_map_info} "$pkgdir"/usr/bin
  mv "$pkgdir"/usr/{VERSION,ChangeLog,CREDITS} "$pkgdir"/usr/share/widelands

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  mv "$pkgdir"/usr/COPYING "$pkgdir"/usr/share/licenses/$pkgname

  install -Dm644 "$srcdir"/$pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -Dm644 "$srcdir"/$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
}
