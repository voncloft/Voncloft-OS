# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux>

pkgname=python-gammu
pkgver=3.1
pkgrel=2
pkgdesc="Python bindings for Gammu library"
arch=('x86_64')
url="https://wammu.eu/python-gammu/"
license=('GPL')
depends=('python' 'gammu')
makedepends=('python-setuptools')
checkdepends=('libdbi-drivers')
source=(https://dl.cihar.com/python-gammu/python-gammu-$pkgver.tar.bz2)
sha512sums=('98da80c1dee3cbffc819332f7936f88c5f95eaf65341e83d038bef7678563e40d724d663d4e83da162117ff2a74d0f46d06d67018903099c561f64bb0e9d3da4')

build() {
  cd "${srcdir}"/python-gammu-$pkgver
  python setup.py build
}

check() {
  # tests can be flaky on slower hardware due to timing
  cd "${srcdir}"/python-gammu-$pkgver
  python setup.py test 
}

package() {
  cd "${srcdir}"/python-gammu-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
}
