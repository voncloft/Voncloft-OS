# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=primus_vk
pkgver=1.5
pkgrel=1
pkgdesc="Nvidia Vulkan offloading for Bumblebee"
arch=(x86_64)
url="https://github.com/felixdoerre/primus_vk"
license=(BSD)
depends=(nvidia-utils bumblebee vulkan-icd-loader primus)
makedepends=(git vulkan-validation-layers)
_tag=95db3c717926839f8c8066b6d64992ffbb8f7e0b # git rev-parse v${pkgver}
source=("git+${url}.git#tag=${_tag}?signed")
sha512sums=('SKIP')
validpgpkeys=(0A9D381479E6D19CE365E301FD4066F7DB7D993F) # Felix DÃ¶rre <felix@dogcraft.de>

_backports=(
)

_reverts=(
)

prepare() {
  cd ${pkgname}

  local _c
  
  for _c in "${_backports[@]}"; do
    git log --oneline -1 "${_c}"  
    git cherry-pick -n "${_c}"
  done

  for _c in "${_reverts[@]}"; do
    git log --oneline -1 "${_c}"
    git revert -n "${_c}"
  done
}

build() {
  cd ${pkgname}

  export CXXFLAGS+=' -DNV_DRIVER_PATH=\"/usr/lib/libGLX_nvidia.so.0\"'
  make all primus_vk_diag
}

package() {
  cd ${pkgname}
  make PREFIX="${pkgdir}"/usr install
  install -Dm755 primus_vk_diag -t "${pkgdir}"/usr/bin/
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
