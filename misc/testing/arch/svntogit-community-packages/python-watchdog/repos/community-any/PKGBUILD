# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-watchdog
pkgver=0.10.5
pkgrel=1
pkgdesc="Python API and shell utilities to monitor file system events"
arch=('any')
url="https://pypi.python.org/pypi/watchdog"
license=('Apache')
depends=('python-pathtools' 'python-yaml' 'python-argh')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov' 'python-pytest-timeout' 'python-pytest-runner')
source=($pkgname-$pkgver.tar.gz::https://github.com/gorakhargosh/watchdog/archive/v$pkgver.tar.gz)
sha512sums=('b107fe23b123360ba8ad049576bdbbb40061c70157bee0663bcf9ee9a9a9f65b95234540a477ba9ae6d617199ca1d77fe30b3b9dc4868bfef08a6b9099d6fe51')

build() {
  cd watchdog-$pkgver
  python setup.py build
}

check() {
  cd watchdog-$pkgver
  python setup.py pytest
}

package() {
  cd watchdog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:ts=2:sw=2:et:
