# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Tony Benoy <me@tonybenoy.com>

_pkgname=typer
pkgname=python-typer
pkgver=0.3.2
pkgrel=2
pkgdesc="Build great CLIs. Easy to code. Based on Python type hints"
arch=('any')
url="https://typer.tiangolo.com/"
license=('MIT')
depends=('python-click')
makedepends=('python-setuptools' 'python-dephell')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tiangolo/typer/archive/${pkgver}.tar.gz")
sha512sums=('fe58cc344a880acec1cd24f15f668c3b730665bc86d6c0faf9265d7ce145fb91232531e4fc1cb8141356d65554917673e51d57d718958234a173788e3ea0b567')

prepare() {
  cd "typer-$pkgver"
  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd "typer-$pkgver"
  python setup.py build
}

package() {
  cd "typer-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
