# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=liri-qbs-shared
pkgver=1.4.0
pkgrel=2
pkgdesc="Extra imports and modules for Qbs"
arch=('any')
url='https://liri.io'
license=('BSD')
depends=('qbs')
groups=('liri')
source=("https://github.com/lirios/qbs-shared/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha512sums=('b8d6e49be3d3dbfc301affba6d8f1b02e0816af8b55a8d19b3d0ae10da0f0f209ef5bbcff13b0ab1e763b9015ee189f0a2706fb63aa1a5e7a608617a70c292ef')

build() {
  cd liri-qbs-shared-$pkgver
  qbs setup-toolchains --type gcc /usr/bin/g++ gcc
  qbs setup-qt /usr/bin/qmake-qt5 qt5
  qbs config profiles.qt5.baseProfile gcc
  qbs build --no-install -d build profile:qt5 project.prefix:/usr
}

package() {
  cd liri-qbs-shared-$pkgver
  qbs install -d build --no-build -v --install-root "$pkgdir" profile:qt5

  install -Dm644 LICENSE.BSD "$pkgdir"/usr/share/licenses/liri-qbs-shared/LICENSE.BSD
}
