# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Strit <danjohansen at strits dot dk>
# Contributor: ValHue <vhuelamo at gmail dot com>
# Contributor: Jaroslav Lichtblau <dragonlord at aur dot archlinux dot org>
# Contributor: Daniel J Griffiths <ghost1227 at archlinux dot us>
# Contributor: Ronald van Haren <ronald at archlinux dot org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok at gmail dot com>
# Contributor: Pawel Rusinek <p.rusinek at gmail dot com>
# Contributor: Jashua Gentry <https://gitlab.com/pixel293>
# Contributor: John Cheetham <https://gitlab.com/johncheetham>

pkgname=griffith
pkgver=0.16
pkgrel=1
pkgdesc="Movie collection manager application"
arch=(any)
url="https://gitlab.com/Strit/griffith"
license=(GPL2)
depends=(gtkspell3 python-chardet python-gobject python-pillow python-reportlab python-sqlalchemy)
source=("https://gitlab.com/Strit/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "build.patch"
        "griffith.appdata.xml")
sha256sums=('0db158bedb9ae598e571fb2180723bd91d2aff07a0dcf4bbb6135e02176b88c8'
            '16fa6f25863a5e040b6d7e8e251ff172c0614a33b6bf7ae1f3f159a67ec0ed9c'
            '2556b99b18e2d53ca1e96c9d0edded9673c80020f9f832faf343eb1a71632f20')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../build.patch
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../$pkgname.appdata.xml "$pkgdir/usr/share/metainfo/$pkgname.appdata.xml"
}
