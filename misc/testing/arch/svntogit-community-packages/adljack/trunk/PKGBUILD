# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=adljack
pkgver=1.2.0
pkgrel=2
pkgdesc="A standalone synthesizer for ADLMIDI and OPNMIDI on the text console"
arch=('x86_64')
url="https://github.com/jpcima/adljack"
license=('Boost')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'libjack.so' 'liblo.so' 'libncursesw.so'
'librtaudio.so' 'librtmidi.so')
makedepends=('cmake')
source=("https://github.com/jpcima/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-1.2.0-devendor-rtaudio-rtmidi.patch")
sha512sums=('2b579fbb3b47c814bc33b68c749b3335c39feee88115cfe41eb9261775d4b296eebf09905adab8cbba1879af13eff3d0f6af390a29b13c6485eca113851012ae'
            '59991d9e55dab988668c644f856fb37bbb01d551ef9c8fe6bd51d414c0c1c1fadef0602409c37cd6f97cbb3fff4ea8cb74e6456cbde42f4e88e0fd46c9fd03bf')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../"${pkgname}-1.2.0-devendor-rtaudio-rtmidi.patch"
  mkdir -vp build
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -B build \
        -S .
  make -C build VERBOSE=1
}

package() {
  cd "$pkgname-$pkgver"
  make -C build DESTDIR="$pkgdir/" install
}
