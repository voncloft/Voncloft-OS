# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Esclapion <esclapion at gmail.com>

pkgname=qastools
pkgver=0.23.0
pkgrel=1
pkgdesc="A collection of desktop applications for the Linux sound system ALSA."
arch=('x86_64')
url="https://gitlab.com/sebholt/qastools"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base'
'qt5-svg')
makedepends=('alsa-lib' 'cmake' 'qt5-tools' 'systemd-libs')
source=("https://gitlab.com/sebholt/${pkgname}/-/archive/v${pkgver}/qastools-v${pkgver}.tar.gz")
sha512sums=('79042a41a0ad37097d297175b899af73052953b7ae14058a1878e3145769db839b8bd4350bd36646d8436a3e8b37b64da7b3004bee3c7c7780d46a942af1d3b9')

prepare() {
  mv -v "${pkgname}-v${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None'\
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libasound.so' 'libudev.so')
  cd "${pkgname}-${pkgver}"
  make -C build VERBOSE=1 DESTDIR="${pkgdir}/" install
  install -vDm 644 {CHANGELOG,README.md,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

