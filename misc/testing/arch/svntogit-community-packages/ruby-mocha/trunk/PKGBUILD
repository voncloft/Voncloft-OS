# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>

_gemname=mocha
pkgname="ruby-${_gemname}"
pkgver=1.11.2
pkgrel=2
pkgdesc='Mocha is a mocking and stubbing library for Ruby'
arch=('any')
url='https://github.com/freerange/mocha'
license=('Custom')
depends=('ruby')
checkdepends=('ruby-rake' 'ruby-bundler')
makedepends=('ruby-minitest')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/freerange/mocha/archive/v${pkgver}.tar.gz"
        'remove-unneeded-dependencies.patch')
sha512sums=('3c547c6b38eb6cdff06f9e7a7061d6f47d0181161852b9fec143b71244eb4b67c650dcf20e6ba88ce62f337b3c67e1120e88276ccd98983b403a10b9e57cf5af'
            '6a3c4568b6cf8009aed70935ddcff96a75d8d92b2e87a2a332a71ec569ed28c7c83e507ad408a0f62f93fc16b066133ba184f13a3e5b9a14bf90dc6276acde60')

prepare() {
  cd "${_gemname}-${pkgver}"

  # remove unneeded dependencies
  # bundler might require them if they are still in the gemspec file
  patch --forward --verbose --strip=1 --input='../remove-unneeded-dependencies.patch'

  # replace upper version boundaries for ruby gems
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec

  # `git` doesn't work within src dir, because we downloaded an archive
  sed --in-place 's|git ls-files|find|' "${_gemname}.gemspec"
}

check() {
  cd "${_gemname}-${pkgver}"
  rake test:units
}

build() {
  cd "${_gemname}-${pkgver}"
  gem build ${_gemname}.gemspec
}

package() {
  cd "${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install --install-dir "${pkgdir}${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"
  install -Dm 644 README.md RELEASE.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING.md MIT-LICENSE.md --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"

  # delete unneeded files
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  echo
  echo
  ls "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/"{test,yard-templates,gemfiles,bin,.github,.gemtest}
  echo
  echo
  rm -rf "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/"{test,yard-templates,gemfiles,bin,.github,.gemtest}
}

# vim: ts=2 sw=2 et:
