# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: James An <james@jamesan.ca>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: mortdeus <mortdeus@gocos2d.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Simon Rutishauser <simon.rutishauser@gmx.ch>

pkgname=htmldoc
pkgver=1.9.11
pkgrel=1
pkgdesc='HTML Conversion Software'
arch=('x86_64')
url='https://www.msweet.org/htmldoc/'
license=('GPL2')
depends=('fltk' 'gnutls' 'libjpeg-turbo' 'libpng' 'libxpm' 'zlib'
         'hicolor-icon-theme')
source=("https://github.com/michaelrsweet/htmldoc/releases/download/v${pkgver}/htmldoc-${pkgver}-source.tar.gz"{,.sig})
sha256sums=('5215afbf538730a3968b6126710c430e27b05ea7a43dd9f1c56b8d0dce5fb29b'
            'SKIP')
validpgpkeys=('845464660B686AAB36540B6F999559A027815955') # Michael R Sweet

build() {
    cd "${pkgname}-${pkgver}"
    ./configure \
        --prefix='/usr' \
        --enable-ssl \
        --enable-gnutls \
        --enable-cdsassl \
        --disable-localjpeg \
        --disable-localzlib \
        --disable-localpng \
        --with-gui
    make
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="$pkgdir" install
}
