# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-crypto
pkgver=0.0.20200303
_commit=78000ba7a073cafc0278790f6bce552a0f25850e
pkgrel=2
pkgdesc='Go supplementary cryptography libraries'
arch=('any')
url="https://github.com/golang/crypto"
license=('BSD')
depends=('go' 'golang-golang-x-net' 'golang-golang-x-sys')
source=("$pkgname-$_commit.tar.gz::https://github.com/golang/crypto/archive/$_commit.tar.gz")
sha512sums=('8d59096bb93936f03155b2b5520ad14c2ac3963c40638e0d715a566257058646cdb614d0afbff94a2d9c4f03228dc8cad58e6ea0e6f34a565dbfa27e9114eeeb')

prepare() {
  # Removed pinned version
  rm crypto-$_commit/go.{mod,sum}
}

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/crypto-$_commit "$srcdir"/build/src/golang.org/x/crypto
  cd "$srcdir"/build/src/golang.org/x/crypto
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/crypto-$_commit "$pkgdir"/usr/share/gocode/src/golang.org/x/crypto

  install -Dm644 crypto-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
