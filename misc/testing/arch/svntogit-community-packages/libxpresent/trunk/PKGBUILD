# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Andrey Vihrov <andrey.vihrov at gmail.com>

pkgname=libxpresent
pkgver=1.0.0
pkgrel=2
pkgdesc="X Present Extension library"
arch=('x86_64')
url="https://gitlab.freedesktop.org/xorg/lib/libxpresent"
license=('custom')
depends=('libxfixes' 'libxrandr')
makedepends=('xorg-util-macros' 'xorgproto')
source=("https://gitlab.freedesktop.org/xorg/lib/${pkgname}/-/archive/libXpresent-${pkgver}/${pkgname}-libXpresent-${pkgver}.tar.gz")
sha256sums=('86e280867498906ae617faaeefbef10e1ef7d367102950b497503a348bd5daab')

build() {
	cd "${pkgname}-libXpresent-${pkgver}"

  	./autogen.sh --prefix=/usr --disable-dependency-tracking
	
	# -Wl,--as-needed should come before all libraries
	sed -i -e '/\$CC/s/-shared/\0 -Wl,--as-needed/' libtool

	make
}

package() {
	cd "${pkgname}-libXpresent-${pkgver}"
	
	make DESTDIR="${pkgdir}" install
	install -D -m 0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
