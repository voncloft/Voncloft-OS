# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-mailman3
pkgname=python-django-mailman3
pkgver=1.3.5
pkgrel=1
pkgdesc="Libraries and templates to Django-based interfaces interacting with Mailman"
arch=('any')
url="https://gitlab.com/mailman/django-mailman3"
license=('GPL3')
depends=('python-django' 'python-django-allauth' 'python-django-gravatar'
'python-mailmanclient' 'python-pytz')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'python-mock' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('8bc16ca07a710e4dceb5678e056b39d14607928c9b25a321341c1b81ac519c6824f024607dd4ae7fe1ba866939100949d9ee8371f74e5bc3386bd89c677a2c14'
            'SKIP')
b2sums=('ddf78a2160777db65b74822a681d701124284161f8a27acf489dde2f91ddf8335626a3bcb5a644b23522969fb1519ec490ed0952eb402ddb8a0b943c3f4b8bd7'
        'SKIP')
validpgpkeys=('541EA0448453394FF77A0ECC9D9B2BA061D0A67C') # Abhilash Raj <raj.abhilash1@gmail.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  django-admin.py test --settings=django_mailman3.tests.settings_test django_mailman3
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
