# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=sshfs
pkgver=3.7.1
pkgrel=1
pkgdesc="FUSE client based on the SSH File Transfer Protocol"
arch=('x86_64')
url="https://github.com/libfuse/sshfs"
license=(GPL)
depends=(fuse3 glib2 openssh)
makedepends=('meson' 'python-docutils')
source=(https://github.com/libfuse/sshfs/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz{,.asc})
sha256sums=('fe5d3436d61b46974889e0c4515899c21a9d67851e3793c209989f72353d7750'
            'SKIP')
validpgpkeys=('ED31791B2C5C1613AF388B8AD113FCAC3C4E599F') # Nikolaus Rath <Nikolaus@rath.org>

prepare() {
  cd $pkgname-$pkgver

  rm -rf build
  mkdir build
  cd build
  arch-meson ..
}

build() {
  cd $pkgname-$pkgver/build
  ninja
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}
