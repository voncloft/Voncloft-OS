# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Leewings Ng <osideal@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-table-chinese
pkgver=1.8.3
pkgrel=1
pkgdesc="Some table-based input method of tables engines for IBus"
arch=('any')
url="https://github.com/definite/ibus-table-chinese"
license=('GPL3')
depends=('ibus-table')
makedepends=('cmake-fedora')
source=("$pkgname-$pkgver.tar.gz::https://github.com/definite/ibus-table-chinese/archive/$pkgver.tar.gz")
sha512sums=('610455343d9c9bf6577c7fd7aef9435c339c2f23c4d5f7ae2cbbc2366a9eb6a8a124e86621387497cc624e3276bf739b55602355ad41c4127f45f6d85cc5b4dc')

build() {
  cd $pkgname-$pkgver
  cmake . -DCMAKE_FEDORA_TMP_DIR=. -DDATA_DIR=/usr/share -DPRJ_DOC_DIR=/usr/share/doc/$pkgname -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
