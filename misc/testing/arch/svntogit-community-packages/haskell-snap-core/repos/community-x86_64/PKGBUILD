# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=snap-core
pkgname=haskell-snap-core
pkgver=1.0.4.2
pkgrel=118
pkgdesc="A Haskell Web Framework (core interfaces and types)"
url="https://github.com/snapframework/snap-core"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-old-locale' 'haskell-hunit' 'haskell-attoparsec'
         'haskell-case-insensitive' 'haskell-lifted-base' 'haskell-io-streams' 'haskell-hashable'
         'haskell-monad-control' 'haskell-random' 'haskell-readable' 'haskell-regex-posix'
         'haskell-transformers-base' 'haskell-unix-compat' 'haskell-unordered-containers'
         'haskell-vector' 'haskell-network' 'haskell-network-uri')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-parallel' 'haskell-test-framework'
             'haskell-test-framework-hunit' 'haskell-test-framework-quickcheck2' 'haskell-zlib')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        snap-core-fix-random-1.2.patch::https://github.com/snapframework/snap-core/pull/299.patch)
sha512sums=('42e710ac58a7d3cf58f9fdeca6c9c4880a94765ee0dc42fbcdaf68a6c2c337e1715f8b89863c7f911232af493bf5622b647d4c962237f211f97d731572fd94f5'
            '4c00e8bcf390c5e7367afe4879e34b5c5db8801080b63598cbfbda2cf73d4fc2180844383c8eabe814809d5a5a2d15c5f4da32a3ecea94a554d9dbedd858f501')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../snap-core-fix-random-1.2.patch
    sed -i -e '/bytestring-builder/d' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
