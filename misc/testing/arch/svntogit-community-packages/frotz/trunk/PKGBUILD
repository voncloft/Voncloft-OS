# Maintainer: Ivy Foster <iff@archlinux.org>

pkgbase=frotz
pkgname=(frotz-dumb frotz-ncurses frotz-sdl)
pkgver=2.51
pkgrel=1
pkgdesc='Z-machine interpreter for interactive fiction games'

makedepends=(
		libao
		libmodplug
		libsamplerate
		libsndfile
		libvorbis

		ncurses

		freetype2
		libjpeg-turbo
		libpng
		sdl2
		sdl2_mixer
		zlib
)

arch=(x86_64)
url='https://gitlab.com/DavidGriffith/frotz'
license=(GPL)
# In the interest of reproducibility, use the Makefile at a specific commit
source=(
	"https://gitlab.com/DavidGriffith/frotz/-/archive/$pkgver/frotz-$pkgver.tar.gz"
	https://gitlab.com/DavidGriffith/frotz/-/raw/588845ef704d2341bf5d3cd8e0c8850919b04500/Makefile
)
# Warning: checksums provided by packager, not upstream.
sha256sums=(
	7916f17061e845e4fa5047c841306c4be2614e9c941753f9739c5d39c7e9f05b
	91c09f404895855aefa7018575951720cd5ed6ce03586533d10344efee021f9c
)

build() {
	cd "frotz-$pkgver"
	make PREFIX=/usr dumb ncurses sdl
}

package_frotz-dumb() {
	pkgdesc='Z-machine interpreter for interactive fiction games, for dumb terminals or scripting'
	provides=(dfrotz)

	cd "frotz-$pkgver"
	# Inexplicably, the latest tarball lacks DESTDIR in its Makefile. It's back in git, though.
	make -f ../Makefile DESTDIR="$pkgdir" PREFIX=/usr install_dumb
}

package_frotz-ncurses() {
	pkgdesc='ncurses-based Z-machine interpreter for interactive fiction games'
	provides=(frotz)
	depends=(
		libao
		libmodplug
		libsamplerate
		libsndfile
		libvorbis

		ncurses
	)

	cd "frotz-$pkgver"
	# Inexplicably, the latest tarball lacks DESTDIR in its Makefile. It's back in git, though.
	make -f ../Makefile DESTDIR="$pkgdir" PREFIX=/usr install_frotz
}

package_frotz-sdl() {
	pkgdesc='Graphical Z-machine interpreter for interactive fiction games'
	provides=(sfrotz)
	depends=(
		libao
		libmodplug
		libsamplerate
		libsndfile
		libvorbis

		freetype2
		libjpeg-turbo
		libpng
		sdl2
		sdl2_mixer
		zlib
	)

	cd "frotz-$pkgver"
	# Inexplicably, the latest tarball lacks DESTDIR in its Makefile. It's back in git, though.
	make -f ../Makefile DESTDIR="$pkgdir" PREFIX=/usr install_sdl
}
