# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=primesieve
pkgver=7.6
pkgrel=1
pkgdesc="Fast prime number generator"
arch=(x86_64)
url="https://github.com/kimwalisch/primesieve"
license=(BSD)
depends=(gcc-libs)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/kimwalisch/primesieve/archive/v$pkgver.tar.gz")
sha256sums=('485669e8f9a6c74e528947d274df705f13caaf276d460d0f037b8dbc0c9c0a99')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_STATIC_LIBS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
