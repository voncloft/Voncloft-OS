# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=tpm2-pkcs11
pkgver=1.5.0
pkgrel=4
pkgdesc='PKCS#11 interface for Trusted Platform Module 2.0 hardware'
arch=('x86_64')
url='https://github.com/tpm2-software/tpm2-pkcs11'
license=('BSD')
depends=('libyaml' 'openssl' 'python' 'python-cryptography' 'python-pyasn1-modules' 'python-yaml' 'sqlite'
         'tpm2-tools' 'tpm2-tss' 'libtss2-esys.so' 'libtss2-fapi.so' 'libtss2-mu.so' 'libtss2-rc.so' 'libtss2-tctildr.so')
makedepends=('cmocka')
checkdepends=('expect' 'iproute2' 'java-hamcrest' 'junit' 'libp11' 'opensc'
              'python-python-pkcs11' 'swtpm' 'tpm2-abrmd' 'tpm2-tss-engine' 'wget' 'xxd')
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc}
        "tpm2-pkcs11-1.5.0_compatibility_tpm2-tss-engine-1.1.0.patch::$url/commit/969408022d1f61bd9a32388f842dae567beb407b.patch")
sha512sums=('999adf3ea031dfbb0eadefbd6b2c5457bff738bad231efa70218032f851fe5ba466e37bdf47b5bce380c378b97e88b2a76c9b59a264db5c5f380a9ba3cce58a0'
            'SKIP'
            '35e2f77079d362ffb5318844e9df3fc2dc2a4791ad146629800efa94226932949893735c019b897eb342277bdf2bc572307b75f0d5812b81b44aa12899abb7b5')
validpgpkeys=('5B482B8E3E19DA7C978E1D016DE2E9078E1F50C1') # William Roberts (Bill Roberts) <william.c.roberts@intel.com>

prepare() {
	cd "$pkgname-$pkgver"
	# Fix compatibility with tpm2-tss-engine 1.1.0 (https://github.com/tpm2-software/tpm2-pkcs11/pull/630)
	patch --forward --strip=1 --input="$srcdir/tpm2-pkcs11-1.5.0_compatibility_tpm2-tss-engine-1.1.0.patch"
}

build() {
	cd "$pkgname-$pkgver"
	export CLASSPATH="/usr/share/java/junit.jar:/usr/share/java/hamcrest-core.jar:$CLASSPATH"
	./configure --prefix=/usr --enable-unit $( ((CHECKFUNC)) && echo --enable-integration)
	make
	cd tools
	python setup.py build
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
	cd tools
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
