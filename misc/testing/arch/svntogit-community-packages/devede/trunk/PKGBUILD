# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Javier 'Phrodo_00' Aravena <phrodo.00@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=devede
_pkgname=devedeng
pkgver=4.16.0
pkgrel=2
pkgdesc='Program to create VideoDVDs and CDs'
arch=('any')
url="https://rastersoft.com/programas/devede.html"
license=('GPL3')
depends=('gtk3' 'python-cairo' 'python-gobject' 'python-setuptools' 'ffmpeg' 'cdrtools' 'dvdauthor' 'vcdimager')
makedepends=(git)
_commit=6fb3a369cdd742b74fbed29c91f2b935e0608645  # tags/4.16.0
source=("git+https://gitlab.com/rastersoft/devedeng#commit=$_commit"
        devedeng.appdata.xml)
sha256sums=('SKIP'
            'f7f4118256cff880004c0eff9be56239dea427ccd83d56a4c838cf17197528ea')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  cd $_pkgname
  python3 setup.py build
}

package() {
  cd $_pkgname
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 ../$_pkgname.appdata.xml "$pkgdir/usr/share/metainfo/$_pkgname.appdata.xml"
  rm -r "$pkgdir/usr/share/pixmaps/"
}
