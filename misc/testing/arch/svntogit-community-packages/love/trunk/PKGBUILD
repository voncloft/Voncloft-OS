# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Linus Sj√∂gren <thelinx@unreliablepollution.net>
# Contributor: Eric Forgeot < http://anamnese.online.fr >, dreeze
pkgname=love
pkgver=11.3
pkgrel=1
pkgdesc="An open-source 2D game engine which uses the versatile Lua scripting language to create dynamic gaming experiences"
arch=(x86_64)
url="http://love2d.org/"
license=('zlib')
depends=('luajit' 'freetype2' 'mpg123' 'openal' 'libvorbis' 'libmodplug' 'sdl2' 'shared-mime-info' 'hicolor-icon-theme' 'desktop-file-utils' 'libtheora')
source=("https://bitbucket.org/rude/love/downloads/love-${pkgver}-linux-src.tar.gz")
sha512sums=('d02900bbad658da7127bdbc52ceb8282a725b32d6a17fc51962327411be94f81b2c871d21b3c4bd8492a4ca355c3cfb1f04a7fe010d6c69ae35c7c5d64579e1b')

build() {
  cd "$srcdir"/love-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/love-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm0644 "license.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
