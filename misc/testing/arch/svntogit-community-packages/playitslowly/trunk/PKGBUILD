# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: mrxx <mrxx at cyberhome dot at>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: sausageandeggs <sausageandeggs@archlinux.us>

pkgname=playitslowly
pkgver=1.5.1
pkgrel=5
pkgdesc="Play back audio files at a different speed or pitch"
arch=(any)
url=https://29a.ch/playitslowly
license=(GPL3)
depends=(gst-plugins-bad gst-plugins-base gtk3 python-gobject)
optdepends=('gst-plugins-good: Extra media codecs'          
            'gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs')
source=(http://29a.ch/$pkgname/$pkgname-$pkgver.tar.gz
        playitslowly.appdata.xml)
sha256sums=('7dfe3da5417971183a13002d1e4ba4f30770baaf1ae3f9ef0ac66a6727f476aa'
            'ebe2890f7e42dfb7988d4304a4faee6d5657556f12bcebcd3279e9de86ec1ae4')

prepare() {
  cd $pkgname-$pkgver
  mkdir -p share/icons/hicolor/{32x32,128x128,scalable}/apps share/metainfo
  mv share/pixmaps/playitslowly.png share/icons/hicolor/128x128/apps/playitslowly.png
  mv share/icons/hicolor/32x32/playitslowly.png share/icons/hicolor/32x32/apps/playitslowly.png
  mv share/icons/hicolor/scalable/playitslowly.svg share/icons/hicolor/scalable/apps/playitslowly.png
  cp ../playitslowly.appdata.xml share/metainfo/playitslowly.appdata.xml
  rm -r share/pixmaps
}

build() {
  cd $pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}
