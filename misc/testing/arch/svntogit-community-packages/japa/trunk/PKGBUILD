# Maintainer: David Runge <dave@sleepmap.de>
pkgname=japa
pkgver=0.9.2
pkgrel=2
pkgdesc="A 'perceptual' or 'psychoacoustic' audio spectrum analyser"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/index.html"
license=('GPL2')
groups=('pro-audio')
depends=('clxclient' 'clthreads' 'fftw' 'jack' 'zita-alsa-pcmi')
makedepends=('gendesk')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('5930488ced374aca7c16ba6243145d941af4dd67f14e2eb94fc5014cf89ca12814a4445812fd1a36b0877d2063ed612044c7e584ecc11c3bb9a35f0ffbd682f8')

prepare() {
  cd "$pkgname-$pkgver"
  # disable optimizations
  sed -e '/native/d' -i source/Makefile
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name JAPA \
          --categories "AudioVideo;Audio"
}

build() {
  cd "$pkgname-$pkgver/source"
  make
}

package() {
  cd "$pkgname-$pkgver/source"
  make PREFIX='/usr' DESTDIR="$pkgdir/" install
  install -vDm 644 ../{AUTHORS,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # xdg desktop file
  install -vDm 644 ../${pkgname}.desktop \
    -t "${pkgdir}/usr/share/applications"
}
