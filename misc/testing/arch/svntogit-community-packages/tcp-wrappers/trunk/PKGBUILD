# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Manuel HÃ¼sers <manuel.huesers@uni-ol.de>
# Contributor: vigo <vigo.the.unholy.carpathian@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: judd <jvinet@zeroflux.org>

pkgbase=tcp-wrappers
pkgname=(
  libwrap
  tcp-wrappers
)
pkgver=7.6.28
pkgrel=2
pkgdesc='Monitors and Controls incoming TCP connections'
arch=(x86_64)
url=ftp://ftp.porcupine.org/pub/security/index.html
license=(BSD)
depends=(
  glibc
  libnsl
)
source=(
  ftp://ftp.porcupine.org/pub/security/tcp_wrappers_${pkgver%.*}.tar.gz
  http://http.debian.net/debian/pool/main/t/tcp-wrappers/tcp-wrappers_${pkgver%.*}.q-${pkgver##*.}.debian.tar.xz
  tcp-wrappers-redhat-bug11881.patch
  hosts.allow
  hosts.deny
)
sha256sums=('9543d7adedf78a6de0b221ccbbd1952e08b5138717f4ade814039bb489a4315d'
            '9e878177878b7796ab9e8cb4abb094bf66b3d37e0e1af3cee3e24afde4b1e11f'
            '2e527e54c1ea208de10a206c667f751e54651eb77c973271d213e3459d690403'
            'c1b19035a14ba552e8795ec3d7171e688592c9317f2eb14373320315435565e3'
            '969414f0a161e95fbe4cfe32df7c657a2793734d09416c00fa1116c5c0a9924f')

prepare() {
  cd tcp_wrappers_${pkgver%.*}

  for patch in $(cat ../debian/patches/series); do
    patch -Np1 -i ../debian/patches/${patch}
  done
  patch -Np1 -i ../tcp-wrappers-redhat-bug11881.patch
}

build() {
  cd tcp_wrappers_${pkgver%.*}

  make \
    COPTS="$CFLAGS" \
    LDOPTS="$LDFLAGS" \
    REAL_DAEMON_DIR=/usr/bin \
    STYLE='-DPROCESS_OPTIONS' \
    linux
}

package_libwrap() {
  provides=(tcp_wrappers-libs)
  conflicts=(tcp_wrappers-libs)

  cd tcp_wrappers_${pkgver%.*}

  install -Dm 644 tcpd.h -t "${pkgdir}"/usr/include/
  install -Dm 644 shared/libwrap.so.0.${pkgver%.*} -t "${pkgdir}"/usr/lib/
  ln -s libwrap.so.0.${pkgver%.*} "${pkgdir}"/usr/lib/libwrap.so.0
  ln -s libwrap.so.0 "${pkgdir}"/usr/lib/libwrap.so

  install -Dm 644 DISCLAIMER -t "${pkgdir}"/usr/share/licenses/libwrap/
}

package_tcp-wrappers() {
  depends=(libwrap)
  provides=(tcp_wrappers)
  conflicts=(tcp_wrappers)
  backup=(
    etc/hosts.allow
    etc/hosts.deny
  )

  cd tcp_wrappers_${pkgver%.*}

  install -Dm 755 safe_finger tcpd tcpdchk tcpdmatch try-from -t "${pkgdir}"/usr/bin/
  install -Dm 644 *.3 -t "${pkgdir}"/usr/share/man/man3/
  install -Dm 644 *.5 -t "${pkgdir}"/usr/share/man/man5/
  install -Dm 644 *.8 -t "${pkgdir}"/usr/share/man/man8/
  install -Dm 644 ../hosts.{allow,deny} -t "${pkgdir}"/etc/
  install -Dm 644 README -t "${pkgdir}"/usr/share/doc/tcp-wrappers/

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libwrap "${pkgdir}"/usr/share/licenses/tcp-wrappers
}

# vim: ts=2 sw=2 et:
