# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-boombuler-barcode
pkgver=1.0.0.20190219
_commit=6c824513baccd76c674ce72112c29ef550187b08
pkgrel=2
pkgdesc='A barcode creation lib for golang'
arch=('any')
url="https://github.com/boombuler/barcode"
license=('MIT')
depends=('go')
source=("$pkgname-$_commit.tar.gz::https://github.com/boombuler/barcode/archive/$_commit.tar.gz")
sha512sums=('441703efde7dac4689b95f8d521947796563d7310b95cd00bb9de57dca997cd42d1a9f06453d60d06076c03792e4f5cd2eecc9450612de3ff830a82dadfccbef')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/boombuler
  cp -a "$srcdir"/barcode-$_commit "$srcdir"/build/src/github.com/boombuler/barcode
  cd "$srcdir"/build/src/github.com/boombuler/barcode
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/boombuler
  cp -a "$srcdir"/barcode-$_commit "$pkgdir"/usr/share/gocode/src/github.com/boombuler/barcode

  install -Dm644 barcode-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
