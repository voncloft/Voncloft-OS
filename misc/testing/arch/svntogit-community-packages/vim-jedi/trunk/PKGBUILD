# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Danilo Bargen <gezuru@gmail.com>

pkgname=vim-jedi
_reponame=jedi-vim
pkgver=0.10.0
pkgrel=4
pkgdesc='Vim plugin for jedi, an awesome Python autocompletion'
url='https://github.com/davidhalter/jedi-vim'
arch=('any')
license=('MIT')
depends=('vim' 'python-jedi' 'python')
optdepends=('vim-supertab: tab completion'
            'python2-jedi: Python 2 support')
checkdepends=('python-pytest')
groups=('vim-plugins')
options=('!emptydirs')
source=(https://github.com/davidhalter/${_reponame}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        vim-jedi-update.patch)
sha256sums=('4a5d9e572d08ee4b7637adba5cda355273c35c04e83790e918ea66483a000ffc'
            'b003c64823ad63fbfc12d6684980f4c99ba977868cc179e5e3f724ff9721eea9')

prepare() {
  cd ${_reponame}-${pkgver}
  patch -p1 -i ../vim-jedi-update.patch
  sed -E 's|zipimport|zipimport\\nzipp|' -i test/vspec/pyimport.vim
}

check() {
  cd ${_reponame}-${pkgver}
  py.test
}

package() {
  cd ${_reponame}-${pkgver}

  install -d "${pkgdir}/usr/share/vim/vimfiles"
  cp -dpr --no-preserve=ownership pythonx "${pkgdir}/usr/share/vim/vimfiles"
  cp -dpr --no-preserve=ownership doc "${pkgdir}/usr/share/vim/vimfiles/doc"
  cp -dpr --no-preserve=ownership after "${pkgdir}/usr/share/vim/vimfiles/after"
  cp -dpr --no-preserve=ownership autoload "${pkgdir}/usr/share/vim/vimfiles/autoload"
  cp -dpr --no-preserve=ownership ftplugin "${pkgdir}/usr/share/vim/vimfiles/ftplugin"
  cp -dpr --no-preserve=ownership plugin "${pkgdir}/usr/share/vim/vimfiles/plugin"

  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
