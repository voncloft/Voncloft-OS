# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=yapf
pkgver=0.30.0
pkgrel=4
pkgdesc="Python style guide checker"
arch=('any')
url="https://github.com/google/yapf"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("https://github.com/google/yapf/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a282213d0171671a24d44b7a00b69f65163e7a7b8edfbede5cc38c809236a71f7adc8bfc3c4a1d1e66ac3cb2d9c6bb54b1303a2875ec09cdaddaa71d2039f84b')

build() {
  cd yapf-$pkgver
  python setup.py build
}

check() {
  cd yapf-$pkgver
  nosetests3
}

package() {
  cd yapf-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}
