# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: TDY <tdy@gmx.com>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=diffuse
pkgver=0.6.0
pkgrel=1
pkgdesc='Graphical tool for merging and comparing text files'
arch=(any)
url='https://github.com/MightyCreak/diffuse'
license=(GPL)
depends=(gtk3 python-cairo python-gobject)
makedepends=(git meson ninja)
source=("git+$url#commit=8eec2c49441e52456f64406a853790dfae804e58") # tag: v0.6.0
sha256sums=('SKIP')

build() {
  mkdir -p build
  arch-meson build $pkgname -D b_ndebug=true
  ninja -v -C build
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
