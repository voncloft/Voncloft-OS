# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule-docker
pkgver=0.3.3
pkgrel=2
pkgdesc="Molecule Docker Driver"
arch=('any')
url="https://github.com/ansible-community/molecule-docker"
license=('MIT')
depends=('molecule' 'python-docker' 'python-selinux')
makedepends=('python-setuptools_scm')
checkdepends=('python-pytest' 'python-pytest-helpers-namespace')
optdepends=('docker: for using a local docker setup')
# 0.3.3a1 == 0.3.3 (CD pipeline mess): https://github.com/ansible-community/molecule-docker/issues/24
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}a1.tar.gz")
sha512sums=('7a13e894f791d77f6ecadee1ebeea91ce368792a9526e14f5b5567d4de229266223f751637e46129deb0915e3446d7489595c98e91653291f17659ac8fe7d00b')
b2sums=('65dfef2f1c9f293ba97e342b6e61c4b9c2e2c40b1e2728b200b988b2d4645ac118b951bc862e36cf6aa13067a7a9c2fa9a711b1f2c7a8e41f89d31fe748657da')

prepare() {
  mv -v "${pkgname}-${pkgver}a1" "${pkgname}-${pkgver}"
  cd "$pkgname-$pkgver"
  sed -e '/pre-commit/d' \
      -e '/addopts/d' \
      -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # NOTE: we can only run the detection test, as the other tests require running docker
  pytest -v "${pkgname//-/_}/test/test_driver.py"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
