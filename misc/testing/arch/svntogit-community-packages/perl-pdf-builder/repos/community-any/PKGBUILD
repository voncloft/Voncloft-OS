# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: web.de jan.stuehler

pkgname=perl-pdf-builder
_perl_namespace=PDF
_perl_module=Builder
pkgver=3.019
pkgrel=1
pkgdesc="Facilitates the creation and modification of PDF files"
arch=('any')
url="https://metacpan.org/release/${_perl_namespace}-${_perl_module}"
license=('LGPL')
depends=('perl' 'perl-font-ttf')
checkdepends=('perl-test-exception' 'perl-test-memory-cycle')
source=("https://www.cpan.org/modules/by-module/${_perl_namespace}/${_perl_namespace}-${_perl_module}-${pkgver}.tar.gz")
sha512sums=('f1869a6ecb202a7661600e91ddc1ab1d21387979d7e42470ec4c663f2b36b8c04e12c475f5ba90a88ce43950723f73ce46c5d0057220d5a53e34cc16b22ce92e')
options=('!emptydirs')

build() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make pure_install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  # Delete unuseful files
  find "${pkgdir}" -name '.packlist' -delete
}
