# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

_name=libsamplerate
pkgname=lib32-libsamplerate
pkgver=0.1.9
pkgrel=3
pkgdesc="Secret Rabbit Code - aka Sample Rate Converter for audio (32-bit)"
arch=('x86_64')
url="http://www.mega-nerd.com/SRC/index.html"
license=('BSD')
depends=('lib32-glibc' 'libsamplerate')
makedepends=('gcc-multilib' 'lib32-libsndfile')
# checkdepends=('fftw')
provides=('libsamplerate.so')
source=("http://www.mega-nerd.com/SRC/${_name}-${pkgver}.tar.gz")
sha512sums=('78596657963cbf06785e3e6e1190b093df71da52ca340e75bd8246a962cd79dd1c90fa5527c607cebcb296e2c1ee605015278b274e3b768f2f3fbeb0eadfb728')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}
build() {
  cd "${pkgname}-${pkgver}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

# # requires lib32-fftw
# check() {
#   cd "${pkgname}-${pkgver}"
#   make -k check
# }

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rfv "${pkgdir}"/usr/{include,share,bin}
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
