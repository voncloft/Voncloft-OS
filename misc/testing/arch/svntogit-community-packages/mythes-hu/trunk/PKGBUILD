# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: AndyRTR <andyrtr@archlinux.org>

pkgname=mythes-hu
pkgver=1.0
_lo_ver=6.1.4
pkgrel=1
epoch=1
pkgdesc="Hungarian thesaurus"
arch=(any)
url="http://magyarispell.sourceforge.net/"
license=(GPL)
optdepends=('libmythes: offers thesaurus library functions')
source=(https://download.documentfoundation.org/libreoffice/stable/${_lo_ver}/rpm/x86_64/LibreOffice_${_lo_ver}_Linux_x86-64_rpm_langpack_hu.tar.gz{,.asc})
validpgpkeys=('C2839ECAD9408FBE9531C3E9F434A1EFAFEEAEA3') # "LibreOffice Build Team (CODE SIGNING KEY) <build@documentfoundation.org>"
sha256sums=('93f8b987189a0ac52cbee9eaca74597e059479a43604b3d3ec409072909e3ab3'
            'SKIP')

prepare() {
  bsdtar -x -f LibreOffice_*_Linux_x86-64_rpm_langpack_hu/RPMS/libreoffice*-dict-hu-*.x86_64.rpm
}

package() {
  cd opt/libreoffice*/share/extensions/dict-hu/
  install -Dm644 th_hu_HU_v2.dat "$pkgdir/usr/share/mythes/th_hu_HU_v2.dat"
  install -Dm644 th_hu_HU_v2.idx "$pkgdir/usr/share/mythes/th_hu_HU_v2.idx"

  # the symlinks
  install -dm755 "$pkgdir/usr/share/myspell/dicts"
  pushd "$pkgdir/usr/share/myspell/dicts"
    for file in "$pkgdir"/usr/share/mythes/*; do
      ln -sv /usr/share/mythes/$(basename $file) .
    done
  popd

  # docs
  install -Dm644 README_th_hu_HU_v2.txt "$pkgdir/usr/share/doc/$pkgname/README_th_hu_HU_v2.txt"
}
