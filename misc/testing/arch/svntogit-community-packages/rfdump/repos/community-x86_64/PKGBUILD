# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rfdump
pkgver=1.6
pkgrel=3
pkgdesc='Tool to detect RFID-Tags and show their meta information'
url='http://www.rfdump.org'
arch=('x86_64')
license=('GPL2')
depends=('libglade' 'gtk2' 'expat')
source=(http://www.rfdump.org/dl/${pkgname}-${pkgver}.tar.bz2)
sha512sums=('3402b9147b3e255b3c957d92f1fa4d7610fb6fde0aef2e3205343f64b252635bcf6345fb904b18ded063df775f15379a726b7b3b166b13d94efc9712373c7df7')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  LIBS=-lexpat ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
