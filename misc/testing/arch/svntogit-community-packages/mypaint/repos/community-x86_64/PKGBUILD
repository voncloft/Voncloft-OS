# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>

pkgname=mypaint
pkgver=2.0.0
pkgrel=2
pkgdesc="Fast and easy painting application for digital painters, with brush dynamics"
arch=('x86_64')
url="http://mypaint.org/"
license=('GPL')
depends=('gtk3' 'libmypaint' 'mypaint-brushes' 'python-cairo' 'python-gobject' 'python-numpy')
makedepends=('python-setuptools' 'swig')
options=('!emptydirs')
source=(https://github.com/mypaint/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz
        mypaint-fix-build.patch::https://github.com/mypaint/mypaint/commit/16d346dbe0e8cf73dda78d5165bfc3491695e3b1.patch)
#validpgpkeys=('C02391F4BBA4F0E2B27C6BFF6E3037E12878B299') # Andrew Chadwick
sha256sums=('d1d67866e342938c1e84b42da10bbed5f613f7e216f800fcc7c0e20200074a5e'
            'a4d3efdba24d9f357fc2dd8abc0d16b7cb071eae6bc34d33fcad877801a70bbd')

prepare() {
  cd $pkgname-$pkgver
  # Fix build
  patch -Np1 -i ../mypaint-fix-build.patch
}

build() {
  cd $pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}
