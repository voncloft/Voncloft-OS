# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Eric Förster <eric DOT foerster AT outlook DOT com>
# Contributor: Patrick Förster <patrick DOT foerster AT outlook DOT de>

pkgname=texlab
pkgver=2.2.2
pkgrel=1
pkgdesc="A cross-platform implementation of the Language Server Protocol for LaTeX."
arch=('x86_64')
url="https://texlab.netlify.com"
license=('MIT')
depends=('gcc-libs')
makedepends=('rustup' 'npm')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/latex-lsp/texlab/archive/v${pkgver}.tar.gz")
sha512sums=('a27777b1e29859449638a3913e95fe24d70c7780367dace12e13e075e5d48f24e032e2c1bd71fa0482e07a7e7e15177223873d6749aec2092d4498a4d2239646')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cargo test --release --all --locked
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm 755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
