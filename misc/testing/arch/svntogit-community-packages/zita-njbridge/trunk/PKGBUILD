# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zita-njbridge
pkgver=0.4.4
pkgrel=2
pkgdesc="Jack client to transmit audio over a local IP network."
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL3')
groups=('pro-audio')
depends=('jack' 'zita-resampler')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('ead0e3fae66581898d145c38c1ae51e40f2ea309bc87686ccdcb8a718f2a36f3490c79c1846af2e83c339162b03aab9051fd4a0023444233bba25a35ab1dacb4')

prepare() {
  cd "${pkgname}-${pkgver}/source"
  # strip march=native
  sed -e '/native/d' -i Makefile
}

build() {
  cd "${pkgname}-${pkgver}/source"
  make
}

package() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 ../{AUTHORS,README}
}
# vim:set ts=2 sw=2 et:
