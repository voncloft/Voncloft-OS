# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=rack-protection
pkgname=ruby-${_gemname}
pkgver=2.1.0
pkgrel=1
pkgdesc='Protect against typical web attacks, works with all Rack apps, including Rails'
url='http://sinatrarb.com/protection/'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rack')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-bundler' 'ruby-minitest')
checkdepends=('ruby-rspec')
options=('!emptydirs')
source=(https://github.com/sinatra/sinatra/archive/v${pkgver}/sinatra-${pkgver}.tar.gz)
sha512sums=('7127593bd44a5648407631f05d5db04100db30ae862c37e22dc264025fe271336c25df3c47839e41ba200da98be6c7b985852cd53d494a8bbda90fe701a89fe0')
b2sums=('05eea600695abf6ee9b2ef0eb2a215c106ac45a343a60017f2cdf8aba8733c1ba5c55235a9c35a4665659797754f78e28d6b8fb1e24391e99a2336ee150bacff')

prepare() {
  cd sinatra-${pkgver}/${_gemname}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec Rakefile
}

build() {
  cd sinatra-${pkgver}/${_gemname}
  rake build
}

check() {
  cd sinatra-${pkgver}/${_gemname}
  # TODO: requires rack-test
  # rake test
}

package() {
  cd sinatra-${pkgver}/${_gemname}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 License -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -r "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
