# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=rosegarden
pkgver=20.12
pkgrel=1
pkgdesc="MIDI/audio sequencer and notation editor"
arch=('x86_64')
url="https://www.rosegardenmusic.com/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'lirc' 'hicolor-icon-theme' 'qt5-base' 'zlib')
makedepends=('alsa-lib' 'cmake' 'dssi' 'fftw' 'jack' 'ladspa' 'liblrdf' 'liblo'
'libsamplerate' 'libsndfile' 'qt5-tools')
checkdepends=('xorg-server-xvfb')
optdepends=('cups: Printing support'
            'evince: Print preview support'
            'lilypond: Notation display support'
            'mupdf: Print preview support'
            'okular: Print preview support'
            'timidity++: Softsynth support'
            'flac: FLAC support'
            'wavpack: Lossless audio codec support')
provides=('dssi-host' 'ladspa-host')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('a2901d35ef7dac9358350fa47e47b9314698790bb2e2a95c6cc444135f49a92e3fb6d6cbf8710194bca6e57e1c82218991ae3db4aa7b6acea311cb3b627d3dc2')
b2sums=('f03862a8523dd43cc7e2b146446bb943229cfa1c598bc36601695465566d1037d360b15ac78802dbcc51c601e605abc9199f3006b3072e5e0ad2b49da9e15c28')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  mkdir -p test-tmp
  chmod 700 test-tmp
  export XDG_RUNTIME_DIR="${PWD}/test-tmp"
  xvfb-run make VERBOSE=1 test -C build || echo "TestNotationViewSelection still fails: https://sourceforge.net/p/rosegarden/bugs/1585/"
}

package() {
  depends+=('libasound.so' 'libfftw3f.so' 'libjack.so' 'liblrdf.so' 'liblo.so'
  'libsamplerate.so' 'libsndfile.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install -C build
  install -vDm 644 {AUTHORS,CHANGELOG,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
# vim:set ts=2 sw=2 et:
