# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qmidiarp
pkgver=0.6.5
pkgrel=5
pkgdesc="A MIDI arpeggiator, phrase generator and controller LFO for the ALSA sequencer."
arch=(x86_64)
url="https://sourceforge.net/projects/qmidiarp/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('alsa-lib' 'jack' 'liblo' 'lv2' 'qt5-tools')
checkdepends=('lv2lint')
optdepends=('jack: for standalone application'
            'liblo: for session management'
            'lv2-host: for LV2 plugins'
            'new-session-manager: for session management')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('171d74230a77b15453367fd38f9bcc94bbbd025a9665853180172bdfd812c2ee5ae0954124706d46c7e5cec0aafaebd5de68435f717789f052e7f86c48917379')
b2sums=('eb2acb80c6fd442b197510eef2be000dc6a8e4cf7142cebbbbd156dad464f80062cc0a41881476aedc41c1b7cff27b1c56ae64354134b255b6cbd7672ef182f0')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-qt5
  make
}

check() {
  local _name

  cd "${pkgname}-${pkgver}"
  for _name in arp lfo seq; do
    cp -v "src/.libs/${pkgname}_${_name}"{,_ui}.so \
      "${pkgname}_${_name}.lv2/"
    lv2lint -Mpack -I "${pkgname}_${_name}.lv2/" \
      "https://git.code.sf.net/p/qmidiarp/${_name}" || echo "Known to fail: https://sourceforge.net/p/qmidiarp/bugs/33/"
  done
}

package() {
  depends+=('libasound.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
