# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=giada
pkgver=0.17.0
pkgrel=1
pkgdesc="A free, minimal, hardcore audio tool for DJs, live performers and electronic musicians"
arch=('x86_64')
url="https://www.giadamusic.com/"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'libx11' 'libxpm')
# upstream vendors a hacked rtaudio :(
makedepends=('alsa-lib' 'cmake' 'fltk' 'gendesk' 'imagemagick' 'jack'
'libpulse' 'libsamplerate' 'libsndfile' 'libxrandr' 'nlohmann-json' 'rtmidi')
checkdepends=('catch2' 'xorg-server-xvfb')
source=("https://www.giadamusic.com/data/${pkgname}-${pkgver}-src.tar.gz"
        "$pkgname-0.17.0-rtmidi_cppflags.patch"
        "$pkgname-0.17.0-devendor_nlohmann_json.patch")
sha512sums=('1a94da20d0901315b05adc225aa3f21e83abaa27157b810bfcf9d55fa2fa77ebaea8305aa20e37851a21797f9a986dde38e76cdb70a0ef3ffdf3a6e0d82b32c6'
            'ae222bb63b0388ef1b02ff2cda0e589545c80fae26cbf06c04e0963a661e32b4d6746eea424359a44bb20e7568dbb5335359c5226c36d9c8b86d10130e83fedc'
            '5b4b6c6c421851f4da72b6fb4ff457156d3332c5c7a795edffe05386d48864830e7b3afb4011996a8cd5284d2fdbc2e0da3d590108e04d00227d25bc3127f506')
b2sums=('5dc1dc140f38c59964a67bc7dc4b0a6f0ccb91887d29e5e35eeb4700f2e1f53557b71a384e7a17b8757cb568b5ca2a02e3e29ffccceced9b6fba7d983524b499'
        'e38277a1c77b2bfe6249618524b15909a91dd33fc635e3698e240797d3a3464737060240fe3956a805855a29f3326216adf6df28726e267cc9a1d0f24bd5d7c8'
        '804fa149c6a3e6f860136a7abe0b1a4263355fb5c3b899643c7f5c87775035669aa22ace3b51729ee976b4a84f57c255ba23ba82367483a739a8baaf520130b7')

prepare() {
  mv -v "$pkgname-$pkgver-src" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name Giada \
          --categories "AudioVideo;Audio;Midi;Sequencer"

  # remove targets for missing files:
  # https://github.com/monocasual/giada/issues/431
  sed -e '/baseButton/d' -i Makefile.am

  # add rtmidi cppflags:
  # https://github.com/monocasual/giada/issues/417
  patch -Np1 -i ../"$pkgname-0.17.0-rtmidi_cppflags.patch"

  # devendor nlohmann-json
  patch -Np1 -i ../"$pkgname-0.17.0-devendor_nlohmann_json.patch"

  # fixing test includes to use system catch2
  sed -e 's|catch\.hpp|catch2/catch\.hpp|g' -i tests/*.cpp
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --target=linux \
              --enable-system-catch
  make
}

check(){
  cd "$pkgname-$pkgver"
  # move binary to expected directory:
  # https://github.com/monocasual/giada/issues/432
  mkdir -vp build
  cp -av "${pkgname}" build/
  xvfb-run -a make -k check
}

package() {
  depends+=('libasound.so' 'libfltk.so' 'libjack.so' 'libpulse.so'
  'libpulse-simple.so' 'librtmidi.so' 'libsamplerate.so' 'libsndfile.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # XDG integration
  install -vDm 644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
  install -vDm 644 "extras/${pkgname}-logo.svg" \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
