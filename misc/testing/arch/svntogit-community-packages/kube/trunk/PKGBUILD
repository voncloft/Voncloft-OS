# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kube
pkgver=0.8.0
pkgrel=2
pkgdesc="A modern groupware client based on QtQuick and Sink"
arch=(x86_64)
url="https://kube.kde.org/"
license=(LGPL)
depends=(sink gpgme qt5-quickcontrols2 qt5-webengine qt5-quickcontrols)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/unstable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"
        kube-kcalcore-19.08.patch)
sha256sums=('2961acf94d528cbf1650cc420add12d5ec2c684d9232043c9d6d2d55b9d39782'
            '329a9630159e8288b2a58044c45e831bfe768311e1cff8667778f31e4bf8a152')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kube-kcalcore-19.08.patch # Fix build with kcalcore 19.08
  find . -name CMakeLists.txt | xargs sed -e '/tests/d' -i # Don't build tests
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
