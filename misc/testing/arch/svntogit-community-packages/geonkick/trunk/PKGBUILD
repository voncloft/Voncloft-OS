# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=geonkick
pkgver=2.7.0
pkgrel=1
pkgdesc="A free software percussion synthesizer"
arch=('x86_64')
url="https://gitlab.com/iurie/geonkick"
license=('GPL3')
groups=('pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libx11')
makedepends=('cmake' 'jack' 'libsndfile' 'lv2' 'rapidjson' 'redkite')
checkdepends=('lv2lint')
optdepends=(
            'lv2-host: for LV2 plugin'
            'jack: for standalone application'
)
source=("https://gitlab.com/iurie/geonkick/-/archive/v${pkgver}/$pkgname-v$pkgver.tar.gz")
sha512sums=('ecbb39795ff8ce0f9ef16f773ac081eabebe70f796d37a8a37268a6550ab1b62977805aa8b38d445fc0677b6b5e7ea4fe5df2f3a5a115ce6941221bf192507f1')
b2sums=('d77106dd9cee9ed5051303be3cadf54723be8ff438b36da1ecbf3a51f98b0897e0013020daeaa15185637920593f9366c0ce0bf3ae23a83d63a0859988b8fbfd')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DGKICK_REDKITE_SDK_PATH='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "$pkgname-$pkgver"
  cp -av build/plugin/lv2/*.so plugin/lv2/${pkgname}.lv2/
  lv2lint -Mpack -I "${PWD}/plugin/lv2/${pkgname}.lv2/" \
          "http://geontime.com/geonkick"
  rm -v plugin/lv2/${pkgname}.lv2/*.so
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make -C build DESTDIR="$pkgdir/" install VERBOSE=1
  install -vDm 644 "data/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
