# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Christoph Brill <egore911@gmail.com>

_name=pytesseract
pkgname=python-pytesseract
pkgver=0.3.7
pkgrel=1
pkgdesc='Python wrapper for Google Tesseract'
arch=('any')
url=https://github.com/madmaze/pytesseract
license=('Apache')
depends=('python-pillow' 'python-setuptools' 'tesseract')
checkdepends=('python-pytest' 'tesseract-data-eng' 'tesseract-data-fra')
source=("$url/archive/v$pkgver/$_name-v$pkgver.tar.gz")
b2sums=('88096f3b4d36fe00dc0123e80d4bbd7fe83726c6aa8a5a72a8ba93e405493869fdcada66a9d10ec54195c278a799ac8fa6bbf9da868896ef760babdadd53ad7f')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  mkdir -p temp
  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --skip-build --root=temp
  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$sitepackages" pytest -k 'not test_image_to_data_common_output[dict]' # https://github.com/madmaze/pytesseract/issues/298
}

package() {
  cd $_name-$pkgver
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
