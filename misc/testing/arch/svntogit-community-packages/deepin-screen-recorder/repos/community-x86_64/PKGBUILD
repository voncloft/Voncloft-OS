# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screen-recorder
pkgver=5.8.1
pkgrel=1
pkgdesc='Deepin Screen Recorder'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-screen-recorder"
license=('GPL3')
depends=('byzanz' 'deepin-qt5integration' 'deepin-turbo' 'ffmpeg' 'kconfig' 'ki18n' 'kwindowsystem')
makedepends=('qt5-tools')
provides=('deepin-screenshot')
conflicts=('deepin-screenshot')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-screen-recorder/archive/$pkgver.tar.gz"
         deepin-screen-recorder-qt5.15.patch)
sha512sums=('e13a3d8dc6aa5d4212e770332993600a310ed71a0f16611772030f646155d5198dffe920ecbf21d14df39389f6a954787567b01a339b3ab80989582c4f75547c'
            'aaef789b5b1e74c9b2972bf94134b639393247ea4c6459e90d0b875d692b6adca71cafc27f1cc402fc81126eb47c52ae816867d7a3d2dfa775594dd839840247')

prepare() {
  cd deepin-screen-recorder-$pkgver

  # X11 header's weirdness with GCC 10
  sed -i '/include <X11.extensions.XTest.h>/a #undef min' src/event_monitor.cpp
  sed -i '/#include <iostream>/d;1i #include <iostream>' src/screen_shot_event.cpp
  sed -i '/include <X11.extensions.shape.h>/a #undef None' src/utils.cpp

  patch -p1 -i ../deepin-screen-recorder-qt5.15.patch # Fix build with Qt 5.15
}

build(){
  cd deepin-screen-recorder-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-screen-recorder-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
