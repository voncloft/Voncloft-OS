# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-sqlalchemy-migrate
pkgver=0.13.0
pkgrel=6
pkgdesc="Database schema migration for SQLAlchemy"
arch=('any')
url="https://pypi.python.org/pypi/sqlalchemy-migrate/$pkgver"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-sqlalchemy' 'python-decorator' 'python-sqlparse'
         'python-tempita')
checkdepends=('python-mock' 'python-testrepository' 'python-scripttest' 'python-ibm-db-sa')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://opendev.org/x/sqlalchemy-migrate/archive/$pkgver.tar.gz")
sha512sums=('3ae1140cb17ea24db7423a8cb62d667620333c8ed559f6bfd6eecfb35f829c9f9c4918f41f8f723d89bcb3c51d6b803e5cbdc30cb485c8cdfc140caaf1d981f0')

export PBR_VERSION=$pkgver

prepare() {
  cd sqlalchemy-migrate
  # Perhaps related to new testtools
  sed -i -e '/self.setUp/d' -e '/self.tearDown/d' migrate/tests/versioning/test_schema.py
}

build() {
  cd sqlalchemy-migrate
  python setup.py build
}

check() {
  cd sqlalchemy-migrate
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" python setup.py testr
}

package() {
  cd sqlalchemy-migrate
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
