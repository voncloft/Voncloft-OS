# Maintainer: David Runge <dvzrv@archlinux.org>

_version=1_1_0
pkgname=oscpack
pkgver=1.1.0
pkgrel=2
pkgdesc="Open Sound Control packet manipulation library"
arch=('x86_64')
url="https://github.com/RossBencina/oscpack"
license=('MIT')
depends=('gcc-libs' 'glibc')
provides=('liboscpack.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/RossBencina/${pkgname}/archive/release_${_version}.tar.gz"
        "$pkgname-1.1.0-makefile.patch")
sha512sums=('7a61a364cab4914c81e113d7aeee2b4accf5e560f500df6634232e0093f564ed4bb0ef8e87d2c8a18f245b0c7ec25f41e64f42f20a6654c22bb5c02aa253bbd0'
            '6a092a5eae7c52001175b59cb86bbbce0a8d565730edf38d8bcd18ed30ae1a0de4766a735a4a5702fa4210355b884a6f28991bfcba31b17be403d5c553c08b2c')

prepare() {
  mv -v "$pkgname-release_${_version}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # fixing makefile to include -fPIC when compiling, honoring LDFLAGS during
  # linking and using DESTDIR during install
  patch -Np1 -i "../$pkgname-1.1.0-makefile.patch"
}

build() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" PREFIX=/usr install
  install -vDm 644 {CHANGES,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
