# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-gdspy
pkgver=1.6.3
pkgrel=1
pkgdesc="Python module for creating GDSII stream files, usually CAD layouts"
url="https://github.com/heitzmann/gdspy"
license=('custom:BSL')
arch=('x86_64')
depends=('python-numpy')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/heitzmann/gdspy/archive/v$pkgver.tar.gz")
sha512sums=('d07ed779cfcf9d4b0384359ca3f5122c8799471eea3a607c208f794d6755fd698bfa20f4d3191a5b2370b63785a665f7c9775eee108906182dfee1179490ba75')

build() {
  cd gdspy-$pkgver
  python setup.py build
}

check() {
  cd gdspy-$pkgver
  python setup.py pytest
}

package() {
  cd gdspy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
