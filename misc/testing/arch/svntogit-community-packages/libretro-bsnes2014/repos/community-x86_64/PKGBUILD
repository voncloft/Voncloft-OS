# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-bsnes2014
pkgver=566
pkgrel=1
epoch=1
pkgdesc='Super Nintendo Entertainment System cores'
arch=(x86_64)
url=https://github.com/libretro/bsnes-libretro
license=(GPL3)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
source=(
  libretro-bsnes2014::git+https://github.com/libretro/bsnes-libretro#commit=feb8c10c672094e689ed057a278c2b354e113f32
  libretro-bsnes2014-flags.patch
)
sha256sums=(
  SKIP
  952646fac97a7899802e12415045650bff21aae508c1f66e220cfa2881f3ae02
)

pkgver() {
  cd libretro-bsnes2014

  git rev-list --count HEAD
}

prepare() {
  cd libretro-bsnes2014

  patch -Np1 -i ../libretro-bsnes2014-flags.patch
}

build() {
  for p in accuracy balanced performance; do
    make -C libretro-bsnes2014 profile=${p}
  done
}

package() {
  for p in accuracy balanced performance; do
    install -Dm 644 libretro-bsnes2014/out/bsnes2014_${p}_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  done
}

# vim: ts=2 sw=2 et:
