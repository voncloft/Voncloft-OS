# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ilya Elenskiy <elenskiy.ilya@gmail.com>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>

pkgname=python-rx
pkgver=3.1.1
pkgrel=3
pkgdesc='Reactive Extensions for Python'
arch=(any)
url=http://reactivex.io
license=(APACHE)
depends=(python)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-coverage
  python-nose
  python-pytest
  python-pytest-asyncio
)
_tag=f2642a87e6b4d5b3dc9a482323716cf15a6ef570
source=(git+https://github.com/ReactiveX/RxPY.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd RxPY

  git describe --tags | sed 's/^v//'
}

build() {
  cd RxPY

  python setup.py build
}

check() {
  cd RxPY

  python setup.py test
}

package() {
  cd RxPY

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
