# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=kresus
pkgver=0.16.0
pkgrel=1
pkgdesc="Self-hosted personal finance manager"
arch=(x86_64)
url="https://kresus.org"
license=(MIT)
backup=('etc/webapps/kresus/config.ini')
depends=(python nodejs-lts-erbium weboob)
makedepends=(yarn npm libpng)
optdepends=('python-pdfminer: La Banque Postale support')
source=(https://framagit.org/kresusapp/kresus/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2
        config.ini
        ${pkgname}.service
        ${pkgname}.sysusers
        ${pkgname}.tmpfiles)
sha256sums=('cf1757f317f1cc3cb659af0a76ed5ff2e0b00b9af19e7e30bec6518466ab6e97'
            '7f86ff72e3002c2906c45e762e073642b8405046f6af1dd049ad73860c7fd042'
            'cbbfcfc7714fa4c714e956fffa203511c47dde67e06dee9d87f8ff44ac3c708b'
            'd9d30f5470c7165e4917487b69d7ab82e463da4e1355056e1035ee501d3f1adc'
            'ba8ad7d9eb5d2b47fde5f6a3ab98596e5c679141b78d76d54b44830604b67632')

prepare() {
    cd ${pkgname}-${pkgver}
    yarn install
}

build() {
    cd ${pkgname}-${pkgver}
    yarn run build:prod
}

package() {
    cd ${pkgname}-${pkgver}

    DESTDIR="${pkgdir}" npm -g install kresus

    # Remove useless stuff
    rm -rf "${pkgdir}"/usr/lib/node_modules/kresus/node_modules/leveldown/prebuilds/{{android,linux}-arm{,64},{darwin,win32}-x64}
    rm -r "${pkgdir}"/usr/lib/node_modules/kresus/support/
    find "${pkgdir}"/usr/lib/node_modules/kresus/ -type d -name 'test*' -exec rm -rf {} \+
    find "${pkgdir}"/usr/lib/node_modules/kresus/ -type f \( -name '*\.md' -o -name 'LICENSE*' -o -name '\.*' \) -delete

    # Compile sqlite support
    #DESTDIR="${pkgdir}" npm -g install sqlite3
    #find "${pkgdir}"/usr/lib/node_modules/sqlite3/ -type d -name 'test*' -exec rm -rf {} \+
    #find "${pkgdir}"/usr/lib/node_modules/sqlite3/ -type f \( -name '*\.md' -o -name 'LICENSE*' -o -name '\.*' \) -delete

    # Fix npm crazyness
    chmod -R go-w "${pkgdir}"/usr
    chown -R root:root "${pkgdir}"/usr

    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/

    cd ..
    install -Dm600 config.ini -t "${pkgdir}"/etc/webapps/kresus/
    install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
    install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
}
