# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=clpeak
pkgver=1.1.0
pkgrel=1
pkgdesc="A tool which profiles OpenCL devices to find their peak capacities"
arch=('x86_64')
url="https://github.com/krrishnarraj/clpeak"
license=(custom:unlicense)
depends=('ocl-icd' 'gcc-libs')
makedepends=('cmake' 'opencl-headers')
source=("$pkgname-$pkgver.tar.gz::https://github.com/krrishnarraj/clpeak/archive/$pkgver.tar.gz")
sha512sums=('748c603397aa7ac3c1e4b5440312123a2e8442f50b8623ed22c277276ea279c69479c9005d6e2d3cf6f5500e25ee7a39ac9c2ea664c6041dd908e8d947f879f3')

prepare() {
    mkdir $pkgname-$pkgver/build
}

build() {
    cd "$pkgname-$pkgver/build"
    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "$pkgname-$pkgver"

    mkdir -p "$pkgdir"/usr/share/clpeak
    cp -r results "$pkgdir"/usr/share/$pkgname/results
    install -Dm755 build/clpeak "$pkgdir/usr/bin/clpeak"
    install -Dm644 README.md "$pkgdir/usr/share/$pkgname/README.md"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
