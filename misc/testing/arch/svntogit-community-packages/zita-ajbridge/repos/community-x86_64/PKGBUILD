# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Joakim Hernberg <jhernberg@alchemy.lu>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=zita-ajbridge
pkgver=0.8.4
pkgrel=1
pkgdesc="Jack client to use additional ALSA devices."
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/zita-ajbridge-doc/quickguide.html"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc')
makedepends=('jack' 'zita-alsa-pcmi' 'zita-resampler')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('591c75324fdb51d705b15b7cb93ff0ff098d354277262468faba2c45741c24e0749ae9fa9169d206a4d589c7fac33d05ed33cb6ea6d9d808b3f640c7e938f244')

build() {
  cd "${pkgname}-${pkgver}"
  make -C source
}

package() {
  depends+=('libjack.so' 'libzita-alsa-pcmi.so' 'libzita-resampler.so')
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="$pkgdir" install -C source
  # docs
  install -vDm 644 {AUTHORS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
# vim:set ts=2 sw=2 et:
