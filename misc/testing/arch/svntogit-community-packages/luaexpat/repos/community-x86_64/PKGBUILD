# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Contributor: Lucas Hermann Negri <kkndrox@gmail.com>

pkgbase=luaexpat
pkgname=(lua-expat lua51-expat lua52-expat lua53-expat)
pkgver=1.3.0
pkgrel=5
pkgdesc='SAX XML parser based on the Expat library'
arch=('x86_64')
url='http://matthewwild.co.uk/projects/luaexpat/'
license=('MIT')
depends=('expat')
makedepends=('gcc' 'lua' 'lua51' 'lua52' 'lua53')
source=("http://matthewwild.co.uk/projects/luaexpat/luaexpat-$pkgver.tar.gz")
sha512sums=('91884653310e2dc89ade6d1653875ac8607640a21853d3ccb1fd0f833812e41981fad5c40101732ec249104d2c50c9a332208d1e44423b8428065a223c60b4ae')

prepare() {
  cp -a $pkgbase-$pkgver $pkgbase-$pkgver-51
  cp -a $pkgbase-$pkgver $pkgbase-$pkgver-52
  cp -a $pkgbase-$pkgver $pkgbase-$pkgver-53
}

build() {
  # 5.1
  cd "$srcdir"/$pkgbase-$pkgver-51
  make LUA_V=5.1 COMMON_CFLAGS="$CPPFLAGS -fPIC"

  # 5.2
  cd "$srcdir"/$pkgbase-$pkgver-52
  make LUA_V=5.2 COMMON_CFLAGS="$CPPFLAGS -fPIC"

  # 5.3
  cd "$srcdir"/$pkgbase-$pkgver-53
  make LUA_V=5.3 COMMON_CFLAGS="$CPPFLAGS -fPIC"

  # 5.4
  cd "$srcdir"/$pkgbase-$pkgver
  make LUA_V=5.4 COMMON_CFLAGS="$CPPFLAGS -fPIC"
}

package_lua-expat() {
  cd $pkgbase-$pkgver
  make LUA_V=5.4 DESTDIR="$pkgdir" install
  install -Dm0644 doc/us/license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"
}

package_lua51-expat() {
  replaces=('luaexpat')
  conflicts=('luaexpat')

  cd $pkgbase-$pkgver-51
  make LUA_V=5.1 DESTDIR="$pkgdir" install
  install -Dm0644 doc/us/license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"
}

package_lua52-expat() {
  cd $pkgbase-$pkgver-52
  make LUA_V=5.2 DESTDIR="$pkgdir" install
  install -Dm0644 doc/us/license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"
}

package_lua53-expat() {
  cd $pkgbase-$pkgver-53
  make LUA_V=5.3 DESTDIR="$pkgdir" install
  install -Dm0644 doc/us/license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"
}
