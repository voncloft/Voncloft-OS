# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>

_pkgname=install
pkgname=python-$_pkgname
pkgver=0.0.3
pkgrel=1
pkgdesc='A simple, correct PEP427 wheel installer'
arch=('any')
url='https://github.com/FFY00/python-install'
license=('GPL')
depends=('python')
makedepends=('git' 'python-setuptools')
source=("git+$url#tag=$pkgver?signed"
        'git+https://github.com/pradyunsg/installer.git')
validpgpkeys=('3DCE51D60930EBA47858BA4146F633CBB0EB4BF2') # Filipe Laíns (FFY00) <lains@archlinux.org>
sha512sums=('SKIP'
            'SKIP')

prepare() {
  cd $pkgname

  git submodule init
  git config submodule.install/_vendor/installer.url "$srcdir"/installer
  git submodule update
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
