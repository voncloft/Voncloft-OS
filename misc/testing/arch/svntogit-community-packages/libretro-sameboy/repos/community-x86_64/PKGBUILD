# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-sameboy
pkgver=1288
pkgrel=1
pkgdesc='Nintendo Game Boy/Game Boy Color core'
arch=(x86_64)
url=https://github.com/libretro/SameBoy
license=(MIT)
groups=(libretro)
depends=(
  glibc
  libretro-core-info
  libgl
)
makedepends=(
  clang
  git
  libglvnd
  rgbds
  sdl2
)
options=(!makeflags)
_commit=f07b50968a473d13b201afc9d890b44ff33a7d19
source=(libretro-sameboy::git+https://github.com/libretro/SameBoy.git#commit=${_commit})
sha256sums=(SKIP)

pkgver() {
  cd libretro-sameboy

  git rev-list --count HEAD
}

build() {
  export CC=clang
  make -C libretro-sameboy/libretro
}

package() {
  install -Dm 644 libretro-sameboy/build/bin/sameboy_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-sameboy/LICENSE -t "${pkgdir}"/usr/share/licenses/libretro-sameboy/
}

# vim: ts=2 sw=2 et:
