# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-gopkg-yaml.v2
pkgver=2.3.0
pkgrel=2
pkgdesc='YAML support for the Go language'
arch=('any')
url="https://github.com/go-yaml/yaml"
license=('Apache')
depends=('go')
checkdepends=('golang-gopkg-check.v1')
source=("$pkgname-$pkgver.tar.gz::https://github.com/go-yaml/yaml/archive/v$pkgver.tar.gz")
sha512sums=('ba934e9cb5ebd2346d3897308b71d13bc6471a8dbc0dc0d46a02644ee6b6553d20c20393471b81025b572a9b03e3326bde9c3e8be156474f1a1f91ff027b6a4f')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/gopkg.in
  cp -a "$srcdir"/yaml-$pkgver "$srcdir"/build/src/gopkg.in/yaml.v2
  cd "$srcdir"/build/src/gopkg.in/yaml.v2
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/gopkg.in
  cp -a "$srcdir"/yaml-$pkgver "$pkgdir"/usr/share/gocode/src/gopkg.in/yaml.v2
}
