# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=options
pkgname=haskell-options
pkgver=1.2.1.1
pkgrel=15
pkgdesc="A powerful and easy-to-use command-line option parser"
url="https://john-millikin.com/software/haskell-options/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-monads-tf')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('615a3767b74ca0ba2eae993f6e475963df2a0933f1e6c9bbfe35144c4dc6018af276144b2ec1b608b695dc7fca904784ebc63cd58a432f966588846cb36f94ea')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

# Tests not enabled due to circular dependency on chell

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/license.txt"
}
