# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Riley Trautman <asonix.dev@gmail.com>
# Contributor: Michael Spencer <sonrisesoftware@gmail.com>

pkgname=qml-material
pkgver=0.2.0
pkgrel=2
pkgdesc="A UI framework for QtQuick implementing Material Design"
arch=("any")
url="https://github.com/papyros/qml-material"
license=("LGPL")
depends=("qt5-base" "qt5-declarative" "qt5-quickcontrols" "qt5-svg" "qt5-graphicaleffects")
makedepends=("git")
# checkdepends=("xorg-server-xvfb")
source=("git+https://github.com/papyros/qml-material.git#tag=v$pkgver")
sha256sums=("SKIP")

prepare() {
  mkdir -p build
}

build() {
  cd build
  qmake "$srcdir/$pkgname"
  make
}

check() {
  cd build
  ## libGL error: No matching fbConfigs or visuals found
  ## libGL error: failed to load driver: swrast
  ## Could not initialize GLX
  # xvfb-run make check
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install
  rm -r "$pkgdir/usr/tests"
}
