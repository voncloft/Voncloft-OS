# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Sebastien Luttringer <seblu+arch@seblu.net>
# Contributor: Jochen Immend≈ìrfer <jochen.immendoerfer@gmail.com>

pkgname=alsa-tools
pkgver=1.2.2
pkgrel=1
pkgdesc="Advanced tools for certain sound cards"
arch=('x86_64')
url="https://alsa-project.org/"
license=('GPL2')
depends=('gcc-libs' 'glibc')
makedepends=('alsa-lib' 'fltk' 'gtk2' 'gtk3')
optdepends=('fltk: hdspconf, hdspmixer'
            'gtk2: envy24control, echomixer, rmeddigicontrol'
            'gtk3: hdajackretask'
            'python-pyalsa: hwmixvolume'
            'python-gobject: hwmixvolume')
provides=("$pkgname-emu10k1" "$pkgname-ice1712" 'liblo10k1.so')
source=("https://www.alsa-project.org/files/pub/tools/$pkgname-$pkgver.tar.bz2")
sha512sums=('935d46ee3cc843566ccea183bcf7ea36439a4a6aa22964ebf6fd2722b78ec634d3be4e6a30b04c608abe6c5a132dd633f8343fff19f9d90a756d6699fc849a16')

# qlo10k1 is removed, because it's unable to build
_tools=( 'as10k1' 'envy24control' 'hdsploader' 'hdspconf' 'hdspmixer'
'mixartloader' 'pcxhrloader' 'rmedigicontrol' 'sb16_csp' 'seq/sbiload'
'sscape_ctl' 'us428control usx2yloader' 'vxloader' 'echomixer' 'ld10k1'
'hwmixvolume' 'hdajackretask' 'hda-verb' 'hdajacksensetest')

prepare() {
  cd "$pkgname-$pkgver"
  for tool in ${_tools[@]}; do
    (
    cd $tool
    autoreconf -vfi
    )
  done
}

build() {
  cd "$pkgname-$pkgver"
  # not using upstream's global makefile as it's trying to be too clever
  for tool in ${_tools[@]}; do
    (
    cd $tool
    ./configure --prefix=/usr --sbindir=/usr/bin
    make
    )
  done
}

package() {
  depends+=('libasound.so')
  cd "$pkgname-$pkgver"
  for tool in ${_tools[@]}; do
    (
    cd $tool
    make DESTDIR="$pkgdir" install
    )
  done
}

# vim:set ts=2 sw=2 et:
