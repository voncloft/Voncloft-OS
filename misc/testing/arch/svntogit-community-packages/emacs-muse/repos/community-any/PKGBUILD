# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  Gour <gour@mail.inet.hr>

pkgname=emacs-muse
_pkgname=muse
pkgver=3.20
pkgrel=7
pkgdesc="Publishing environment for Emacs"
url="https://www.gnu.org/software/emacs-muse/index.html"
arch=('any')
license=('GPL3')
depends=('emacs' )
install=${pkgname}.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/alexott/muse/archive/v$pkgver.tar.gz")
sha256sums=('2ef519db1c1119b2346d40ac8ea640143a5ea939d7b40ac3d142200dc275d584')

prepare() {
  cd "$srcdir"/muse-$pkgver
  # python2 fix
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' experimental/muse-mathml.el
  cp Makefile.defs.default Makefile.defs
  sed -i -e "s|\(PREFIX.*= \)/usr/local|\1/usr|" Makefile.defs
}

build() {
  cd "$srcdir"/muse-$pkgver
  make -j1
}

package() {
  cd "$srcdir"/muse-$pkgver
  make DESTDIR="$pkgdir" install
}
