# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=tldr
pkgver=1.2.0
pkgrel=1
pkgdesc="Command line client for tldr, a collection of simplified and community-driven man pages."
arch=('any')
url="https://github.com/tldr-pages/tldr-python-client"
license=('MIT')
depends=('python-argcomplete' 'python-colorama' 'python-termcolor')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/tldr-pages/tldr-python-client/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8d68cd10e9923c4cd76c7ce46de7e3a15062621154034768ae7539129021a9a8b60c4f0023982830443bf6eb3f66801ca02bc2698843aaba1ee62375e1a85438')

build() {
  cd tldr-python-client-$pkgver
  python setup.py build
}

check() {
  cd tldr-python-client-$pkgver
  python -m pytest
}

package() {
  cd tldr-python-client-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
