# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=mcp-plugins
pkgver=0.4.0
pkgrel=6
pkgdesc="A set of LADSPA filters plugins"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/ladspa/"
license=('GPL2')
groups=('ladspa-plugins' 'pro-audio')
depends=('gcc-libs')
makedepends=('ladspa')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/MCP-plugins-${pkgver}.tar.bz2")
sha512sums=('8a5e431ea539a46ce3d3f554fae21fc09028d898e24c316ab1b0d3d6540650107a1d10ce9a4cb6d1f0d2fd8868e2b43cf439f47ed6d90fb066375bbf3883d344')

build() {
  cd "MCP-plugins-${pkgver}"
  make
}

package() {
  cd "MCP-plugins-${pkgver}"
  # libs
  install -t "${pkgdir}/usr/lib/ladspa/" -Dm755 *.so
  # docs
  install -vDm 644 {AUTHORS,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
