# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-qrcode
pkgver=6.1
pkgrel=6
pkgdesc='Python library to generate QR codes'
url='https://github.com/lincolnloop/python-qrcode'
arch=('any')
license=('custom')
depends=('python-pillow' 'python-six' 'python-setuptools')
source=(https://github.com/lincolnloop/python-qrcode/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('3af3b67959be8dd30b372145ff7b8aa1e20153c403b17c688d5a7189e3b25da872c6eee916e6d47e4f6545c87c0c0eb9af52e115353a0e8c0f73e4948ddc6829')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  python -m unittest discover qrcode
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
