# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Joey Dumont <joey.dumont@gmail.com>
# Contributor: Giuseppe Borzi <gborzi@ieee.org>
# Contributor: Stefan Paquay <stefanpaquay@gmail.com>

pkgname=superlu
pkgver=5.2.2
pkgrel=2
pkgdesc="Set of subroutines to solve a sparse linear system"
arch=(x86_64)
url="https://github.com/xiaoyeli/superlu/"
license=(BSD)
depends=(blas)
makedepends=(cmake gcc-fortran)
source=("https://github.com/xiaoyeli/$pkgname/archive/v${pkgver}/$pkgname-$pkgver.tar.gz")
sha256sums=('470334a72ba637578e34057f46948495e601a5988a602604f5576367e606a28c')

prepare() {
  [[ -d build ]] || mkdir build
}

build() {
  cd build
  cmake -G "Unix Makefiles" ../$pkgname-$pkgver/ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -Denable_internal_blaslib:BOOL=OFF \
    -DCMAKE_INSTALL_INCLUDEDIR:PATH=include/superlu/
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 ../$pkgname-$pkgver/DOC/ug.pdf "$pkgdir/usr/share/doc/$pkgname/ug.pdf"
  install -Dm644 ../$pkgname-$pkgver/License.txt "$pkgdir/usr/share/licenses/$pkgname/License.txt"
}
