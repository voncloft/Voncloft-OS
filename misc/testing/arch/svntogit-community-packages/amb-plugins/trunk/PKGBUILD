# Maintainer : David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=amb-plugins
pkgver=0.8.1
pkgrel=7
pkgdesc="A set of LADSPA ambisonics plugins"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/ladspa/"
license=('GPL2')
groups=('ladspa-plugins' 'pro-audio')
depends=('gcc-libs')
makedepends=('ladspa')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/AMB-plugins-${pkgver}.tar.bz2")
sha512sums=('cdf0f6f656012f1e9d31f54d4446392223165327804f44c8d767904c51ab54a858099f8ad5f57362494d1a3cd73c4b258b4748022b5d9b3f860ab4bb84dd4a71')

prepare() {
  mv -v "AMB-plugins-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  # libs
  install -vDm 755 *.so -t "${pkgdir}/usr/lib/ladspa/"
  # docs
  install -vDm 644 {AUTHORS,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
