# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: DonVla <donvla@users.sourceforge.net>

pkgname=caps
pkgver=0.9.26
pkgrel=2
pkgdesc="The LADSPA C* Audio Plugin Suite"
arch=('x86_64')
url="http://quitte.de/dsp/caps.html"
license=('GPL3')
groups=('ladspa-plugins' 'pro-audio')
depends=('gcc-libs')
makedepends=('ladspa' 'python')
source=("http://quitte.de/dsp/${pkgname}_${pkgver}.tar.bz2")
sha512sums=('ce9a7219ef0c6c33a5debb407e9b6ea7432c3f50ab32c1915cf91c40cda541a812da887bf57ee536d653195158939889c4baf5fcbe3ded551abc676517e1da7a')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure.py
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX="$pkgdir/usr" install
  install -vDm 644 {CHANGES,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
