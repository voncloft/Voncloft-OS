# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=hardinfo
pkgver=0.5.1.816.g877ea2b
_commit=877ea2bc7777626c6fe77b6934a09261f1f1409e
pkgrel=2
pkgdesc="A system information and benchmark tool."
arch=('x86_64')
url="https://github.com/lpereira/hardinfo"
license=('GPL2')
depends=('gtk2')
makedepends=('cmake' 'git')
source=($pkgname::git+https://github.com/lpereira/hardinfo#commit=$_commit)
sha1sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long | sed "s/^release-//;s/^0.5-/0.5.1-/;s/-/./g"
}

prepare() {
  cd ${pkgname}
  mkdir build
}

build() {
  cd ${pkgname}/build
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    ..
  make
}

package() {
  cd ${pkgname}/build
  make DESTDIR="${pkgdir}" install
}
