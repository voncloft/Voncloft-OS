# Maintainer: David Runge <dave@sleepmap.de>
pkgname=luppp
pkgver=1.2.1
pkgrel=2
pkgdesc="A music creation tool, intended for live use"
arch=('x86_64')
url="http://openavproductions.com/luppp/"
license=('GPL2')
groups=('pro-audio')
depends=('jack' 'liblo' 'ntk')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openAVproductions/openAV-${pkgname}/archive/release-${pkgver}.tar.gz")
sha512sums=('8a0dc5688f7bc1868aca02205408cf31d380c0e230d8eae703e23c4f888f4a2738ba48540aec3940112ece576da59882036f3057fb04793af72add2ce04fb01f')

prepare() {
  mv -v "openAV-Luppp-release-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             build
  ninja -C build
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  #docs
  install -vDm 644 {CHANGELOG,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  # controllers
  install -vDm 644 "resources/controllers/"*.ctlr \
    -t "${pkgdir}/usr/share/${pkgname}/"
}
