# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Peter Mattern <pmattern at arcor dot de>

pkgname=feathernotes
pkgver=0.8.0
pkgrel=1
pkgdesc='Lightweight Qt hierarchical notes-manager'
arch=(x86_64)
url='https://github.com/tsujan/FeatherNotes'
license=(GPL3)
depends=(hicolor-icon-theme hunspell qt5-svg qt5-translations qt5-x11extras)
makedepends=(qt5-tools)
source=("https://github.com/tsujan/FeatherNotes/releases/download/V$pkgver/FeatherNotes-$pkgver.tar.xz"{,.asc}
        "feathernotes.appdata.xml")
sha256sums=('9b9a85ce82e67001c5e7b575f09ad4a4d2c14501d79532c902d8d10d7043fc41'
            'SKIP'
            '064e5dbf74cfa4c5932fbe6e516d29f8ed3caf489b30c2a01c71d488ac822ae6')
validpgpkeys=('19DFDF3A579BD509DBB572D8BE793007AD22DF7E')

build() {
  cd FeatherNotes-$pkgver
  qmake PREFIX=/usr WITH_HUNSPELL=YES
  make
}

package() {
  cd FeatherNotes-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 ../$pkgname.appdata.xml "$pkgdir/usr/share/metainfo/$pkgname.appdata.xml"
}
