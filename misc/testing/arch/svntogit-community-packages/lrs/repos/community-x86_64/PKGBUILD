# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=lrs
pkgver=071.a
_pkgver=${pkgver/./}
pkgrel=1
pkgdesc="C implementation of the reverse search algorithm for vertex enumeration/convex hull problems"
arch=(x86_64)
url="http://cgm.cs.mcgill.ca/~avis/C/lrs.html"
license=(GPL)
depends=(gmp)
source=("http://cgm.cs.mcgill.ca/~avis/C/lrslib/archive/lrslib-$_pkgver.tar.gz"
        lrs-system-cflags.patch
        lrs-missing-include.patch)
sha256sums=('926636ea68de46625f141f6e025dce967cc7e68cf4bf4a597375c063f5c11673'
            '8e43f9f6c583c653e28b75907f27f616f45f36d6fc677a4aae6a7004d60424c0'
            '61da0543e92ab242df1d5adcb6bb842a3c489c011c858705278de1c6cf580649')

prepare() {
  cd lrslib-$_pkgver
  patch -p1 -i ../lrs-system-cflags.patch # Use system CFLAGS/LDFLAGS
  patch -p1 -i ../lrs-missing-include.patch # Install missing header
}

build() {
  cd lrslib-$_pkgver
  make
}

package() {
  cd lrslib-$_pkgver
  make DESTDIR="$pkgdir" prefix=/usr install
}
