# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Filip Wojciechowski, filip at loka dot pl

pkgname=cpuburn
pkgver=1.4a
pkgrel=7
pkgdesc="CPU testing utilities in optimized assembler for maximum loading of CPUs"
arch=('x86_64')
url="https://launchpad.net/ubuntu/+source/cpuburn/"
license=('GPL')
depends=()
source=("https://launchpad.net/ubuntu/+archive/primary/+files/cpuburn_$pkgver.orig.tar.gz"
	cpuburn.patch)
md5sums=('a9e1df40c660324eb08e91847cbc41b9'
         '30f0eb72a0cb061955ab8dee8551d451')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../cpuburn.patch
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  for i in burn{BX,K6,K7,MMX,P5,P6}; do
    install -Dm755 $i "$pkgdir/usr/bin/$i"
  done
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm644 Design "$pkgdir/usr/share/doc/$pkgname/Design"
}
