# Maintainer: Chris Brannon <chris@the-brannons.com>
# Contributor: Kyle <kyle@gmx.ca>

pkgname=espeakup
pkgver=0.80
pkgrel=3
pkgdesc='Allows the Speakup screen review system to use the ESpeak synthesizer.'
arch=('x86_64')
url='https://github.com/williamh/espeakup'
license=('GPL3')
source=(https://github.com/williamh/espeakup/archive/v$pkgver.tar.gz
        espeakup.service espeakup.modules-load.d)
depends=('speakup-utils' 'espeak')

prepare() {
  cd $pkgname-$pkgver
  sed -e 's|/usr/local|/usr|' -i Makefile
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install 
  install -m644 -D "${srcdir}/espeakup.service" "${pkgdir}/usr/lib/systemd/system/espeakup.service"
  install -m644 -D "${srcdir}/espeakup.modules-load.d" "${pkgdir}/usr/lib/modules-load.d/espeakup.conf"
}

sha256sums=('4de284e30b6bdb0f9dee9c8ea02d13a3d20a61415b812338446b8b381e997ef4'
            '0d17413611db3fc4d70e34e84c1e55e832d9eec29482ed7d8c3d737e4ba162b8'
            '916abe0c03b843194705940ef062046809a68b51c5696f505d5444a73b45f214')
