# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aioredis
pkgver=1.3.1
pkgrel=1
pkgdesc="asyncio (PEP 3156) Redis client library"
url="https://github.com/aio-libs/aioredis"
license=('MIT')
arch=('any')
depends=('python-async-timeout' 'python-hiredis')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov' 'redis')
source=("https://github.com/aio-libs/aioredis/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('beeb4b087bb01b0efaa0ccc3601ea15674bb790a97a3a3f84b0848d13a6d2cb87af9baf4e44090cb4fc8cf0a5196912f2b632361d8628b911cbc5704f8eba63a')

build() {
  cd aioredis-$pkgver
  python setup.py build
}

check() {
  cd aioredis-$pkgver
  python -m pytest || echo "Tests failed"
}

package() {
  cd aioredis-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
