# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin "juster" Davis <jrcd83@gmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-xml-libxslt
pkgver=1.99
pkgrel=2
pkgdesc="Interface to the gnome libxslt library"
arch=('x86_64')
url="https://search.cpan.org/dist/XML-LibXSLT"
license=('GPL')
depends=('perl-xml-libxml' 'libxslt')
checkdepends=('perl-test-pod')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/S/SH/SHLOMIF/XML-LibXSLT-$pkgver.tar.gz)
sha512sums=('d716fe44673c83c5670a8f98cf82057e7df35d43ee452843adb7c605d36e7a42b35bdf26937a2e3d8c55494ac83c23f1421ab4481a4817c011074668bc951739')

build() {
  cd XML-LibXSLT-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd XML-LibXSLT-$pkgver
  make test
}

package() {
  cd XML-LibXSLT-$pkgver
  make DESTDIR="$pkgdir" install
}
