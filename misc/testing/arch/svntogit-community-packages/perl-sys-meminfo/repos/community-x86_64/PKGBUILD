# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: An Nguyen (stk) <an@linux.com>
# Contributor: Francesco Di Battista <teme.[nospace]84 -_AT_- the domain name google offers a mail service at ending in dot com>

pkgname=perl-sys-meminfo
pkgver=0.99
pkgrel=2
pkgdesc='Perl extension for return the total amount of physical memory'
arch=('x86_64')
url='https://metacpan.org/release/Sys-MemInfo'
license=('GPL')
depends=('perl')
options=('!emptydirs' purge)
source=("https://cpan.metacpan.org/authors/id/S/SC/SCRESTO/Sys-MemInfo-${pkgver}.tar.gz")
b2sums=('f45ebe017ff40cd612d69b9e804348bed8b78526c18b308a16accf7ffeec1263c2bd8ac6de8ffdf25f9c4e01562a9aa91563f159f0e76268e4bc4f188654a814')

build() {
  cd Sys-MemInfo-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd Sys-MemInfo-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd Sys-MemInfo-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
}
