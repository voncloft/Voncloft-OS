# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: onestep_ua <onestep@ukr.net>

pkgname=polkit-gnome
pkgver=0.105
pkgrel=5
pkgdesc="Legacy polkit authentication agent for GNOME"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/polkit/"
license=('LGPL')
depends=('polkit' 'gtk3')
makedepends=('intltool')
source=(https://download.gnome.org/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz
        polkit-gnome-authentication-agent-1.desktop)
sha512sums=('eae2504959bca6f2b53dbad1b743fc0414b82897e62623af80ee74c4080a10d6b07a4c90151ee1264891f27373321b8697ac8a747cba5254c765b4cd9161bd42'
            '8b24b7a39556e6a17284e052baa7af81237401e4010049839871ea5487643c92b2600f513213b9003a1a452492540e1d1cf6a5faf7a2ce5a28ad4f00e4b835aa')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/polkit-gnome
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/polkit-gnome-authentication-agent-1.desktop" \
      "$pkgdir/usr/share/applications/polkit-gnome-authentication-agent-1.desktop"
}
