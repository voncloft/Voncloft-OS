# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-power
pkgver=2.4.2
pkgrel=1
pkgdesc='Switchboard Power Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-power
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgee
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
source=(git+https://github.com/elementary/switchboard-plug-power.git#tag=633d85f2e9349e0d514e3f692286e22c4ba244cd)
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-power

  git describe --tags
}

build() {
  arch-meson switchboard-plug-power build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
