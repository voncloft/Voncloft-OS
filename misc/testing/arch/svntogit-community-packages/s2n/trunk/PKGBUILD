# Maintainer: Anatol Pomozov

pkgname=s2n
pkgver=0.10.25
pkgrel=1
pkgdesc='A C99 implementation of the TLS/SSL protocols that is designed to be simple, small, fast, and with security as a priority'
arch=(x86_64)
url='https://github.com/awslabs/s2n'
license=(Apache)
depends=(openssl)
makedepends=(cmake)
source=(s2n-$pkgver.zip::https://github.com/awslabs/s2n/archive/v$pkgver.zip
        missing_symbol_workaround.patch)
sha256sums=('2d6e229e9fb1e4fcc16aee24155089772176db4d3a32e6731634b7e4c27728ee'
            'd86e81598a79de7353262eeffc490c1e266dc8a92f7057166d447cd36d04eb35')

prepare() {
  cd s2n-$pkgver

  patch -p1 < ../missing_symbol_workaround.patch # https://github.com/awslabs/s2n/issues/2401
}

build() {
  cd s2n-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON -S . -B build
  cmake --build build
}

check() {
  cd s2n-$pkgver
  cmake --build build --target test
}

package() {
  cd s2n-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
