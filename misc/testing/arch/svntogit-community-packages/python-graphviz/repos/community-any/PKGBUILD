# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=graphviz
pkgname=python-graphviz
pkgver=0.16
pkgrel=1
pkgdesc="Simple Python interface for Graphviz"
arch=('any')
url="https://github.com/xflr6/${_pkgname}"
license=('MIT')
depends=('graphviz' 'python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-mock')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.zip")
sha256sums=('d2d25af1c199cad567ce4806f0449cb74eb30cf451fd7597251e1da099ac6e57')
b2sums=('abedbe170eaf9d3ad83f91b5ad13aa9fbda8669ac19ebbd1087aeab3f9401b0b8e6557313e04d791451ee501c75bc2843d89d1a0f789a219b0db771467b4b8bb')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # do not run python-coverage in unittests :/
    sed -i 's/--cov --cov-report=term --cov-report=html//' setup.cfg
    # don't add pointless dependency on python-mock needed on python 3.5
    sed -i '/mock_use_standalone_module/d' setup.cfg
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
