# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=fstrm
pkgver=0.6.0
pkgrel=2
pkgdesc="A C implementation of the Frame Streams data transport protocol"
arch=(x86_64)
url="https://github.com/farsightsec/fstrm"
license=(Apache)
depends=(libevent)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('82851b2424ae3e94b4fba310b60629b33d6757fbeb15662566b1857cd5c31517')

prepare() {
	cd ${pkgname}-${pkgver}
    autoreconf -vfi
}

build() {
	cd ${pkgname}-${pkgver}
	./configure --prefix=/usr
	make
}

check() {
	cd ${pkgname}-${pkgver}
	make -k check
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
