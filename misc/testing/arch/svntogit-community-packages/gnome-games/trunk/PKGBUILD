# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Tom Richards <tom@tomrichards.net>

pkgname=gnome-games
pkgver=3.38.0
pkgrel=1
pkgdesc='Simple game launcher for GNOME'
arch=('x86_64')
license=('GPL3')
url='https://wiki.gnome.org/Apps/Games'
depends=('grilo' 'libarchive' 'libhandy' 'libmanette' 'retro-gtk' 'tracker')
makedepends=('meson' 'vala' 'git')
_commit=d23a495c900ee59ca4853c872cdef942b509cf83  # tags/3.38.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-games.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
