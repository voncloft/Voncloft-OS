# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=xorg-xev
pkgver=1.2.4
pkgrel=1
pkgdesc='Print contents of X events'
url='https://xorg.freedesktop.org/'
arch=('x86_64')
license=('custom')
depends=('libx11' 'libxrandr')
makedepends=('xorg-util-macros' 'xorgproto')
source=(https://xorg.freedesktop.org/archive/individual/app/xev-${pkgver}.tar.bz2{,.sig})
sha512sums=('383472ddbfad639ba46a69080ecdd11a514a0578fd32c463fae18da02e0265c944589eebf12a858994f5fd601c02a0d51517e9b3ff61c6afe740d374f02781ca'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>
validpgpkeys+=('3BB639E56F861FA2E86505690FDD682D974CA72A') # "Matt Turner <mattst88@gmail.com>" 
groups=('xorg-apps' 'xorg')

build() {
	cd xev-${pkgver}
	./configure --prefix=/usr
	make
}

package() {
	cd xev-${pkgver}
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
