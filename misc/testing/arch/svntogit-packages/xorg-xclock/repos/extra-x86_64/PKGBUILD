# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xclock
pkgver=1.0.9
pkgrel=2
pkgdesc="X clock"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxmu' 'libxaw' 'libxrender' 'libxft' 'libxkbfile')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xclock-${pkgver}.tar.bz2{,.sig})
sha512sums=('fcbac0b4ab1cea4ae0751ce1c0c6302048e5d98d459873c7580e4ca786ec49fa27f73e5831bdfe135abb5cfeabe886456276e9d01351ea7905c606f40f84522d'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd "xclock-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "xclock-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
