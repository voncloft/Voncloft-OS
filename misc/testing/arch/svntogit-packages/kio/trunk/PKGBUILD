# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.78.0
pkgrel=3
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet ktextwidgets kded)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools qt5-doc)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'kio-fuse: to mount remote filesystems via FUSE')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
         kdebug-431731.patch::"https://invent.kde.org/frameworks/kio/commit/a183dd0d.patch")
sha256sums=('e211dba2c1ed73b67188d3ddbaf40043409912ab731aed4eba5bea32a587d620'
            'SKIP'
            '90d624ddcdae50f90e35417b0007a310241703060ac9edef0d035ce3f88c087f')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../kdebug-431731.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
