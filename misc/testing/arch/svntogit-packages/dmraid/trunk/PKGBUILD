# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
#Contributor: Urs Wolfer <uwolfer @ fwo.ch>

pkgname=dmraid
pkgver=1.0.0.rc16.3
pkgrel=13
pkgdesc="Device mapper RAID interface"
url="https://people.redhat.com/~heinzm/sw/dmraid/"
conflicts=('mkinitcpio<0.7')
depends=('device-mapper>=2.0.54')
arch=('x86_64')
license=('GPL')
source=(#https://sources.archlinux.org/other/dmraid/$pkgname-$pkgver.tar.bz2
        https://people.redhat.com/~heinzm/sw/dmraid/src/$pkgname-1.0.0.rc16-3.tar.bz2
        dmraid_install
        dmraid_hook
        dmraid_tmpfiles
        dmraid.service)
install=dmraid.install
md5sums=('819338fcef98e8e25819f0516722beeb'
         '7a040ebcba305aba1e47dfe6ca8323b5'
         'faec669dc85f87187b45b5d3968efe2c'
         '56a8bb0ece8d206cd8efb504ee072ddd'
         'ea6d280fc6f63fb799abcd882bca53c8')

build() {
  cd "$pkgname/1.0.0.rc16-3/$pkgname"
  ./configure --enable-led --enable-intel_led
  make -j1
}

package() {
  cd "$pkgname/1.0.0.rc16-3/$pkgname"
  make DESTDIR="$pkgdir" sbindir=/usr/bin prefix=/usr libdir=/usr/lib mandir=/usr/share/man includedir=/usr/include install
  install -D -m644 "$srcdir"/dmraid_install "$pkgdir"/usr/lib/initcpio/install/dmraid
  install -D -m644 "$srcdir"/dmraid_hook "$pkgdir"/usr/lib/initcpio/hooks/dmraid
  install -D -m644 "$srcdir"/dmraid_tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/dmraid.conf

  # fix permissions
  chmod 644 "$pkgdir"/usr/include/dmraid/* "$pkgdir"/usr/lib/libdmraid.a

  install -Dm644 "$srcdir/dmraid.service" "$pkgdir/usr/lib/systemd/system/dmraid.service"
}
