# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kaccounts-providers
pkgver=20.12.2
pkgrel=1
pkgdesc='Small system to administer web accounts for the sites and services across the KDE desktop, including: Google, Facebook, Owncloud, IMAP, Jabber and others'
arch=(x86_64)
url='https://www.kde.org/applications/internet/'
license=(GPL)
depends=(kaccounts-integration)
makedepends=(extra-cmake-modules intltool)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('804905e0ccedf53ba9c334a4a488cffeaaa5ea5bf983949565a67f3be6a00508'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
