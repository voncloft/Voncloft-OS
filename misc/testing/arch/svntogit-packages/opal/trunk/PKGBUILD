# Maintainer: Jan de Groot <jan@archlinux.org>
# Contributor: Tom K <tomk@runbox.com>
pkgname=opal
pkgver=3.10.11
pkgrel=17
pkgdesc="Open Phone Abstraction Library"
arch=(x86_64)
url="http://www.opalvoip.org"
license=(GPL)
depends=(ptlib libtheora x264 speex gsm celt)
options=(!makeflags)
source=(https://downloads.sourceforge.net/opalvoip/$pkgname-$pkgver.tar.bz2
        disable-samples-ftbfs.diff
        opal-gcc7.patch)
sha256sums=('f38e7969e88a28dd9a10a6051315aceecde79d0bbd1a35676868dc5787aed706'
            'f6497c74bbfd47fac036ff97dd74e8c87e6040d686944d29a926ccf3b7525be2'
            'cb7270055f1f2a29dec19ecab398ce0e75b1e440312e17df8a893308bfb3732c')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../disable-samples-ftbfs.diff
  patch -Np1 -i ../opal-gcc7.patch # Fix build with GCC 7
  sed -e '/<< mime.PrintContents/ s/mime/(const std::string\&)&/' -i src/im/msrp.cxx
}

build() {
  cd $pkgname-$pkgver
  CXXFLAGS="$CXXFLAGS -fpermissive" \
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-libavcodec
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
