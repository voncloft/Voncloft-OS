# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Jason Hall <cakersq@gmail.com>

pkgname=thin-provisioning-tools
pkgver=0.9.0
pkgrel=1
pkgdesc='Suite of tools for manipulating the metadata of the dm-thin device-mapper target'
arch=('x86_64')
url="https://github.com/jthornber/thin-provisioning-tools"
license=('GPL3')
depends=('expat' 'gcc-libs' 'libaio')
makedepends=('boost')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jthornber/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('a2508d9933ed8a3f6c8d302280d838d416668a1d914a83c4bd0fb01eaf0676e8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  autoconf
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" BINDIR="${pkgdir}"/usr/bin MANDIR=/usr/share/man install
}

