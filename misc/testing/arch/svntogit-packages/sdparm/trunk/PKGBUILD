# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=sdparm
pkgver=1.11
pkgrel=1
pkgdesc="An utility similar to hdparm but for SCSI devices"
arch=('x86_64')
url="http://sg.danny.cz/sg/sdparm.html"
license=('BSD')
depends=('glibc' 'bash')
source=(http://sg.danny.cz/sg/p/${pkgname}-${pkgver}.tar.xz)
sha512sums=('5b292e03e7260ea484871cb8cb779372a503c817de427623d2fde88dc175a2f0434f6bc782eaefd8d82af497fc75b5ca4386b22933e3e1333276e396944028a5')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
