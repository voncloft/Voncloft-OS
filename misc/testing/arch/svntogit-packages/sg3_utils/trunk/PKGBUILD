# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=sg3_utils
pkgver=1.45
pkgrel=1
pkgdesc="Generic SCSI utilities"
arch=(x86_64)
url="http://sg.danny.cz/sg/sg3_utils.html"
license=('GPL' 'custom:BSD')
depends=('glibc')
source=(http://sg.danny.cz/sg/p/${pkgname}-${pkgver}.tar.xz)
sha512sums=('fa25dedc447154d33937248a856af9a7ef380dda282a681d465a57d7dfc24b6c0409fa06aabf8cc28ba191e91db80fed0757b45212eddf713855574b48a97868')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
