# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: drrossum <youremail@domain.com>
pkgname=edac-utils
pkgver=0.18
pkgrel=3
pkgdesc="Userspace helper for Linux kernel EDAC drivers"
arch=(x86_64)
url='https://github.com/grondo/edac-utils'
license=('GPL2')
makedepends=('git')
depends=('perl' 'sysfsutils' 'dmidecode')
backup=('etc/edac/labels.db')
source=(git+https://github.com/grondo/edac-utils.git#tag=${pkgver}
        edac.service)

build() {
  cd "$srcdir/$pkgname"
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --mandir=/usr/share/man --infodir=/usr/share/info
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install
  # remove initscript
  rm -r "$pkgdir"/etc/init.d
  # add systemd service file
  install -D -m644 "$srcdir"/edac.service "$pkgdir"/usr/lib/systemd/system/edac.service
}
sha512sums=('SKIP'
            '1c960e41b6f723803e53b36cb25341d5ec1d7f9fa9bfde6fe0580fbdc6e94b76b2faa056bb723c7ae42e1484e5137bac51b8c88710b869d490384c9f9a06efb2')
