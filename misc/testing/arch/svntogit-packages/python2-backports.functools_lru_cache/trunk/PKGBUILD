# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Crass00 <crass00 @ hotmail . com>

pkgname=python2-backports.functools_lru_cache
pkgver=1.6.1
pkgrel=2
pkgdesc="Backport of functools.lru_cache from Python 3.3"
arch=('any')
url="https://github.com/jaraco/backports.functools_lru_cache"
license=('MIT')
depends=('python2-backports')
makedepends=('python2-setuptools-scm')
checkdepends=('python2-pytest')
source=("https://pypi.io/packages/source/b/backports.functools_lru_cache/backports.functools_lru_cache-$pkgver.tar.gz")
sha512sums=('5764cc6ed83dc4abc2a7e5f8b5bb30c7b7554aafcbb6dfcececdb3646fe9298a8c1791a02112238ec94a0e383855bdec43bf9094f40b57124d0a0c9aa7c67f88')

prepare() {
  cd backports.functools_lru_cache-$pkgver
  # Style or coverage checks are not relevant for us
  sed -i 's/--flake8 --black --cov//' pytest.ini
}

build() {
  cd backports.functools_lru_cache-$pkgver
  python2 setup.py build
}

check() {
  cd backports.functools_lru_cache-$pkgver
  python2 -m pytest
}

package() {
  cd backports.functools_lru_cache-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1

  rm "$pkgdir"/usr/lib/python2.7/site-packages/backports/__init__.py*
}
