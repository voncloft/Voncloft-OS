# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-integration-module
_pkgname=ktp-kded-module
pkgver=20.12.2
pkgrel=1
pkgdesc='Sits in KDED and takes care of various bits of system integration like setting user to auto-away or handling connection errors'
arch=(x86_64)
url='https://community.kde.org/Real-Time_Communication_and_Collaboration'
license=(GPL)
groups=(kde-applications kde-network telepathy-kde)
depends=(telepathy-kde-common-internals kidletime kactivities)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$_pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('2fbf95833206659b2d4b255613836971fa2ebf028fbab60723026f9768897bf2'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $_pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
