# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Hussam Al-Tayeb <ht990332@gmail.com>

pkgname=hunspell
pkgver=1.7.0
pkgrel=3
pkgdesc="Spell checker and morphological analyzer library and program"
arch=('x86_64')
url="https://hunspell.github.io/"
license=('GPL' 'LGPL' 'MPL')
depends=('gcc-libs' 'readline')
optdepends=('perl: for ispellaff2myspell')
source=($pkgname-$pkgver.tar.gz::https://github.com/hunspell/hunspell/archive/v${pkgver}.tar.gz)
sha256sums=('bb27b86eb910a8285407cf3ca33b62643a02798cf2eef468c0a74f6c3ee6bc8a')

prepare() {
  cd hunspell-$pkgver
  autoreconf -vfi
}

build() {
  cd hunspell-$pkgver
  ./configure --prefix=/usr --disable-static \
	--with-ui --with-readline # --with-experimental breaks build in this release
  make
}

check() {
  cd hunspell-$pkgver
  make check
}

package() {
  cd hunspell-$pkgver
  make DESTDIR="$pkgdir" install
  
  # add generic hunspell.so for development and projects not using pkgconfig flags - FS#30592
  pushd "$pkgdir"/usr/lib
  ln -s libhunspell-?.?.so libhunspell.so
  popd
}
