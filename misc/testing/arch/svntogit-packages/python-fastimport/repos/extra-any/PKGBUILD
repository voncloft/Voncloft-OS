# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=python-fastimport
pkgver=0.9.8
pkgrel=3
pkgdesc='VCS fastimport/fastexport parser'
arch=(any)
url=https://github.com/jelmer/python-fastimport
license=(GPL2)
depends=(python)
makedepends=(
  git
  python-setuptools
)
source=(git+https://github.com/jelmer/python-fastimport.git#tag=fd59e7d835b63d1c9181c4c84dd311d9d6da6d6b)
sha256sums=(SKIP)

pkgver() {
  cd python-fastimport

  git describe --tags | sed 's/fastimport-//'
}

build() {
  cd python-fastimport

  python setup.py build
}

package() {
  cd python-fastimport

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
