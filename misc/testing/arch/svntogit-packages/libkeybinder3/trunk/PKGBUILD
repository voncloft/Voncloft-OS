# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=libkeybinder3
pkgver=0.3.2
pkgrel=3
pkgdesc="A library for registering global keyboard shortcuts"
arch=('x86_64')
url="https://github.com/engla/keybinder/tree/keybinder-3.0"
license=('MIT')
depends=('gtk3')
makedepends=('gtk-doc' 'gobject-introspection')
optdepends=('lua-lgi: lua bindings')
source=(https://github.com/kupferlauncher/keybinder/releases/download/keybinder-3.0-v$pkgver/keybinder-3.0-$pkgver.tar.gz{,.sig}
        libkeybinder3-gtk-doc.patch::https://github.com/kupferlauncher/keybinder/pull/18.patch)
sha1sums=('d23c12440b54cb0f40e7e876c22001dc7b5714b0'
          'SKIP'
          '32333351bda812c4449e943f7da860fe443a4d0a')
validpgpkeys=('2578ABA1F9023159DFF211B7368712C4D7A44445') # Ulrik Sverdrup <ulrik.sverdrup@gmail.com>

prepare() {
  cd keybinder-3.0-$pkgver
  patch -p1 -i ../libkeybinder3-gtk-doc.patch
}

build() {
  cd keybinder-3.0-$pkgver
  ./configure --prefix=/usr --enable-gtk-doc
  make
}

package() {
  cd keybinder-3.0-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
