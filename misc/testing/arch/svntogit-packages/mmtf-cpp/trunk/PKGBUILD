# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mmtf-cpp
pkgver=1.0.0
pkgrel=2
pkgdesc="The pure C++ implementation of the MMTF API, decoder and encoder"
arch=(any)
url="http://mmtf.rcsb.org/"
license=(MIT)
depends=(msgpack-c)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/rcsb/mmtf-cpp/archive/v$pkgver.tar.gz")
sha256sums=('881f69c4bb56605fa63fd5ca50842facc4947f686cbf678ad04930674d714f40')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
