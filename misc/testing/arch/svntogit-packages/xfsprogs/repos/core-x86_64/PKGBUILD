# Maintainer : Tobias Powalowski <tpowa@archlinux.org>
pkgname=xfsprogs
pkgver=5.10.0
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=('x86_64')
license=('LGPL')
url="https://xfs.org"
depends=('sh' 'util-linux-libs' 'icu' 'device-mapper' 'libinih')
optdepends=('python: for xfs_scrub_all script'
            'smtp-forwarder: for xfs_scrub_fail script')
makedepends=('git')
options=('!makeflags')
source=("git+https://git.kernel.org/pub/scm/fs/xfs/xfsprogs-dev.git#tag=v${pkgver}")
md5sums=('SKIP')

build() {
  cd ${pkgname}-dev
  make configure
  export OPTIMIZER="-march=${CARCH/_/-} -O1"
  export DEBUG=-DNDEBUG
  ./configure --prefix=/usr --sbindir=/usr/bin --with-systemd-unit-dir=/usr/lib/systemd/system
  make
}

package() {
  cd ${pkgname}-dev
  make DIST_ROOT="${pkgdir}" PKG_ROOT_SBIN_DIR="/usr/bin" install install-dev
  chown -R root "$pkgdir"
  chgrp -R root "$pkgdir"
  # add hack as we cannot set rootlibdir
  mv "${pkgdir}"/lib/libhandle.so* "${pkgdir}/usr/lib/"
  rm -rf "${pkgdir}/lib"
}
