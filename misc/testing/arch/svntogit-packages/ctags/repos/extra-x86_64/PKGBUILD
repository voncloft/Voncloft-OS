# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=ctags
_commit=08b1c490cc4a20c3983314ae112c48edba0d51ed  # p5.9.20210110.0
pkgver=r20210106+g08b1c490
pkgrel=1
epoch=1
pkgdesc='Generates an index file of language objects found in source files'
url='https://ctags.io/'
arch=('x86_64')
license=('GPL')
depends=('glibc' 'libxml2' 'jansson' 'libyaml' 'libseccomp' 'libseccomp.so')
makedepends=('git' 'python-docutils')
provides=('universal-ctags')
source=("git+https://github.com/universal-ctags/ctags.git#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  git -C ctags show --pretty='r%cd+g%h' --date='format:%Y%m%d' --no-patch
}

prepare() {
  cd ctags
  autoreconf -fiv
}

build() {
  cd ctags
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib
  make
}

check() {
  make -C ctags check
}

package() {
  make -C ctags DESTDIR="${pkgdir}" install
}

# vim:set sw=2 et:
