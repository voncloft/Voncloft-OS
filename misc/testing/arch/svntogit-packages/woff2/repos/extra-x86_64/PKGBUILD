# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=woff2
pkgver=1.0.2
pkgrel=3
pkgdesc="Web Open Font Format 2 reference implementation"
url="https://github.com/google/woff2"
arch=(x86_64)
license=(custom:MIT)
depends=(brotli)
makedepends=(git cmake)
provides=(libwoff2{common,dec,enc}.so)
_commit=1bccf208bca986e53a647dfe4811322adb06ecf8  # tags/v1.0.2
source=("git+$url#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  cd $pkgname

  # Install binaries
  sed -i 's/NOT BUILD_SHARED_LIBS/TRUE/' CMakeLists.txt
}

build() {
  cmake -H$pkgname -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --build build --target install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/LICENSE
}
