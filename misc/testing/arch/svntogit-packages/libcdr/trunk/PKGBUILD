# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: megadriver <megadriver at gmx dot com>
# Contributor: Luca Bennati <lucak3 AT gmail DOT com>

pkgname=libcdr
pkgver=0.1.6
pkgrel=3
pkgdesc="CorelDraw file format importer library for LibreOffice"
arch=('x86_64')
url="https://wiki.documentfoundation.org/DLP/Libraries/libcdr"
license=('GPL2' 'LGPL2.1' 'MPL')
depends=('libwpd' 'lcms2' 'icu' 'librevenge')
makedepends=('libwpg' 'boost' 'doxygen' 'cppunit')
source=(https://dev-www.libreoffice.org/src/$pkgname/$pkgname-$pkgver.tar.xz
        icu68.patch)
sha256sums=('01cd00b04a030977e544433c2d127c997205332cd9b8e35ec0ee17110da7f861'
            'bd044f2d983d5a5ca513e7fcfde302d6ef3175f427e49b3fc7937167b6bbaacb')

prepare() {
  cd $pkgname-$pkgver
  patch -Np0 -i ../icu68.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
