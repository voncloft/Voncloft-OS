# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=eog-plugins
pkgver=3.26.6
pkgrel=1
pkgdesc="Plugins for Eye of Gnome"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/EyeOfGnome/Plugins"
license=(GPL2)
depends=(eog libchamplain libgdata python-gobject)
makedepends=(intltool gobject-introspection git)
_commit=4c99aece9953a92dbf1f80a1dc0f0ecb1af96d3f  # tags/3.26.6^0
source=("git+https://gitlab.gnome.org/GNOME/eog-plugins.git#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
