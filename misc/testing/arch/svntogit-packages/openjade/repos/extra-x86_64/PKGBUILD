# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=openjade
pkgver=1.3.2
pkgrel=6
_debpatch=13
pkgdesc="Implementation of the DSSSL language"
arch=('x86_64')
url="http://openjade.sourceforge.net/" # no https
license=('custom')
depends=('opensp' 'sgml-common')
makedepends=('perl-perl4-corelibs')
install=${pkgname}.install
conflicts=('jade')
provides=('jade')
source=(https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://deb.debian.org/debian/pool/main/o/${pkgname}1.3/${pkgname}1.3_${pkgver}-${_debpatch}.diff.gz)
sha256sums=('1d2d7996cc94f9b87d0c51cf0e028070ac177c4123ecbfd7ac1cb8d0b7d322d1'
            '11d90e242eae60ce06bf27fd234adbd8efd7d4a9a4a2da058faa4e8336dc423a')

prepare() {
  cd ${pkgname}-$pkgver
  patch -Np1 -i "$srcdir"/${pkgname}1.3_${pkgver}-${_debpatch}.diff
  # https://bugs.archlinux.org/task/55331 / https://gcc.gnu.org/bugzilla/show_bug.cgi?id=69534#c9
  export CXXFLAGS+=' -fno-lifetime-dse'
}

build() {
  cd ${pkgname}-$pkgver
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-default-catalog=/etc/sgml/catalog \
    --datadir=/usr/share/sgml/openjade-${pkgver} \
    --enable-html \
    --enable-http \
    --enable-mif
  make
}

package() {
  cd ${pkgname}-$pkgver
  make DESTDIR="$pkgdir/" install install-man 
  
  # add unversioned symlink
  ln -svf openjade-${pkgver} "${pkgdir}"/usr/share/sgml/openjade
  
  # openjade -> jade compat symlinks
  ln -svf /usr/bin/openjade "$pkgdir"/usr/bin/jade
  ln -svf /usr/lib/libogrove.so "$pkgdir"/usr/lib/libgrove.so
  ln -svf /usr/lib/libospgrove.so "$pkgdir"/usr/lib/libspgrove.so
  ln -svf /usr/lib/libostyle.so "$pkgdir"/usr/lib/libstyle.so
  ln -svf /usr/share/man/man1/openjade.1.gz "$pkgdir"/usr/share/man/man1/jade.1.gz
  
  # license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -dm755 "$pkgdir"/usr/share/sgml/openjade-${pkgver}
  install -m644 dsssl/builtins.dsl dsssl/dsssl.dtd \
    dsssl/style-sheet.dtd dsssl/fot.dtd \
    dsssl/catalog "$pkgdir"/usr/share/sgml/openjade-${pkgver}
}
