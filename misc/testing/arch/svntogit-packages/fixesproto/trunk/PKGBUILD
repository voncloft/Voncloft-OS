#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=fixesproto
pkgver=5.0+9+g4292ec1
pkgrel=1
pkgdesc="X11 Fixes extension wire protocol"
arch=('any')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('xproto' 'xextproto')
makedepends=('xorg-util-macros' git)
_commit=4292ec1c63180c5f4e7c0e606fa68c51913f172b  # master
source=("git+https://anongit.freedesktop.org/git/xorg/proto/fixesproto#commit=$_commit")
sha1sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^fixesproto-//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${srcdir}/$pkgname"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/$pkgname"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
