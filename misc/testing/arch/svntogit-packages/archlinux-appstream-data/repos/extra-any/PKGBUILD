# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20210125
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="https://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra community; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('a4d96797751f66288c5689d90a33fc89017a5049039d9d2a44a448bba9ddb3f7'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '7c1c62be2c65a82f66b4ee0c67ccb770963fa6e208526dda6d8837962eeb0c3d'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '7f38625be2806c0ab7e6a77d5ddfa567795398defd40e33feed9147bb1c3bbc7'
            '430b27ef92cbf0b05388126c02dd9f9445ecd266909594a9d4a2a3df37e5dc6e'
            'b1ddef21f7c252290e5554c04e6df4583680ef7e22a978f0cdc4fa91f5ad9a7f'
            'e3c030d8d33cb2f0c5df87df01b8e540e4d9a05ba5c807b87535910b5c32d380'
            '0884115a9ccdf19e2770eb5d55866e53333eaefc750782522f866eb3dc714e3b'
            'e80278ef3caef9a25c856037419452018863108fde733b49a9cdd006e4ce3b3a'
            '3b261eadec092877c143ad0eaedff7030d5a1dffaeee8ee823b1d91de0aff9de'
            'a12d35f37d0ed514e431ded5fece2c9ff6617efb53b3e30dc16d49bcd619aeab')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{48x48,64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/app-info/xmls/$_repo.xml.gz
  done
}
