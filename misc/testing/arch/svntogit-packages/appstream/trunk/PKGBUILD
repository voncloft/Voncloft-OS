# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: Tim Jester-Pfadt <t.jp<at>gmx.de>

pkgbase=appstream
pkgname=(appstream appstream-qt)
pkgver=0.14.0
pkgrel=4
pkgdesc="Provides a standard for creating app stores across distributions"
arch=(x86_64)
url="https://distributions.freedesktop.org/wiki/AppStream"
license=(GPL)
depends=(libyaml libxml2 libstemmer glib2 lmdb libsoup)
makedepends=(meson xmlto gobject-introspection gtk-doc qt5-tools itstool vala gperf)
source=("https://www.freedesktop.org/software/appstream/releases/AppStream-$pkgver.tar.xz"{,.asc}
        update-appstream-cache.hook
        appstream-crash-fix.patch::"https://github.com/ximion/appstream/commit/306eb287.patch")
sha256sums=('29eb617599eefec0b63c2894446e8cf7c5717f0285bab5df19fa53a2d5eecd16'
            'SKIP'
            '3a96a1479cfd18dad36c2ca3181aabe46af9bf772c00b965d86ec5f55cd0e0eb'
            '48e90e05d89af97487adc3343da13690df1c9fd97d1636769de7ef1d3b5acca5')
validpgpkeys=(D33A3F0CA16B0ACC51A60738494C8A5FBF4DECEB) # Matthias Klumpp <matthias@tenstral.net>

prepare() {
  patch -d AppStream-$pkgver -p1 -i ../appstream-crash-fix.patch # Fix crashes in discover and krunner
}

build() {
  meson build AppStream-$pkgver \
    --prefix=/usr \
    -Dqt=true \
    -Dvapi=true \
    -Dcompose=true
  ninja -C build
}

package_appstream() {
  DESTDIR="$pkgdir" ninja install -C build

# provided by -qt subpackage
  rm -r "$pkgdir"/usr/{include/AppStreamQt,lib/cmake,lib/libAppStreamQt.*}

  install -Dm644 update-appstream-cache.hook "$pkgdir"/usr/share/libalpm/hooks/update-appstream-cache.hook
}

package_appstream-qt() {
  pkgdesc='Qt5 interface for AppStream'
  depends=(appstream qt5-base)

  DESTDIR="$pkgdir" ninja install -C build

# provided by appstream
  rm -r "$pkgdir"{/etc,/usr/{bin,include/appstream{,-compose},lib/{girepository-1.0,libappstream*,pkgconfig},share}}
}
