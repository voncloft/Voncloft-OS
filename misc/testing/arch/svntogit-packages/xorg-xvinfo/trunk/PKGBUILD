# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xvinfo
pkgver=1.1.4
pkgrel=2
pkgdesc="Prints out the capabilities of any video adaptors associated with the display that are accessible through the X-Video extension"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxv')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xvinfo-${pkgver}.tar.bz2{,.sig})
sha512sums=('85e1f936c5c826f0fdde3670bd77d5cde8641c6e8e7849e42142e91b5aa01d7e0e60d2b064eb6fef4f5666321813ca2dc9f9656756ccbe6dcda37b119ee8a63e'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xvinfo-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xvinfo-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
