# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Veeti Paananen <veeti.paananen@rojekti.fi>

pkgname=cmark
pkgver=0.29.0
pkgrel=2
pkgdesc="CommonMark parsing and rendering library and program in C"
arch=('x86_64')
url="https://github.com/jgm/cmark"
license=('custom:BSD2')
depends=('glibc')
makedepends=('cmake' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jgm/cmark/archive/$pkgver.tar.gz" python3.8-normalize-test.patch)
sha512sums=('06eb110cfd90c9e980c022b7588e28864d15a4da5d07d61ad4b27c6de47367492b9e58e9434e62b07517aa6dc484f17af13916808be3188f38c37d20cbf33112'
            '74f2566b06fc9ea856d1ef784a1e72bd88eddcc2429d1009f43f6c1467eb6f0a4267c41fe93a585087b95032f72849209a0be9d97298b4a8129bb6329113b087')

prepare() {
  mkdir -p build

  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/python3.8-normalize-test.patch
}

build() {
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib ../cmark-$pkgver
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make install DESTDIR="$pkgdir"

  install -D -m 644 "$srcdir"/cmark-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
