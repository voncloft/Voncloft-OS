# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=gd
pkgver=2.3.1
pkgrel=1
pkgdesc="Library for the dynamic creation of images by programmers"
arch=('x86_64')
url="https://libgd.github.io/"
license=('custom')
depends=('fontconfig' 'libxpm' 'libwebp')
optdepends=('perl: bdftogd script')
checkdepends=('ttf-liberation')
source=("https://github.com/libgd/libgd/archive/${pkgname}-${pkgver}.tar.gz")
sha256sums=('795978dcec1bf931148ebcdca9ed1e9cdfa9e71acff8bf035a8867dd1f7af6cd')

build() {
  cd libgd-${pkgname}-${pkgver}

  ./bootstrap.sh
  ./configure \
    --prefix=/usr \
    --disable-rpath
  make
}

check() {
  cd libgd-${pkgname}-${pkgver}

  make check
}

package() {
  cd libgd-${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
