# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-astroid
pkgver=2.4.2
pkgrel=4
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://github.com/PyCQA/astroid"
license=('LGPL')
depends=('python-six' 'python-lazy-object-proxy' 'python-wrapt')
makedepends=('python-setuptools')
checkdepends=('python-dateutil' 'python-pytest' 'python-numpy' 'python-nose' 'python-pytest-runner')
replaces=('python-logilab-astng')
conflicts=('python-logilab-astng')
source=(https://github.com/PyCQA/astroid/archive/astroid-$pkgver.tar.gz
        $pkgname-py39-dict-union.patch::https://github.com/PyCQA/astroid/commit/d18469799108.patch)
sha512sums=('5306428e55bb8fec388353844afb842638114d6163096d4eb4335ac488a88b7a09a03d143e3ea991ea07371d9d87e911578981e4d74e45f6b3819e389f227ba0'
            '86be5f3646201d19d56b7c6bd19b642462a5d76b4b6584ee53ef27883e3d69839774b19b3f7b767f3903426cb0f8c838e70f8a94e32c5fe2b9d86a76fd0e323b')

prepare() {
  cd astroid-astroid-$pkgver
  patch -Np1 -i ../$pkgname-py39-dict-union.patch
  sed -i -e 's/six~=[0-9.*]*/six/;s/wrapt~=[0-9.*]*/wrapt/;s/lazy_object_proxy==[0-9.*]*/lazy_object_proxy/' astroid/__pkginfo__.py
}

build() {
  cd astroid-astroid-$pkgver
  python setup.py build
}

check() {
  cd astroid-astroid-$pkgver
  # Some tests fail on Python 3.9 (https://github.com/PyCQA/astroid/issues/845)
  pytest -k 'not test_namedtuple_few_args and not test_namedtuple_few_fields and not test_namedtuple_inference_nonliteral and not test_dataclasses_subscript_inference_recursion_error'
}

package() {
  cd astroid-astroid-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
