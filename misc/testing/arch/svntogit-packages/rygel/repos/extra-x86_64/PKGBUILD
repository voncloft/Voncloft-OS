# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=rygel
pkgver=0.40.0+6+g2843de82
pkgrel=1
epoch=1
pkgdesc="UPnP AV MediaServer and MediaRenderer"
url="https://wiki.gnome.org/Projects/Rygel"
arch=(x86_64)
license=(LGPL)
depends=(gupnp gupnp-av gupnp-dlna libgee gtk3 libunistring tracker3
         libmediaart gst-editing-services)
makedepends=(vala gobject-introspection git meson)
optdepends=('gst-plugins-base: Extra media codecs'
            'gst-plugins-good: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs'
            'tracker3-miners: Share indexed media files')
backup=(etc/rygel.conf)
groups=(gnome)
_commit=2843de8274493913ff3acdc2c539fcc76cf2d149  # rygel-0-40
source=("git+https://gitlab.gnome.org/GNOME/rygel.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^RYGEL//;s/^rygel-//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Add Tracker3 section to config
  git cherry-pick -n 732f982411f379065445658a288e5a5d06f1d7be
}

build() {
  arch-meson $pkgname build \
    -D plugins=external,gst-launch,lms,media-export,mpris,playbin,ruih,tracker3
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
