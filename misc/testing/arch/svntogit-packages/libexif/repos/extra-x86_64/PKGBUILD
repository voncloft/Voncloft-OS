# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=libexif
pkgver=0.6.22
pkgrel=1
pkgdesc='Library to parse an EXIF file and read the data from those tags'
url='https://github.com/libexif/libexif'
arch=(x86_64)
license=('LGPL')
depends=('glibc')
provides=('libexif.so')
source=(${url}/archive/${pkgname}-${pkgver//./_}-release.tar.gz)
sha512sums=('6c63abe2734c9e83fb04adb00bdd77f687165007c0efd0279df26c101363b990604050c430c7dd73dfa8735dd2fd196334d321bdb114d4869998f21e7bed5b43')
b2sums=('db424f537984c95cad73a69c7812009caa336b22ca69135cef9760a03f9a8f10dc727f8900edd58a7f3211f44e7ca2fa115575e33cb0c980bfc10f84d7e169fe')

prepare() {
  cd ${pkgname}-${pkgname}-${pkgver//./_}-release
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgname}-${pkgver//./_}-release
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgname}-${pkgver//./_}-release
  make DESTDIR="${pkgdir}" install
}
