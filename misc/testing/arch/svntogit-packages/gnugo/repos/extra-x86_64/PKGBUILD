# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: damir <damir@archlinux.org>

pkgname=gnugo
pkgver=3.8
pkgrel=7
pkgdesc='Program that plays the game of Go'
url='https://www.gnu.org/software/gnugo/'
license=('GPL3')
arch=('x86_64')
depends=('ncurses')
validpgpkeys=('67CCE2F5220D3E03024A175B68E7E09135C3EC3E')
source=("https://ftp.gnu.org/gnu/gnugo/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('da68d7a65f44dcf6ce6e4e630b6f6dd9897249d34425920bfdd4e07ff1866a72'
            'SKIP')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	rm -r "${pkgdir}"/usr/share/info
}
