# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=sysfsutils
pkgver=2.1.0
pkgrel=11
pkgdesc="System Utilities Based on Sysfs"
arch=(x86_64)
license=('GPL' 'LGPL')
url="http://linux-diag.sourceforge.net/Sysfsutils.html"
depends=('glibc')
source=(https://downloads.sourceforge.net/sourceforge/linux-diag/${pkgname}-${pkgver}.tar.gz)
sha512sums=('485902d98b41a69343cb037883d0c0a1de8a1a4aed657cd4528fe10bc845ac51629657dff01deef042e57c9bd9243095e23fc931d06d74db31b03c5f0a18cf07')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  rm "$pkgdir/usr/lib/libsysfs.a"
}
