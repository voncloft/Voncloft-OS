# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=kseexpr
pkgver=4.0.1.0
pkgrel=1
pkgdesc="An embeddable expression evaluation engine (Krita fork)"
arch=(x86_64)
url="https://krita.org"
license=(Apache)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-tools)
replaces=(seexpr-krita)
source=("https://download.kde.org/stable/kseexpr/${pkgver%.*}/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('13b8455883001668f5d79c5734821c1ad2a0fbc91d019af085bb7e31cf6ce926'
            'SKIP')
validpgpkeys=(4894424D2412FEE5176732A3FC00108CFD9DBF1E) # L. E. Segovia (GitHub signing key) <amy@amyspark.me>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_DEMOS=OFF \
    -DBUILD_TESTS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
