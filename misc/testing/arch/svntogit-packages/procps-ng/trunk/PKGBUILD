# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric Bélanger <eric@archlinux.org>

pkgname=procps-ng
pkgver=3.3.16
pkgrel=3
pkgdesc='Utilities for monitoring your system and its processes'
url='https://gitlab.com/procps-ng/procps'
license=(GPL LGPL)
arch=(x86_64)
depends=(glibc ncurses libncursesw.so systemd-libs)
makedepends=(systemd)
conflicts=(procps sysvinit-tools)
provides=(procps sysvinit-tools libprocps.so)
replaces=(procps sysvinit-tools)
source=(https://downloads.sourceforge.net/project/$pkgname/Production/${pkgname}-${pkgver}.tar.xz
        fs66093.patch)
sha256sums=('925eacd65dedcf9c98eb94e8978bbfb63f5de37294cc1047d81462ed477a20af'
            '48eb1f6e1b84d9dfec27556771c05f6a02880aefbe774a3db71bee0c35228992')
b2sums=('fbacc1edb0711cda0fecbf5f589c8cc18bd74f0430866e946f7c4612b78f383b9299840ab7fa5bbc65c581cc9484dbb37b111c493cf88f55ba247a343fd0c80d'
        '14e4dfce2d9915993bb3afb205bdb10f666de6fdef4e4e801254e1fc1909ec9561f443c500c2be3954b04eb5288018d52dba17fbc415ad613bd9c1e1d15dff64')

prepare() {
  cd $pkgname-$pkgver
  sed 's:<ncursesw/:<:g' -i watch.c

  # pgrep: check sanity of SC_ARG_MAX 
  # https://bugs.archlinux.org/task/66093
  patch -p1 -i ../fs66093.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --exec-prefix=/ \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --bindir=/usr/bin \
    --sbindir=/usr/bin \
    --enable-watch8bit \
    --with-systemd \
    --disable-modern-top \
    --disable-kill

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
