# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  Hyacinthe Cartiaux <hyacinthe.cartiaux at free.fr>
# Contributor: Bartlomiej Piotrowski <nospam at bpiotrowski dot pl>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=discount
pkgver=2.2.7
pkgrel=1
pkgdesc='A Markdown implementation written in C'
arch=(x86_64)
url="https://www.pell.portland.or.us/~orc/Code/discount/"
license=('custom:BSD')
source=($pkgname-$pkgver.tar.gz::"https://github.com/Orc/discount/archive/v$pkgver.tar.gz"
        discount-ldflags.patch::"https://github.com/Orc/discount/commit/55cea9c5.patch")
sha256sums=('72c1325ddfc40871d6810f1e272cf2d45b361f26357eb38f170fd04d737bb9f2'
            '7eced2472fdbe25badc13c35dfd6920b68b5a208889ee031ef0c060c093cc89b')
depends=(glibc)
conflicts=(markdown)
provides=(markdown)
replaces=(markdown)

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../discount-ldflags.patch # Honor system ldflags
}

build() {
  cd $pkgname-$pkgver
  ./configure.sh --prefix=/usr --enable-all-features --with-fenced-code --shared
  make -j1
}

package() {
  cd $pkgname-$pkgver
  # fix ldconfig call
  sed -e 's|/sbin/ldconfig|/sbin/ldconfig -n|' -i librarian.sh

  make DESTDIR="$pkgdir" install.everything
  install -Dm644 COPYRIGHT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
