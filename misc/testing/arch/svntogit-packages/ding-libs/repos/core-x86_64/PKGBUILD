# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Mantas M. <grawity@gmail.com>

pkgname=ding-libs
pkgver=0.6.1
pkgrel=3
pkgdesc="'D is not GLib' utility libraries"
arch=('x86_64')
url="https://pagure.io/SSSD/ding-libs/"
license=('GPL3' 'LGPL3')
depends=('glibc')
makedepends=('doxygen')
checkdepends=('check')
source=(https://releases.pagure.org/SSSD//$pkgname/$pkgname-$pkgver.tar.gz{,.asc}
       ding-libs-0.6.1-ini_validators_ut_check-glibc-2.28.patch)
sha1sums=('dab4c855b065bd728021437af81ae726c31c5272'
          'SKIP'
          '78ef3f3fcb25a9a98fcd82353d20b89000aee21a')
validpgpkeys=('7B54CAE8A03D66F3D70A5B516F5A90EB44FBC7C7'  # "Pavel Reichl <preichl@redhat.com>"
              'E4E366758CA0716AAB8048671EC6AB7532E7BC25'  # "Jakub Hrozek <jhrozek@redhat.com>"
              '16F24229488E736048952737BA88000FE6398272') # "Michal Å½idek (rh_work) <mzidek@redhat.com>"

prepare() {
  cd $pkgname-$pkgver
  # fix a test; FS#60089
  patch -Np1 -i ../ding-libs-0.6.1-ini_validators_ut_check-glibc-2.28.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make all docs
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
} 
