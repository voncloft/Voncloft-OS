# Maintainer: 
# Committer: Judd Vinet <jvinet@zeroflux.org>

pkgname=dosfstools
pkgver=4.1
pkgrel=3
pkgdesc="DOS filesystem utilities"
arch=(x86_64)
depends=('glibc')
source=(https://github.com/$pkgname/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz{,.sig}
)
url="https://github.com/dosfstools/dosfstools"
license=('GPL2')
validpgpkeys=('25714AECDBFDACEE1CE95FE77F6022516E869F64') # Andreas Bombe
md5sums=('07a1050db1a898e9a2e03b0c4569c4bd'
         'SKIP')


build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib \
              --sbindir=/usr/bin --mandir=/usr/share/man \
              --docdir=/usr/share/doc/dosfstools --enable-compat-symlinks
   make
}

package () {
   cd $pkgname-$pkgver
   make DESTDIR="$pkgdir" install
}
