# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=xfce4-screensaver
pkgver=4.16.0
pkgrel=1
pkgdesc='Xfce Screensaver'
arch=('x86_64')
url='https://docs.xfce.org/apps/screensaver/start'
license=('GPL2')
groups=('xfce4-goodies')
depends=('libxss' 'libxklavier' 'libwnck3' 'garcon' 'dbus-glib' 'python' 'python-gobject')
makedepends=('intltool')
backup=('etc/pam.d/xfce4-screensaver')
source=("https://archive.xfce.org/src/apps/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('6d4d143e3e62db679ce83ce7da97903390773ee0a8ceb05ff4c3dac36616268d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
