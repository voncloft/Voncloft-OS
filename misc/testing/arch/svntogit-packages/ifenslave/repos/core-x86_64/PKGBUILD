# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=ifenslave
pkgver=1.1.0
pkgrel=11
pkgdesc='Utility for bonding ethernet interfaces'
url='https://www.linuxfoundation.org/en/Net:Bonding'
arch=('x86_64')
license=('GPL2')
makedepends=('linux-api-headers')
source=(ifenslave.c::'https://git.kernel.org/?p=linux/kernel/git/torvalds/linux-2.6.git;a=blob_plain;f=Documentation/networking/ifenslave.c;h=450faacc')
sha256sums=('1c2432a121d9c752175af75b26575b79c3c50d90195ce71c9e736d831f1e50e1')

build() {
	cd "${srcdir}"
	gcc -Wall -Wstrict-prototypes ${CFLAGS} ${LDFLAGS} ifenslave.c -o ifenslave
}

package() {
	cd "${srcdir}"
	install -Dm755 ifenslave "${pkgdir}"/usr/bin/ifenslave
}
