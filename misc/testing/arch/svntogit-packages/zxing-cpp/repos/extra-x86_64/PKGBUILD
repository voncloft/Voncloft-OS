# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Maxime Arthaud <maxime@arthaud.me>

pkgname=zxing-cpp
pkgver=1.1.1
pkgrel=1
pkgdesc="A C++ library to decode QRCode"
arch=(x86_64)
url="https://github.com/nu-book/zxing-cpp"
license=(Apache)
depends=(gcc-libs)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/nu-book/zxing-cpp/archive/v$pkgver.tar.gz")
sha256sums=('e595b3fa2ec320beb0b28f6af56b1141853257c2611686685639cebb3b248c86')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_EXAMPLES=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
