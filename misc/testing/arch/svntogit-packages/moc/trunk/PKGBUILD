# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=moc
pkgver=2.5.2
pkgrel=3
epoch=1
pkgdesc="An ncurses console audio player designed to be powerful and easy to use"
arch=('x86_64')
url="https://moc.daper.net/"
license=('GPL')
depends=('libmad' 'libid3tag' 'jack' 'curl' 'libltdl' 'file')
makedepends=('speex' 'ffmpeg' 'taglib' 'libmpcdec' 'wavpack' 'libmodplug' 'faad2')
optdepends=('speex: for using the speex plugin'
	    'ffmpeg: for using the ffmpeg plugin'
	    'taglib: for using the musepack plugin'
	    'libmpcdec: for using the musepack plugin'
            'wavpack: for using the wavpack plugin'
            'faad2: for using the aac plugin'
	    'libmodplug: for using the modplug plugin')
source=(http://ftp.daper.net/pub/soft/moc/stable/${pkgname}-${pkgver}.tar.bz2{,.sig}
        moc-ffmpeg4.patch
        moc-https.patch)
sha1sums=('9d27a929b63099416263471c16367997c0ae6dba'
          'SKIP'
          '007a0580ac754e1c318a0d0b6f0d403883797eaf'
          'e3362ddd41126e2be874cd372a053fdaccf0f616')
validpgpkeys=('59359B80406D9E73E80599BEF3121E4F2885A7AA')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i ../moc-ffmpeg4.patch # Fix build with ffmpeg 4
  patch -p0 -i ../moc-https.patch   # Allow https for urls https://moc.daper.net/node/1872
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --without-rcc \
    --with-oss --with-alsa --with-jack --with-aac --with-mp3 \
    --with-musepack --with-vorbis --with-flac --with-wavpack \
    --with-sndfile --with-modplug --with-ffmpeg --with-speex \
    --with-samplerate --with-curl  --disable-cache --disable-debug
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
