# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Matt Harrison <matt@harrison.us.com>
# Contributor: Erik van der Kolk <developer at smerik dot nl>
# Contributor: Hong Xu

pkgname=editorconfig-core-c
pkgver=0.12.4
pkgrel=1
pkgdesc="EditorConfig core code written in C (for use by plugins supporting EditorConfig parsing)"
arch=(x86_64)
url="https://github.com/editorconfig/editorconfig-core-c"
license=(BSD)
depends=(pcre2)
makedepends=(cmake doxygen)
source=($pkgname-$pkgver.tar.gz::"https://github.com/editorconfig/${pkgname}/archive/v$pkgver.tar.gz")
sha256sums=('c2671595f1793b498cdf50b9dc03d632cc724891de7909f2ea78588fbffba289')

build() {
  cmake -B build -S $pkgname-$pkgver \
      -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
