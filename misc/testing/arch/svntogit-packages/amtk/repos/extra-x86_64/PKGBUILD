# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=amtk
pkgver=5.2.0
pkgrel=2
pkgdesc="Actions, Menus and Toolbars Kit for GTK+ applications"
url="https://wiki.gnome.org/Projects/Amtk"
arch=(x86_64)
license=(LGPL)
depends=(gtk3)
makedepends=(autoconf-archive git gobject-introspection gtk-doc)
_commit=4ce1fe51fb4b031eef8e26657e1d72e2164758d0  # tags/5.2.0^0
source=("git+https://gitlab.gnome.org/GNOME/amtk.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk-doc --disable-Werror
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -C $pkgname check
}

package() {
  depends+=(libg{lib,object,io}-2.0.so libgtk-3.so)
  provides+=("libamtk-${pkgver%%.*}.so")

  cd $pkgname
  make DESTDIR="$pkgdir" install
}
