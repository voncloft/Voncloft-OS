# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgbase=alsa-plugins
pkgname=(alsa-plugins pulseaudio-alsa)
pkgver=1.2.2
pkgrel=2
epoch=1
pkgdesc="Additional ALSA plugins"
arch=(x86_64)
url="https://www.alsa-project.org"
license=(LGPL2.1)
makedepends=(alsa-lib dbus jack libavtp libpulse libsamplerate speexdsp)
source=("https://www.alsa-project.org/files/pub/plugins/$pkgbase-$pkgver.tar.bz2"
        pulse-sysdefault.diff)
sha256sums=('1c0f06450c928d711719686c9dbece2d480184f36fab11b8f0534cb7b41e337d'
            'd8c59b891e2913619d057ffea308293d73892b16fa5a01583d056e372e55309d')

prepare() {
  cd $pkgbase-$pkgver

  # Make use of the pulse plugin's "fallback" feature
  # Keeps parity with our old config from pulseaudio-alsa
  patch -Np1 -i ../pulse-sysdefault.diff

  # a52, lavrate plugins not compatible with ffmpeg >= 4.0
  # https://mailman.alsa-project.org/pipermail/alsa-devel/2019-December/159425.html
  # https://bugs.archlinux.org/task/60586
  autoreconf -fvi
}

build() {
  cd $pkgbase-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-maemo-plugin \
    --enable-maemo-resource-manager
  make
}

package_alsa-plugins() {
  depends=(glibc libasound.so)
  optdepends=('dbus: for maemo plugin'
              'jack: for pcm_jack plugin'
              'libavtp: for pcm_aaf plugin'
              'libsamplerate: for rate_samplerate plugin'
              'libpulse: for conf_pulse, ctl_pulse and pcm_pulse plugins'
              'speexdsp: for pcm_speex and rate_speexrate plugins')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/doc/$pkgbase" -m644 doc/README* doc/*.txt

  # remove maemo plugin configuration (it overrides defaults for pcm and ctl),
  # until a better way is found to package default overrides:
  # https://bugs.archlinux.org/task/65440
  rm -v "${pkgdir}/etc/alsa/conf.d/98-maemo.conf"

  # make a proper off-by-default config template out of the example file
  mv -v "${pkgdir}/etc/alsa/conf.d/99-pulseaudio-default.conf.example" \
    "${pkgdir}/usr/share/alsa/alsa.conf.d/99-pulseaudio-default.conf"
}

package_pulseaudio-alsa() {
  pkgdesc="ALSA Configuration for PulseAudio"
  depends=('alsa-plugins>=1.2.2-2' pulseaudio)

  mkdir -p "${pkgdir}/etc/alsa/conf.d"
  ln -st "${pkgdir}/etc/alsa/conf.d" \
    /usr/share/alsa/alsa.conf.d/99-pulseaudio-default.conf
}

# vim:set sw=2 et:
