# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgbase=libxml++2.6
pkgname=('libxml++2.6' 'libxml++2.6-docs')
pkgver=2.42.0
pkgrel=1
pkgdesc="C++ bindings to libxml2"
url="https://libxmlplusplus.github.io/libxmlplusplus/"
arch=(x86_64)
license=(LGPL)
depends=(libxml2 glibmm)
makedepends=(glibmm-docs git meson mm-common)
options=(!emptydirs)
_commit=8f804be36d53729190062346fb32d727b49a2448  # tags/2.42.0^0
source=("git+https://github.com/libxmlplusplus/libxmlplusplus#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libxmlplusplus
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd libxmlplusplus
}

build() {
  arch-meson libxmlplusplus build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libxml++2.6() {
  provides=("libxml++2=$pkgver" libxml++-2.6.so)
  replaces=(libxml++2)
  conflicts=(libxml++2)

  DESTDIR="$pkgdir" meson install -C build

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_libxml++2.6-docs() {
  pkgdesc+=" (documentation)"
  depends=(glibmm-docs)

  mv -t "$pkgdir" docs/*
}
