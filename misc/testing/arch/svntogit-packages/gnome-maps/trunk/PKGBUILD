# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-maps
pkgver=3.38.3
pkgrel=1
pkgdesc="A simple GNOME 3 maps application"
url="https://wiki.gnome.org/Apps/Maps"
arch=(x86_64)
license=(GPL)
depends=(geocode-glib geoclue gjs libchamplain libgee folks gfbgraph)
makedepends=(gobject-introspection git meson yelp-tools)
groups=(gnome)
_commit=5236a3fd8a346309d94951e9827cafa1d0b3e360  # gnome-3-38
source=("git+https://gitlab.gnome.org/GNOME/gnome-maps.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  git tag -f v3.38.3 5236a3fd8a346309d94951e9827cafa1d0b3e360  # Missing tag
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
