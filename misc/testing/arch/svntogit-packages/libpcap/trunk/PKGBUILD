# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@mirantis.com>
# Contributor: Thomas Bächler <thomas@archlinux.org>

pkgname=libpcap
pkgver=1.10.0
pkgrel=1
pkgdesc='A system-independent interface for user-level packet capture'
arch=('x86_64')
url='https://www.tcpdump.org/'
license=('BSD')
depends=('glibc' 'libnl' 'sh')
makedepends=('bluez-libs' 'dbus')
provides=('libpcap.so')
source=("https://www.tcpdump.org/release/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('007710386ff3435ef97fc99293076eae3c39b424e986141184c712b0285f8589357a1b25085f7eba28730de04312042c724d193934a399797e95a56f5301da7f'
            'SKIP')
b2sums=('54e809af242b18d600b88f29bbe31b01784b5160366242f313908b58dfb1e6ee2ceae8d359f730881e15c1b26ed90c553869dffb25a74eb3cea9f597dd490b1d'
        'SKIP')
validpgpkeys=('1F166A5742ABB9E0249A8D30E089DEF1D9C15D0D') # The Tcpdump Group

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix='/usr' \
              --enable-ipv6 \
              --enable-bluetooth \
              --enable-usb \
              --with-libnl
  make
}

package() {
  depends+=('libdbus-1.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CHANGES,{CONTRIBUTING,README}.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
