# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xmodmap
pkgver=1.0.10
pkgrel=2
pkgdesc="Utility for modifying keymaps and button mappings"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xmodmap-${pkgver}.tar.bz2{,.sig})
sha512sums=('324c7dcef843186088f16b3bc47485eb3c9b4331e56ce43b692deb4bb3d4f4f27512480e91a379cceac8383df920dc5e37cd825246b50b6343291cec48134c04'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xmodmap-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xmodmap-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
