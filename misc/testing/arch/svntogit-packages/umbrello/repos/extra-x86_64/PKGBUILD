# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=umbrello
pkgver=20.12.2
pkgrel=1
pkgdesc='UML modeller'
arch=(x86_64)
url='https://kde.org/applications/development/umbrello'
license=(GPL)
depends=(kdelibs4support kdevelop-php qt5-webkit)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin kdevelop-pg-qt llvm kinit doxygen)
groups=(kde-applications kdesdk)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('4e4fdc0cbff6d578d6e2510c186a27e402995d35265ac203e100e6950c61f17f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() { 
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_UNITTESTS=OFF \
    -DBUILD_KF5=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
