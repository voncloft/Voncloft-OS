# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=plasma-integration
pkgver=5.20.90
pkgrel=1
pkgdesc="Qt Platform Theme integration plugins for the Plasma workspaces"
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(kio kwayland libxcursor noto-fonts ttf-hack qqc2-desktop-style perl)
makedepends=(extra-cmake-modules breeze)
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('5dd760226680c908af35bc0d095789ef28da7002a477e18d3cb5781767f8a8f4'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
