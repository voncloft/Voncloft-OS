# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Bob Finch <w9ya@qrparci.net>

pkgname=portaudio
# versioning is very bizarre:
# https://app.assembla.com/wiki/show/portaudio/ReleaseNotes
_version=190600_20161030
pkgver=19.6.0
pkgrel=7
epoch=1
pkgdesc='A free, cross-platform, open source, audio I/O library.'
arch=('x86_64')
url="http://www.portaudio.com/"
license=('MIT')
depends=('gcc-libs' 'glibc')
makedepends=('alsa-lib' 'jack')
provides=('libportaudio.so' 'libportaudiocpp.so')
source=("${pkgname}-${pkgver}.tgz::http://www.portaudio.com/archives/pa_stable_v${_version}.tgz")
sha512sums=('7ec692cbd8c23878b029fad9d9fd63a021f57e60c4921f602995a2fca070c29f17a280c7f2da5966c4aad29d28434538452f4c822eacf3a60af59a6dc8e9704c')

prepare() {
  mv -v "${pkgname}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-cxx
  # build crashes somewhere above > 8 jobs
  make -j1
}

package() {
  depends+=('libasound.so' 'libjack.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
}
