# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Sylvain HENRY <hsyl20@yahoo.fr>
# Contributor: Hervé YVIQUEL <elldekaa@gmail.com>

pkgname=hwloc
pkgver=2.4.0
pkgrel=1
pkgdesc='Portable Hardware Locality is a portable abstraction of hierarchical architectures'
url='https://www.open-mpi.org/projects/hwloc/'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'libtool' 'systemd-libs' 'libpciaccess')
makedepends=('cairo' 'libxml2' 'pciutils' 'systemd' 'libx11')
optdepends=('cairo' 'libxml2' 'pciutils' 'libx11')
options=('!docs')
source=(https://www.open-mpi.org/software/hwloc/v${pkgver%.*}/downloads/${pkgname}-${pkgver}.tar.bz2)
sha512sums=('5a7a1d91ab4bdfa9f8db8ea49032f25ea12b01de02b53d8a24346b427a4bd9d70ad3a45199615202aaaa2557e18f12b869c84d7348f2c82b1a81e2856ce74c71')
b2sums=('fb444ebeafdd131cf929dcac718b0ce2e7af598a9777f7005e4adf3155e9ddc36222e9708970e2de9dd2659bb2d2baec3531e363eca747c11410da7ecd8c0cdf')

prepare() {
  cd hwloc-${pkgver}
  autoreconf -fiv
}

build() {
  cd hwloc-${pkgver}
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --enable-plugins \
    --sysconfdir=/etc
  make
}

check() {
  cd hwloc-${pkgver}
  make check
}

package() {
  cd hwloc-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
