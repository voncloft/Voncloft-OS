# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-testtools
pkgver=2.4.0
pkgrel=3
pkgdesc="Extensions to the Python standard library unit testing framework"
arch=('any')
license=('MIT')
url="https://github.com/testing-cabal/testtools"
depends=('python-pbr' 'python-extras' 'python-fixtures' 'python-pyrsistent' 'python-mimeparse')
makedepends=('python-setuptools')
checkdepends=('python-testscenarios')
source=("$pkgname-$pkgver.tar.gz::https://github.com/testing-cabal/testtools/archive/$pkgver.tar.gz"
        $pkgname-py39.patch::https://github.com/testing-cabal/testtools/commit/1d698cf91cb2.patch)
sha512sums=('7983c6666b8d4dae3bc4e37d967e09f735d0753a9f517f00e53c4884d86c56d3272bb34d56a8246c74e44cff0087a713973a9cd4aa5d86f9ec8d60e842a6db92'
            'a554d49e74f43269a847d767611f86fda655fe9ca27cf182c91975702b6928b4161e12d78283113b88b4d50b9fce240cd4fbf236e884e8b92698bdd512c42598')

export PBR_VERSION=$pkgver

prepare() {
  patch -Np1 -d testtools-$pkgver <$pkgname-py39.patch

  # These are for python 2.x and old python 3.x only
  sed -i 's|unittest2|unittest|' testtools-$pkgver/testtools/tests/test_run.py \
                                 testtools-$pkgver/testtools/tests/test_testsuite.py \
                                 testtools-$pkgver/testtools/tests/twistedsupport/test_matchers.py \
                                 testtools-$pkgver/testtools/tests/twistedsupport/test_deferred.py \
                                 testtools-$pkgver/testtools/tests/twistedsupport/test_runtest.py
  sed -i '/unittest2>=1.0.0/d' testtools-$pkgver/requirements.txt
  sed -i 's|import linecache2 as linecache|import linecache|' testtools-$pkgver/testtools/tests/test_compat.py
  sed -i "s|traceback = try_import('traceback2')|import traceback|" testtools-$pkgver/testtools/content.py
  sed -i '/traceback2/d' testtools-$pkgver/requirements.txt
}

build() {
  cd testtools-$pkgver
  python setup.py build
}

check() {
  cd testtools-$pkgver
  python -m testtools.run testtools.tests.test_suite
}

package() {
  cd testtools-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
