# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-moto
_pkgname=moto
pkgver=1.3.16
pkgrel=5
pkgdesc='Moto is a library to mock out the boto library.'
arch=(any)
url='https://github.com/spulec/moto'
license=(Apache)
depends=(python-aws-xray-sdk python-boto python-boto3
         python-cryptography python-dateutil python-docker python-jinja
         python-jsondiff python-jose python-mock python-yaml
         python-pytz python-requests python-responses python-xmltodict
         python-werkzeug python-cfn-lint python-setuptools python-sshpubkeys)
checkdepends=(python-flask python-freezegun python-nose python-parameterized
              python-sure tk)
optdepends=(
  'python-flask: for moto_server'
)
source=("https://files.pythonhosted.org/packages/source/m/moto/moto-${pkgver}.tar.gz"
        "$pkgname-cfn-lint-0.39.patch::https://github.com/spulec/moto/commit/403b06d9c5d9fda4d9c51f44d5213cf584d7dc11.patch"
        "$pkgname-boto3-1.14.59.patch::https://github.com/spulec/moto/commit/9f0f230d130a839fb2de6bfc97af4182360fbcdb.patch"
        "$pkgname-responses-0.12.1.patch::https://github.com/spulec/moto/commit/7749c1f75781cd5fb2367b089c31a92e127b07b1.patch"
        # Rebased from https://github.com/spulec/moto/commit/9ecea2012a19cf929f03271dddf5ce3e17943f48
        "$pkgname-botocore-1.19.41.patch"
        sdist-no-pyc.diff)
sha256sums=('6c686b1f117563391957ce47c2106bc3868783d59d0e004d2446dce875bec07f'
            'afb3a13ccfe1646403e5727c812e26ab88106c212b735d5ab9a55879fcb34e43'
            'f902d1b01c12e8a1b8e87109a984ab1463b60b04e1dbdeaa94550276d2580a11'
            'cca3d8749c07eac50a17cff7b6dffdb9aa8d5ce9ca974453a63516d35cb2025a'
            'af4505592277c795d2abdf8418c528c7ef3669282077654954307c9d1ab321ad'
            '4193bd88380f6ab8dac49be37d940993dee4e31351ffd60c8167b7c8e5ec3208')

prepare() {
  cd $_pkgname-$pkgver

  patch -Np1 -i ../sdist-no-pyc.diff

  # https://github.com/spulec/moto/pull/3412
  patch -Np1 -i ../$pkgname-cfn-lint-0.39.patch
  # https://github.com/spulec/moto/pull/3308
  patch -Np1 -i ../$pkgname-boto3-1.14.59.patch
  # https://github.com/spulec/moto/pull/3466
  patch -Np1 -i ../$pkgname-responses-0.12.1.patch
  # https://github.com/spulec/moto/pull/3575
  patch -Np1 -i ../$pkgname-botocore-1.19.41.patch

  # Remove upper bounds of dependencies
  sed --in-place=.orig -r 's#,?<[0-9.]+,?##;s#==([0-9.]+)#>=\1#' setup.py
  diff -u setup.py{.orig,} || true
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  # * test_lambda tests moto.awslambda, which requires a running docker.service
  # * test_submit_job_by_name calls batch_client.submit_job, and the latter
  #   creates a Job object, which requires docker
  # * In test_create_stack_lambda_and_dynamodb, the CloudFormation template
  #   contain AWS::Lambda resources and thus uses moto.awslambda
  TZ=UTC nosetests -s ./tests/ \
    --exclude='test_lambda' \
    --exclude='test_list_jobs' \
    --exclude='test_submit_job' \
    --exclude='test_submit_job_by_name' \
    --exclude='test_terminate_job' \
    --exclude='test_create_stack_lambda_and_dynamodb' \
    --exclude='test_delete_subscription_filter_errors' \
    --exclude='test_put_subscription_filter_update' \
    --exclude='test_put_subscription_filter_with_lambda'
}

package() {
  cd $_pkgname-$pkgver

  # use PYTHONHASHSEED=0 work around https://bugs.python.org/issue34722
  PYTHONHASHSEED=0 python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
