# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=zict
pkgname=python-${_pkg}
pkgver=2.0.0
pkgrel=3
pkgdesc="Useful Mutable Mappings"
arch=(any)
url="https://github.com/dask/zict/"
license=(BSD)
makedepends=(python-setuptools)
depends=(python python-heapdict)
checkdepends=(python-pytest python-lmdb)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
#source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('8e2969797627c8a663575c2fc6fcb53a05e37cdb83ee65f341fc6e0c3d0ced16')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  pytest zict
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
