# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=python-xlsxwriter
pkgver=1.3.7
pkgrel=1
pkgdesc="A Python module for creating Excel XLSX files"
arch=('any')
url="https://github.com/jmcnamara/XlsxWriter"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jmcnamara/XlsxWriter/archive/RELEASE_${pkgver}.tar.gz")
sha256sums=('b08a1b30cde383cf3b09b50fa18fbfca28ec977e5df9ffab89f01a2ca93be2c1')

build() {
  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}
  python setup.py build
}

package() {
  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -D -m644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
