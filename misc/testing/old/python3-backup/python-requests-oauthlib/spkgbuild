# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Roberto Catini <roberto.catini@gmail.com>

pkgname=python-requests-oauthlib
pkgver=1.3.0
pkgrel=4
pkgdesc="First-class OAuth library support for Requests"
arch=('any')
url="https://pypi.python.org/pypi/requests-oauthlib"
license=('custom:ISC')
depends=('python-requests' 'python-oauthlib')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-requests-mock' 'python-mock' 'python-pyjwt')
source=("$pkgname-$pkgver.tar.gz::https://github.com/requests/requests-oauthlib/archive/v$pkgver.tar.gz")
sha512sums=('0eebd209dad892567de7d37f1482b5c7523eced3a73a68054674b29447d090c885b2388fd6f2db26436f14098833051c8888ace8b137889a4012e010efb0e86b')

build() {
  cd requests-oauthlib-$pkgver
  python setup.py build
}

check() {
  cd requests-oauthlib-$pkgver
  py.test
}

package() {
  cd requests-oauthlib-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
