# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Andrew Steinke <rkcf@rkcf.me>

pkgname=python-astunparse
_pkgname=astunparse
pkgrel=3
pkgver=1.6.3
pkgdesc="An AST unparser for Python"
depends=('python' 'python-six')
makedepends=('python-setuptools' 'python-wheel')
url="https://github.com/simonpercivall/astunparse"
license=('python')
arch=('any')
source=("$pkgname-$pkgver.tar.gz::https://github.com/simonpercivall/astunparse/archive/v$pkgver.tar.gz")
sha512sums=('8f85d848c65d1728df767b1c2aee44aa4ce79a6810c348a5b3669b56f4bc5ce7f09414557be38d0a5103a77872d75083762bd2829d02ac8414df17221d30492b')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
