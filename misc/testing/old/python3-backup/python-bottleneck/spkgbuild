# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Michael Schubert <mschu.dev at gmail>
# Contributor: Panagiotis Mavrogiorgos (pmav99) <> (gmail)

pkgname=python-bottleneck
pkgver=1.3.2
pkgrel=3
pkgdesc="Fast NumPy array functions written in Cython"
arch=('x86_64')
url="https://github.com/kwgoodman/bottleneck"
license=('BSD')
makedepends=('python-setuptools')
depends=('python-numpy')
optdepends=('python-nose: needed for integrated test suite'
            'python-scipy: needed for integrated benchmarks')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kwgoodman/bottleneck/archive/v${pkgver}.tar.gz")
sha256sums=('5efac98f689c445eecc95400fb53d7bbc92435fa6db7a3dc75b427641d757807')

build() {
  cd "$srcdir"/bottleneck-${pkgver}
  python setup.py build
}

package() {
  cd "$srcdir"/bottleneck-${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
