# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contrinutor: shining <shiningxc@gmail.com>
# Contributor: phillid <dbphillipsnz _at_thingy_that_swirly_a_symbol gmaildott comm>

pkgname=python-pydot
pkgver=1.4.1
pkgrel=5
pkgdesc="Python interface to Graphviz's Dot"
arch=('any')
url="https://github.com/erocarrera/pydot"
license=('MIT')
depends=('python-pyparsing' 'graphviz')
makedepends=('python-setuptools')
checkdepends=('python-chardet')
source=("$pkgname-$pkgver.tar.gz::https://github.com/erocarrera/pydot/archive/v$pkgver.tar.gz")
sha512sums=('b5f16b59908586df61cd415cb1a3e242fb69c39674a82b171862beff1f3727f47cba39bdd806901cb56bc8a6888783a53d112b1b70044e124ea992789d77f938')

build() {
  cd pydot-$pkgver
  python setup.py build
}

check() {
  # https://github.com/pydot/pydot/issues/204

  cd "$srcdir"/pydot-$pkgver/test
  PYTHONPATH="$PWD/../build/lib:$PYTHONPATH" python pydot_unittest.py || echo "Tests failed"
}

package() {
  cd pydot-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
