# Maintainer: David Runge <dvzrv@archlinux.org>

_name=micawber
pkgname=python-micawber
pkgver=0.5.2
pkgrel=1
pkgdesc="A small library for extracting rich content from urls."
arch=('any')
url="https://github.com/coleifer/micawber"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-beautifulsoup4' 'python-django')
optdepends=('python-django: django support'
            'python-flask: flask support'
            'python-redis: redis caching support')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('d32752c73cff9a12947f18d1b2359b272a7fb31bb1d757e02c484a212a5d130feb1e79f08cf7574518263e75f8552d05812071b326326b530975ef4d706ccc73')
b2sums=('53b10f45074d766613f1ed2dba4d6ad7836f0904ce61fc22ca52ef68ec80b03d276e2629f4a3b779ad3e880f2638f10a4106983b47ad5bb3bbe3dc5f78b4409d')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib:${PYTHONPATH}"
  python runtests.py
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
