# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-trustme
_pyname=${pkgname/python-/}
pkgver=0.6.0
pkgrel=3
pkgdesc='Library for fake certificate authority (CA) to generate fake TLS certs'
url='https://trustme.readthedocs.io'
arch=('any')
license=('MIT')
depends=('python' 'python-cryptography')
makedepends=('python' 'python-setuptools' 'python-cryptography')
checkdepends=('python-pytest' 'python-pyopenssl' 'python-service_identity')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/python-trio/trustme/archive/v${pkgver}.tar.gz)
sha512sums=('4c76afebde7e57965aa5c86e4e14c6a4663355bc4a713366973ee521d235ca26bc0a96b08208f6288a713a83135ed0412e991de36c0c0de1e0155357142f5ec6')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH=. py.test
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE.MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
