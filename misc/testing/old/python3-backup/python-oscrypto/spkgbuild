# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=python-oscrypto
pkgver=1.2.1
pkgrel=3
pkgdesc='Compiler-free Python crypto library backed by the OS'
arch=('any')
url='https://github.com/wbond/oscrypto'
license=('MIT')
depends=('python' 'python-asn1crypto')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('bf858f59bf6e54d1ac962df4b90eb674844e50f47b217d73d94b05e27280b78671dc04f4211276c04e71b48f92b446bc11f964b7223def24f5d0d878166547f3')

build() {
    cd "${pkgname#python-}-$pkgver"
    python setup.py build
}

check() {
	cd "${pkgname#python-}-$pkgver"
	python setup.py test
}

package() {
    cd "${pkgname#python-}-$pkgver"
    python setup.py install --root="$pkgdir" --optimize=1 --skip-build
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
