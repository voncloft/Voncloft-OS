# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-fields
pkgname=('python-fields' 'python2-fields')
pkgver=5.0.0
pkgrel=9
arch=('any')
pkgdesc='A totally different take on container boilerplate'
url='https://github.com/ionelmc/python-fields'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest' 'python-attrs' 'python2-attrs'
              'python-characteristic' 'python2-characteristic')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/ionelmc/python-fields/archive/v$pkgver.tar.gz")
sha512sums=('feaba23b98271936d6dae20b1001978483c0fe89ccf217637fac536ae5814c2c02bac1c3ddc2a86dc0d2419a3b3658871018c0ea4b7905e1c2649aa905b8e5a3')

prepare() {
  sed -i 's/\[pytest\]/[tool:pytest]/;s/--benchmark-disable//' python-fields-$pkgver/setup.cfg 
  cp -a python-fields-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-fields-$pkgver
  python setup.py build

  cd "$srcdir"/python-fields-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-fields-$pkgver
  python -m pytest --deselect tests/test_perf.py

  cd "$srcdir"/python-fields-$pkgver-py2
  python2 -m pytest --deselect tests/test_perf.py
}

package_python-fields() {
  depends=('python')

  cd python-fields-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-fields() {
  depends=('python2')

  cd python-fields-$pkgver-py2
  python2 setup.py install --root "$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
