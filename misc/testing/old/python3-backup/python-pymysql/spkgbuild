# Maintainer: David Runge <dvzrv@archlinux.org>

_name=PyMySQL
pkgname=python-pymysql
pkgver=1.0.2
pkgrel=1
pkgdesc="Pure Python MySQL Client"
arch=('any')
url="https://github.com/PyMySQL/PyMySQL/"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'mariadb')
optdepends=('mariadb: for using a local MariaDB instance'
            'python-cryptography: for RSA cryptography'
            'python-pynacl: for ed25519 cryptography')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('c831505dafe6aebf039129ec4b09ef6dcafd490bcee2b515f42c0d0412fe9c379ba28650fb329d21f1da7e9fcae64968e1bc6065a04a08e00497a24967e27146')
b2sums=('93f6326da2f7f5e717cdba8daecc747e9e6568f4112f1a5e4408b715b978042f3564b4d2e30c8325ef25f79f1dba912c56cd64f0aa64ca61e8c49e804c6285e7')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 {CHANGELOG.md,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
