# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-itemadapter
pkgver=0.2.0
pkgrel=1
pkgdesc="Common interface for data container classes"
url="https://github.com/scrapy/itemadapter"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-attrs' 'python-pytest' 'scrapy')
source=("https://github.com/scrapy/itemadapter/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('317e42ebe7f161e114719c942fb7261b3b06e38fe81f609ccdacebfb051a5eaea90286bfef79c8bcf56d041d26e087dbbc9a74270c7a61bdd73841596a01f174')

build() {
  cd itemadapter-$pkgver
  python setup.py build
}

check() {
  cd itemadapter-$pkgver
  pytest
}

package() {
  cd itemadapter-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
