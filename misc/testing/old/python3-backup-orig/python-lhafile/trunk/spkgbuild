# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: grimi

pkgname=python-lhafile
pkgver=0.2.2
pkgrel=5
pkgdesc='LHA archive support for Python'
arch=('x86_64')
url='https://fengestad.no/python-lhafile/'
license=('BSD')
depends=('python')
source=("https://github.com/FrodeSolheim/python-lhafile/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '010-python-lhafile-fix-version.patch'::'https://github.com/FrodeSolheim/python-lhafile/commit/f1e8a9dc7b80eaf1e06eff242fb1bbae5d722ad9.patch')
sha256sums=('18396276b77d8c7e094f169d3b2471e60de3d412a2454a0f73c452a54817ee68'
            '39b63fdbc51b72989175faeeb26a44aaca93612e7847b596bcb4af34d9d348c9')

prepare() {
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/010-python-lhafile-fix-version.patch"
}

build() {
    cd "${pkgname}-${pkgver}"
    python setup.py build
}

package() {
    cd "${pkgname}-${pkgver}"
    PYTHONHASHSEED='0' python setup.py install --root="$pkgdir" --skip-build --optimize='1'
    install -D -m644 COPYING.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
