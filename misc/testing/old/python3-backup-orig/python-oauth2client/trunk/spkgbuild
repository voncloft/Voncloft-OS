# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Atlanis <emallson@archlinux.us>

pkgname=python-oauth2client
pkgver=4.1.3
pkgrel=6
pkgdesc="A client library for OAuth 2.0"
arch=('any')
url="https://github.com/google/oauth2client"
license=('Apache')
depends=('python-httplib2' 'python-pyasn1' 'python-pyasn1-modules' 'python-rsa' 'python-six')
optdepends=('python-gflags: for oauth2client.tools.run function')
makedepends=('python-setuptools')
source=("https://github.com/google/oauth2client/archive/v${pkgver}.tar.gz")
md5sums=('e5495da59867779d47bacf4bc4c10d86')

build() {
  cd "$srcdir"/oauth2client-${pkgver}
  python setup.py build
}

package() {
  cd "$srcdir"/oauth2client-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1
}
