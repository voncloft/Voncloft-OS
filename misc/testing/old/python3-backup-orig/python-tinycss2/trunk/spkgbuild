# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-tinycss2
pkgver=1.1.0
pkgrel=1
pkgdesc="Low-level CSS parser for Python"
url="https://pypi.python.org/pypi/tinycss2"
license=('BSD')
arch=('any')
depends=('python-webencodings')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov' 'python-pytest-isort' 'python-pytest-runner')
_css_tests_commit=c5749e51dda3868b3e8062e65a36584c2fec8059
source=("https://pypi.io/packages/source/t/tinycss2/tinycss2-$pkgver.tar.gz"
        css-parsing-tests-$_css_tests_commit.tar.gz::https://github.com/SimonSapin/css-parsing-tests/archive/$_css_tests_commit.tar.gz)
sha512sums=('218d28511802afc89a608def03084f5d460512a41d47280b5c8bfe72314c8ab3b14c98fbb0200bea6b8509c4d6f0107410d340e2d7f1830aaa92669a2699211e'
            'ef44eb1144a6484f308fabe4187310b2ef0067f328bf5213969d87c475fccae3d716dc66640a0a554ab2a3d7da0efb3a168a288ac6eef7722f3ae46959943b49')

prepare() {
  cd tinycss2-$pkgver
  sed -i 's/--flake8//' pyproject.toml
  cp ../css-parsing-tests-$_css_tests_commit/* tests/css-parsing-tests/
}

build() {
  cd tinycss2-$pkgver
  python setup.py build
}

check() {
  cd tinycss2-$pkgver
  PYTHONPATH=build/lib pytest
}

package() {
  cd tinycss2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
