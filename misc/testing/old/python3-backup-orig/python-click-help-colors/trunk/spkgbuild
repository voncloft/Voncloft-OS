# Maintainer: David Runge <dvzrv@archlinux.org>

_name=click-help-colors
pkgname=python-click-help-colors
pkgver=0.9
pkgrel=1
pkgdesc="Colorization of help messages in Click"
arch=('any')
url="https://github.com/click-contrib/click-help-colors"
license=('MIT')
depends=('python-click')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('be8896d6c6f7d3a66d9ac424b9169fc9cc42c5e5ba88f310ce15e7c400731a677b4adf2968f110ab63d00af2374005812429d53f595362ca29150082602064c6')
b2sums=('7ad26e2a8ac478abf2ef46e325e1759ce243e4930d5931ed3bfbdeda520c93d80096ef1cb97345f6fbd97be7d36ea2035c7120072ba20cd1f7f605d1a6ab8114')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 examples/*.py \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}
