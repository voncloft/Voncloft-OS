# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pystemmer
pkgver=2.0.1
pkgrel=3
pkgdesc="Snowball stemming algorithms, for information retrieval"
arch=('x86_64')
license=('BSD' 'MIT')
url="http://snowball.tartarus.org"
depends=('python')
makedepends=('cython')
source=("https://pypi.io/packages/source/P/PyStemmer/PyStemmer-$pkgver.tar.gz")
sha512sums=('3ac0ef16f0caae74cb19a73e7de7a56e3947a6f81a14273885b3461392f7f33e82a433d4ffa7b4a61d87bb94aff7e12c8e0c9726de2eb6b386a9f2c558678a6c')

prepare() {
  # Force cython rebuild
  rm PyStemmer-$pkgver/src/Stemmer.c
}

build() {
  cd "$srcdir/PyStemmer-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/PyStemmer-$pkgver"
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.9:$PYTHONPATH" python runtests.py
}

package() {
  cd PyStemmer-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
