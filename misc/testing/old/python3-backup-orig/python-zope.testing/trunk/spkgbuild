# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgbase=python-zope.testing
_pkgname=${pkgbase#python-}
pkgname=(python-$_pkgname python2-$_pkgname)
pkgver=4.7
pkgrel=1
arch=('any')
pkgdesc="Zope testing frameworks"
url="https://github.com/zopefoundation/$_pkgname"
license=('custom:ZPL')
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('8d285e64a5ec0f7df8e12e8887246f76b14a6bf8c5e1febd83afbd2baff28bc7ce98bbc4fffae69678270219fab84849b05a892cbd94674b3925623d338b0176')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}
}

build() {
  msg2 "Building python-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver
  python setup.py build

  msg2 "Building python2-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver-py2
  python2 setup.py build
}

check() {
  msg2 "Running tests on python-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver
  python setup.py -q test -q

  msg2 "Running tests on python2-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver-py2
  python2 setup.py -q test -q
}

package_python-zope.testing() {
  depends=('python')
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-zope.testing() {
  depends=('python2')
  cd $_pkgname-$pkgver-py2

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
