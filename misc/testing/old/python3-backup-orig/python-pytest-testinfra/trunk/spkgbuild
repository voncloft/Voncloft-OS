# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: gardar <aur@gardar.net>
# Contributor: Morten Linderud <morten@linderud.pw>

_name=pytest-testinfra
pkgname=python-pytest-testinfra
pkgver=6.1.0
pkgrel=1
pkgdesc='Testinfra test your infrastructures'
url="https://github.com/pytest-dev/pytest-testinfra"
arch=('any')
license=('Apache')
depends=('python-pytest')
makedepends=('python-hacking' 'python-setuptools_scm' 'python-sphinx')
checkdepends=('ansible' 'python-mock' 'python-paramiko' 'python-pytest-cov'
'python-pytest-xdist' 'python-pywinrm' 'python-tornado' 'salt')
# TODO: add salt to optdepends, after it has been ported to python3
optdepends=('ansible: for tests using ansible inventories'
            'docker: for test running docker containers'
            'kubectl: for test running containers in kubernetes'
            'lxd: for test running LXC or LXD containers'
            'podman: for test running podman containers'
            'python-paramiko: for testing remote hosts'
            'python-pywinrm: for testing on Windows hosts')
conflicts=('python-testinfra')
provides=('python-testinfra')
replaces=('python-testinfra')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('15c8f3539f63f8587c5b19979215673fb51d476353fd37e35aeb2308a6c4be681f3d247783c87ba0dac68673394ba116fe9fbf32d386baade3f6a8769851d2ec')
b2sums=('ed4787d3ec504a757716a7e5c8e00f9c203db18d22a7fba7da411f9cb1fe89bac156a7f4600684a202bdab3785344c58e22b8ff8f011f7e06707d922bccc4ad9')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
  make -C doc man
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build:$PYTHONPATH"
  # salt is not python3 (yet): https://bugs.archlinux.org/task/61129
  pytest -v -k 'not test_backend_importables'
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  # man page
  install -Dm 644 doc/build/man/*.1 \
    -t "${pkgdir}/usr/share/man/man1/"
  # docs
  install -vDm 644 {CHANGELOG,CONTRIBUTING,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
