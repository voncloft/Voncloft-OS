# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: googol <googol@posteo.de>
# Contributor: Oliver Mangold <o.mangold at gmail dot com>

pkgname=python-alembic
pkgver=1.4.3
pkgrel=3
pkgdesc='Lightweight database migration tool for usage with SQLAlchemy'
url='https://bitbucket.org/zzzeek/alembic'
arch=('any')
license=('MIT')
depends=('python' 'python-mako' 'python-sqlalchemy' 'python-editor' 'python-dateutil')
makedepends=('python-setuptools' 'python-mako' 'python-sqlalchemy' 'python-editor' 'python-dateutil')
checkdepends=('python-pytest' 'python-mock')
source=(https://pypi.org/packages/source/a/alembic/alembic-${pkgver}.tar.gz{,.asc}
        $pkgname-pytest6.patch::https://github.com/sqlalchemy/alembic/commit/869094097654.patch)
sha512sums=('925d8957fd9008d041737a9e79ec8d7a4c5deba1976e980362d355a75348c1638d783d9d48234a5bc3b2dc3f2f09dcb281d0c80ae0f0c5bc32e176c3dfa05463'
            'SKIP'
            '9ca880e30f837821c7b237bd9e7db16606817d039434358e8648507ecd94b8431e5e229dc2b208803fae9c4f6b56307a0e69b5ded827658aeb6a1e49d62144cf')
b2sums=('d9c8b6d6aebce7a45194dd6c654f4a2627714a5c1f7cb7a1cf80ed8c3fe861e3bb7857811128787ddd6c093b4370784b40cddb385cfbd30f6134d537fe949d2d'
        'SKIP'
        '6a7a579307a951492191f880419d3e3a7e15bbc1e0c419898aae8ce3e2c342552aa1d82509c48db295c0ddd891b51c34156f030e7a8986c3eebff56647989840')
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1') # Michael Bayer <classic@zzzcomputing.com>

prepare() {
  cd alembic-${pkgver}
  sed -n '/diff --git a\/alembic\/testing/,/elif/p' ../$pkgname-pytest6.patch |
    patch -Np1
}

build() {
  echo 'Building python...'
  (cd alembic-${pkgver}
    python setup.py build
  )
}

check() {
  echo 'Checking python...'
  (cd alembic-${pkgver}
    py.test
  )
}

package() {
  cd alembic-${pkgver}
  python setup.py install --root="${pkgdir}" --prefix=/usr -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst CHANGES -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
