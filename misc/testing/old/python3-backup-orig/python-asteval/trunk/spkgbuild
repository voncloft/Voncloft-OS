# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-asteval
pkgver=0.9.21
pkgrel=1
pkgdesc="Minimalistic evaluator of python expression using ast module"
url="https://github.com/newville/asteval"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-numpy')
source=("https://github.com/newville/asteval/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e96937e41093db65a1eec4f3427124bee980590bc57309c7892fbec18637742d9f7b673d465e4f40325beece8912f7b4afc66319f5fd6adafb85320ca8e54c09')

build() {
  cd asteval-$pkgver
  python setup.py build
}

check() {
  cd asteval-$pkgver
  python -m pytest
}

package() {
  cd asteval-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
