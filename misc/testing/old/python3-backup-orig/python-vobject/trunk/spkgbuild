# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Carsten Feuls <archlinux@carstenfeuls.de>

_name=vobject
pkgname=python-vobject
pkgver=0.9.6.1
pkgrel=6
pkgdesc='Module for parsing and generating vCard and vCalendar files'
url="https://eventable.github.io/vobject/"
license=('Apache')
arch=('any')
depends=('python-dateutil' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('0b299fd4ca105a063f4ac7b03b157c54a3867d6fb798f9072897939b0ba275ba0887bb001c5ed38002a8eb1c10aee710af880413c072140b855af4b62d5aff72')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  python setup.py test
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {ACKNOWLEDGEMENTS.txt,README.md} \
    -t "${pkgdir}/usr/share/${pkgname}"
}
