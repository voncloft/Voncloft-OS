# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-zconfig
pkgver=3.5.0
pkgrel=5
pkgdesc="Structured Configuration Library"
url="https://github.com/zopefoundation/ZConfig"
license=('ZPL')
arch=('any')
makedepends=('python-setuptools')
checkdepends=('python-zope-testrunner' 'python-manuel' 'python-docutils' 'python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/ZConfig/archive/$pkgver.tar.gz"
        "https://github.com/zopefoundation/ZConfig/commit/f0c2990d35ac3c924ecc8be4a5c71c8e4abbd0e5.patch")
sha512sums=('0aa5ad1344ac599a10513e1189d0c990717311b39cc6dd6a44315812dce66c09760979931839e3739089d30c69c626bf8259695e1afb4691d19662f41100ecee'
            '463caac8729a3dc61e47fd955d29eb603f961984f20c01a88096af9c0782dbe78dd0abf269b7b98652ee2e2ffd70cc63dc80b6e2d0aa13e6ee1d2308e267866d')

prepare() {
  cd "$srcdir"/ZConfig-$pkgver
  # python 3.8 fix: https://github.com/zopefoundation/ZConfig/issues/69
  patch -p1 -i ../f0c2990d35ac3c924ecc8be4a5c71c8e4abbd0e5.patch
}

build() {
  cd "$srcdir"/ZConfig-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/ZConfig-$pkgver
  nosetests3
}

package() {
  cd ZConfig-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}
