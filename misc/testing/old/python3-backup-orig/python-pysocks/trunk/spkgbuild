# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Michael Schubert <mschu.dev at gmail>

pkgbase=python-pysocks
pkgname=(python-pysocks python2-pysocks)
pkgver=1.7.1
_commit=f660e75d4844e64e4b162b0c01f8df8d866ee000
pkgrel=3
pkgdesc="SOCKS4, SOCKS5 or HTTP proxy (Anorov fork PySocks replaces socksipy)"
arch=('any')
license=('BSD')
url="https://github.com/Anorov/PySocks"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('lib32-glibc' 'python2-twisted' 'python2-tornado')
source=("$pkgbase-$_commit.tar.gz::https://github.com/Anorov/PySocks/archive/$_commit.tar.gz")
sha512sums=('d5db8d55603758cecd0e4974729df437febcfabe8a80dcbfe6132d0ce5a23caeccfefff01bf918cdbb9f26db4cb795d26ed1f3c536c415b496e4ac1bd43e9240')

prepare() {
  mv PySocks-{$_commit,$pkgver}
  cp -a PySocks-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/PySocks-$pkgver
  python setup.py build

  cd "$srcdir"/PySocks-$pkgver-py2
  python2 setup.py build
}

# Tests disabled as it requires external services to run
check_disabled() {
  cd "$srcdir"/PySocks-$pkgver-py2/test
  python2 socks4server.py &
  _SOCKS4SERVER=$!
  python2 httpproxy.py &
  _HTTPPROXY=$1

  ./mocks start

  sleep 1

  python2 sockstest.py

  cd "$srcdir"/PySocks-$pkgver/test
  python sockstest.py

  ./mocks shutdown

  kill $_SOCKS4SERVER $_HTTPPROXY
}

package_python-pysocks() {
  depends=('python')
  provides=('python-socks')
  replaces=('python-socks')

  cd PySocks-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

package_python2-pysocks() {
  depends=('python2')
  provides=('python2-socks')
  replaces=('python2-socks')

  cd PySocks-$pkgver-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
