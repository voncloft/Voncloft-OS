# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-responses
pkgver=0.12.1
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=('any')
license=('Apache')
url='https://github.com/getsentry/responses'
depends=('python-requests' 'python-biscuits' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'flake8' 'python-pytest-localserver')
source=("https://github.com/getsentry/responses/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f99e9a127328bd000c78d160fdd0cf8288999c47c933b185e9cce323697f2728ec28dc6dd8a4e29b55797e7fff94076190165bc63e1bc36e050027f875d95775')

prepare() {
  cd responses-$pkgver
  sed -i 's/, < 5.0.0//' setup.py
}

build() {
  cd responses-$pkgver
  python setup.py build
}

check() {
  cd responses-$pkgver
  python setup.py pytest
}

package() {
  cd responses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
