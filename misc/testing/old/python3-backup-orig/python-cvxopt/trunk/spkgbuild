# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: BinkyTheClown <binky_at_archlinux_dot_us>
# Contributor: Julien Ugon <bzklrm at gmail dot com>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Gustavao A. Gomez Farhat <gustavo_gomez_farhat_at_gmail_dot_com>

pkgname=python-cvxopt
pkgver=1.2.5
pkgrel=3
pkgdesc="A free software package for convex optimization written in Python"
url="https://cvxopt.org/"
arch=('x86_64')
license=(GPL3)
depends=(python suitesparse)
makedepends=(gsl fftw glpk dsdp python-setuptools suitesparse)
optdepends=('gsl: for custom random number generators'
            'fftw: for FFTW interface'
            'glpk: solver for linear cone and PWL programming problems'
            'dsdp: solver for linear cone semidefinite programming problems')
source=("$pkgname-$pkgver.tar.gz::https://github.com/cvxopt/cvxopt/archive/${pkgver}.tar.gz")
sha256sums=('80f8001f24a12a5ee3a4a43281145e0e51a1ceca681657ff40eebe1d6c967250')

build() {
  cd cvxopt-$pkgver

  for lib in GSL FFTW GLPK DSDP; do
    eval "export $'CVXOPT_BUILD_$lib'=1"
  done
  python setup.py build
}

package() {
  cd cvxopt-${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
}
