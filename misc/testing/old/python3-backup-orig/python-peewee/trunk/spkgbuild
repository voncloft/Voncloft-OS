# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Blackleg <blacklg@openaliasbox.org>
# Contributor: Nidhogg
# Contributor: juantascon

pkgname=python-peewee
pkgver=3.14.0
pkgrel=3
pkgdesc="a little orm"
url="https://pypi.python.org/pypi/peewee/"
arch=('x86_64')
license=('MIT')
depends=('python' 'sqlite')
optdepends=('python-psycopg2: for PostgreSQL database support'
            'mysql-python: for MySQL database support')
makedepends=('python-setuptools' 'cython')
checkdepends=('python-apsw' 'python-flask' 'python-psycopg2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/coleifer/peewee/archive/$pkgver.tar.gz")
sha512sums=('083bb24952a6d05cd6c8376e185e050df0ab5b37bc553241128b0ed3b0ebdd322d9626c32943658324d413cff8ab0bb2ddf6c84d99a0fbe057c0e5c8ec93d7f9')

build() {
  cd peewee-$pkgver
  python setup.py build
}

check() {
  cd peewee-$pkgver
  python runtests.py
}

package() {
  cd peewee-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
