# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-service-identity
pkgname=('python-service-identity' 'python2-service-identity')
pkgver=18.1.0
pkgrel=6
pkgdesc="Service identity verification for pyOpenSSL"
arch=('any')
license=('MIT')
url="https://pypi.python.org/pypi/service_identity"
makedepends=('python-pyopenssl' 'python2-pyopenssl' 'python-pyasn1' 'python2-pyasn1'
             'python-setuptools' 'python2-setuptools' 'python-pyasn1-modules' 'python2-pyasn1-modules'
             'python-attrs' 'python2-attrs' 'python-idna' 'python2-idna')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pyca/service-identity/archive/$pkgver.tar.gz")
sha512sums=('6b6da48c543bfbe0d9da9f0d03dbd735ce97e64a76cbf9eae2b615ced7b91c5fcca1f25353e53858c48da9c4e0c0be4e33fa4559adb50899a79689cbb372f49b')

prepare() {
  cp -a service-identity-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/service-identity-$pkgver
  python setup.py build
 
  cd "$srcdir"/service-identity-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/service-identity-$pkgver
  python setup.py pytest

  cd "$srcdir"/service-identity-$pkgver-py2
  python2 setup.py pytest
}
 
package_python-service-identity() {
  depends=('python-pyopenssl' 'python-pyasn1' 'python-pyasn1-modules' 'python-attrs')
  optdepends=('python-idna: for Internationalized Domain Names support')
 
  cd service-identity-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
 
package_python2-service-identity() {
  depends=('python2-pyopenssl' 'python2-pyasn1' 'python2-pyasn1-modules' 'python2-attrs')
  optdepends=('python2-idna: for Internationalized Domain Names support')
 
  cd service-identity-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
