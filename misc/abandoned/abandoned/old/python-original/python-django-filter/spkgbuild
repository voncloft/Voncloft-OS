# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-filter
pkgname=python-django-filter
pkgver=2.4.0
pkgrel=2
pkgdesc="A generic system for filtering Django QuerySets based on user selections"
arch=('any')
url="https://github.com/carltongibson/django-filter"
license=('BSD')
depends=('python-django')
makedepends=('python-setuptools')
checkdepends=('python-django-crispy-forms' 'python-django-rest-framework'
'python-mock' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('0584222b46add89aa7dd1dfd334fdd19903e0119d455a2c46644c501a66df224f9f51c33394d130bcd1fc0f28c6ede6d6853b9790430040ec7228f9a1dc55264')
b2sums=('994b07c0f5e511451964f4f1e007ff9906975600d0bc8a02dcbeb86f91bf906490ca394ca95c23c57c1fff25a447f50a40054c0abfe385300f9db87645753978')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  python runtests.py
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 {AUTHORS,{CHANGES,README}.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
