# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol 'Kenji Takahashi' Wo≈∫niak <kenji.sx>
# Contributor: Tianjiao Yin <ytj000+AUR@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: TDY <tdy@gmx.com>
# Contributor: Tiago Pierezan Camargo <tcamargo@gmail.com>

pkgbase=python-pyflakes
pkgname=(python-pyflakes python2-pyflakes)
pkgver=2.2.0
pkgrel=3
pkgdesc='A lint-like tool for Python to identify common errors quickly without executing code'
arch=('any')
url='https://pypi.python.org/pypi/pyflakes'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/p/pyflakes/pyflakes-$pkgver.tar.gz"
        py39.patch)
sha512sums=('6a411efef261874c216b71bcb095412448a8cbeefdf7fa5577d4f4edd48a4a740a4433665e87e5dda2c08fd9ee3bfb7f134f56c7523e1303243edfa92b0ccb35'
            'c85c175abd2ef89e93e77e5096dd1531d3b6da8a5b6fdd55b0289a21a5792d517576dd354c13688bf1ff7072cc3fd20cbcd695691895b8a059bdd0e31d7b9996')

prepare() {
  patch -Np1 -d pyflakes-$pkgver <py39.patch
  cp -a "pyflakes-$pkgver"{,-py2}
}

build() {
  cd "$srcdir"/pyflakes-$pkgver
  python setup.py build

  cd "$srcdir"/pyflakes-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pyflakes-$pkgver
  python setup.py test

  cd "$srcdir"/pyflakes-$pkgver-py2
  python2 setup.py test
}

package_python-pyflakes() {
  depends=('python-setuptools')

  cd pyflakes-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  # We have python 3 as default python, and want to keep compatibility with the old pyflakes3k naming
  ln -s pyflakes "$pkgdir/usr/bin/pyflakes3k"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pyflakes() {
  depends=('python2-setuptools')

  cd pyflakes-$pkgver-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/pyflakes{,-python2}

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
