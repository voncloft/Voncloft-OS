# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-guardian
pkgname=python-django-guardian
pkgver=2.3.0
pkgrel=2
pkgdesc="Per object permissions for Django"
arch=('any')
url="https://github.com/django-guardian/django-guardian"
license=('BSD' 'MIT')
depends=('python-django' 'python-django-environ')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-django')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('84f9825fae21e89fa62209bdb9dcd3078d280a2f6eca5cd3cde470bd416e6a788317dcf127615052b4b840b80a6b770fa5a3348ff9ea23d5c6a5518aa01fbb7d')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {AUTHORS,CHANGES,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
