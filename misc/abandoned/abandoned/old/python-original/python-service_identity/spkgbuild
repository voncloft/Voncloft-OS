# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-service_identity
_pyname=service-identity
pkgver=18.1.0
pkgrel=6
pkgdesc='Service Identity Verification for Python'
url='https://service-identity.readthedocs.io/'
arch=('any')
license=('MIT')
depends=('python' 'python-attrs' 'python-pyasn1' 'python-pyasn1-modules' 'python-idna')
makedepends=('python' 'python-setuptools' 'python-attrs' 'python-pyasn1'
             'python-pyasn1-modules' 'python-idna' 'python-sphinx')
checkdepends=('python-pytest' 'python-cryptography')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/pyca/service_identity/archive/${pkgver}.tar.gz)
sha512sums=('6b6da48c543bfbe0d9da9f0d03dbd735ce97e64a76cbf9eae2b615ced7b91c5fcca1f25353e53858c48da9c4e0c0be4e33fa4559adb50899a79689cbb372f49b')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
  sphinx-build -b text docs docs/_build/text
  sphinx-build -b man docs docs/_build/man
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH=src py.test
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/service_identity.1 "${pkgdir}/usr/share/man/man1/service_identity.1"
}

# vim: ts=2 sw=2 et:
