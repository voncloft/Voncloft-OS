# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Tomas Schertel<tschertel@gmail.com>

pkgname=python-logbook
pkgver=1.5.3
pkgrel=8
pkgdesc="Logging sytem for Python that replaces the standard libraryâ€™s logging module"
arch=('x86_64')
url="https://logbook.readthedocs.io/en/stable/"
license=('BSD')
depends=('python-six')
makedepends=('python-setuptools' 'cython' 'python-six' )
checkdepends=('redis' 'python-pytest' 'python-sqlalchemy'
              'python-mock' 'python-redis' 'python-pyzmq'
              'python-execnet' 'python-jinja' 'python-brotli' 
              'pifpaf')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mitsuhiko/logbook/archive/$pkgver.tar.gz"
        "python-logbook-1.5.3-reproducibility.patch::https://github.com/getlogbook/logbook/commit/7b51f1adbdfd71180b8da9b1462552db32350b1d.patch")
sha512sums=('c9068324f12a189ced98e2c02753489847fa533ce3a3af07b62c73e9959c2e014d47515d47e756d031639cb220a2c4a9a380cba4cb3ea1b190c50106decba686'
            'c8e993de55d95ad7ca5087ab808462abebcd40a836bcdaf797b9febe02844bec15fdfc1f76ddf5ac303caf9dc81ce450fc91a0fd03919ff7e5e7c69005835eb6')

prepare() {
  cd "$srcdir"/logbook-$pkgver
  # Make ordering of requires.txt reproducible (https://github.com/getlogbook/logbook/pull/313)
  patch -Np1 -i "$srcdir/python-logbook-1.5.3-reproducibility.patch"
}

build() {
  cd "$srcdir"/logbook-$pkgver
  cython logbook/_speedups.pyx
  python setup.py build
}

check() {
  cd "$srcdir"/logbook-$pkgver
  LC_CTYPE=en_US.UTF-8 PYTHONDONTWRITEBYTECODE=1 pifpaf run redis py.test tests
}

package() {
  cd logbook-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
