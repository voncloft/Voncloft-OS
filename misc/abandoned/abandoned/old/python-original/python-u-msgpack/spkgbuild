# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=u-msgpack-python
pkgbase='python-u-msgpack'
pkgname=('python-u-msgpack' 'python2-u-msgpack')
pkgver=2.7.1
pkgrel=1
arch=('any')
url="https://github.com/vsergeev/${_pkgname}"
license=('MIT')
makedepends=('python' 'python2')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('b7e7d433cab77171a4c752875d91836f3040306bab5063fb6dbe11f64ea69551')
b2sums=('10008f0653f969f1f5655c894be5b2497d692bf7ebf6d50de221695cab9c3a973c0870a8162efe6b61f2d9d4fb062c4130c07cddff8d64d6258d7d832efe4021')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    python setup.py build
    python2 setup.py build
}

check() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    pytest
    pytest2
}

package_python-u-msgpack() {
    pkgdesc="portable, lightweight MessagePack serializer and deserializer for Python"
    depends=('python')

    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-u-msgpack() {
    pkgdesc="portable, lightweight MessagePack serializer and deserializer for Python2"
    depends=('python2')

    cd "${srcdir}/${_pkgname}-${pkgver}"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
