# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgbase=python-uritemplate
pkgname=('python2-uritemplate' 'python-uritemplate')
pkgver=3.0.1
pkgrel=3
pkgdesc="a Python implementation of RFC6570"
arch=('any')
url="https://uritemplate.readthedocs.io/"
license=('Apache' 'BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/sigmavirus24/uritemplate/archive/${pkgver}.tar.gz")
sha1sums=('5478f460fd61838328c1916bb6bdb54bcc4677f6')

prepare() {
  cd "$srcdir"
  cp -a uritemplate-${pkgver} uritemplate-py2-${pkgver}
  cd uritemplate-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py' | grep -v old)
}

build() {
  # "Building Python2"
  cd "$srcdir"/uritemplate-py2-${pkgver}
  python2 setup.py build

  # "Building Python3"
  cd "$srcdir"/uritemplate-${pkgver}
  python setup.py build
}

package_python2-uritemplate() {
  depends=('python2')

  cd "$srcdir"/uritemplate-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 LICENSE.APACHE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.APACHE
  install -Dm644 LICENSE.BSD "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.BSD
}

package_python-uritemplate() {
  depends=('python')

  cd "$srcdir"/uritemplate-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 LICENSE.APACHE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.APACHE
  install -Dm644 LICENSE.BSD "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.BSD
}
