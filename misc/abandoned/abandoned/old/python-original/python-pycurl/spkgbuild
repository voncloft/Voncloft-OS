# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>

pkgname=python-pycurl
pkgver=7.43.0.6
pkgrel=4
pkgdesc="A Python 3.x interface to libcurl"
arch=('x86_64')
url="http://pycurl.sourceforge.net/"
license=('LGPL' 'MIT')
depends=('python' 'curl')
checkdepends=('python-bottle' 'python-nose-show-skipped' 'vsftpd' 'python-flaky' 'python-pyflakes')
source=("https://github.com/pycurl/pycurl/archive/REL_${pkgver//./_}/$pkgname-$pkgver.tar.gz")
sha512sums=('ea0160794d30758c8163f1f82fc6315c27f2cfd1b9981a7c349a48cee892fc2307e3edd25456378f1a780bc134fca199d8d6431310fd47b2ca6126a8822fea40')

prepare() {
  mv pycurl-REL_${pkgver//./_} pycurl-$pkgver
  cd pycurl-$pkgver
  find . -name '*.py' -exec sed -i -e "s|#! /usr/bin/env python|#!/usr/bin/env python3|" {} +
  sed -i 's/"share", "doc", PACKAGE/"share", "doc", "python-pycurl"/' setup.py
  sed -e "s/nosetests/nosetests3/" -e "s/python/python3/" -i tests/run.sh
}

build() {
  cd pycurl-$pkgver
  make
  python3 setup.py build
}

check() {
  export PYCURL_VSFTPD_PATH=vsftpd
  # We don't have HTTP2 enabled

  cd pycurl-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.9:$PYTHONPATH" make test || warning "Tests failed"
}

package() {
  cd pycurl-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -D -m644 COPYING-MIT -t "$pkgdir"/usr/share/licenses/$pkgname/
}
