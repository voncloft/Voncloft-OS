# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Guillaume Duboc <guilduboc@gmail.com>

_pipname=jupyterlab_server
pkgname=python-$_pipname
pkgver=2.2.0
pkgrel=1
pkgdesc="Launch an application built using JupyterLab"
arch=(any)
url="https://jupyter.org/"
license=(custom)
depends=(jupyter-server python-requests python-json5 python-babel)
makedepends=(python-setuptools)
conflicts=(jupyterlab_server)
replaces=(jupyterlab_server)
source=(https://pypi.io/packages/source/j/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('68b9322eee2561c89a22fcdf755c57fd750e8132f18fda81d47ca336d1f9b066')

build() {
  cd $_pipname-$pkgver
  python setup.py build 
}

package() {
  cd $_pipname-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
