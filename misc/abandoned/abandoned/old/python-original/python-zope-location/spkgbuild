# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-location
pkgver=4.2
pkgrel=6
pkgdesc="Special objects that have a structural location"
arch=('any')
url="https://github.com/zopefoundation/zope.location"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface' 'python-zope-schema' 'python-zope-proxy')
checkdepends=('python-zope-testrunner' 'python-zope-copy' 'python-zope-configuration' 'python-zope-component')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.location/archive/$pkgver.tar.gz")
sha512sums=('3478f28738399acbdd24491361ad3454bb8415ff30a407608780758b881a6abe504ee913d28074baad52e275046a48aa13569fd5700fbb77db36aff89d79a148')

build() {
  cd "$srcdir"/zope.location-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.location-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src || echo "Tests failed"
}

package() {
  cd zope.location-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
