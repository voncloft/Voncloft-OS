# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-libevdev
pkgver=0.9
pkgrel=3
arch=('any')
url='https://gitlab.freedesktop.org/libevdev/python-libevdev'
license=('MIT')
pkgdesc='Python wrapper for libevdev'
depends=('python' 'libevdev')
makedepends=('python-setuptools' 'python-sphinx' 'python-sphinx_rtd_theme')
checkdepends=('python-pytest' 'python-py' 'python-pluggy')
source=("$url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8b61b7b5d7f2ccc7c510afa321e048c0b1a77cf6d646d611269c75787ed4fcdb70d463dfdc7bf62b260788db3695a9797fa5809c5ff4d0441180dc31028a3b2a')

build() {
  cd $pkgname-$pkgver

  python setup.py build

  make doc
}

check() {
  cd $pkgname-$pkgver

  PYTHONPATH=. pytest test/*.py
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root "$pkgdir" --skip-build -O1

  # Install documentation
  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  cp -r -a --no-preserve=ownership doc/html "$pkgdir"/usr/share/doc/$pkgname
  rm -rf "$pkgdir"/usr/share/doc/$pkgname/html/.doctrees
}
