# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=furl
pkgname=python-furl
pkgver=2.1.0
pkgrel=6
pkgdesc="A small Python library that makes parsing and manipulating URLs easy"
url="https://github.com/gruns/furl"
depends=('python-orderedmultidict' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
license=('Unlicense')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('c7de9c87e5368a3ccb7c0639b9c679b3c71ac75144f275b2491e88d361bfca8612b3b23e76e538a98b02c1666e2dac000cb9f5add2e97d240cead7f40a1411ca')
b2sums=('9dedf9eec6bc94b7ab78c93832f144184f201d12bdcf82a4d81056d88bfd69bf15dc189d8e8d633a231221fc80c65db36c76e1519b1e64b8420f7defa22898d6')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  # disabling failing test for rather exotic url schemes:
  # https://github.com/gruns/furl/issues/121
  pytest -v -k "not test_join"
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {API,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
