# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pymediainfo
pkgname=python-pymediainfo
pkgver=5.0.3
pkgrel=1
pkgdesc="A Python wrapper around the MediaInfo library"
arch=('any')
url="https://github.com/sbraz/pymediainfo/"
license=('MIT')
depends=('libmediainfo' 'python')
makedepends=('mediainfo' 'python-setuptools-scm')
checkdepends=('python-pytest' 'python-pytest-runner')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('4124b69910050fb84162140cda5e184cb0fc13b06b85ae61ff6e47b4fca236242558b9c3c8ea90ab804ed9400171f7b1dccc25ec5961b37d1f68de21fe64580e')
b2sums=('f3ff936100badf6899cd0eb3e1155ac82b2f6831dd9a32baf9c1f67f3f5948e5731798c3630aecd97cd86ecbd5a492e73353c19637775453a1fe291d0a15ce33')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build:$PYTHONPATH"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {AUTHORS,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
