# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hypothesis-auto
pkgver=1.1.4
_commit=4ed588ab631d6c44c8959334a3425a8c0d207eff
pkgrel=3
pkgdesc="Extends Hypothesis to add fully automatic testing of type annotated functions"
url="https://github.com/timothycrosley/hypothesis-auto"
license=('MIT')
arch=('any')
depends=('python-pydantic' 'python-hypothesis')
makedepends=('python-dephell')
checkdepends=('python-pytest')
source=("https://github.com/timothycrosley/hypothesis-auto/archive/$_commit/$pkgname-$_commit.tar.gz")
sha512sums=('96a9b541dedcbe20eae242201ad2d57366f61ab60aa388ad67102a18bd1fdd916f3a45cee67ff7021dc4358707457f6f607e6a0666b85005150d09e398bfda58')

prepare() {
  cd hypothesis-auto-$_commit
  # poetry-generated setup.py are fatally broken, see:
  # https://github.com/sdispater/poetry/issues/866
  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd hypothesis-auto-$_commit
  python setup.py build
}

check() {
  cd hypothesis-auto-$_commit
  python -m pytest
}

package() {
  cd hypothesis-auto-$_commit
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
