# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>

_name=click-log
pkgname=python-click-log
pkgver=0.3.2
pkgrel=7
pkgdesc="Logging integration for Click"
arch=("any")
url="https://github.com/click-contrib/click-log"
license=("MIT")
depends=("python-click")
makedepends=("python-setuptools")
checkdepends=("python-pytest")
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('8ae5c43d739127367bbcdcf32bfd4b34b2ea1c377948df3b8588e4bfe2b0f5e7a61d1ccc6cb32c77045b15251222866daffa5a7bfb7a907487548784ba345351')
b2sums=('212ac421b402fc0f32a9aec11a9e20c87a8148c461af87af78f8068ca95d60cab0c47bf24162aefd1c299ba783fb7a70e7e9dde01f0fc1f7c44a69fa8cb07121')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build:$PYTHONPATH"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  export PYTHONHASHSEED=0
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
