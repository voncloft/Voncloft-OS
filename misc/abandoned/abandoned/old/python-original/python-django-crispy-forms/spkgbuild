# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-crispy-forms
pkgname=python-django-crispy-forms
pkgver=1.11.0
pkgrel=1
pkgdesc="The best way to have DRY Django forms"
arch=('any')
url="https://github.com/django-crispy-forms/django-crispy-forms"
license=('MIT')
depends=('python-django')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-django')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('62c332527f8d7a6a338a0959163012d9164f833f2a20662dcec17419149a8752a1beb6ff899f80c96e335ace68b95d6c8f2577a46a44197a864e464fc1b40439')
b2sums=('0d48a1fffb76a83679f21ef20141ab51389b64864be564911b821286c21deb09a1790b52266037b773ec3a00d7be3c7f27883edbfdd0195f15f9a01f3af3a068')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  DJANGO_SETTINGS_MODULE=crispy_forms.tests.test_settings pytest -v crispy_forms/tests
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CONTRIBUTORS.txt,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
