# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-argh
pkgver=0.26.2
pkgrel=8
pkgdesc="An unobtrusive argparse wrapper with natural syntax"
arch=(any)
url="https://pypi.python.org/pypi/argh"
license=('LGPL3')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-iocapture')
source=("https://github.com/neithere/argh/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('SKIP')

build() {
  cd argh-$pkgver
  python setup.py build
}

check() {
  cd argh-$pkgver
  py.test
}

package() {
  cd argh-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
