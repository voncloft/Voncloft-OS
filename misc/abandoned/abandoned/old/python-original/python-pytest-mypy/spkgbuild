# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-mypy
pkgver=0.8.0
pkgrel=1
pkgdesc='Mypy static type checker plugin for Pytest'
arch=('any')
license=('MIT')
url='https://github.com/dbader/pytest-mypy'
depends=('mypy' 'python-filelock' 'python-pytest')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-xdist')
source=("$pkgname-$pkgver.tar.gz::https://github.com/dbader/pytest-mypy/archive/v$pkgver.tar.gz")
sha512sums=('c1ab3df0125420d347acd9bb727d1e24b0b18a4f184c42a2fe178bcab00164f4b39e838990c1c3fe2522f00510c7d0dba44f272be3e48ccfdd24c5161d2931cf')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-mypy-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-mypy-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-mypy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
