# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: nblock <nblock [/at\] archlinux DOT us>
# Contributor: neithere <neithere at gmail>
# Contributor: mitsuse <mitsuse at gmail>

_pyname=wtforms
name=python-wtforms
version=2.2.1
release=10
pkgdesc='Flexible forms validation and rendering library for python web development'
url='https://wtforms.readthedocs.io/'
depends=('python')
makedepends=('python-setuptools' 'python-babel' 'python-sphinx')
source=(${name}-${version}.tar.gz::https://github.com/wtforms/wtforms/archive/${version}.tar.gz)
sha512sums=('db989ff5352d6834fc51c0ed208de11c12a2c0ad90f73dfeaa8f9995edc7f840deb427f1ffc6167f1c24e44f2137ecbb27ee1113258a6c55ed256a603657ef97')

build() {
  cd ${_pyname}-${version}
  python setup.py compile_catalog build
  make -C docs html SPHINXBUILD=sphinx-build
}

package() {
  cd ${_pyname}-${version}
  python setup.py install --root="${pkgdir}" -O1
  find "$pkgdir" -type f -name \*.po -delete
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${name}"
  install -Dm 644 CHANGES.rst README.rst  -t "${pkgdir}/usr/share/doc/${name}"
  cp -r docs/_build/html "${pkgdir}/usr/share/doc/${name}/html"
}

# vim: ts=2 sw=2 et:
