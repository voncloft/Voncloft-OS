# Maintainer: David Runge <dvzrv@archlinux.org>

_name=toolz
name=python-toolz
version=0.11.1
release=3
pkgdesc="A functional standard library for Python"
url="https://github.com/pytoolz/toolz/"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${version}.tar.gz")
sha512sums=('8bea2b75b74d9da988c777577f3ad574192659dcc4bea54ac6d1400a81bef07818c49c8da40028a8c42905f9ab5b5111c50c239fb56f34c315865f90413af68b')
b2sums=('934f122bbf9e762e72f502f8b9ba8b5aa946b6e2b758e9501c2bc58fefa3443cda4b3e2c423be221643f51b2a12de0961be835ae3a23e97525fa203c969edff4')

prepare() {
  mv -v "${_name}-${version}" "$name-$version"
}

build() {
  cd "$name-$version"
  python setup.py build
}

check() {
  cd "$name-$version"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$name-$version"
  export PYTHONHASHSEED=0
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${name}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${name}"
}
