# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

name=python-ujson
_name=ultrajson
version=4.0.2
release=1
pkgdesc="Ultra fast JSON encoder and decoder for Python"
url="https://github.com/ultrajson/ultrajson"
depends=('python')
makedepends=('python-setuptools-scm' 'python-pip')
checkdepends=('python-six' 'python-pytest')
source=(https://github.com/ultrajson/ultrajson/archive/$version/$name-$version.tar.gz)
sha512sums=('8a33a1e26721d72ead950dcb5bbff7fa6775ee81b01348a6fe49f8307f09d0f818b7283110ed7838d6d35ee421fcd284d0a23d3a2c6faf93db1cf0aedbf1934d')
b2sums=('1c7a0548659283eb936080d42b6d3a2c55c1547efc27ddcf827b54bfe8813052e3085e6d0680ce3f511e1675d8cb09572d8c11cab2c78bf93021a07fc5ab0cb8')

export SETUPTOOLS_SCM_PRETEND_VERSION=$version

build() {
  cd ${_name}-${version}
  python setup.py build
}

check() {
  cd ${_name}-${version}
  local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
  PYTHONPATH="build/lib.linux-${CARCH}-${PYTHONVERSION}" pytest
}

package() {
  cd ${_name}-${version}
  python setup.py install --skip-build -O1 --root="${pkgdir}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${name}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${name}"
}

# vim: ts=2 sw=2 et:
