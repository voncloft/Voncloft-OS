# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Evgeniy Filimonov <evgfilim1@gmail.com>

name=python-aiogram
version=2.11.2
release=3
pkgdesc="A pretty simple and fully asynchronous library for Telegram Bot API written with asyncio and aiohttp"
url="https://github.com/aiogram/aiogram"
depends=('python-aiohttp' 'python-babel')
makedepends=('python-setuptools')
checkdepends=('python-aiohttp-socks' 'python-aioredis' 'python-aresponses' 'python-pytest' 'pifpaf' 'redis')
optdepends=('python-uvloop: fast, drop-in replacement of the built-in asyncio event loop'
            'python-ujson: ultra fast JSON encoder and decoder written in pure C'
            'python-rapidjson: extremely fast C++ JSON parser and serialization library'
            'python-emoji: emojize and demojize support'
            'python-aioredis: Redis storage support'
            'python-aiohttp-socks: SOCKS4(a) and SOCKS5 proxy support'
#            'python-rethinkdb: RethinkDB storage support'  # No such package yet
)
source=("$url/archive/v$version/$name-$version.tar.gz")
sha256sums=('8a56570704a278534c9158c832a307b82215df1608c076d7a4842a7fc77e75d6')

prepare() {
  cd aiogram-$version
  sed -e '/import certifi/d' \
      -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
      -i aiogram/bot/base.py
}

build() {
  cd aiogram-$version
  python setup.py build
}

check() {
  cd aiogram-$version
  pifpaf run redis -- bash -c 'python -m pytest --redis $PIFPAF_REDIS_URL'
}

package() {
  cd aiogram-$version
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$name/
  install -Dm644 README.md -t "$pkgdir"/usr/share/doc/$name/
}
