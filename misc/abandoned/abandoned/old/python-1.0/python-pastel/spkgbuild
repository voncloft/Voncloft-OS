# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_name=pastel
name=python-pastel
version=0.2.0
release=3
pkgdesc="Bring colors to your terminal"
url="https://github.com/sdispater/${_name}"
depends=('python')
makedepends=('python-dephell' 'python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name:0:1}/${_name}/${_name}-${version}.tar.gz")
sha256sums=('46155fc523bdd4efcd450bbcb3f2b94a6e3b25edc0eb493e081104ad09e1ca36')
b2sums=('dbf758eba2eaf81d6aaa7cf2eec7d3055f22a30d6caa7814e0a49e8d6b93c387efe67096ea3eb126a0abec71917b3cd8bb627bfc16ed2be089d406ae073c5b24')

prepare() {
    cd "${srcdir}"/${_name}-${version}

    # poetry does not generate correct setup.py, nuke it and recreate from scratch
    dephell deps convert --from pyproject.toml --to setup.py
}

build(){
    cd "${srcdir}"/${_name}-${version}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_name}-${version}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_name}-${version}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${name}/LICENSE
}
