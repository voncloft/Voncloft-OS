# Maintainer: David Runge <dvzrv@archlinux.org>

_name=poyo
name=python-poyo
version=0.5.0
release=4
pkgdesc="A lightweight YAML Parser for Python"
url="https://github.com/hackebrot/poyo"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${version}.tar.gz")
sha512sums=('aad4334a2aaf63cafab0ecdbdd6f94e9e3d412e6476d7ac13a4399b4af0f43b1eb910e6a3cc7438e84262bc39413fadbe91674534e1e66f446d738c1328b9905')

prepare() {
  mv -v "${_name}-${version}" "$name-$version"
}

build() {
  cd "$name-$version"
  python setup.py build
}

check() {
  cd "$name-$version"
  export PYTHONPATH="build/lib:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$name-$version"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${name}"
  install -vDm 644 {CHANGES,COMMUNITY,CONTRIBUTING,README}.md \
    -t "${pkgdir}/usr/share/doc/${name}"
}
