# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_name=starlette
name=python-$_name
version=0.13.8
release=3
pkgdesc='The little ASGI framework that shines'
url='https://github.com/encode/starlette'
depends=('python')
optdepends=('python-aiofiles' 'python-graphene' 'python-itsdangerous' 'python-jinja'
            'python-yaml' 'python-requests' 'python-ujson')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner'
              'python-aiofiles' 'python-graphene' 'python-itsdangerous' 'python-jinja'
              'python-yaml' 'python-requests' 'python-ujson'
              'python-unidecode')
source=("$name-$version.tar.gz::$url/archive/$version.tar.gz")
sha512sums=('362d5422868aa8541406e8d69a408bc5acdd64989c5f63e6fe3119a2e842eea248bb8646af9c9e0ac35c5c59a91ee1ff15d064c74d7f760ae536193e13ae9ced')

prepare() {
  cd $_name-$version

  # needs python-multipart
  rm tests/test_formparsers.py

  # needs python-databases
  rm tests/test_database.py

  # needs a different graphql version
  rm tests/test_graphql.py
}

build() {
  cd $_name-$version

  python setup.py build
}

check() {
  cd $_name-$version

  # test_request_form_urlencoded needs python-multipart
  python setup.py pytest --addopts -k-test_request_form_urlencoded
}

package() {
  cd $_name-$version

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.md "$pkgdir"/usr/share/licenses/$name/LICENSE
}
