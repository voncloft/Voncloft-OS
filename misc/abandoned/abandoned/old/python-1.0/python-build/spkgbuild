# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>

_name=build
name=python-$_name
version=0.1.0
release=2
pkgdesc='A simple, correct PEP517 package builder'
url='https://github.com/pypa/build'
depends=('python-toml' 'python-pep517' 'python-packaging')
makedepends=('git' 'python-setuptools'
             'python-sphinx' 'python-sphinxcontrib-autoprogram' 'python-sphinx-autodoc-typehints' 'python-sphinx-furo')
source=("git+$url#tag=$version?signed")
validpgpkeys=('3DCE51D60930EBA47858BA4146F633CBB0EB4BF2') # Filipe Laíns (FFY00) <lains@archlinux.org>
sha512sums=('SKIP')

build() {
  cd $_name

  python setup.py build

  cd docs

  PYTHONPATH=../src sphinx-build -b dirhtml -v source build/html
}

package() {
  cd $_name

  python setup.py install --root="$pkgdir" --skip-build
  python -m compileall --invalidation-mode=checked-hash "$pkgdir"

  install -dm 755 "$pkgdir"/usr/share/doc/$name
  cp -r -a --no-preserve=ownership docs/build/html "$pkgdir"/usr/share/doc/$name
  rm -rf "$pkgdir"/usr/share/doc/$name/html/.doctrees

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$name/LICENSE
}
