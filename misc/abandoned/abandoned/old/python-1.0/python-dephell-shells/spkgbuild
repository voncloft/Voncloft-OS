# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_name=dephell_shells
name=python-dephell-shells
version=0.1.3
release=5
pkgdesc="Activate virtual environment for current shell"
url="https://github.com/dephell/${_name}"
depends=('python-attrs' 'python-pexpect' 'python-shellingham')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name:0:1}/${_name}/${_name}-${version}.tar.gz")
sha256sums=('420de1c53cfd6c4a6b11aef5d8a93701b479ab6ef72bae8e0b5b51167e7ba1f9')
b2sums=('29c29131e428af3b9680056f1ace55eedc6e4efa8ad115e78a4879fdb6665abc40752e1d4d1ab46a932ba4cae9d17f3ee887ccd8a617663415386ca7b4e2a335')

prepare() {
    cd "${srcdir}"/${_name}-${version}

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_name}-${version}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_name}-${version}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_name}-${version}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${name}/LICENSE
}
