# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: minder

name=python-pillow
version=8.1.0
release=1
_appname=Pillow
_py3basever=3.9
pkgdesc="Python Imaging Library (PIL) fork."
url="https://python-pillow.github.io/"
depends=('python' 'lcms2' 'libtiff' 'openjpeg2' 'libimagequant' 'libxcb')
optdepends=('freetype2: for the ImageFont module'
            'libraqm: for complex text scripts'
            'libwebp: for webp images'
            'tk: for the ImageTK module'
            'python-olefile: OLE2 file support'
            'python-pyqt5: for the ImageQt module')
makedepends=('python-setuptools' 'freetype2' 'libraqm' 'libwebp' 'tk')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/P/$_appname/$_appname-$version.tar.gz")
md5sums=('9e3ab8e9b30993099ae9fee73ff92276')

prepare() {
  cd "$srcdir/$_appname-$version"
}

build() {
  cd "$srcdir/$_appname-$version"
  python setup.py build
}

check() {
  cd "$srcdir/$_appname-$version"

  export PYTHONPATH="$PWD/build/lib.linux-$CARCH-${_py3basever%m}"
  python selftest.py
  pytest
}

package() {
  cd "$srcdir/$_appname-$version"
  python3 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$name/LICENSE"

  install -dm755 "$pkgdir/usr/include/python$_py3basever/"
  install -m644 -t "$pkgdir/usr/include/python$_py3basever/" src/libImaging/*.h
}
