# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_name=SolidPython
name=python-solidpython
version=1.0.1
release=4
pkgdesc='Python frontend for solid modelling that compiles to OpenSCAD'
url='https://github.com/SolidCode/SolidPython'
depends=('python-euclid3' 'python-pypng' 'python-prettytable' 'python-regex')
makedepends=('python-setuptools' 'python-dephell')
source=("$name-$version.tar.gz::$url/archive/v$version.tar.gz")
sha512sums=('4b2c9193be5389b12759f94fd9aa448dfa96b860fd182e6b17bb9106c83bdff1627a858eeaa46fcfd30b41c418f3986adc9d06cd5bbf2ecf18e31a44abce0816')

prepare() {
  cd $_name-$version

  sed -i -e '/tox/d' -e 's|regex = "^2019.4"|regex = ">=2019.4"|' pyproject.toml

  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd $_name-$version

  python setup.py build
}

check() {
  cd $_name-$version

  PYTHONPATH=$(pwd) solid/test/run_all_tests.sh
}

package() {
  cd $_name-$version

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
