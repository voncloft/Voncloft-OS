# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=python-feedparser
name=('python2-feedparser' 'python-feedparser' 'python-sgmllib')
version=5.2.1
release=9
pkgdesc="Parse RSS and Atom feeds in Python"
url="https://github.com/kurtmckee/feedparser/"
makedepends=('python' 'python2' 'libxml2' 'python2-setuptools' 'python-setuptools')
source=(feedparser-${version}.tar.gz::https://github.com/kurtmckee/feedparser/archive/${version}.tar.gz
        don-t-always-expect-base64.decodestring-to-exist.patch)
sha1sums=('13c9a17f821e46ba9a34d8777e0ae47a6eb86de4'
          '24542c47555fb1b07855e00d2fdf732582f397ae')

prepare() {
  patch -Np1 -d feedparser-${version} <don-t-always-expect-base64.decodestring-to-exist.patch

  cp -r feedparser-${version} feedparser-${version}-python2
  cp -r feedparser-${version} feedparser-${version}-python
  cp -r feedparser-${version} feedparser-${version}-sgmllib

  (cd feedparser-${version}-python2; sed -i 's#env python$#env python2#' feedparser/feedparsertest.py)
  (cd feedparser-${version}-python; 2to3 -w feedparser/feedparser.py feedparser/feedparsertest.py)
}

build() {
  cd feedparser-${version}-sgmllib
  python /usr/lib/python3.9/compileall.py feedparser/sgmllib3.py
}

package_python2-feedparser() {
  depends=('python2' 'libxml2' )
  pkgdesc="Parse RSS and Atom feeds in Python2"

  cd "${srcdir}/feedparser-${version}-python2"
  python2 setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${name}/license"
}

package_python-feedparser() {
  depends=('libxml2' 'python-sgmllib')

  cd "${srcdir}/feedparser-${version}-python"
  python setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${name}/license"
}

package_python-sgmllib() {
  depends=('python')
  pkgdesc="Port of sgmllib to Python3"

  cd "${srcdir}/feedparser-${version}-sgmllib"
  install -Dm644 feedparser/sgmllib3.py \
    "${pkgdir}/usr/lib/python3.9/site-packages/sgmllib.py"
  install -Dm644 feedparser/__pycache__/sgmllib3.cpython-39.pyc \
    "${pkgdir}/usr/lib/python3.9/site-packages/__pycache__/sgmllib.cpython-39.pyc"
}
