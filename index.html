# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: gmes78 <gmes.078 at gmail dot com>

pkgname=python-qasync
pkgver=0.22.0
pkgrel=1
pkgdesc="Python library for using asyncio in Qt-based applications"
arch=(any)
url="https://github.com/CabbageDevelopment/qasync"
license=('BSD')
depends=('python' 'qt5-python-bindings')
makedepends=('python-setuptools')
checkdepends=('python-pytest-raises' 'python-pyqt5' 'pyside2' 'xorg-server-xvfb')
source=("https://github.com/CabbageDevelopment/qasync/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('96c0d3560c947a66c91edbe284dd9878df03070f0f97314c3f9d62d0b39c0d9d44695ae126e99a0b4429705e60e7248ca2fd1ce3de0fee22929211af69ec4e5c')

build() {
  cd qasync-$pkgver
  python setup.py build
}

check() {
  cd qasync-$pkgver
  QT_API=PyQt5 xvfb-run python -m pytest
  QT_API=PySide2 xvfb-run python -m pytest
}

package() {
  cd qasync-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
