# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@antergos.com>

pkgname=cinnamon-menus
pkgver=4.8.2
pkgrel=1
pkgdesc="Cinnamon menu specifications"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL' 'LGPL')
depends=('glib2')
makedepends=('gobject-introspection' 'meson' 'samurai')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('6c19ebcea0d35a7a65cd70f1cdca2c5c739467ac6d2c2a1c38a5d580296707bd')
b2sums=('6b731c21bee03fbd69360258d7573e35b774cc42ffdaf7536e7c8a3779e8ef9e8bc3684166a02f114838144fa9b7ea71a46b439e00f90abee56da1925bafec76')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr \
          --buildtype=plain \
          ..
    samu
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" samu install
}
