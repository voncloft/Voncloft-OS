# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgdesc="A module with some convenient utilities not included with the standard Python install"
name=python-utils
version=2.5.6
release=1
url="https://github.com/WoLpH/python-utils"
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.org/packages/source/p/${name}/${name}-${version}.tar.gz"{,.asc})
sha512sums=('35299880afdecd4a44144b95e6b2073e51680dfd2b00f31fb333d578cfa2c2f8bf5f58128cab88a7b81e658e6d5dca303f1c9f5be3870160679e1defbf94859e'
            'SKIP')
b2sums=('2f66ab26cd6cda25f73c9f40081048404cde819e3e3c2fd11d134244ab2b80a05ab2b943e56a45bd8656bf3ba231853e3bfa9efbab9cb70515daaec70f6e0715'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

prepare() {
  cd "${name}-${version}"
  rm -v pytest.ini
}

build() {
  cd "${name}-${version}"
  python setup.py build
}

check() {
  cd "${name}-${version}"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${name}-${version}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${name}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${name}"
}
