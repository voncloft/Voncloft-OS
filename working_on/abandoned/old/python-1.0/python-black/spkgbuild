# Maintainer: Maxim Baz <$name at maximbaz dot com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: James Zhu <jameszhu@berkeley.edu>

name=python-black
version=20.8b1
release=5
pkgdesc='Uncompromising Python code formatter'
url=https://github.com/psf/black
depends=('python' 'python-appdirs' 'python-click' 'python-mypy_extensions'
         'python-pathspec' 'python-regex' 'python-setuptools' 'python-toml'
         'python-typed-ast' 'python-typing_extensions')
checkdepends=('python-aiohttp' 'python-aiohttp-cors')
optdepends=('python-aiohttp: for the blackd HTTP server'
            'python-aiohttp-cors: for the blackd HTTP server'
            'python-colorama: for colored diffs')
source=("https://files.pythonhosted.org/packages/source/b/black/black-$version.tar.gz")
sha256sums=('1c02557aa099101b9d21496f8a914e9ed2222ef70336404eeeac8edba836fbea')

build() {
  cd "black-$version"
  python setup.py build
}

check() {
  cd "black-$version"
  mkdir -p temp
  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --skip-build --root=temp
  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$sitepackages" python -m unittest tests/test_black.py
}

package() {
  cd "black-$version"
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir/usr/share/licenses/$name" LICENSE
}

# vim:set ts=2 sw=2 et:
