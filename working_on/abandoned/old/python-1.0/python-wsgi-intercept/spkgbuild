# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Neil Santos <nsantos16+aur@gmail.com>

_name=wsgi_intercept
name=python-wsgi-intercept
version=1.9.2
release=2
pkgdesc='Intercept socket connection to wsgi applications for testing.'
url="https://github.com/cdent/wsgi-intercept"
depends=('python-six')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-httplib2' 'python-pytest' 'python-requests'
'python-urllib3')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${version}.tar.gz")
sha512sums=('a313fe6302c9f168bd050c83ded1d58ef74feb54870197eb85b3527c2cc77eb76b4ca61ff4b554e13cca10ec7106ad9ba35c9a41a00bbd82ef39a26df2af7769')

build() {
  cd "${_name}-${version}"
  python setup.py build
  cd docs
  make man
}

check() {
  cd "${_name}-${version}"
  pytest -v
}

package_python-wsgi-intercept() {
  cd "${_name}-${version}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${name}/"
  install -vDm 644 README -t "${pkgdir}/usr/share/${name}/"
  install -vDm 644 docs/_build/man/${_name}.1 \
    -t "${pkgdir}/usr/share/man/man1/"
}
