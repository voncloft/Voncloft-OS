# Maintainer: David Runge <dvzrv@archlinux.org>

_name=git-url-parse
name=python-git-url-parse
version=1.2.2
release=3
pkgdesc="A simple GIT URL parser"
url="https://github.com/coala/git-url-parse"
depends=('python-pbr')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-cov' 'python-pytest-mock' 'yapf')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${version}.tar.gz")
sha512sums=('d9a1633fd08b2d4bbd33ff886776906d2981f53178e295fb9907fdb261e863e2b70293d962962355b96ea156e17bb7e224497a4a708c8d9d0cc559d111fa188b')

prepare() {
  mv -v "${_name}-$version" "$name-$version"
}

build() {
  cd "$name-$version"
  python setup.py build
}

check() {
  cd "$name-$version"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$name-$version"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${name}"
  install -vDm 644 {AUTHORS,CHANGELOG,README}.rst \
    -t "${pkgdir}/usr/share/doc/${name}"
}
