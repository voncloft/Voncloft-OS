# Maintainer: David Runge <dvzrv@archlinux.org>

_name=calmjs
name=python-calmjs
version=3.4.1
release=3
pkgdesc="framework for building toolchains and utilities for working with the JavaScript/Node.js ecosystem"
url="https://github.com/calmjs/calmjs"
depends=('python-calmjs.parse' 'python-calmjs.types' 'python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${version}.zip")
sha512sums=('3ac22ee03b0450141376ddcad1ac559e23fc15b7b496937a3b43c7334510ee1b6338b3a21c5ea875c908f4c8bb0c3657f18d83ac77cd9c76cc9f584a330316ca')

prepare() {
  mv -v "${_name}-${version}" "$name-$version"
}

build() {
  cd "$name-$version"
  python setup.py build
}

check() {
  cd "$name-$version"
  export PYTHONPATH="build:${PYTHONPATH}"
  # direct pytest setup not supported (yet)
  # https://github.com/calmjs/calmjs/issues/58
  python setup.py test
}

package() {
  cd "$name-$version"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGES,README}.rst -t "${pkgdir}/usr/share/doc/${name}"
}
