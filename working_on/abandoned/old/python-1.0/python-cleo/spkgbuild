# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_name=cleo
name=python-cleo
version=0.7.6
release=4
pkgdesc="create beautiful and testable command-line interfaces"
url="https://github.com/sdispater/${_name}"
depends=('python-clikit')
makedepends=('python-dephell' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-mock')
source=("${_name}-${version}.tar.gz::${url}/archive/${version}.tar.gz")
sha256sums=('844d732c3306745cd2ffb2bd79327155a303844e3f9111bd7b578d9dd79ad2f7')
b2sums=('592c4e2d877909ccddc814293bb346c90bc5f4426231bcf8aa8fdae24b2f3ef8531d3cafb77e6f294868a34f66c5bd83b31633917e5b9d37c54e68502cbacf03')

prepare() {
    cd "${srcdir}"/${_name}-${version}

    # poetry-generated setup.py are fatally broken, see:
    # https://github.com/sdispater/poetry/issues/866
    dephell deps convert --from pyproject.toml --to setup.py
}

build(){
    cd "${srcdir}"/${_name}-${version}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_name}-${version}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_name}-${version}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${name}/LICENSE
}
