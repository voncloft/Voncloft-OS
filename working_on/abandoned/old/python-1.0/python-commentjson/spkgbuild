# Maintainer: Felix Yan <felixonmars@archlinux.org>

name=python-commentjson
version=0.9.0
release=3
pkgdesc="Add Python and JavaScript style comments in your JSON files"
url="https://github.com/vaidik/commentjson"
depends=('python-lark-parser')
makedepends=('python-setuptools')
checkdepends=('python-tests')
source=("$name-$version.tar.gz::https://github.com/vaidik/commentjson/archive/v$version.tar.gz")
sha512sums=('cd3d28adecbc445ed3faed7847c65a23829f408cf8aa3fa85544a0f0cf9fa5544b8aff560725cf51921f249123daf9ce9953069e99ca64a312bb4bff652512fe')

prepare() {
  cd commentjson-$version
  sed -i 's/,<0/,<1/' setup.py
}

build() {
  cd commentjson-$version
  python setup.py build
}

check() {
  cd commentjson-$version
  python setup.py test
}

package() {
  cd commentjson-$version
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.rst -t "$pkgdir"/usr/share/licenses/$name/
}
