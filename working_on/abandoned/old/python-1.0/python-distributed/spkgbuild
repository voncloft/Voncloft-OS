# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=distributed
name=python-${_pkg}
version=2020.12.0
release=2
pkgdesc="Distributed task scheduler for Dask"
url="https://distributed.dask.org/"
depends=(
    python
    python-click
    python-cloudpickle
    python-dask
    python-msgpack
    python-psutil
    python-setuptools
    python-sortedcontainers
    python-tblib
    python-toolz
    python-tornado
    python-yaml
    python-zict
)
makedepends=(cython)
checkdepends=(ipython
    python-pytest
    python-pytest-asyncio
    python-pytest-repeat
    python-pytest-timeout
    python-blosc
    python-cryptography
    python-distributed
    python-fsspec
    python-h5py
    python-ipykernel
    python-ipywidgets
    python-joblib
    python-jsonschema
    python-jupyter_client
    python-lz4
    python-netcdf4
    python-numpy
    python-pandas
    python-paramiko
    python-prometheus_client
    python-pytorch
    python-requests
    python-scipy
    python-snappy
    python-zstandard
)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${version}.tar.gz
        py39.patch)
#source=(https://github.com/dask/distributed/archive/${version}/${name}-${version}.tar.gz)
sha256sums=('2a0b6acc921cd4e0143a7c4383cdcbed7defbc4bd9dc3aab0c7f1c45f14f80e1'
            'e872d3655fc634f226b178d579377ed862c14ff488058586e61c82fd903ed680')

prepare() {
  cd ${_pkg}-${version}
  # https://github.com/spyder-ide/spyder-kernels/issues/258
  # https://github.com/dask/distributed/pull/4234
  patch -Np1 -i ../py39.patch
}

build() {
  cd ${_pkg}-${version}
  python setup.py build --with-cython
}

check() {
  cd ${_pkg}-${version}
  # The deselected test aborts the test suite
  # Even without this, ~30 are failing with `RuntimeError: There is no current event loop in thread`
  pytest distributed -v --deselect distributed/deploy/tests/test_old_ssh.py::test_cluster || echo "Tests failed"
}

package() {
  cd ${_pkg}-${version}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build --with-cython
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${name}/
}
