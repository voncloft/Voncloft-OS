# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

name=python-flasgger
version=0.9.5
release=3
pkgdesc="Extract swagger specs from your flask project"
url="https://github.com/flasgger/flasgger"
depends=('python-flask' 'python-yaml' 'python-jsonschema' 'python-mistune' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-flex' 'python-marshmallow' 'python-decorator'
              'python-apispec-webframeworks' 'python-flask-restful' 'python-flask-jwt')
source=("$name-$version.tar.gz::https://github.com/flasgger/flasgger/archive/$version.tar.gz")
sha512sums=('d785c272a3d2fc4753ceb061587fff1db2ec726fe7cea7b0ec723a56f2f1f6296545f52249a08a1ae042e2c002c88eb2d9442928c53b00345bdde82442149fae')

prepare() {
  cd flasgger-$version
  # apispec 4
  sed -i 's/schema2parameters(v)/schema2parameters(v, location="body")/' flasgger/marshmallow_apispec.py
}

build() {
  cd flasgger-$version
  python setup.py build
}

check() {
  cd flasgger-$version
  PYTHONPATH="$PWD/etc/flasgger_package" python -m pytest tests
}

package() {
  cd flasgger-$version
  python setup.py install --root="$pkgdir" --optimize=1
}
