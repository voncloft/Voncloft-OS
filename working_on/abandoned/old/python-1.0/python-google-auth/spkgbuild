# Maintainer: Felix Yan <felixonmars@archlinux.org>

name=python-google-auth
version=1.25.0
release=1
pkgdesc="Google Authentication Library"
url="https://github.com/GoogleCloudPlatform/google-auth-library-python"
depends=('ca-certificates' 'python-pyasn1-modules' 'python-rsa' 'python-setuptools'
         'python-cachetools')
checkdepends=('python-pytest' 'python-mock' 'python-requests' 'python-flask'
              'python-oauth2client' 'python-pytest-localserver' 'python-cryptography'
              'python-freezegun' 'python-responses' 'python-pyopenssl')
source=("$name-$version.tar.gz::https://github.com/GoogleCloudPlatform/google-auth-library-python/archive/v$version.tar.gz")
sha512sums=('5d8bf7225c8d3aede075011a3880a1db427ed75fdfadc6a4da7ada3313eeca5b424863725f5f33acfab790fbc2d1e1bf2a8d010d2dddd06fc02fd018ab75671f')

prepare() {
  # Use system cert
  sed -i -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
         -e 's|if certifi is not None:|if True:|' \
         -e 's|import certifi|pass|' \
         google-auth-library-python-$version/google/auth/transport/{urllib3,requests}.py
}

build() {
  cd google-auth-library-python-$version
  python setup.py build
}

check() {
  # Skip the without_certifi tests because we always have the system ca bundle

  cd google-auth-library-python-$version
  python -m pytest tests --deselect tests/transport/test_urllib3.py::test__make_default_http_without_certifi
}

package() {
  cd google-auth-library-python-$version
  python setup.py install --root="$pkgdir" --optimize=1
}
