# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

name=python-openstacksdk
version=0.53.0
release=1
pkgdesc="An SDK for building applications to work with OpenStack"
url="https://developer.openstack.org/sdks/python/openstacksdk"
depends=('python-six' 'python-pbr' 'python-yaml' 'python-jsonpatch' 'python-appdirs'
         'python-keystoneauth1' 'python-os-service-types' 'python-deprecation'
         'python-requestsexceptions' 'python-decorator' 'python-jmespath' 'python-iso8601'
         'python-netifaces' 'python-dogpile.cache' 'python-munch' 'python-cryptography')
makedepends=('python-setuptools')
checkdepends=('python-stestr' 'python-ddt' 'python-mock' 'python-requests-mock' 'python-oslotest'
              'python-jsonschema' 'python-oslo-config' 'python-prometheus_client' 'python-hacking')
source=("$name-$version.tar.gz::https://github.com/openstack/openstacksdk/archive/$version.tar.gz")
sha512sums=('57ea90c4db381a0ccf9f427fbc6653d63d0a95ba85b278da27165b5ee6a734773830cb9b3551fc7643bd27cdaa8a1aa97a5804b6feae19f06e9a4269703b21e0')

export PBR_VERSION=$version

prepare() {
  echo -e "\nglobal-include *.json" >> openstacksdk-$version/MANIFEST.in
  sed -i 's/assertItemsEqual/assertCountEqual/' openstacksdk-$version/openstack/tests/unit/*.py openstacksdk-$version/openstack/tests/unit/*/*.py
}

build() {
  cd openstacksdk-$version
  python setup.py build
}

check() {
  # From https://github.com/openstack/openstacksdk/blob/master/tox.ini
  export OS_LOG_CAPTURE=true OS_STDOUT_CAPTURE=true OS_STDERR_CAPTURE=true

  cd openstacksdk-$version
  # TODO
  stestr run || :
}

package() {
  cd openstacksdk-$version
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
