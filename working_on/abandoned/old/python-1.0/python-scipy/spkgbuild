# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Bodor Dávid Gábor <david.gabor.bodor@gmail.com>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

_name=scipy
name=python-scipy
version=1.6.0
release=1
pkgdesc="SciPy is open-source software for mathematics, science, and engineering."
url="https://www.scipy.org/"
depends=(python-numpy)
provides=(scipy)
makedepends=(gcc-fortran python-setuptools pybind11)
checkdepends=(python-pytest)
optdepends=('python-pillow: for image saving module')
source=("https://pypi.python.org/packages/source/${_name:0:1}/$_name/$_name-$version.tar.gz")
sha256sums=('cb6dc9f82dfd95f6b9032a8d7ea70efeeb15d5b5fd6ed4e8537bb3c673580566')

build() {
  cd scipy-${version}
  python setup.py config_fc build
}

check() {
  cd scipy-${version}
  python setup.py config_fc install \
    --prefix=/usr --root="$srcdir"/test --optimize=1
  export PYTHONPATH="$srcdir"/test/usr/lib/python3.9/site-packages
  cd ..
  python -c "from scipy import test; test('full')"
}

package() {
  cd scipy-$version
  python3 setup.py config_fc install \
    --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$name
}
