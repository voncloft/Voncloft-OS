# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pyxDamerauLevenshtein
name=python-${_pkg,,}
version=1.6.1
release=1
pkgdesc="Implements the Damerau-Levenshtein (DL) edit distance algorithm"
url="https://github.com/gfairchild/pyxDamerauLevenshtein"
depends=(python-numpy)
makedepends=(python-setuptools cython)
checkdepends=(python-pytest)
# No tests in PyPi tarballs
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${version}.tar.gz)
source=(${url}/archive/v${version}/${_pkg}-${version}.tar.gz)
sha256sums=('26f843b0af9e04e538c7a2eda51ce9bcbd3f5723a1e5c6ff842fca11f5a08b27')

build() {
  cd ${_pkg}-${version}
  python setup.py build
}

check() {
  cd ${_pkg}-${version}
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="${PWD}"/build/lib.linux-${CARCH}-${python_version} pytest
}

package() {
  cd ${_pkg}-${version}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${name}
}
