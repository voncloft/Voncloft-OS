# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Philipp A. <flying-sheep@web.de>

_name=aiohttp
name=python-aiohttp
_gitcommit=2f655a59d0daedfa2a794996c4355b576c98ecc8
version=3.7.3
release=1
pkgdesc='HTTP client/server for asyncio'
url='https://aiohttp.readthedocs.io'
depends=('python' 'python-chardet' 'python-multidict' 'python-async-timeout'
         'python-yarl' 'python-attrs')
makedepends=('cython' 'python-setuptools' 'git')
checkdepends=('python-pytest' 'python-pytest-runner' 'python-pytest-mock'
              'python-pytest-timeout' 'python-async_generator' 'python-brotlipy'
              'python-pytest-xdist' 'python-pytest-forked' 'python-pytest-cov'
              'python-trustme' 'python-freezegun' 'gunicorn' 'python-re-assert')
optdepends=('gunicorn: to deploy using Gunicorn'
            'python-aiodns: for fast DNS resolving'
            'python-cchardet: for faster encoding detection'
            'python-brotlipy: for Brotli transfer-encodings support')
source=(${name}::"git+https://github.com/aio-libs/aiohttp#commit=${_gitcommit}"
        git+https://github.com/nodejs/http-parser)
sha512sums=('SKIP'
            'SKIP')

version() {
  cd ${name}
  git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${name}
  git submodule init
  git config submodule."vendor/http-parser".url "${srcdir}/http-parser"
  git submodule update --recursive
  sed 's|.install-cython ||' -i Makefile
}

build() {
  cd ${name}
  make cythonize
  python setup.py build
}

check() {
  cd ${name}
  python setup.py test
}

package() {
  cd ${name}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
}

# vim: ts=2 sw=2 et:
