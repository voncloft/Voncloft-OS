# Maintainer: Felix Yan <felixonmars@archlinux.org>

name=python-py3c
version=1.3
release=1
pkgdesc="A Python 2/3 compatibility layer for C extensions"
url="https://github.com/encukou/py3c"
depends=('python')
makedepends=('python-setuptools')
source=("$name-$version.tar.gz::https://github.com/encukou/py3c/archive/v$version.tar.gz")
sha512sums=('cf43808f415a2a8832c669b8bfdb74754c99941694afaa6ecc7d11d0c9fa542136cf0066bffab05b2e0cacc532d3301ca4275da358f7947723c64c5f1a2633ed')

prepare() {
  cd py3c-$version
  # Don't regenerate with broken path in "install" target
  sed -i 's/install: py3c.pc/install:/' Makefile
}

build() {
  cd py3c-$version
  python setup.py build
  make prefix=/usr py3c.pc
}

check() {
  cd py3c-$version
  make test-python
}

package() {
  cd py3c-$version
  make prefix="$pkgdir"/usr install
  python setup.py install --root="$pkgdir" --optimize=1

  # Remove headers in broken location
  rm -r "$pkgdir"/usr/include/python*

  install -Dm644 LICENSE.MIT -t "$pkgdir"/usr/share/licenses/$name/
}
