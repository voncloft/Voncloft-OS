# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

name=python-libevdev
version=0.9
release=3
url='https://gitlab.freedesktop.org/libevdev/python-libevdev'
pkgdesc='Python wrapper for libevdev'
depends=('python' 'libevdev')
makedepends=('python-setuptools' 'python-sphinx' 'python-sphinx_rtd_theme')
checkdepends=('python-pytest' 'python-py' 'python-pluggy')
source=("$url/-/archive/$version/$name-$version.tar.gz")
sha512sums=('8b61b7b5d7f2ccc7c510afa321e048c0b1a77cf6d646d611269c75787ed4fcdb70d463dfdc7bf62b260788db3695a9797fa5809c5ff4d0441180dc31028a3b2a')

build() {
  cd $name-$version

  python setup.py build

  make doc
}

check() {
  cd $name-$version

  PYTHONPATH=. pytest test/*.py
}

package() {
  cd $name-$version

  python setup.py install --root "$pkgdir" --skip-build -O1

  # Install documentation
  install -dm 755 "$pkgdir"/usr/share/doc/$name
  cp -r -a --no-preserve=ownership doc/html "$pkgdir"/usr/share/doc/$name
  rm -rf "$pkgdir"/usr/share/doc/$name/html/.doctrees
}
