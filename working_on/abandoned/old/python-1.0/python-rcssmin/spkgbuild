# Maintainer: David Runge <dvzrv@archlinux.org>

_name=rcssmin
name=python-rcssmin
version=1.0.6
release=4
pkgdesc="Fast CSS minifier for Python"
url="https://github.com/ndparker/rcssmin"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${version}.tar.gz")
sha512sums=('5129a6007755a5b1949256bba7ad3616a08034d7bfcf06a1f9d8143897dddc5d2f6b8a0ebde7abb2e8b12e5b1aee2e46260e59101a78f35026e15f9f6503db77')

prepare() {
  mv -v "${_name}-${version}" "$name-$version"
}

build() {
  cd "$name-$version"
  python setup.py build
}

check() {
  cd "$name-$version"
  export PYTHONPATH="build:${PYTHONPATH}"
  # in the next release hopefully plain pytest -v
  python run_tests.py -q tests
}

package() {
  cd "$name-$version"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  mv -v "${pkgdir}/usr/share/doc/"{${_name},${name}}
}
