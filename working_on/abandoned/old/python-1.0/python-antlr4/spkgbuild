# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Baptiste Jonglez <archlinux at bitsofnetworks dot org>

_name=antlr4
name=python-${_name}
version=4.9.1
release=1
pkgdesc="ANTLR4 runtime for Python"
url="https://www.antlr.org/"
depends=('python')
makedepends=('python-setuptools')
source=("${name}-${version}.tar.gz::https://github.com/antlr/${_name}/archive/${version}.tar.gz"
        'fix-tests.patch')
sha256sums=('db170179917ce6fec7bc4ecf72edba36b97c9881e09e03af6ac0c901eba52a8f'
            '3a1735cafc4794b4e7c268b1de3028001cacd181c99044ae6779420d2a2f5e32')

prepare() {
  patch -Np1 -i "${srcdir}"/fix-tests.patch -d "${srcdir}/${_name}-${version}"
}

build() {
  cd "${srcdir}/${_name}-${version}/runtime/Python3"
  python setup.py build
}

check() {
  cd "${srcdir}/${_name}-${version}/runtime/Python3"
  python setup.py test
}

package() {
  cd "${srcdir}/${_name}-${version}/runtime/Python3"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -d "${pkgdir}/usr/share/licenses/${name}"
  install -Dm644 "${srcdir}/${_name}-${version}"/LICENSE.txt "${pkgdir}/usr/share/licenses/${name}"/LICENSE
}
