# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pdfminer
name=python-${_pkg}
version=20201018
release=2
pkgdesc="Python PDF Parser"
url="https://github.com/pdfminer/pdfminer.six"
depends=(python-chardet python-cryptography python-sortedcontainers)
makedepends=(python-setuptools cython)
checkdepends=(python-nose)
# Provided tarballs do not include tests
#source=(${url}/releases/download/${version}/${_pkg}.six-${version}.tar.gz)
source=(${url}/archive/${version}/${_pkg}-${version}.tar.gz)
sha256sums=('0e9c8b0a9c40c2aee044c3091ad6b18b5e6279722e64b6b975f8aedc26bf8e38')

build() {
  cd ${_pkg}.six-${version}
  python setup.py build
}

check() {
  cd ${_pkg}.six-${version}
  nosetests
}

package() {
  cd ${_pkg}.six-${version}
  python setup.py install --skip-build --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${name}/
  # Fix ownership
  chmod a+r "${pkgdir}"$(python -c "import site; print(site.getsitepackages()[0])")/pdfminer/cmap/*
}
