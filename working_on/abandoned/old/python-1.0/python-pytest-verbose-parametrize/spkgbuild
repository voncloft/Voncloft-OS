# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pytest-verbose-parametrize
name=python-pytest-verbose-parametrize
version=1.7.0
release=3
pkgdesc="Pytest parametrize hook to generate ids for parametrized tests"
url="https://github.com/man-group/pytest-plugins/tree/master/pytest-verbose-parametrize"
depends=('python-pytest' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'python-mock' 'python-pytest-virtualenv')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${version}.tar.gz")
sha512sums=('ac81cdf002d60b4af916c4beb4c832c9c39dc9dcfa3eea7385b093f9ea18391cbf968a4e908abdb23523a7cb1e294dce2017083414e61eef6762f157319e2cf4')

prepare() {
  mv -v "${_name}-${version}" "$name-$version"
}

build() {
  cd "$name-$version"
  python setup.py build
}

check() {
  cd "$name-$version"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v -k 'not test_handles_apparent_duplicates and not test_truncates_long_ids'
}

package() {
  cd "$name-$version"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${name}"
  install -vDm 644 {CHANGES,README}.md -t "${pkgdir}/usr/share/doc/${name}"
}
