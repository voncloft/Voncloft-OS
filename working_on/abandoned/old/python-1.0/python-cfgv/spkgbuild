# Maintainer: David Runge <dvzrv@archlinux.org>

_name=cfgv
name=python-cfgv
version=3.2.0
release=3
pkgdesc="Validate configuration and produce human readable error messages"
url="https://github.com/asottile/cfgv"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
# pypi sdist tarballs don't contain tests:
# https://github.com/asottile/cfgv/issues/23
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${version}.tar.gz")
source=("${_name}-${version}.tar.gz::https://github.com/asottile/${_name}/archive/v${version}.tar.gz")
sha512sums=('6f1a54b505d298a9806a739edffc79feea41638f766ddc25f0712001e5bceb206153e3a957dcf4909fbe125b6e9bd077aba6317d00e632a06ed24f1dfd06c555')

prepare() {
  mv -v "${_name}-${version}" "$name-$version"
}

build() {
  cd "$name-$version"
  python setup.py build
}

check() {
  cd "$name-$version"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$name-$version"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${name}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${name}"
}
