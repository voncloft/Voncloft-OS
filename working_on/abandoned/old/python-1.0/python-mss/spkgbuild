# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=mss
name=python-${_pkg}
version=6.0.0
release=3
pkgdesc="Ultra fast cross-platform multiple screenshots module"
url="https://github.com/BoboTiG/python-mss"
depends=(python)
makedepends=(python-setuptools)
checkdepends=(python-pytest xorg-server-xvfb libxrandr lsof python-flaky python-wheel)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${version}.tar.gz)
#source=(${url}/archive/v${version}/${name}-${version}.tar.gz)
sha256sums=('b42bcc170a138c1afbe05f532374d97486f52c2c69328ae1591dafa64ab92c36')

build() {
  cd ${_pkg}-${version}
  python setup.py build
}

check() {
  cd ${_pkg}-${version}
  PYTHONPATH="${PWD}/build/lib/" xvfb-run -a -s "+extension RANDR +render -screen 0 1024x768x24" pytest
}

package() {
  cd ${_pkg}-${version}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${name}
}
