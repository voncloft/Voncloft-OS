# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Anthony25 <Anthony Ruhier>

_pkgname=Flask-Migrate
pkgname=python-flask-migrate
pkgver=2.5.3
pkgrel=3
pkgdesc='SQLAlchemy database migrations for Flask applications using Alembic..'
url='https://github.com/miguelgrinberg/Flask-Migrate/'
arch=('any')
license=('MIT')
depends=('python' 'python-alembic' 'python-flask' 'python-flask-script' 'python-flask-sqlalchemy'
         'python-setuptools')
makedepends=('python-sphinx')
source=(https://github.com/miguelgrinberg/Flask-Migrate/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('4af1feb1b5e5a0332c11c3ebe7981228719776b73c0bfa28b4aead541dff46d8')
sha512sums=('f9fff3d94d07a5d79bdd40b04cfe808c40915c3f4e48b0db8f5268897892591604480612aefb1587bc9736fbca023bd5738f5d168f16e6460568151521fa4609')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
  make -j1 -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${_pkgname}-${pkgver}
  python setup.py test
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  install -Dm 644 README.md CHANGES.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/flask-migrate.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
