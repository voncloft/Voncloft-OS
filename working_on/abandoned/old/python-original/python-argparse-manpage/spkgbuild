# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=python-argparse-manpage
pkgver=1.4
pkgrel=2
pkgdesc='Automatically build man-pages for your Python project'
url='https://github.com/praiskup/argparse-manpage'
arch=('any')
license=('Apache')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/praiskup/argparse-manpage/archive/v${pkgver}.tar.gz")
sha512sums=('4756781dbe988541cb0e1fd8a79c4d262c7bf54397fb302ff3ad706e9a896d68f781931e73013cdd7136efc6e46914118b862e0377f49b73fb25b20d0a23744d')

build() {
  cd "$srcdir/argparse-manpage-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/argparse-manpage-$pkgver"
  python setup.py test
}

package() {
  cd "$srcdir/argparse-manpage-$pkgver"
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
