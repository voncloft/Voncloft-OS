# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bence Balint <balintbence1337@gmail.com>

pkgname=python-w3lib
pkgver=1.22.0
pkgrel=3
pkgdesc="Library of web-related functions"
arch=('any')
license=('BSD')
url="https://w3lib.readthedocs.org"
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/scrapy/w3lib/archive/v$pkgver.tar.gz")
sha512sums=('14e12726ca46a60d0a86f670f8b19415c2a6e304eeb9a152008a2d1ea81267ddc5ff36b47fdd308bb4764869ffff61e2c243c20cfe85f79a8eb7e31ba85d46dd')

build() {
  cd "$srcdir"/w3lib-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/w3lib-$pkgver
  py.test
}

package() {
  cd w3lib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
