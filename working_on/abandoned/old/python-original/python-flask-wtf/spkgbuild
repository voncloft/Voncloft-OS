# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Serge Victor <arch@random.re>

_pyname=flask-wtf
pkgname=python-flask-wtf
pkgver=0.14.3
pkgrel=3
pkgdesc='Simple integration of Flask and WTForms'
url='https://flask-wtf.readthedocs.io/'
arch=('any')
license=('BSD')
depends=('python-flask' 'python-wtforms' 'python-werkzeug')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-flask-babel' 'python-tox')
options=('!makeflags')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/lepture/flask-wtf/archive/${pkgver}.tar.gz)
sha512sums=('f6d3c1e94647b33dbbc5ee9da86a749c879de4e43f98a58f43811393a5e05b5e6a1cc2546adc381ccdefeabafeb90cc75a00464491872465f9b60b641112e3c9')

prepare() {
  sed -r 's|dist = get_distribution|#|' -i ${_pyname}-${pkgver}/docs/conf.py
  sed -r "s|(version =)|\\1 '${pkgver}'  #|" -i ${_pyname}-${pkgver}/docs/conf.py
  sed -r "s|(release =)|\\1 '${pkgver}'  #|" -i ${_pyname}-${pkgver}/docs/conf.py
}

build() {
  cd ${_pyname}-${pkgver}
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${_pyname}-${pkgver}
  tox -e py39
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst  -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/_build/text "${pkgdir}/usr/share/doc/${pkgname}/text"
  install -Dm 644 docs/_build/man/flask-wtf.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
