# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libtmux
pkgname=python-libtmux
pkgver=0.8.5
pkgrel=3
pkgdesc="Python api for tmux"
arch=('any')
url="https://libtmux.git-pull.com/"
license=('MIT')
depends=('python' 'tmux')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'procps-ng')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tmux-python/${_name}/archive/v${pkgver}.tar.gz")
# pypi sources don't include the tests: https://github.com/tmux-python/libtmux/issues/173
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('d456725c3ca41a672ee5239bfb5f42a262674638d308fa141627995be8b8fe348bb8713b735eb05c5a3cc73f35fe3f6fac12024f6254c950a3048b2e07606363')
b2sums=('5af148555eb89c66bdc66f3f214967f7e901d9b86ea85e02a9fba64f2eb3559828519ba8120eaee5eb86ec75bee2e599a08384dd096de8dc0969129c58172c1e')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {CHANGES,README.rst,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
