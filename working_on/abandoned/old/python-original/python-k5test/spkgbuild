# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-k5test
pkgver=0.9.2
pkgrel=1
pkgdesc='Library for setting up self-contained Kerberos 5 environments'
url='https://github.com/pythongssapi/k5test'
arch=('any')
license=('custom')
depends=('python' 'krb5')
makedepends=('python-setuptools')
source=("https://github.com/pythongssapi/k5test/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('0feb0335da75c6201f43760834c723b82428838791f9adddd75a7259e55d9d74e752ce11fdfad9a2964abf9ca2cfe03e4914127af5f3d421fe78966cbeab647e')
b2sums=('7a4864c6c37725f87b0c4b6263093ac01a190bfd769a288c4f47c5590b42fa452af56150e89fa867b7a5b1ea56204f54b769b31b49113c5921a5209336ac9a36')

build() {
  cd "k5test-${pkgver}"
  python setup.py build
}

package() {
  cd "k5test-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
