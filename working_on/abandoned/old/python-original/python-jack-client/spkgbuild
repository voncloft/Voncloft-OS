# Maintainer: David Runge <dvzrv@archlinux.org>

_name=JACK-Client
pkgname=python-jack-client
pkgver=0.5.3
pkgrel=2
pkgdesc="JACK Audio Connection Kit (JACK) Client for Python"
arch=('any')
url="https://github.com/spatialaudio/jackclient-python/"
license=('MIT')
depends=('jack' 'python-cffi')
makedepends=('python-setuptools')
optdepends=('python-numpy: Access input/output buffers as numpy arrays')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('7bafa6e375e96a34394e6b191b26306df446e47db78ede8e0dcb3e6953e9c200533e915559d0f9b00d358c34b0466fcea7dfbcfccff615f8f2ec947021321304')
b2sums=('b683e135e7cc350087ef8effe670129a5ad58fd0cba9d66aedc575f9f71a6496c6511167f906b385bb42355e9cb787986d02e643c40849bec2d615733fe070d2')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # docs
  install -vDm 644 {CONTRIBUTING,NEWS,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # examples
  install -vDm 755 examples/*.py \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples/"
}
