# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-copr
pkgver=1.108
pkgrel=1
pkgdesc="Python client for copr service"
url="https://pagure.io/copr/copr"
license=('GPL')
arch=('any')
depends=('python-marshmallow' 'python-requests' 'python-requests-toolbelt' 'python-six' 'python-munch')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock')
source=("https://pagure.io/copr/copr/archive/python-copr-$pkgver-1/copr-python-copr-$pkgver-1.tar.gz")
sha512sums=('0dd6b4bb230f0777059d97863dd2d654851601ee01522089edb75e96b278713842b56f9235f79e6defa46711177862f8891b4cd84178405cf257fce334b79bb0')

build() {
  cd copr-python-copr-$pkgver-1/python
  python setup.py build
}

check() {
  cd copr-python-copr-$pkgver-1/python
  pytest
}

package() {
  cd copr-python-copr-$pkgver-1/python
  python setup.py install --root="$pkgdir" --optimize=1
}
