# Maintainer: David Runge <dvzrv@archlinux.org>

_name=subprocess-tee
pkgname=python-subprocess-tee
pkgver=0.2.0
pkgrel=1
pkgdesc="A subprocess.run that works like tee"
arch=('any')
url="https://github.com/pycontribs/subprocess-tee"
license=('MIT')
depends=('python')
makedepends=('python-setuptools_scm')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-xdist' 'python-enrich')
optdepends=('python-enrich: for rich text rendering')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('f500f5bded2eeeb9b3035181d4c98d89f47c007f6e35121bde776348d7933d5b6213c9328562cdabed06b61c8999d7ad985a10b6e736d323b4770b968ec07315')
b2sums=('678edc6fe9a467e78c3d1affa53471850df96e428c72785c03a8b9c7c64b080837d121f9cc3a98133ea9d43db3fa0b0513efc915bcd2a01cbf6821afd9d8b1a9')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # do not require to run with python-pytest-cov
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
