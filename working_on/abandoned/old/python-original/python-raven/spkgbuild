# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Benjamin A. Shelton <zancarius@gmail.com>

pkgname=python-raven
pkgver=6.10.0
pkgrel=7
pkgdesc="Python client for Sentry"
arch=('any')
url="https://pypi.python.org/pypi/raven"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov' 'python-bottle' 'python-celery' 'python-django'
              'python-pycodestyle' 'python-mock' 'python-nose' 'python-pytz' 'python-exam'
              'python-requests' 'python-tornado' 'python-paste' 'python-webob' 'python-webtest'
              'python-anyjson' 'python-flask' 'python-blinker' 'python-logbook'
              'python-pytest-django' 'python-flask-login' 'python-pytest-timeout' 'python-zconfig'
              'python-pytest-xdist')
optdepends=('python-setuptools: for "raven" script')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/getsentry/raven-python/archive/$pkgver.tar.gz")
sha512sums=('4e65c03b1c5d084588ef381223b06c7b2ded3239fb2f2da4b2694fcf897a590ba5b03ac421b6dd88e0ce408f2fafd605d92a2b544b4f975ece5510eb6010583b')

build() {
  cd "$srcdir"/raven-python-$pkgver
  python setup.py build
}

check() {
  # Hack distribution check by installing it
  # "-p no:logging": https://github.com/getsentry/raven-python/issues/1164

  cd "$srcdir"/raven-python-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages:$PYTHONPATH" \
    py.test -p no:logging tests || echo "https://github.com/getsentry/raven-python/issues/1275"
}

package() {
  cd "$srcdir"/raven-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
