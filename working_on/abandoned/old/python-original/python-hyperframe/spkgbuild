# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hyperframe
pkgver=6.0.0
pkgrel=1
pkgdesc="HTTP/2 framing layer for Python"
arch=('any')
license=('MIT')
url="https://hyper.rtfd.org"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/python-hyper/hyperframe/archive/v$pkgver.tar.gz")
sha512sums=('32c70e4728e084a90ae3b1793ed9531dc3c647ea66bcbfe22de1a335c324fef58fbf8004a1703da4925e8752942ee491512a79c5a4db8e3f42aac1de431c6c9f')

build() {
  cd "$srcdir"/hyperframe-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/hyperframe-$pkgver
  python setup.py pytest
}

package_python-hyperframe() {
  cd hyperframe-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
