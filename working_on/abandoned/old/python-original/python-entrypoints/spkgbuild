# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-entrypoints
_name=entrypoints
pkgver=0.3
pkgrel=6
pkgdesc="Discover and load entry points from installed packages."
arch=('any')
url="https://github.com/takluyver/entrypoints"
#url="https://pypi.python.org/pypi/entrypoints"
license=('MIT')
depends=('python')
makedepends=('python')
#makedepends=('python-pip')
# several sources because the WHL doesn't come with a license
# and pypi version is the only one with setup.py
source=("$pkgname-$pkgver.tgz::https://github.com/takluyver/entrypoints/archive/$pkgver.tar.gz"
        "https://files.pythonhosted.org/packages/py2.py3/e/$_name/$_name-$pkgver-py2.py3-none-any.whl"
        "$pkgname-pypi-$pkgver.tgz::https://pypi.io/packages/source/e/$_name/$_name-$pkgver.tar.gz")
md5sums=('3b2d456ff069b080aa95f162a53a6679'
         'c47396b6c43dadc81e25d29b9fac8bd3'
         'c5c61ea2e46a0c50ea08f4af7955a0b1')

package() {
  cd "$srcdir"
  # install is like this because of dist-info related bug report
  # pip actually doesn't do much and is remarkably slow
  #pip install --compile --no-deps --ignore-installed --root="$pkgdir" *.whl
  install -Dm644 entrypoints.py "$pkgdir/usr/lib/python3.9/site-packages/entrypoints.py"
  _dist="entrypoints-$pkgver.dist-info"
  cd $_dist
  for _i in ./*; do
    install -Dm644 $_i "$pkgdir/usr/lib/python3.9/site-packages/$_dist/$_i"
  done

  cd "$srcdir/entrypoints-$pkgver"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
