# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cmarkgfm
pkgver=0.5.2
pkgrel=1
pkgdesc="Minimal bindings to GitHub's fork of cmark"
url="https://github.com/theacodes/cmarkgfm"
license=('MIT')
arch=('x86_64')
depends=('cmark-gfm' 'python-cffi')
makedepends=('cmark-gfm' 'python-setuptools' 'python-cffi')
checkdepends=('python-pytest-runner')
source=("https://github.com/theacodes/cmarkgfm/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e9faf48158d627139ef1aede19b0aad9f7ce9822795fc98d96f9676fe14a4efcaa687b867a3732acd25763955159d176cb9160ceda0f0985e23c8ee77f64f9c9')

prepare() {
  cd cmarkgfm-$pkgver
  sed -e '/include_dirs=/a libraries=["cmark-gfm", "cmark-gfm-extensions"],' \
      -i src/cmarkgfm/build_cmark.py
}

build() {
  cd cmarkgfm-$pkgver
  python setup.py build
}

check() {
  cd cmarkgfm-$pkgver
  python setup.py pytest
}

package() {
  cd cmarkgfm-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
