# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-apipkg
pkgname=('python-apipkg' 'python2-apipkg')
pkgver=1.5
pkgrel=5
pkgdesc="Namespace control and lazy-import mechanism"
arch=('any')
license=('MIT')
url="https://github.com/pytest-dev/apipkg"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://files.pythonhosted.org/packages/source/a/apipkg/apipkg-$pkgver.tar.gz"
         apipkg-pytest5.patch)
sha512sums=('828937ca5e203915248fac54db8e7c13f941e006403f2a415c27fa4d1aa114790be3d7b5dd892f528611e5e6dfe75114ee80f4f4589a03c3f789ae6ddfcae0bf'
            '1228d7c8c79ea1a0d136585bfe236bc8c22a4053c044602082d9f88006ba2893dd615399e3bae0bbf7f94c719cc9d61d578bc8c8c956cb6f3ff3de3e0c00f7d6')

prepare() {
  cd apipkg-$pkgver
  patch -p1 -i ../apipkg-pytest5.patch # port away from removed pytest api
}

build() {
  cd apipkg-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd apipkg-$pkgver
  python setup.py pytest
  python2 setup.py pytest
}

package_python-apipkg() {
  depends=('python')

  cd apipkg-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-apipkg() {
  depends=('python2')

  cd apipkg-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
