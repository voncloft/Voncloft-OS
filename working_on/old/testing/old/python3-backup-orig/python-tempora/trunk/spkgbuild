# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Laurent Soest <laurent.soest(at)gmail.com>

pkgname='python-tempora'
_pkgbase="${pkgname//python-/}"
pkgver=4.0.1
pkgrel=3
pkgdesc="Objects and routines pertaining to date and time (tempora)."
arch=('any')
url="https://github.com/jaraco/tempora"
license=('MIT')
depends=('python' 'python-six' 'python-pytz' 'python-jaraco')
makedepends=('python-setuptools' 'python-setuptools-scm' 'python-six'
             'python-pytz' 'python-jaraco')
checkdepends=('python-pytest' 'python-pytest-flake8' 'python-pytest-cov'
              'python-pytest-black' 'python-freezegun' 'python-pytest-freezegun'
              'python-pytest-mypy')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        'LICENSE')
sha512sums=('6d5411c20f7955633aae67ba3f2e71134b030f8e1b7f1d83086f9cf93dc25d3ec0dcda2e397859d88469caa723f0b2b0666266a16484d9b59355afa45d6471e6'
            '1106afed483b7258e4ae89c5d9459c3834412b31aac90169725ed62d2ab44f61f6f79e894d4c9e4d8bd99e14530ab778df2187784f0b25eaab86d312fad68944')

# setuptools won't find version from git tag
export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    python ./setup.py build
}

check() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    pytest
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    python ./setup.py install --root="${pkgdir}" --optimize=1

    # the author has promised to include a LICENSE file in future releases:
    # https://github.com/jaraco/skeleton/issues/1
    # for now the LICENSE file has been taken from:
    # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
    install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
