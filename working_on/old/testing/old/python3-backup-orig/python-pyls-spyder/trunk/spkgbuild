# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pyls-spyder
pkgname=python-${_pkg}
pkgver=0.3.0
pkgrel=1
pkgdesc="Spyder extensions for the Python Language Server"
arch=(any)
url="https://github.com/spyder-ide/pyls-spyder"
license=(MIT)
depends=(python-language-server)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
# PyPi tarballs do not have the tests
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/v${pkgver}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('fe64f0f601c6a2fca1add29fc27405547ebc9b47e8dadaf8a40bcdf56e993d52')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  pytest -v
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  rm -r "${pkgdir}"$(python -c "import site; print(site.getsitepackages()[0])")/${_pkg//-/_}/tests
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
