# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jsonpickle
pkgver=1.5.1
pkgrel=1
arch=('any')
pkgdesc="Python library for serializing any arbitrary object graph into JSON"
url="https://jsonpickle.github.io/"
license=('BSD')
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-numpy' 'python-feedparser' 'python-simplejson'
              'python-pymongo' 'python-ujson' 'python-pandas' 'python-pytest')
source=("https://github.com/jsonpickle/jsonpickle/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('aa51b0f34f0c07c957783d59597d4bffa0138867b628d2b4cdd4a629269fc288ece0039314a2192e7fc90652b4c1127c428a303e6c43c26407109be115209dd6')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd jsonpickle-$pkgver
  # do not run coverage and other non-integration tests in an integration test
  sed -i 's/--flake8 --black --cov --cov-append//' pytest.ini
}

build() {
  cd jsonpickle-$pkgver
  python setup.py build
}

check() {
  cd jsonpickle-$pkgver
  # https://github.com/jsonpickle/jsonpickle/issues/336
  pytest --deselect tests/numpy_test.py::test_dtype_roundtrip
}

package() {
  cd jsonpickle-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
