# Maintainer: Otreblan <otreblain@gmail.com>

_name=pygls
pkgname=python-pygls
pkgver=0.9.1
pkgrel=4
pkgdesc='Pythonic generic implementation of the Language Server Protocol'
arch=('any')
url=https://github.com/openlawlibrary/pygls
license=('APACHE')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-asyncio')
source=("$url/archive/v$pkgver/$_name-v$pkgver.tar.gz")
b2sums=('f723ce70d1d080c52266b9449c589b9aabf04c748793a67d775436fa326fbdd6fd63876061fb5bdad95434c832e4ee351e36483b2a8406ad7b4dd9bdc9a1c2d2')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  pytest
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
