# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Pyphen
pkgname=python-pyphen
pkgver=0.10.0
pkgrel=1
pkgdesc="Pure Python module to hyphenate text"
arch=('any')
url="https://pyphen.org/"
license=('GPL2' 'LGPL2.1' 'MPL')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('e7d5676fbf10282fa84a53a3a28bf9fb4a4e45bb16ef4fca9daabbd3fa24d18ef6d172f1555256ebff10ecbcc1a759cfd66f0c34b6f81d9ed1926c7ec655b8d4')
b2sums=('6f33ecbc5567dc24efdc00a1105768e5135013e54af687194b04de7f23d601b28d3e85602bccb282c5b7e677fe63e7f6f3726d066e5f6393b540ec88b2e5a068')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  nosetests -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
