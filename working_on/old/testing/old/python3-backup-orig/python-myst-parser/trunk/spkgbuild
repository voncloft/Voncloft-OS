# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=MyST-Parser
pkgname=python-myst-parser
pkgver=0.13.3
pkgrel=1
pkgdesc='An extended commonmark compliant parser, with bridges to docutils/sphinx'
arch=('any')
url='https://github.com/executablebooks/MyST-Parser'
license=('MIT')
depends=('python-markdown-it-py' 'python-mdit_py_plugins' 'python-yaml' 'python-docutils' 'python-sphinx' 'python-jinja')
optdepends=('python-linkify-it-py: linkify support')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-regressions' 'python-beautifulsoup4' 'python-linkify-it-py')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('6095cb8adfa0224479b5e39d0205ce4562afa315a5f400ff9c11727f73516ee7caac7aad45f021d310fb8fefe9786d84f515bf6eeb9aca1b249e16b5f8131902')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
