# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-haystack
pkgname=python-django-haystack
pkgver=3.0
pkgrel=3
pkgdesc="Modular search for Django"
arch=('any')
url="https://github.com/django-haystack/django-haystack"
license=('BSD')
depends=('python-django' 'python-six')
makedepends=('git' 'python-setuptools_scm')
checkdepends=('gdal' 'python-coverage' 'python-dateutil' 'python-elasticsearch'
'python-geopy' 'python-mock' 'python-nose' 'python-pysolr' 'python-requests'
'python-pytest' 'python-whoosh')
optdepends=('python-elasticsearch: interface with an elasticsearch instance as search backend'
            'python-pysolr: interface with a solr instance as search backend'
            'python-whoosh: use whoosh as search backend'
            'python-xapian-haystack: use python-xapian as search backend')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('466ac18bbb6d6add1482d9debdffd195a2049c6c287cc4620e4db2ce4a702c97ca8ffb3ce048dc6c278dc50be701daab4f32b05a71c94ac67645890afb9f49bf')
b2sums=('0766711b127dfa1515583998058f588bea1619a792d6eef353da569a212b94c36683c0a743bdabee563c473f19f3197800bd585faf0b4eb39663b5ffbac6d6b8')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # remove useless version pinning
  sed -e 's/==/>=/g' -i setup.py
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 {AUTHORS,CONTRIBUTING.md,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
