# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-fixtures
pkgver=3.0.0
pkgrel=10
pkgdesc="Fixtures, reusable state for writing clean tests and more"
arch=('any')
license=('Apache')
url="https://launchpad.net/python-fixtures"
depends=('python-pbr' 'python-six' 'python-testtools')
checkdepends=('python-subunit' 'python-mock')
source=("https://pypi.io/packages/source/f/fixtures/fixtures-$pkgver.tar.gz"
        skip-tests-failing-in-Python-3.9.patch)
sha512sums=('e52ba864c42d15a8baa60583761006b2dd78e3cd5f3f043f5702346aef428b933d62e8fa5889b7cb4196e4072acc5df3b7ffa39702a1b782887bdc109299b695'
            'c75dcab7db5db8a441ee9ff0630796d3a7cad36631d6d3f20eb6349e7eb4aa191a897a9948cedcabc5585e181def95d2adb2edb9714599c2e0566e81bdcea3b5')

prepare() {
  cd fixtures-$pkgver
  patch -Np1 -i ../skip-tests-failing-in-Python-3.9.patch
}

build() {
  cd fixtures-$pkgver
  python setup.py build
}

check() {
  cd fixtures-$pkgver
  python -m testtools.run fixtures.test_suite
}

package() {
  cd fixtures-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
