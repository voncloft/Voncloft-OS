# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-oauthlib
pkgver=3.1.0
pkgrel=6
_libname=oauthlib
pkgdesc="A generic, spec-compliant, thorough implementation of the OAuth request-signing logic"
arch=(any)
url="https://github.com/idan/oauthlib"
license=('BSD')
source=(https://files.pythonhosted.org/packages/source/${_libname:0:1}/$_libname/$_libname-$pkgver.tar.gz)
depends=(python-cryptography)
makedepends=(python-setuptools)
sha256sums=('bee41cc35fcca6e988463cacc3bcb8a96224f470ca547e697b604cc697b2f889')

build() {
	cd "$srcdir/$_libname-$pkgver"
	python setup.py build
}

package_python-oauthlib() {
	cd "$srcdir/$_libname-$pkgver"
	python setup.py install --skip-build -O1 --root="$pkgdir"
	install -m0644 -D "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	chmod -R a+r "$pkgdir/usr/lib"
}
