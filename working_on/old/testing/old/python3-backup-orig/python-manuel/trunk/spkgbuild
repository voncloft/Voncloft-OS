# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-manuel
pkgver=1.10.1
pkgrel=5
pkgdesc="Manuel lets you build tested documentation"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/manuel"
depends=('python-setuptools')
checkdepends=('python-zope-testing')
source=("$pkgname-$pkgver.tar.gz::https://github.com/benji-york/manuel/archive/$pkgver.tar.gz")
sha512sums=('7109852a80097a12237a363c91ac433dd2662e238ab4f20757b4d4b93019d0ada19069fd45a13a66128adf4dbc32f162d12a90879db2f2eb4748635118d75072')

build() {
  cd manuel-$pkgver
  python setup.py build
}

check() {
  cd manuel-$pkgver
  python setup.py test
}

package() {
  cd manuel-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
