# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=html5-parser
pkgname=python-html5-parser
pkgver=0.4.9
pkgrel=5
pkgdesc="Fast C based HTML 5 parsing for python"
arch=('x86_64')
url="https://github.com/kovidgoyal/${_pkgname}"
license=('Apache')
depends=('python-chardet' 'python-lxml')
makedepends=('python-setuptools')
checkdepends=('python-beautifulsoup4')
optdepends=('python-beautifulsoup4: to use the soup treebuilder')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('25fe8f6848cbc15187f6748c0695df32bcf1b37df6420b6a01b4ebe1ec1ed48f'
            'SKIP')
b2sums=('7744798d74db1d591490c75eabae5333e90f053a8ba1c5517f08c1b9c253042796e42618ae0a674e1b1cfa97390448ffc595e861234f5b980a1f7c0b9056cc88'
        'SKIP')
validpgpkeys=('3CE1780F78DD88DF45194FD706BC317B515ACE7C') # Kovid Goyal (New longer key) <kovid@kovidgoyal.net>

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py test
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
