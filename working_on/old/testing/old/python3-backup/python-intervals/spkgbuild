# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=intervals
pkgname="python-${_name}"
pkgver=0.8.1
pkgrel=5
pkgdesc='Python tools for handling intervals (ranges of comparable objects).'
url='https://github.com/kvesteri/intervals'
depends=('python' 'python-infinity')
checkdepends=('python-pytest')
makedepends=('python' 'python-setuptools')
license=('BSD')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"
        'pytest5.patch')
sha512sums=('f97142d5f1c8d4da21692b856803481878ac2b2afc3b81937de456bacbf66ebb129b1fe9a070ac442cbc6bbe26fdaf2ed839f8fc8b26392b4bd1ce032d054e65'
            'af545bec7d8157259197e045396d69ac1791b5e0420056fca69bea444cceacaa208ae568fd80792f614e1c775611ea22c48521bdd47dbdd976a699c150835f96')

prepare() {
  cd "${srcdir}/${_name}-${pkgver}"
  patch -Np1 -i ../pytest5.patch
}
build() {
  cd "${srcdir}/${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${srcdir}/${_name}-${pkgver}"
  py.test
}

package() {
  cd "${srcdir}/${_name}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
