# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: cclin <cclinet@outlook.com>

pkgname=python-aiofiles
pkgver=0.6.0
pkgrel=3
pkgdesc='File support for asyncio'
arch=(any)
license=(APACHE)
url=https://github.com/Tinche/aiofiles
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-coverage
  python-pytest-asyncio
  python-pytest
  python-pytest-cov
  python-tox
)
_tag=b4482aeef5312d4bf0c1abda5892df80957fa01f
source=(git+https://github.com/Tinche/aiofiles.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd aiofiles

  git describe --tags | sed 's/^v//'
}

build() {
  cd aiofiles

  python setup.py build
}

check() {
  cd aiofiles

  tox -e py39
}

package() {
  cd aiofiles

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE "${pkgdir}"/usr/share/licenses/python-aiofiles
}

# vim: ts=2 sw=2 et:
