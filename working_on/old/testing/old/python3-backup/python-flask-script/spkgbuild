# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Benjamin A. Shelton <zancarius@gmail.com>
# Contributor: mitsuse <mitsuset - gmail>

_pkgname=flask-script
pkgname=python-flask-script
pkgver=2.0.6
pkgrel=7
pkgdesc='Flask extension to help writing external scripts for Flask applications'
url='https://github.com/smurfix/flask-script'
arch=('any')
license=('BSD')
depends=('python-flask')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/smurfix/flask-script/archive/v${pkgver}.tar.gz
        flask.ext.script_import.patch::https://github.com/smurfix/flask-script/commit/fcf894b6e4d0ad17489480b722c870aaea600db1.patch)
sha256sums=('1c18e25966e966e6ffa9e8798840b1e663f471cba09be8e463e1b0addf4e76ef'
            'cb1feb6965baf7c7e9250e8eecfd821bd57d0432d8c15ac83ff66e7f0a87972a')
sha512sums=('0b1237436e6c1c1f01c63596e99ecf3a25c48b38b6f26b9a35dd4b3637d8133ee06126497a9b08100eab7351e4a7992ab926e89efc9f3a89b5ef6463251cf48f'
            '7019325c5d04c3cad3fc220aa0cece0e76207fc47f69035ddab9bf59fed9a7d8cdf502e895d7de2bbd1d65b834666b26ca0f50003bc4ad6999453b0506a036d2')

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -Np1 -i "${srcdir}"/flask.ext.script_import.patch
}

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  py.test tests.py
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 664 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
