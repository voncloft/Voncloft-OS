# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-swiftclient
pkgver=3.11.0
pkgrel=1
pkgdesc="An SDK for building applications to work with OpenStack"
arch=('any')
url="https://docs.openstack.org/python-swiftclient"
license=('Apache')
depends=('python-six' 'python-requests')
optdepends=('python-keystoneclient: for Auth versions 2.0 and 3 support')
makedepends=('python-setuptools' 'python-pbr')
checkdepends=('python-stestr' 'python-mock' 'python-keystoneauth1' 'python-keystoneclient')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-swiftclient/archive/$pkgver.tar.gz")
sha512sums=('82955176e1314c027d106881343640268affe9994cdf7544334320f0c57f7cabdc9288e0e54460dfa63c624361dd770e1f029eb5a92cec0afd66133c58782861')

prepare() {
  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/python-swiftclient-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/python-swiftclient-$pkgver
  stestr run
}

package() {
  cd "$srcdir"/python-swiftclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
