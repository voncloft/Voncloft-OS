# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Steven Allen <steven@stebalien.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Wieland Hoffmann <the_mineo@web.de>
# Contributor: Amr Hassan <amr.hassan@gmail.com>

pkgname=python-pylast
pkgver=4.1.0
pkgrel=1
pkgdesc='A Python interface to Last.fm and Libre.fm'
arch=(any)
url='https://github.com/pylast/pylast'
license=(Apache)
depends=(python-six)
makedepends=(
  git
  python-setuptools
)
_tag=26ffcf5ad61765f9940e463717d0f5f3e56ecbf1
source=(git+https://github.com/pylast/pylast.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pylast

  git describe --tags
}

build() {
  cd pylast

  python setup.py build
}

package() {
  cd pylast

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
