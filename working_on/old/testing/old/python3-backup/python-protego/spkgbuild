# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-protego
pkgver=0.1.16
pkgrel=3
pkgdesc="A pure-Python robots.txt parser with support for modern conventions"
url="https://github.com/scrapy/protego"
license=('BSD')
arch=('any')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/protego/archive/$pkgver.tar.gz")
sha512sums=('3f029f34c4da6dc612dcdd747f56d421dca60f163315e0f72497435dbd74da77a5a02ee35f15774aad431e2b6e69a582c038d435ba0c07a53b3f0c83cb1c958a')

build() {
  cd protego-$pkgver
  python setup.py build
}

check() {
  cd protego-$pkgver
  python setup.py pytest
}

package() {
  cd protego-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
