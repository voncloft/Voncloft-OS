# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol "Kenji Takahashi" Wo≈∫niak <kenji.sx>

pkgname=python-mccabe
pkgver=0.6.1
pkgrel=8
pkgdesc="McCabe complexity checker for Python"
arch=('any')
url="https://github.com/flintwork/mccabe"
license=('custom:Expat')
depends=('python')
makedepends=('python-setuptools' 'python-pytest-runner')
source=("https://github.com/flintwork/mccabe/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1da239933f36c8b7c0f9f026822d673d0b207ac49e45027d77500001327964dd66cc7074be22311927176fc313174f4eb7908d0fc45da9fc4623bf1e237293b0')

build() {
  cd mccabe-$pkgver
  python setup.py build
}

check() {
  cd mccabe-$pkgver
  python setup.py test
}

package() {
  cd mccabe-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --skip-build --optimize=1

  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
