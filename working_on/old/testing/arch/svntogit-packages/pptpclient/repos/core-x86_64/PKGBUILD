# Maintainer:
# Contributor: Daniel Isenmann <daniel @ archlinux.org>
# Contributor: Mark Nikkel <mnikkel@gmail.com>

pkgname=pptpclient
pkgver=1.10.0
pkgrel=2
pkgdesc="Client for the proprietary Microsoft Point-to-Point Tunneling Protocol, PPTP."
arch=('x86_64')
url="http://pptpclient.sourceforge.net/"
license=('GPL')
depends=('ppp' 'iproute2' 'perl')
backup=('etc/ppp/options.pptp')
source=("https://downloads.sourceforge.net/sourceforge/pptpclient/pptp-$pkgver.tar.gz")
md5sums=('8d25341352fdae5ad5b36b9f18254908')

prepare() {
  cd pptp-$pkgver

  # use our CFLAGS and LDFLAGS
  sed -i -e "/CFLAGS  =/ c\CFLAGS = ${CFLAGS}" Makefile
  sed -i -e "/LDFLAGS =/ c\LDFLAGS = ${LDFLAGS}" Makefile

  # adjust ip path
  sed -i 's#/bin/ip#/usr/bin/ip#' routing.c

  # put binaries in /usr/bin
  sed -i "/BINDIR/s#/sbin#/bin#" Makefile
}

build() {
  cd pptp-$pkgver
  make 
}

package() {
  cd pptp-$pkgver
  make DESTDIR="$pkgdir" install
}
