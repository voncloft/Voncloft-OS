# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgbase=bullet
pkgname=('bullet' 'bullet-docs' 'python-pybullet')
pkgver=3.08
pkgrel=1
pkgdesc="A 3D Collision Detection and Rigid Body Dynamics Library for games and animation"
arch=('x86_64')
url="http://www.bulletphysics.com/Bullet/"
license=('custom:zlib')
makedepends=('cmake' 'doxygen' 'graphviz' 'ttf-dejavu' 'mesa' 'glu' 'python' 'python-numpy' 'python-setuptools' 'ninja')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bulletphysics/bullet3/archive/${pkgver}.tar.gz"
        bullet3_examplebrowser.sh)
sha512sums=('4177c2b2f3b007691102973e7a3d4337b8de559d20058db75a2d25eab35b9be62fa14e3362435ef6f7ea2f6b8cf02a78770cc49c17e979adf3a2b41f19ee702a'
            '8741ad94b6c46c226d89aebc8ab06d8a11bac3c04d3f0a2bf7a7524792a3375aa7bf7d295410b16fbeb4c348a31057b4570acdebe9bbaea251f44daca8d9fe81')

build() {
  cd bullet3-${pkgver}

  cmake . \
      -Bbuild \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_SHARED_LIBS=1 \
      -DINSTALL_LIBS=1 \
      -DINSTALL_EXTRA_LIBS=1 \
      -DBUILD_PYBULLET=ON \
      -DBUILD_PYBULLET_NUMPY=ON \
      -DBUILD_OPENGL3_DEMOS=ON \
      -DCMAKE_BUILD_TYPE=Release

  ninja -C build

  python setup.py build
  doxygen
}

package_bullet() {
  optdepends=('glu: for the example browser'
              'python: python bindings'
              'python-numpy: python bindings'
              'bullet-docs: documentation')

  cd bullet3-${pkgver}

  DESTDIR="${pkgdir}" ninja -C build install

  cd build

  install -Dm755 examples/pybullet/pybullet.so.${pkgver} "${pkgdir}"/usr/lib/libpybullet.so.${pkgver}
  install -Dm755 examples/ExampleBrowser/libBulletExampleBrowserLib.so.${pkgver} "${pkgdir}"/usr/lib/libBulletExampleBrowserLib.so.${pkgver}
  install -Dm755 examples/OpenGLWindow/libOpenGLWindow.so "${pkgdir}"/usr/lib/libOpenGLWindow.so
  install -Dm755 examples/ThirdPartyLibs/Gwen/libgwen.so "${pkgdir}"/usr/lib/libgwen.so
  install -Dm755 examples/ThirdPartyLibs/BussIK/libBussIK.so "${pkgdir}"/usr/lib/libBussIK.so
  install -Dm755 "${srcdir}"/bullet3_examplebrowser.sh "${pkgdir}"/usr/bin/bullet3_examplebrowser
  install -Dm755 examples/ExampleBrowser/App_ExampleBrowser "${pkgdir}"/opt/bullet/App_ExampleBrowser
  cp -r data "${pkgdir}"/opt/bullet/

  install -Dm644 ../LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgbase}/LICENSE
}

package_python-pybullet() {
  pkgdesc="Bullet Python bindings"
  depends+=('bullet' 'gcc-libs')

  cd bullet3-${pkgver}

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_bullet-docs() {
  pkgdesc="Documentation for bullet"

  cd bullet3-${pkgver}

  # install docs
  install -Dm644 docs/GPU_rigidbody_using_OpenCL.pdf "${pkgdir}"/usr/share/doc/bullet/GPU_rigidbody_using_OpenCL.pdf
  install -Dm644 docs/Bullet_User_Manual.pdf "${pkgdir}"/usr/share/doc/bullet/Bullet_User_Manual.pdf
  install -Dm644 docs/BulletQuickstart.pdf "${pkgdir}"/usr/share/doc/bullet/BulletQuickstart.pdf
  cp -r html "${pkgdir}"/usr/share/doc/bullet/html
}
# vim: sw=2 ts=2 et:
