# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=libwmf
pkgver=0.2.12
pkgrel=2
pkgdesc="A library for reading vector images in Microsoft's native Windows Metafile Format (WMF)"
arch=('x86_64')
url="http://wvware.sourceforge.net/libwmf.html"
license=('LGPL')
depends=('libx11' 'libjpeg' 'gsfonts' 'freetype2' 'expat')
makedepends=('gtk2' 'libxt')
optdepends=('gdk-pixbuf2: for pixbuf loader')
options=('!docs' '!emptydirs')
source=($pkgname-$pkgver.tar.gz::https://github.com/caolanm/libwmf/archive/v$pkgver.tar.gz libwmf-freetype.patch)
sha1sums=('490a26175ad667fbc0c009d390bf56644919bed6'
          'ef4d452cd5e7fcb36751771c6f44b4b7a3f8693a')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../libwmf-freetype.patch # Port away from freetype-config, patch from openembedded.org
  autoreconf -vif -Ipatches
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
              --with-gsfontdir=/usr/share/fonts/Type1 \
	      --with-fontdir=/usr/share/fonts/Type1 \
	      --with-gsfontmap=/usr/share/ghostscript/9.10/Resource/Init/Fontmap.GS
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install -j1
  #Remove fonts, these are in gsfonts
  rm -rf "${pkgdir}/usr/share/fonts"
}
