# Maintainer:
# Contributor: nut543 <kfs1@online.no>
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=re2c
pkgver=2.0.3
pkgrel=1
arch=(x86_64)
depends=(gcc-libs)
pkgdesc='A tool for generating C-based recognizers from regular expressions'
url='https://re2c.org'
license=('public domain')
source=(re2c-$pkgver.tar.gz::https://github.com/skvadrik/re2c/archive/$pkgver.tar.gz)
sha1sums=('52d16354fc70f2e20c8584d980fd83bb8c9e2382')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --enable-golang
  make
}

check() {
  cd $pkgname-$pkgver
  make tests
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
