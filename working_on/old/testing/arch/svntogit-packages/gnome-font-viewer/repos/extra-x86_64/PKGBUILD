# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-font-viewer
pkgver=3.34.0+27+g5ffd1ba
pkgrel=1
pkgdesc="A font viewer utility for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-font-viewer"
arch=(x86_64)
license=(GPL)
depends=(gtk3 gnome-desktop)
makedepends=(git meson)
groups=(gnome)
_commit=5ffd1bae5061587c500dbc7569ff83820e5142be  # master
source=("git+https://gitlab.gnome.org/GNOME/gnome-font-viewer.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
