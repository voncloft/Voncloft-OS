# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=libsamplerate
pkgver=0.1.9
pkgrel=3
pkgdesc="Secret Rabbit Code - aka Sample Rate Converter for audio"
arch=('x86_64')
url="http://www.mega-nerd.com/SRC/index.html"
license=('BSD')
depends=('glibc')
makedepends=('libsndfile.so')
checkdepends=('fftw')
optdepends=('libsndfile.so: for sndfile-resample')
provides=('libsamplerate.so')
# upstream might soonish become github (requested detached signatures):
# https://github.com/erikd/libsamplerate/issues/39
source=("http://www.mega-nerd.com/SRC/${pkgname}-${pkgver}.tar.gz")
sha512sums=('78596657963cbf06785e3e6e1190b093df71da52ca340e75bd8246a962cd79dd1c90fa5527c607cebcb296e2c1ee605015278b274e3b768f2f3fbeb0eadfb728')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make -k check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 {AUTHORS,NEWS,README,ChangeLog} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
