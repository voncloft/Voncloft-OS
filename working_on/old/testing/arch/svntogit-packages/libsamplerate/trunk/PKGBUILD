# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=libsamplerate
pkgver=0.2.1
pkgrel=1
pkgdesc="An audio sample rate conversion library"
arch=('x86_64')
url="https://libsndfile.github.io/libsamplerate/"
license=('BSD')
depends=('glibc')
makedepends=('alsa-lib' 'cmake' 'libsndfile' 'opus')
checkdepends=('fftw')
provides=('libsamplerate.so')
source=("https://github.com/libsndfile/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('f54f7f12c9536868d7a11fc9cbb86857505e7b75fe34cedaf0b9bfc864da6037296b3eae303a33d4c87b7fd20d96933b91ef59c8cc3d1313b9fc21654e5daa2d'
            'SKIP')
b2sums=('83540f3e75cfa79cbd166f075d22cab6a63e0e057b90ac6a3760c07196cac962df7d1ca26620a9033de046e0528bee3ded2b482e8629b1ae316844b5b31f3074'
        'SKIP')
validpgpkeys=('31D95CAB6D80D262244A1750A47620E801E47E95')  # David Seifert soap@gentoo.org

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DBUILD_SHARED_LIBS=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  make test -C build
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 {AUTHORS,NEWS,README.md,ChangeLog} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
