# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ryotaro Ko <aur@p1kachu.net>

pkgname=aribb24
pkgver=1.0.3
pkgrel=3
pkgdesc='Library for ARIB STD-B24, decoding JIS 8 bit characters and parsing MPEG-TS stream'
url='https://github.com/nkoriyama/aribb24'
arch=('x86_64')
license=('LGPL3')
depends=("libpng")
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/nkoriyama/aribb24/archive/v${pkgver}.tar.gz
        ${pkgname}-1.0.3-add-missing-libm.patch::https://github.com/nkoriyama/aribb24/commit/b08931895599e42ce6930a720a3fca74f6f6d9cb.patch)
sha512sums=('622cc0c3928fd6db0b5ab3921f27348c956af20f8c0133ad5d9bf4de3d199077d9f23cc86ae149a9f0d13c7ee5906ec95de3fb8388207160cebd1f0c59078c8f'
            'b7f8f737a96931fec4c983b2acf0e62ed6638d4ef6af8bf9491931235f6aca7589d81c444104fc5a7a5ce363c4aa0f9376b4990d3eae745315d197055f21e310')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 < ../${pkgname}-1.0.3-add-missing-libm.patch
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
