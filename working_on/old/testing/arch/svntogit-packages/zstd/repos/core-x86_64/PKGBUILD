# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Johan Förberg <johan@forberg.se>

pkgname=zstd
pkgver=1.4.8
pkgrel=1
pkgdesc='Zstandard - Fast real-time compression algorithm'
arch=(x86_64)
url='https://facebook.github.io/zstd/'
license=(BSD GPL2)
depends=(zlib xz lz4)
makedepends=(gtest)
source=(https://github.com/facebook/zstd/releases/download/v${pkgver}/zstd-${pkgver}.tar.zst)
sha256sums=('c7ea10e20dd61b457220455e3cf553069987b968b7c63d1b9d46acbdb45623eb')

build() {
  cd $pkgname-$pkgver
  make
  make -C contrib/pzstd
}

check() {
  cd $pkgname-$pkgver
  make check    
  make -C contrib/pzstd test
}

package() {
  cd $pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  ln -sf /usr/bin/zstd "$pkgdir/usr/bin/zstdmt"
  install -Dm755 contrib/pzstd/pzstd "$pkgdir/usr/bin/pzstd"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
