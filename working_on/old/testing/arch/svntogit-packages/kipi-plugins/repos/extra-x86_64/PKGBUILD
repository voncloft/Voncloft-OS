# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kipi-plugins
pkgver=20.12.2
pkgrel=1
pkgdesc="A collection of plugins extending the KDE graphics and image applications"
arch=(x86_64)
license=(GPL)
url="https://userbase.kde.org/KIPI"
depends=(kio libkipi)
optdepends=('libmediawiki: MediaWiki Export plugin'
            'libkvkontakte: VKontakte.ru Exporter plugin'
            'qt5-xmlpatterns: rajce.net plugin')
makedepends=(extra-cmake-modules qt5-xmlpatterns libmediawiki libkvkontakte)
groups=(kde-applications kde-graphics)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('880ad1541cbe786da960510e16b618505c779e7b44a3c39019e46cece6891e88'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {

  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
