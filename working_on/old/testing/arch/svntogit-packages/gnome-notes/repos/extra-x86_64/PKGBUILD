# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-notes
pkgver=3.38.0
pkgrel=1
pkgdesc="Write out notes, every detail matters"
url="https://wiki.gnome.org/Apps/Notes"
arch=(x86_64)
license=(GPL)
depends=(tracker3 webkit2gtk gnome-online-accounts evolution-data-server
         libhandy0)
makedepends=(evolution python appstream-glib yelp-tools git meson)
provides=("bijiben=$pkgver")
conflicts=(bijiben)
replaces=(bijiben)
_commit=c95db2b0efc327685b87396c1175872062e37b3e  # tags/BIJIBEN_3_38_0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-notes.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^BIJIBEN_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D update_mimedb=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
