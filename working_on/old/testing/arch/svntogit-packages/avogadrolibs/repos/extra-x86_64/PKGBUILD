# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=avogadrolibs
pkgver=1.93.0
pkgrel=5
pkgdesc="Libraries that provide 3D rendering, visualization, analysis and data processing useful in computational chemistry, molecular modeling, bioinformatics, materials science, and related areas"
arch=(x86_64)
url="https://www.openchemistry.org/"
license=(custom)
depends=(molequeue glew spglib libmsym libarchive hdf5)
makedepends=(cmake eigen boost python mmtf-cpp pybind11 vtk qt5-tools genxrdpattern)
optdepends=('vtk: For the Qt plugins')
source=($pkgname-$pkgver.tar.gz::"https://github.com/OpenChemistry/avogadrolibs/archive/$pkgver.tar.gz")
sha256sums=('01fea1f3b21c69091c60739616f86123db92f445e4c9ca53f1dec09624aceab4')

prepare() {
  mkdir -p build
}

build() {
  cd build

  # Workaround for building with hdf5 1.12
  CXXFLAGS+=' -DH5_USE_110_API'

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_TRANSLATIONS=ON \
    -DUSE_HDF5=ON \
    -DUSE_VTK=ON \
    -DUSE_PYTHON=ON \
    -DUSE_SYSTEM_GENXRDPATTERN=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname

# Move python module to the correct place
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  mkdir -p "$pkgdir${site_packages}"
  mv "$pkgdir"/usr/lib/avogadro "$pkgdir${site_packages}"
}
