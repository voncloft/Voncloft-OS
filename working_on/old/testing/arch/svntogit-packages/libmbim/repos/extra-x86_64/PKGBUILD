# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libmbim
pkgver=1.24.6
pkgrel=1
pkgdesc="MBIM modem protocol helper library"
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Software/libmbim/"
license=(GPL2)
depends=(glib2 bash systemd libgudev)
makedepends=(gtk-doc python git help2man autoconf-archive gobject-introspection)
provides=(libmbim-glib.so)
_commit=5bf12d88b5345f4b0bca7c1f55be36cd3cb2c6de  # tags/1.24.6
source=("git+https://gitlab.freedesktop.org/mobile-broadband/libmbim.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-rc/rc/;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --disable-static --enable-gtk-doc \
    --enable-compile-warnings=yes
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
