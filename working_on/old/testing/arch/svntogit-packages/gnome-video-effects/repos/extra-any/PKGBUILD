# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-video-effects
pkgver=0.5.0+4+g9554041
pkgrel=2
pkgdesc="Collection of GStreamer effects for GNOME"
arch=(any)
url="https://gitlab.gnome.org/GNOME/gnome-video-effects"
license=(GPL2)
depends=(gst-plugins-good gst-plugins-bad frei0r-plugins)
makedepends=(meson git)
_commit=955404195ada606819974dd63c48956f25611e14  # master
groups=(gnome)
source=("git+$url.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
