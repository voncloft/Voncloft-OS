# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xsetroot
pkgver=1.1.2
pkgrel=2
pkgdesc="Classic X utility to set your root window background to a given pattern or color"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxmu' 'libx11' 'libxcursor')
makedepends=('xorg-util-macros' 'xbitmaps')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xsetroot-${pkgver}.tar.bz2{,.sig})
sha512sums=('0b9df1b72305e5c24f1e1664578aa4411a5ffcdc5d318458117594e641f01c34700205b0d16c08abf4a8106d36128ccdbe1ea1f1035fce9018d6b52801a8b72b'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd xsetroot-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xsetroot-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
