# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-setxkbmap
pkgver=1.3.2
pkgrel=2
pkgdesc='Set the keyboard using the X Keyboard Extension'
url='https://xorg.freedesktop.org/'
license=('custom')
arch=('x86_64')
depends=('libxkbfile')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/app/setxkbmap-${pkgver}.tar.bz2{,.sig})
sha512sums=('bfa8015dee0d8d3fdbbd89afbd71c8606ce168d6edca8521d5ed05a00fecbea39bc4bfcce84b71458bdeb34c60bd80a5df27e0691ccee3966443ecdc937faf38'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>
groups=('xorg-apps' 'xorg')

build() {
	cd setxkbmap-${pkgver}
	./configure --prefix=/usr
	make
}

package() {
	cd setxkbmap-${pkgver}
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
