# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libmp4v2
pkgver=4.1.3
pkgrel=1
pkgdesc="An API to create and modify mp4 files as defined by ISO-IEC:14496-1:2001 MPEG-4 Systems"
arch=('x86_64')
license=('MPL')
url="https://github.com/TechSmith/mp4v2"
depends=('gcc-libs')
source=("https://github.com/TechSmith/mp4v2/archive/Release-ThirdParty-MP4v2-$pkgver.tar.gz"
        libmp4v2-c++11.patch)
sha256sums=('e3ad6c2dc451b0875dbe34bfe7f51f4fe278b391434c886083e6d3ecd5fa08c2'
            '946a1c9c8a927d2739248aa1164591de08507152c856f88e8c5d759c213871ee')

prepare() {
  cd mp4v2-Release-ThirdParty-MP4v2-$pkgver
  patch -p0 -i ../libmp4v2-c++11.patch # Fix build with C++11
}

build() {
  cd mp4v2-Release-ThirdParty-MP4v2-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd mp4v2-Release-ThirdParty-MP4v2-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install-man
}
