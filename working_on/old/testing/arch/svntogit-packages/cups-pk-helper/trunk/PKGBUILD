# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=cups-pk-helper
pkgver=0.2.6
pkgrel=4
pkgdesc="A helper that makes system-config-printer use PolicyKit"
arch=(x86_64)
url="https://www.freedesktop.org/software/cups-pk-helper/releases/"
license=('GPL')
depends=(polkit libcups)
makedepends=(intltool)
source=(https://www.freedesktop.org/software/$pkgname/releases/$pkgname-$pkgver.tar.xz)
sha256sums=('959af8f2f5a2761e7e498b61c9caf25ae963335031eae9972d999e9a0d97a228')

build() {
  cd "$pkgname"-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/cups-pk-helper
  make
}

package() {
  cd "$pkgname"-$pkgver
  make DESTDIR="$pkgdir" install

  # fix dbus policy location - --with-dbusdir doens't work
  install -dm755 "${pkgdir}"/usr/share/dbus-1/system.d
  mv "${pkgdir}"/etc/dbus-1/system.d/* "${pkgdir}"/usr/share/dbus-1/system.d
  rm -rf "${pkgdir}"/etc/dbus-1
}
