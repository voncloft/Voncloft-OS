# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=dump_syms
pkgver=0.0.7
pkgrel=1
pkgdesc="Symbol dumper for Firefox"
url="https://github.com/mozilla/dump_syms"
arch=(x86_64)
license=(APACHE MIT)
depends=(gcc-libs openssl bzip2)
makedepends=(cargo git)
_commit=1899b09775037cd9466154dfbaf69abd4880e54c  # tags/v0.0.7
source=("git+$url#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  cargo fetch --locked --target x86_64-unknown-linux-gnu
}

build() {
  cd $pkgname
  cargo build --release --frozen --all-targets
}

check() {
  cd $pkgname
  cargo test --release --frozen
}

package() {
  cd $pkgname
  install -Dt "$pkgdir/usr/bin" target/release/dump_syms
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE*
}
