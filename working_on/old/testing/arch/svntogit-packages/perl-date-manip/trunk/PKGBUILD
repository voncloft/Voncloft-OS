# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-date-manip
pkgver=6.83
pkgrel=1
pkgdesc="Date::Manip - date manipulation routines"
arch=('any')
license=('PerlArtistic')
url="https://search.cpan.org/dist/Date-Manip/"
depends=('perl')
makedepends=('perl-file-find-rule' 'perl-test-inter' 'perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/S/SB/SBECK/Date-Manip-$pkgver.tar.gz")
sha512sums=('67983bdd0eb26ef06fa1b908f8b4f7832882442afaff94da009f149e66128df18dc36fba8ba4d72071a76eb3f080e6991e0854cb965e8f5ec4a4525f79e64d39')

build() {
  cd Date-Manip-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Date-Manip-$pkgver
  make test
}

package() {
  cd Date-Manip-$pkgver
  make DESTDIR="$pkgdir" install
}
