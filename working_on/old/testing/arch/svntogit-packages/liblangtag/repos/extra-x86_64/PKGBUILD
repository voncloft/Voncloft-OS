# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=liblangtag
pkgver=0.6.3
pkgrel=2
pkgdesc="interface library to access/deal with tags for identifying languages"
arch=('x86_64')
url="https://bitbucket.org/tagoh/liblangtag/wiki/Home"
license=('LGPL3' 'MPL') # actually MPL2.0
depends=('libxml2' 'glib2')
makedepends=('gobject-introspection')
optdepends=('gobject-introspection-runtime: use with liblangtag-gobject')
source=(https://bitbucket.org/tagoh/liblangtag/downloads/$pkgname-$pkgver.tar.bz2)
sha256sums=('1f12a20a02ec3a8d22e54dedb8b683a43c9c160bda1ba337bf1060607ae733bd')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
