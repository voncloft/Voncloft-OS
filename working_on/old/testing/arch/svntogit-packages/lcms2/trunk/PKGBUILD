# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=lcms2
pkgver=2.11
pkgrel=2
pkgdesc='Small-footprint color management engine, version 2'
arch=('x86_64')
url='http://www.littlecms.com/'
license=('MIT')
depends=('libtiff')
provides=('liblcms2.so')
source=("https://github.com/mm2/Little-CMS/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('478c9c3938d7a91b1171de4616f8b04308a8676d73eadc19505b7ace41327f28')

build() {
  cd Little-CMS-${pkgver}

  ./configure \
    --prefix=/usr
  make
}

check() {
  cd Little-CMS-${pkgver}

  make check
}

package() {
  cd Little-CMS-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
