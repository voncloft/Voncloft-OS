# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: gDD (usrgdd | gmail)
# Contributor: cantabile

pkgname=convertlit
pkgver=1.8
pkgrel=10
pkgdesc="An extractor/converter for .LIT eBooks"
arch=('x86_64')
url="http://www.convertlit.com/"
license=('GPL')
depends=(libtommath)
source=("http://www.convertlit.com/clit18src.zip")
md5sums=('d691d4729485fe5d73e3f0937d8fb42e')

prepare() {
# Link to shared libtommath and use system LDFLAGS
  sed -i 's|../libtommath-0.30/libtommath.a|/usr/lib/libtommath.so ${LDFLAGS}|' clit18/Makefile
}

build() {
  cd lib
  make 

  cd ../clit18
  make
}

package() {
  cd clit18

  install -Dm755 clit "$pkgdir/usr/bin/clit"
}
