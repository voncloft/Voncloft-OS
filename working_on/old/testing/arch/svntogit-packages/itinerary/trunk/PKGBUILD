# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=itinerary
pkgver=20.12.2
pkgrel=1
pkgdesc="Itinerary and boarding pass management application"
arch=(x86_64)
url="https://www.kde.org"
license=(LGPL BSD)
depends=(solid knotifications kdbusaddons kitinerary kholidays networkmanager-qt kosmindoormap qt5-location kirigami2 prison hicolor-icon-theme)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-pim)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('656831e45b87fa64c366adfc3943ee15360986e125d8bd3a98e627ce71d46fb6'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/LICENSES/* -t "$pkgdir"/usr/share/licenses/$pkgname
}
