# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
 
pkgname=libportal
pkgver=0.3
pkgrel=2
pkgdesc="GIO-style async APIs for most Flatpak portals"
url="https://github.com/flatpak/libportal"
arch=(x86_64)
license=(LGPL)
depends=(glib2)
makedepends=(git meson gtk-doc)
_commit=7b11b2a42fd4e7681bd38f3986b47108c0a3cc66  # tags/0.3^0
source=("git+https://github.com/flatpak/libportal#commit=$_commit"
        0001-filechooser-Don-t-try-to-free-the-method-name-it-s-s.patch)
sha256sums=('SKIP'
            '934374cb20a3bb76df2c1982a56a15e1c9808a1dfa46273db6529dd6213bde1f')

pkgver() {
  cd $pkgname
  git describe --tags | sed -r 's/[^-]+-g[^-]+$/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
