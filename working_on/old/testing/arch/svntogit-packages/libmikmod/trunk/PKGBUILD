# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=libmikmod
pkgver=3.3.11.1
pkgrel=4
pkgdesc="Module player library supporting many formats, including MOD, S3M, IT and XM"
url=http://mikmod.sourceforge.net
license=(GPL2 LGPL2.1)
arch=(x86_64)
depends=(glibc libpulse sh)
makedepends=(alsa-lib cmake)
provides=(libmikmod.so)
source=(https://downloads.sourceforge.net/mikmod/$pkgname-$pkgver.tar.gz
        cmake-man.diff)
md5sums=('f69d7dd06d307e888f466fc27f4f680b'
         'e8a338d92027072971f49b495e87c442')
sha256sums=('ad9d64dfc8f83684876419ea7cd4ff4a41d8bcd8c23ef37ecb3a200a16b46d19'
            '3331b6ff7f6f3dfd33f543d64affc10a127f61bed1160f75b07344c03f21b840')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../cmake-man.diff
}

build() {
  # cmake ignores CPPFLAGS
  CFLAGS+=" $CPPFLAGS"

  cmake -H$pkgname-$pkgver -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_DL=1
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --build build --target install

  install -Dt "$pkgdir/usr/share/aclocal" -m644 $pkgname-$pkgver/libmikmod.m4
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 \
    $pkgname-$pkgver/{NEWS,README,TODO} build/docs/mikmod.html
  install -Dt "$pkgdir/usr/share/info" build/docs/mikmod.info
  install -Dt "$pkgdir/usr/share/man/man1" build/docs/libmikmod-config.1
}

# vim: ts=2 sw=2 et:
