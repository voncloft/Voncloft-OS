# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Tai Chi Minh Ralph Eastwood <tcmreastwood@gmail.com>

pkgname=libpgf
pkgver=7.19.3
pkgrel=2
pkgdesc="An implementation of the Progressive Graphics File (PGF)"
arch=('x86_64')
url="https://www.libpgf.org"
depends=('gcc-libs')
makedepends=('dos2unix')
license=('GPL')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}-latest/libPGF-codec-and-console-src.zip")
sha1sums=('7e5ccb2fc99614eba7358404c4f1bbc1cca0b989')

build() {
  cd libPGF-codec-and-console-src/PGF/Codec
  dos2unix configure.ac
  sh ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd libPGF-codec-and-console-src/PGF/Codec
  make DESTDIR="${pkgdir}" install
}
