# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=libgudev
pkgver=234
pkgrel=1
pkgdesc="GObject bindings for libudev"
url="https://wiki.gnome.org/Projects/libgudev"
arch=(x86_64)
license=(LGPL2.1)
depends=(systemd-libs glib2)
makedepends=(systemd gobject-introspection gtk-doc git)
_commit=e9342ee019482a08fe435d6b656f8a6bdd196bce  # tags/234^0
source=("git+https://gitlab.gnome.org/GNOME/libgudev.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

check() {
  cd $pkgname
  make check
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --enable-gtk-doc \
    --disable-umockdev
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  depends+=(libg{lib,object}-2.0.so libudev.so)
  provides+=(libgudev-1.0.so)

  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
