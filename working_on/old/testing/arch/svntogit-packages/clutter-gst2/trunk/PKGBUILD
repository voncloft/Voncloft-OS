# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=clutter-gst2
pkgver=2.0.18
pkgrel=1
pkgdesc="GStreamer bindings for clutter"
arch=(x86_64)
url="http://www.clutter-project.org/"
license=(LGPL)
depends=(clutter gst-plugins-base-libs libxdamage)
makedepends=(gobject-introspection)
conflicts=('clutter-gst<=2.0.14')
source=(http://download.gnome.org/sources/clutter-gst/${pkgver:0:3}/clutter-gst-$pkgver.tar.xz)
sha256sums=('8d82bebf01b65d433bfb0f8da7bf7a8da42289f58b66bc4dfa775df0385bf8b5')

build() {
  cd clutter-gst-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd clutter-gst-$pkgver
  make check
}

package() {
  cd clutter-gst-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
