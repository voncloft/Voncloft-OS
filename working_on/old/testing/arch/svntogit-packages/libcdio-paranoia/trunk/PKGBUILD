# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=libcdio-paranoia
pkgver=10.2+2.0.1
pkgrel=2
pkgdesc="CD paranoia libraries from libcdio"
arch=('x86_64')
license=('GPL' 'LGPL')
url="https://www.gnu.org/software/libcdio/"
depends=('libcdio')
source=(https://github.com/rocky/libcdio-paranoia/archive/release-$pkgver.tar.gz)
sha256sums=('7a4e257c85f3f84129cca55cd097c397364c7a6f79b9701bbc593b13bd59eb95')
validpgpkeys=('DAA63BC2582034A02B923D521A8DE5008275EC21') # R. Bernstein <rocky@panix.com>

prepare() {
  cd ${pkgname}-release-${pkgver/+/-}
  ./autogen.sh
}

build() {
  cd ${pkgname}-release-${pkgver/+/-}
  ./configure --prefix=/usr --enable-cpp-progs --disable-static --disable-example-progs
  make
}

package() {
  cd ${pkgname}-release-${pkgver/+/-}
  make DESTDIR="${pkgdir}" install
}
