# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: George Brooke <george+arch.aur@george-brooke.co.uk>

pkgname=telepathy-logger-qt
pkgver=17.09.0
pkgrel=1
pkgdesc="Qt bindings for telepathy logger"
arch=(x86_64)
url='https://www.kde.org/'
license=(GPL)
depends=(telepathy-qt telepathy-logger)
makedepends=(extra-cmake-modules doxygen python qt5-tools)
source=("https://download.kde.org/stable/$pkgname//${pkgver%.*}/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('3190729d0df9001d683ba15605aaee65e46edcedad0e828e51312ea965ce135a'
            'SKIP')
validpgpkeys=(D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heirecka@exherbo.org>

prepare () {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
