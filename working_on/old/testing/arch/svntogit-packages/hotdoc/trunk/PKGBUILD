# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=hotdoc
pkgver=0.12.2
pkgrel=1
pkgdesc="The Tastiest Documentation Tool"
url="https://github.com/hotdoc/hotdoc"
arch=(x86_64)
license=(LGPL)
depends=(python-lxml python-pyaml clang llvm glib2 json-glib)
makedepends=(git cmake flex python-setuptools npm)
_commit=04d6d5230f30035693e5a6d42b3392ec5bf20f31  # tags/0.12.2
source=("git+https://github.com/hotdoc/hotdoc#commit=$_commit"
        "git+https://github.com/MathieuDuponchelle/cmark"
        "git+https://github.com/PrismJS/prism"
        "git+https://github.com/hotdoc/hotdoc_bootstrap_theme")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url cmark "$srcdir/cmark"
  git submodule set-url hotdoc/extensions/syntax_highlighting/prism "$srcdir/prism"
  git submodule set-url hotdoc/hotdoc_bootstrap_theme "$srcdir/hotdoc_bootstrap_theme"
  git submodule update
}

build() {
  cd $pkgname
  python setup.py build
}

package() {
  cd $pkgname
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
