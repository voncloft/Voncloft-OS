# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=librsvg
pkgver=2.50.3
pkgrel=1
epoch=2
pkgdesc="SVG rendering library"
url="https://wiki.gnome.org/Projects/LibRsvg"
arch=(x86_64)
license=(LGPL)
depends=(gdk-pixbuf2 pango)
makedepends=(gobject-introspection vala gtk-doc git rust)
provides=(librsvg-${pkgver%%.*}.so)
_commit=b52b148d796c61b2f4c3c1a97e70ff554b5fee3a  # tags/2.50.3^0
source=("git+https://gitlab.gnome.org/GNOME/librsvg.git#commit=$_commit"
        lto.diff)
sha256sums=('SKIP'
            '3cc8ad1af515b2cb4071f0647319c1cf3280dc054875634239061800af1616b6')

pkgver() {
  cd librsvg
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd librsvg

  # Turn LTO back on
  git apply -3 ../lto.diff

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd librsvg
  ./configure --prefix=/usr --disable-static --enable-vala \
    --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
  make
}

check() {
  cd librsvg
  # Test suite is very dependent on the versions of
  # Cairo, Pango, FreeType and HarfBuzz
  make check || :
}

package() {
  cd librsvg
  make DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
