# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=irssi
pkgver=1.2.2
pkgrel=4
pkgdesc='Modular text mode IRC client with Perl scripting'
url='https://irssi.org/'
arch=('x86_64')
license=('GPL')
depends=('glibc' 'glib2' 'openssl' 'libotr' 'perl' 'ncurses' 'libncursesw.so')
optdepends=('perl-libwww: for the scriptassist script')
replaces=('irssi-otr')
backup=('etc/irssi.conf')
source=(https://github.com/irssi/irssi/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz{,.asc}
        irssi-1.2.2-ctrl-space-freeze.patch::https://github.com/irssi/irssi/commit/76802985a4d99105374a64cea5f973298ed89ca5.patch)
sha512sums=('5444ac102ff9ad3a6399a47c967d138e181330dd226eac68886d35fee4ad455932b9306a367bee3478095158e41ba67fb46deb8f0a33512046b9b83bae37c610'
            'SKIP'
            'bfcdbfb1c06a13412594d5d14f4441ba06c3db3a4260aaa1c8bbfa1c68841ad1efc1c8af07f3fe4d12bb2e4c9fa7f067ace2b439745293082de6427a30242547')
validpgpkeys=('7EE65E3082A5FB06AC7C368D00CCB587DDBEF0E1') # The Irssi project <staff@irssi.org>

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 < ../irssi-1.2.2-ctrl-space-freeze.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-proxy \
    --with-perl-lib=vendor \
    --with-otr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
