# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=libdvbpsi
pkgver=1.3.3
pkgrel=2
epoch=1
pkgdesc='Library designed for decoding and generation of MPEG TS and DVB PSI tables'
url='https://www.videolan.org/developers/libdvbpsi.html'
arch=('x86_64')
license=('LGPL2.1')
depends=('glibc')
provides=('libdvbpsi.so')
source=(${pkgname}-${pkgver}.tar.bz2::https://code.videolan.org/videolan/libdvbpsi/repository/${pkgver}/archive.tar.bz2)
sha512sums=('d68bb742b22cc525f04e48597edb9a6ffeccfecd16e2cb9e16cf0f80cbd755bc9d3dc50e4679b2c2be107bb8f18717060d0f80a9994a0060c80a7b98b2cc4354')

prepare() {
  cd ${pkgname}-${pkgver}-*
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}-*
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}-*
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
