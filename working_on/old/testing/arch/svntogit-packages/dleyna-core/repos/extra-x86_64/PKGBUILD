# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=dleyna-core
pkgver=0.6.0+2+g1c6853f
pkgrel=2
pkgdesc="Library of utility functions that are used by the higher level dLeyna"
arch=(x86_64)
url="https://01.org/dleyna"
license=(LGPL2.1)
depends=(gupnp)
makedepends=(git)
_commit=1c6853f5bc697dc0a8774fd70dbc915c4dbe7c5b  # master
source=("git+https://github.com/01org/dleyna-core#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
