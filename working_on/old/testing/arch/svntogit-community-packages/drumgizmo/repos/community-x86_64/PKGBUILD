# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: sekret, mail=$(echo c2VrcmV0QHBvc3Rlby5zZQo= | base64 -d)

pkgname=drumgizmo
pkgver=0.9.19
pkgrel=1
pkgdesc="Multichannel, multilayered, cross-platform drum plugin and stand-alone application"
arch=('x86_64')
url="https://www.drumgizmo.org"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc' 'libxext' 'libx11')
makedepends=('alsa-lib' 'jack' 'libsmf' 'libsndfile' 'lv2' 'zita-resampler')
optdepends=('alsa-lib: for drumgizmo standalone'
            'jack: for drumgizmo standalone'
            'lv2-host: for LV2 plugin')
source=("https://www.${pkgname}.org/releases/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha512sums=('beb693997a333e4f553a0d26da7e26ec8be1348de729aab26edb1e062d5d685f35acf507d78b7a0223eb3af6b594e324ce76400b534ba344dee84de06dbfdc8f'
            'SKIP')
b2sums=('d777c693b00cd291bce974d684bcbdcb304a59df74e9d5d0f56718bdcbb82c153604beff1075e6be4e89c062240d587d28e0386296d25a9e927a7a6eefe9c00b'
        'SKIP')
validpgpkeys=('F39C94D556CCB995B1AE540E9EB89445BF071867') # DrumGizmo <info@drumgizmo.org>

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-lv2
  make
}

package() {
  depends+=('libsmf.so' 'libsndfile.so' 'libzita-resampler.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -vDm 644 {AUTHORS,ChangeLog,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
