# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yangtse <yangtsesu@gmail.com>
# Contributor: WU Jun <quark at lihdd dot net>

pkgname=libpinyin
pkgver=2.6.0
_modelver=19
pkgrel=1
pkgdesc="Library to deal with pinyin"
arch=('x86_64')
url="https://github.com/libpinyin/libpinyin"
license=('GPL')
depends=('kyotocabinet' 'glib2')
makedepends=('gnome-common')
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/$pkgver.tar.gz"
        "$pkgname-model$_modelver.text.tar.gz::https://downloads.sourceforge.net/libpinyin/model$_modelver.text.tar.gz")
noextract=("$pkgname-model$_modelver.text.tar.gz")
sha512sums=('db9c2bd1b367d67dcdf08b25efb465ea955d53c42eb9bc517f32bf2b675e842bd917a7ae659235eeeeb0aaeb244155bbb0920b72ca60e41dbf291909d7b74726'
            '93c70423ba14faa3402bce775f82769dd4ee3a49083beddc540825f88facbd847328e9d01d99473adb7cf26de0f853e2b2e14fe849b1c49531134d1ca4ed2ef2')

prepare() {
  cd $pkgname-$pkgver

  ln -s "$srcdir"/$pkgname-model$_modelver.text.tar.gz data/model$_modelver.text.tar.gz
  sed -i "/wget.*model$_modelver\.text\.tar\.gz/ d" data/Makefile.am
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --with-dbm=KyotoCabinet --enable-libzhuyin
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
