# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=tabular
pkgname=vim-${_pkgname}
pkgver=1.0.0+5+g339091a
pkgrel=1
pkgdesc="Vim script for text filtering and alignment"
arch=('any')
url="https://github.com/godlygeek/tabular"
license=('custom')
depends=('vim')
makedepends=('git')
groups=('vim-plugins')
_commit=339091ac4dd1f17e225fe7d57b48aff55f99b23a
source=("git+${url}#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
    cd ${_pkgname}
    git describe --tags | sed 's/-/+/g'
}

package() {
    cd ${_pkgname}
    install -d "${pkgdir}"/usr/share/vim/vimfiles
    cp -r after autoload doc plugin "${pkgdir}"/usr/share/vim/vimfiles/
    install -Dm644 LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
