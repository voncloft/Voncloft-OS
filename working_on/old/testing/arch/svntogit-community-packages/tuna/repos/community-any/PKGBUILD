# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Joakim Hernberg <jhernberg@alchemy.lu>
# Contributor: Ninez <triplesquarednine@gmail.com>

pkgname=tuna
pkgver=0.14.1
pkgrel=2
epoch=1
pkgdesc="Thread and IRQ affinity setting GUI and cmd line tool"
url="https://rt.wiki.kernel.org/index.php/Tuna"
arch=('any')
license=('GPL2')
groups=('realtime')
depends=('python-ethtool' 'python-linux-procfs' 'python-numpy'
'python-schedutils')
optdepends=('python-inet_diag: Show network sockets in use by threads')
backup=("etc/${pkgname}.conf")
source=("https://git.kernel.org/pub/scm/utils/${pkgname}/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5a4dddcaac4d73506f9c50515108e35f9bc808e857a8d8c344b3a6fe082543a41ed3aeb38ccffecde063af1f41f5b792f6bed354f22962dcd698edf7d8423273')

prepare() {
  cd "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --prefix=/usr \
                          --root="${pkgdir}"

  # tuna was ported to python3, but pygtk (for the GUI) was not ported to
  # pygobject yet: https://bugs.archlinux.org/task/58188
#  install -vDm 755 oscilloscope-cmd.py "${pkgdir}/usr/bin/oscilloscope"
#  install -vDm 644 "${pkgname}/${pkgname}_gui.glade" \
#    -t "${pkgdir}/usr/share/${pkgname}/"
#  install -vDm 644 "${pkgname}.desktop" \
#    -t "${pkgdir}/usr/share/applications/"
  install -vDm 755 "${pkgname}-cmd.py" "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 644 "etc/${pkgname}/example.conf" \
    -t "${pkgdir}/etc/${pkgname}/"
  install -vDm 644 "etc/${pkgname}.conf" -t "${pkgdir}/etc/"
  install -vDm 644 "docs/oscilloscope+${pkgname}.pdf" \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 help/kthreads/* \
    -t "${pkgdir}/usr/share/${pkgname}/help/kthreads"
  install -vDm 644 "org.${pkgname}.policy" \
    -t "${pkgdir}/usr/share/polkit-1/actions/"
  install -vDm 644 "docs/${pkgname}.8" \
    -t "${pkgdir}/usr/share/man/man8/"
  # translations
  for language in `cat po/LINGUAS`; do
    translation="po/${language}.po"
    install -dm 755 "${pkgdir}/usr/share/locale/${language}/LC_MESSAGES"
    msgfmt $translation \
      -o "${pkgdir}/usr/share//locale/${language}/LC_MESSAGES/${pkgname}.mo"
  done
}
# vim:set ts=2 sw=2 et:
