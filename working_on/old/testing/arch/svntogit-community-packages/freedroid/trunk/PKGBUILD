# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=freedroid
pkgver=1.0.2
pkgrel=11
pkgdesc="a clone of the classic game 'Paradroid' on Commodore 64"
arch=('x86_64')
url="http://freedroid.sourceforge.net/"
license=('GPL')
depends=('glibc' 'sdl_image' 'sdl_mixer')
source=(https://downloads.sourceforge.net/sourceforge/freedroid/$pkgname-$pkgver.tar.gz
	freedroid.desktop
	paraicon.png)
md5sums=('585a65f61c2cd308ab45d5c514f695dc'
         'e4a34f00f1cd983331999022569f2b57'
         '68a11e92695978a9aac58cc21a4899db')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm0644 "$srcdir"/paraicon.png "$pkgdir"/usr/share/icons/paraicon.png
  install -Dm0644 "$srcdir"/freedroid.desktop "$pkgdir"/usr/share/applications/freedroid.desktop

  rm -rf "$pkgdir"/usr/share/freedroid/mac-osx
  rm -rf "$pkgdir"/usr/share/freedroid/freedroid.6
  find "$pkgdir"/usr/share/freedroid -type f -name Makefile -exec rm -f {} \;
  find "$pkgdir"/usr/share/freedroid -type f -name Makefile.in -exec rm -f {} \;
  find "$pkgdir"/usr/share/freedroid -type f -name Makefile.am -exec rm -f {} \;
}
