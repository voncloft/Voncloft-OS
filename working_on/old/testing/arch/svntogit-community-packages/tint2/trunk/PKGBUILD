# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Blue Peppers <bluepeppers@archlinux.us>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Yannick LM <LMyannicklm1337@gmail.com>

pkgname=tint2
pkgver=16.7
pkgrel=3
pkgdesc='Basic, good-looking task manager for WMs'
arch=(x86_64)
url='https://gitlab.com/o9000/tint2'
license=(GPL2)
depends=(gtk2 imlib2 startup-notification) # a lot of work to port to gtk3
makedepends=(cmake git ninja setconf startup-notification)
source=("git+https://gitlab.com/o9000/tint2#commit=78313502d3b26c217f5583a23ef571bc9e0edc45") # tag: v16.7
sha256sums=('SKIP')

prepare() {
  setconf $pkgname/get_version.sh VERSION="$pkgver"
}

build() {
  mkdir -p build
  cd build
  cmake ../$pkgname \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D ENABLE_TINT2CONF=1 \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# getver: gitlab.com/o9000/tint2/raw/master/README.md
