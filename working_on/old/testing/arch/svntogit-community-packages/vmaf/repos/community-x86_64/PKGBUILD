# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=vmaf
pkgver=1.5.3
pkgrel=1
pkgdesc='Perceptual video quality assessment algorithm based on multi-method fusion'
arch=('x86_64')
url='https://github.com/Netflix/vmaf/'
license=('BSD')
depends=('gcc-libs')
makedepends=('meson' 'nasm')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/Netflix/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('440652ae417d88be083ffd9fa5967662172601e31c458a9743f6008d7150c900')

build() {
    arch-meson "${pkgname}-${pkgver}/libvmaf/build" "${pkgname}-${pkgver}/libvmaf"
    ninja -v -C "${pkgname}-${pkgver}/libvmaf/build"
}

check() {
    ninja -v -C "${pkgname}-${pkgver}/libvmaf/build" test
}

package() {
    DESTDIR="$pkgdir" ninja -v -C "${pkgname}-${pkgver}/libvmaf/build" install
    install -D -m755 "${pkgname}-${pkgver}/libvmaf/build/tools"/vmaf_{feature,rc} -t "${pkgdir}/usr/bin"
    install -D -m644 "${pkgname}-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
