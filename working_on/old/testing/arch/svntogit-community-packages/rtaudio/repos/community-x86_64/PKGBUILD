# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: rtfreedman  <rob dot til dot freedman at gmail dot com>
# Contributor: Limao Luo <luolimao+AUR at gmail dot com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=rtaudio
pkgver=5.1.0
pkgrel=3
pkgdesc="A set of C++ classes that provide a common API for realtime audio input/output."
arch=('x86_64')
url="https://www.music.mcgill.ca/~gary/rtaudio/"
license=('custom:RtAudio')
depends=('alsa-lib' 'gcc-libs' 'glibc' 'libjack.so' 'libpulse')
makedepends=('cmake' 'doxygen')
provides=('librtaudio.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/thestk/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('338a3a14cd4ea665ac7e94a275cb017bffd87cb10eb8ab6784fad320345ee828b8874439edd08c39efa48736edf9aa0622620784adc320473b03a8f81e17fff6')

prepare() {
  cd "${pkgname}-${pkgver}"
  # license is in README.md
  # https://github.com/thestk/rtaudio/issues/197
  tail -n 25 README.md | sed 's/^\ \ \ \ //g' > LICENSE
  autoreconf -fi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --with-alsa \
              --with-jack \
              --with-pulse
  make
  # python bindings not compatible with python3:
  # https://github.com/thestk/rtaudio/issues/199
}

check() {
  cd "${pkgname}-${pkgver}"
  make -C tests -k
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # docs
  install -vDm 644 {README.md,ChangeLog,doc/release.txt} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  # html
  install -vDm 644 doc/html/* -t "${pkgdir}/usr/share/doc/${pkgname}/html"
  install -vDm 644 doc/images/* \
    -t "${pkgdir}/usr/share/doc/${pkgname}/images"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
