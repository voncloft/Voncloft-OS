# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>

pkgname=libmmtf
_pkgname=mmtf-cpp
pkgver=1.0.0
pkgrel=2
pkgdesc="A binary encoding of biological structures library"
arch=('any')
url="https://github.com/rcsb/mmtf-cpp"
license=('MIT')
makedepends=('cmake')
depends=()
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/rcsb/mmtf-cpp/archive/v${pkgver}.tar.gz")
sha512sums=('0e502ae76e31689d440bff2fbc90fdab7ff62d5369e26f7ab2836966a30bda72625d6d5da33bc0770cf471a1549a02f7478ea16a01dd6be2809568d8daecb073')

prepare() {
  rm -rf build
  mkdir build
}

build() {
  cd build
  cmake "../${_pkgname}-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

