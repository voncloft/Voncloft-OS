# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=samplv1
pkgver=0.9.19
pkgrel=1
pkgdesc="Polyphonic sampler synthesizer with stereo fx"
arch=('x86_64')
url="https://samplv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('alsa-lib' 'cmake' 'jack' 'liblo' 'libsndfile' 'lv2' 'qt5-tools')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'liblo: for standalone application'
            'new-session-manager: for session management using the standalone application')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('8795fe99eef9987b39e9c3e268b1dae4044de1044cc306c1ee21cbd6bd94fe1db51316e5197d13e9ebee1aee2b64f017a192b5f386a37de8c945726b57d4a6a7')
b2sums=('bad6e32b7a0a10917d8f3133bed9b1a04d031b272d70d9516492dd1ac291714ce5b8187a9ddfdf1aae6ed93969a636a38ae6f106894c1c88883b23e72617a05a')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libsndfile.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
