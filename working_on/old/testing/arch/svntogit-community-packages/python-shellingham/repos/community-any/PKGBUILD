# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Clint Valentine <valentine.clint@gmail.com>

_pkgname=shellingham
pkgname=python-shellingham
pkgver=1.4.0
pkgrel=1
pkgdesc="Detect what shell the current Python executable is running in."
arch=('any')
url="https://github.com/sarugaku/${_pkgname}"
license=('ISC')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-mock')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('df6c61abc6d4112cdb8665e780cd4692dd7fcd88c9b8513cb096a0dd247effe0')
b2sums=('06c642a0e88a41683f7a316304c28f66f89d6215ff27244a0b984725b7c888bc0de6e6936012c02a1a178f827f300f99704f0d2f61476ce49a27e8c2aaf0f9a7')

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
