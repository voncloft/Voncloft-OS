# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-picodrive
pkgver=1263
pkgrel=1
pkgdesc='Sega MS/MD/CD/32X core'
arch=(x86_64)
url=https://github.com/libretro/picodrive
license=(BSD)
groups=(libretro)
depends=(
  glibc
  libretro-core-info
)
makedepends=(git)
source=(libretro-picodrive::git+https://github.com/libretro/picodrive.git#commit=c1ee525ac9fbd5f091a2cf34879131f3e00bf176)
sha256sums=(SKIP)

pkgver() {
  cd libretro-picodrive

  git rev-list --count HEAD
}

build() {
  make -C libretro-picodrive -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-picodrive/picodrive_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-picodrive/COPYING -t "${pkgdir}"/usr/share/licenses/libretro-picodrive/
}

# vim: ts=2 sw=2 et:
