# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jack_delay
pkgver=0.4.2
pkgrel=1
pkgdesc="Measure the round-trip latency of a soundcard"
arch=('x86_64')
url="http://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'gcc-libs' 'glibc' 'libjack.so')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('12ba971abe8a99211bf6bc8b1a1c17ee18707fb3ebf09b5320cb0a83e8698183365ca6fefb651ac6dd337eb8c620e8fe804d7353c86a6f1d04455d9e2c319728')

prepare() {
  cd "$pkgname-$pkgver"
  sed -e '/march=native/d' \
      -i source/Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make -C source PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver"
  make -C source PREFIX=/usr DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
