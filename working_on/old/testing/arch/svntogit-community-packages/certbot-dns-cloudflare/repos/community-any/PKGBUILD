# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-cloudflare
pkgver=1.12.0
pkgrel=1
pkgdesc="Cloudflare DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=('certbot' 'python-acme' 'python-cloudflare' 'python-mock' 'python-setuptools'
         'python-zope-interface')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('090b79c7419508ac10de6c691803337758b25a33376af2a92d4a10cb2d50ed01e6828ea2f2b25931d127d68cc891b347c0b6bdab2aaad89b01be72a3077f6ce8'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python -m pytest
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
