# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Benjamin Auder <benjamin.a@mailoo.org>

pkgname=fuse-zip
pkgver=0.7.1
pkgrel=1
pkgdesc='Mount and modify zip files'
arch=(x86_64)
url='https://bitbucket.org/agalanin/fuse-zip'
license=(GPL)
depends=(fuse libzip)
makedepends=(setconf)
source=("https://bitbucket.org/agalanin/fuse-zip/downloads/$pkgname-$pkgver.tar.gz")
sha256sums=('7dac61c3f27f9041545ab1a22bb772ea282ed2dea25a0220dcecfa6801b5b121')

prepare() {
  cd "$pkgname-$pkgver"
  setconf Makefile prefix /usr
  setconf Makefile RELEASE_CXXFLAGS '-O3 -w'
}

build() {
  make -C "$pkgname-$pkgver" release
}

package() {
  install -d "$pkgdir/usr/"{bin,share/man/man1}
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
