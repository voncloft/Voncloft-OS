# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: svvac <_@svvac.net>

pkgname="libspf2"
pkgver=1.2.10
pkgrel=4
pkgdesc="Sender Policy Framework record checking library"
arch=('x86_64')
url="https://www.libspf2.org/"
license=('LGPL')
depends=('glibc' 'libnsl')
makedepends=('libmilter' 'glibc' 'libnsl')
source=("https://www.libspf2.org/spf/${pkgname}-${pkgver}.tar.gz"
        'fix-variadic-macros.patch')
sha256sums=('d91e3de81ae287a2976c44f60283bd3000d720e6a112dc7142eedf1831b821c9'
            '1396d155665f52748cfc7e06e01a92cc98c65193f36e807b8b1de4fb31d4caec')
sha512sums=('162ce382628c6fcadac3e11f5a12442db622bb23f7ec503e16f5ba7fc88afdd777bce6b093c12a58210355985fd11b74b140f08fab347334d82d953dd183b130'
            '886a347c6526c4e81bc035d7e0069f72aaa5ad2103f0e035e46dbd7e1e5f328ebbbc81842fd32397d195a5b18cf841784455a4142291276c1be8942a1c753b4d')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -p1 -i "$srcdir/fix-variadic-macros.patch"
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make check
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix='/usr'
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}/" install
    rm -v "${pkgdir}"/usr/bin/*_static "${pkgdir}/usr/bin/spfd" "${pkgdir}/usr/bin/spf_example" "${pkgdir}/usr/bin/spftest"
}
