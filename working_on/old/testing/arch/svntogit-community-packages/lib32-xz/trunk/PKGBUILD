# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

pkgname=lib32-xz
_pkgbasename=xz
pkgver=5.2.5
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files (32-bit)'
arch=('x86_64')
url='https://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
depends=('lib32-glibc' 'xz')
source=("https://tukaani.org/xz/${_pkgbasename}-${pkgver}.tar.gz"{,.sig})
sha512sums=('7443674247deda2935220fbc4dfc7665e5bb5a260be8ad858c8bd7d7b9f0f868f04ea45e62eb17c0a5e6a2de7c7500ad2d201e2d668c48ca29bd9eea5a73a3ce'
            'SKIP')
validpgpkeys=('3690C240CE51B4670D30AD1C38EE757D69184620')  # Lasse Collin

build() {
	cd "${srcdir}"/xz-${pkgver}

	export CC="gcc -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
	
	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--disable-rpath \
		--enable-werror
	make
}

check() {
	cd "${srcdir}"/xz-${pkgver}

	make check
}

package() {
	cd "${srcdir}"/xz-${pkgver}
	
	make DESTDIR="${pkgdir}" install

	rm -rf "${pkgdir}"/usr/{bin,include,share}
	install -d -m755 "${pkgdir}"/usr/share/licenses
	ln -s xz "$pkgdir/usr/share/licenses/lib32-xz"
}
