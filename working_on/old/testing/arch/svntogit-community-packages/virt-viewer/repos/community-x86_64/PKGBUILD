# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgname=virt-viewer
pkgver=9.0
pkgrel=1
pkgdesc='A lightweight interface for interacting with the graphical display of virtualized guest OS.'
arch=('x86_64')
url='https://gitlab.com/virt-viewer/virt-viewer'
license=('GPL')
depends=('gtk-vnc' 'libvirt' 'spice-gtk' 'desktop-file-utils' 'libvirt-glib' 'libgovirt')
makedepends=('intltool' 'spice-protocol' 'gobject-introspection')
replaces=('virtviewer')
source=("https://virt-manager.org/download/sources/virt-viewer/virt-viewer-${pkgver}.tar.gz"{,.asc})
b2sums=('bc4df73575195888b769dddd8de5c0eda9daa257902f3f3ce8099f9096d0cd9be28547c8671decb70e83e5f0de71633793886bfe78bd9ab5f360dc9759afcc5b'
        'SKIP')
validpgpkeys=('DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF') # Daniel P. Berrange

build() {
  cd virt-viewer-${pkgver}
  ./configure --prefix=/usr --with-spice --disable-update-mimedb
  make
}

package() {
  cd virt-viewer-${pkgver}
  make DESTDIR="${pkgdir}" install
}
