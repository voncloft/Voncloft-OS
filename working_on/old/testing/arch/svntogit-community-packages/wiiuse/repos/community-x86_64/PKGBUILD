# Maintainer: David Runge <dvzrv@archlinux.org>
pkgname=wiiuse
pkgver=0.15.5
pkgrel=1
pkgdesc="A library written in C that connects with several Nintendo Wii remotes"
arch=('x86_64')
url="https://github.com/wiiuse/wiiuse"
license=('GPL3')
depends=('bluez-libs' 'glibc')
provides=('libwiiuse.so')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('51a5abb8473b02a4537a73beb3f0012207294a828c332163ed2e99fa06e9905f590c7a0dc756a9182547c055c9b2218106e0a90dee7373fe33b8187c797cd3b8')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -v build
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -B build
  make -C build
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" -C build install
  install -vDm 644 {{CHANGELOG,README}.mkd,RELEASE.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -v "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
