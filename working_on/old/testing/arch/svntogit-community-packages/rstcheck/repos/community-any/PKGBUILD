# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rstcheck
pkgver=3.3.1
pkgrel=4
pkgdesc='Checks syntax of reStructuredText and code blocks nested within it'
arch=('any')
url=https://github.com/myint/rstcheck
license=('MIT')
depends=('python-docutils' 'python-setuptools')
checkdepends=('python-sphinx')
optdepends=('python-sphinx: Sphinx support')
source=("$url/archive/v$pkgver/rstcheck-v$pkgver.tar.gz")
sha512sums=('07431d6c4789e4c005c4bf4be66f627c390f57e56f25b4f3db8b392bc7b904ca93c3e26145ab344e9b78109d2054e5f36167ab40b1ab023f08c85d6ec12cafbc')

build() {
  cd rstcheck-$pkgver
  python setup.py build
}

check() {
 cd rstcheck-$pkgver
 ./test_rstcheck.py
}

package() {
  cd rstcheck-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/rstcheck LICENSE
}

# vim:set ts=2 sw=2 et:
