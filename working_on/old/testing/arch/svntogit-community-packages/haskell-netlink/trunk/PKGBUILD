# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=netlink
pkgname=haskell-netlink
pkgver=1.1.1.0
pkgrel=6
pkgdesc="Netlink communication for Haskell"
url="https://github.com/Ongy/netlink-hs"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-cereal' 'haskell-monad-loops' 'haskell-pretty-hex')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('2bb3d3c1a86e2de61e18a50366d9ecfa863df4959533db5ab3b127b4e02853cf4295ce7519a8ba7bbee9af0534c510aae99c567a5b23e8587e8e56113f530da3')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
