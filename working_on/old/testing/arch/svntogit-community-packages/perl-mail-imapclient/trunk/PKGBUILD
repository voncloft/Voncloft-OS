# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: DonVla <donvla@users.sourceforge.net>
# Contributor: achterin <headlock@its-crap.de>

pkgname=perl-mail-imapclient
pkgver=3.42
pkgrel=3
pkgdesc='Perl/CPAN Module Mail::IMAPClient : An IMAP Client API'
arch=('any')
url='https://search.cpan.org/dist/Mail-IMAPClient'
license=('GPL')
depends=('perl-parse-recdescent' 'perl-digest-hmac')
optdepends=("perl-authen-sasl: for Authmechanism 'DIGEST-MD5'")
options=('!emptydirs' purge)
source=("https://cpan.metacpan.org/authors/id/P/PL/PLOBBES/Mail-IMAPClient-${pkgver}.tar.gz")
b2sums=('360006381e318dfc46835cabb1a1c72f4039813563a58d585bf9b042b880ece3e5b00a29283bf4de5c382a02628df2094e1772762251c167498c9fcd70ed85a2')

build() {
  cd Mail-IMAPClient-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd Mail-IMAPClient-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd Mail-IMAPClient-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
}
