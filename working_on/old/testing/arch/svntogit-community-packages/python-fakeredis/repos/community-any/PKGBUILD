# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=fakeredis
pkgname=python-fakeredis
pkgver=1.3.0
pkgrel=3
pkgdesc="Fake implementation of redis API (redis-py) for testing purposes"
arch=('any')
url="https://github.com/jamesls/${_pkgname}"
license=('BSD' 'MIT')
depends=('python-redis' 'python-six' 'python-sortedcontainers')
makedepends=('python-setuptools' 'python-lupa')
checkdepends=('python-pytest' 'python-hypothesis')
optdepends=('python-lupa: for lua scripting support')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('4324cb4c0855f353952e43ded4e35045dabeb8500ee732d1b3c4c6d74e0c39470947280c3ef8df91b3a350d0a4773712b3a71d78f8f5b30c7b3c19694ec6df7a')
b2sums=('b717c99da81af1c0bf6ed94242fad3a4caaea233c35e5f4fd604eb1c3e3d8786143485d0f59ca697c4bef8ae8b5712092ca09dcdc41e4cda6f9aea3891d36ec0')

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
