# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-loengfan
pkgver=0.0.0.20200805
_commit=edb7600335c399a06e90be76b207c5fe9c184ff0
pkgrel=1
pkgdesc="Cantonese version of Liang Fen input method"
arch=('x86_64')
url="https://github.com/CanCLID/rime-loengfan"
license=('CCPL:by')
depends=('rime-cangjie')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/CanCLID/rime-loengfan/archive/$_commit/rime-loengfan-$_commit.tar.gz")
sha512sums=('cd8aed8872c79b28d730b4d964ec72ee4b166ffd21e58abc8dc44d7f6679c81ec17551d998c546f4d87439a78e7ec112e4be5b62627ff11f4160a7e39f367dd7')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
