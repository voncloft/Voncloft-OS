# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-persistent
pkgver=4.6.4
pkgrel=3
pkgdesc="Translucent persistent objects"
arch=('x86_64')
license=('ZPL')
url="http://www.zope.org/Products/ZODB"
depends=('python-zope-interface')
makedepends=('python-setuptools' 'python-cffi')
checkdepends=('python-zope-testrunner' 'python-manuel')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/persistent/archive/$pkgver.tar.gz")
sha512sums=('5b17e969bedb800985526118eb5d4ef9e318e77bb8e50e70da36808eeb227583373563808a8ca638b889b498ff84cb8233c7930d7b652d6d69f4b7382546aa6b')

build() {
  cd persistent-$pkgver
  python setup.py build
}

check() {
  cd persistent-$pkgver
  python -m venv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py develop
    python -m zope.testrunner --test-path=.
  )
}

package() {
  cd persistent-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
