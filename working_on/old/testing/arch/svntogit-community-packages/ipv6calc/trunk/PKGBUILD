# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Co-Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: mortzu

pkgname=ipv6calc
pkgver=2.2.0
pkgrel=2
pkgdesc='Small utility to manipulate, convert and calculate (more than) IPv6 addresses'
url='https://www.deepspace6.net/projects/ipv6calc.html'
license=('GPL2')
arch=('x86_64')
# See https://www.deepspace6.net/projects/ipv6calc.html#idm140634407144416
makedepends=('geoip')
optdepends=('geoip: for country GeoIP support'
            'geoip-database-extra: for city-level GeoIP support'
            'perl: to run the provided scripts')
checkdepends=('perl-digest-sha1' 'perl-uri' 'perl-html-parser' 'perl-proc-processtable')
source=("ftp://ftp.bieringer.de/pub/linux/IPv6/ipv6calc/${pkgname}-${pkgver}.tar.gz"{,.asc}
        "ftp://ftp.bieringer.de/pub/linux/IPv6/ipv6calc/CODE-GPG-KEY-bieringer.de-2013"
        'cache_lru_limit.patch'
        'anti-dos_test.patch')
sha256sums=('1935352f6171b07f18ce0487ee95ffcc006ea3f653f7cba564d2d8e135f04ca1'
            'SKIP'
            '59a84dfb8bef98fc801f374bf3f1c19adb5d8cda1a2233eb3366a71c6b1fd154'
            '28a76581cdadecd73d2179be6fdef1e45349a809aa72e35c037f8b6b9c746d18'
            '22a9897ca52a46a896b440cf8a441b72579730d72759cac1edd2af1a8f33a4ca')
validpgpkeys=('AAB38CB797C750C778C608C6DDEB141DF7380F61') # Peter Bieringer (Code Signing Key 2013) <code@bieringer.de>

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/cache_lru_limit.patch"
  patch -Np1 -i "${srcdir}/anti-dos_test.patch"

  autoconf
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    --prefix="/usr" \
    --libdir="/usr/lib" \
    --datarootdir="/usr/share" \
    --enable-shared \
    --enable-bundled-md5 \
    --enable-geoip \
    --with-geoip-dynamic=yes
  make
}

check() {
  cd "${pkgname}-${pkgver}"

  make test-minimal
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
