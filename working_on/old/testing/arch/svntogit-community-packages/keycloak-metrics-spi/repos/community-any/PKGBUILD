# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=keycloak-metrics-spi
pkgver=2.0.1
_jarversion=2.0.2
pkgrel=1
pkgdesc='A Service Provider that adds a Prometheus metrics endpoint to Keycloak'
url="https://github.com/aerogear/keycloak-metrics-spi"
arch=('any')
license=('Apache')
depends=('java-runtime>=8')
makedepends=('java-environment=8' 'gradle' 'keycloak')
source=($pkgname-$pkgver.tar.gz::https://github.com/aerogear/keycloak-metrics-spi/archive/${pkgver}.tar.gz)
sha512sums=('a39e59f65e5a42f2ed7b7cf1214c7208188190e97e59b1bc66700cfc3a11ed00eb2707af279ee515c59a4f296be90871f9fa5ebc3cc1f6056b0916b65aba3bcb')

build() {
  cd ${pkgname}-${pkgver}
  gradle jar
}

check() {
  cd ${pkgname}-${pkgver}
  gradle test
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}/opt/keycloak/standalone/deployments/"
  install -Dm644 "build/libs/keycloak-metrics-spi-${_jarversion}-SNAPSHOT.jar" "${pkgdir}/usr/share/java/${pkgname}/keycloak-metrics-spi-${pkgver}.jar"
  ln -s "/usr/share/java/${pkgname}/keycloak-metrics-spi-${pkgver}.jar" "${pkgdir}/opt/keycloak/standalone/deployments/keycloak-metrics-spi-${pkgver}.jar"
}
