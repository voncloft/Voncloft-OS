# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Robin Broda <robin at broda dot me>

pkgname=bonzomatic
# Inspired by the MAJOR, MINOR and PATCH versions that are defined in CMakeLists.txt
pkgver=1.0.20200306
pkgrel=1
pkgdesc='Live shader coding tool'
arch=(x86_64)
url='https://github.com/Gargaj/bonzomatic'
license=(custom:Unlicense)
depends=(alsa-lib fontconfig hicolor-icon-theme libglvnd)
makedepends=(chrpath cmake git glu imagemagick libxcursor libxi libxinerama libxrandr ninja)
# Using latest master from 2020-03-06, while awaiting the next release
source=("$pkgname::git+$url#commit=55c24f7447536728df4e5a519d0763760a670587")
md5sums=('SKIP')

prepare() {
  convert "$pkgname/data/unix/icon.png" -resize 128x128 "$srcdir/$pkgname.png"
}

build() {
  mkdir -p build
  cd build
  cmake "$srcdir/$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_C_FLAGS="$CFLAGS -fPIC -w" \
    -DCMAKE_CXX_FLAGS="$CFLAGS -fPIC -w" \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  # Fix insecure rpath
  chrpath -d "$pkgdir/usr/bin/bonzomatic"

  # Install license
  install -Dm644 $pkgname/LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Install 128x128 icon
  install -Dm644 $pkgname.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# getver: github.com/Gargaj/Bonzomatic/releases
# vim: ts=2 sw=2 et:
