# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>

pkgname=soapysdr
_pkgname=SoapySDR
pkgver=0.7.2
_gitver=soapy-sdr-$pkgver
pkgrel=3
pkgdesc='Vendor and platform neutral SDR support library'
arch=('x86_64')
url='https://github.com/pothosware/SoapySDR'
license=('Boost')
depends=('python')
makedepends=('cmake' 'swig' 'doxygen' 'graphviz')
optdepends=('soapyairspy: Airspy backend'
            'soapyaudio: Audio devices backend'
            'soapybladerf: BladeRF backend'
            'soapyhackrf: HackRF backend'
            'soapynetsdr: NetSDR backend'
            'soapyosmo: OsmoSDR backend'
            'soapyremote: SoapySDR remote support'
            'soapyrtlsdr: rtl-sdr backend'
            'soapyuhd: UHD backend')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_gitver.tar.gz")
sha512sums=('2454b9b6bf4542f3bce2c6a8e1c886cee9179ca0e13e3c1e22817a05ef671428a3cb8fea69e496c8b62573f360c248508edf3d0871ec5063dfac8211b122d371')

build() {
  mkdir -p $_pkgname-$_gitver/build
  cd $_pkgname-$_gitver/build

  cmake .. \
    -DSOAPY_SDR_EXTVER=ARCH \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd $_pkgname-$_gitver/build

  make DESTDIR="$pkgdir" install

  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  cp -r -a --no-preserve=ownership docs/html "$pkgdir"/usr/share/doc/$pkgname
}
