# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>

pkgname=stk
pkgver=4.6.1
pkgrel=3
pkgdesc='The Synthesis ToolKit in C++'
arch=('x86_64')
url='https://ccrma.stanford.edu/software/stk/'
license=('MIT')
depends=('gcc-libs' 'glibc')
makedepends=('alsa-lib' 'doxygen' 'jack')
optdepends=('tk: run provided demos/samples')
# CAUTION: introduces soname change on *every* release:
# https://github.com/thestk/stk/issues/89
provides=("libstk-${pkgver}.so")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/thestk/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('61d4db7b4a45446e231dedc13e139cb488e2ce805278f0b20aa95e69ddb1fa9be549ab5f1fe24c69aa865ebc2940d5fba6e3819a1a7fb1d68e236131fcfb4cac')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  export RAWWAVE_PATH="/usr/share/${pkgname}/rawwaves/"
  ./configure --prefix=/usr \
              --enable-shared \
              --with-alsa \
              --with-jack
  make
  make -C projects/demo
  cd doc/doxygen
  doxygen
}

package() {
  depends+=('libasound.so' 'libjack.so')
  cd "$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install
  # demo
  install -vDm 755 projects/demo/stk-demo -t "${pkgdir}/usr/bin"
  install -vDm 644 projects/demo/tcl/*.tcl \
    -t "${pkgdir}/usr/share/${pkgname}/tcl"
  install -vDm 644 projects/demo/scores/{README,*.ski} \
    -t "${pkgdir}/usr/share/${pkgname}/scores"
  install -vDm 644 rawwaves/*.{raw,c} \
    -t "${pkgdir}/usr/share/${pkgname}/rawwaves"
  # docs
  install -vDm 644 doc/{README-Linux,ReleaseNotes,SKINI,hierarchy}.txt \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 doc/html/*.{html,png,js} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/html"
  # license
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/${pkgname}"
}
