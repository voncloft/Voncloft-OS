# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BChoppr
pkgname=bchoppr
pkgver=1.10.4
pkgrel=1
pkgdesc="An audio stream chopping LV2 plugin"
arch=('x86_64')
url="https://github.com/sjaehn/BChoppr"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11' 'lv2-host')
makedepends=('lv2')
checkdepends=('lv2lint')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sjaehn/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('421ed001b8793728a5c2440e963df3a7294b712606745af2a3f341354472150dfc4493d108887d12a0afab7498025dfee5d3a308aee15d2ff5694e92db58919d')
b2sums=('6b9567b4f41254b66d340033223497ec50dcc419fc1b6a252ea3d9a08172f756f0b00dfd68ccef6f4ee03a3fa85a02a6029cd14211539863671b6977aff30b64')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  make PREFIX='/usr'
}

check() {
  cd "$pkgname-$pkgver"
  lv2lint -Mpack -I "${PWD}/${_name}.lv2/" "https://www.jahnichen.de/plugins/lv2/${_name}"
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" PREFIX='/usr' install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  # remove useless license file
  find "${pkgdir}/usr/lib/" -type f -iname "*LICENSE*" -delete
}
