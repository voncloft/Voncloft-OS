# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Wes Brewer <brewerw@gmail.com>
# Contributor: VuDu <vudu.curse@gmail.com>

pkgname=nfoview
pkgver=1.28
pkgrel=2
pkgdesc="Simple NFO file viewer in GTK+ 3"
arch=('any')
url="https://otsaloma.io/nfoview/"
license=('GPL3')
depends=('gtk3' 'python-gobject')
checkdepends=('python-pytest' 'xorg-server-xvfb')
makedepends=('git' 'python-setuptools')
optdepends=('terminus-font: recommended font')
source=("${pkgname}::git+https://github.com/otsaloma/${pkgname}.git#tag=${pkgver}?signed"
        'x-nfo.desktop')
sha512sums=('SKIP'
            'a800af55df103ee12ce7a8815780e15896d7ad3eee947760ff4486ffb57fea8cf4105b5ffc61dc81e849ddf03eeac77f593a4ffe4fd73b5ea026672c72dff108')
validpgpkeys=('8DD6B3FCDAB33C91EA915B913A584A9504CACA67') # Osmo Salomaa <otsaloma@iki.fi>

prepare() {
  mv -v "${pkgname}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  xvfb-run pytest -v "${pkgname}"
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --prefix=/usr \
                          --root="${pkgdir}/"
  # docs
  install -vDm 644 {AUTHORS,NEWS,README,TODO}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"

  install -vDm 644 "${srcdir}/x-nfo.desktop" \
    -t "${pkgdir}/usr/share/mimelnk/text/"
}

# vim:set ts=2 sw=2 et:
