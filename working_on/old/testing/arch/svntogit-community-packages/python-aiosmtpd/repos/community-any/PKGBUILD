# Maintainer: David Runge <dvzrv@archlinux.org>

_name=aiosmtpd
pkgname=python-aiosmtpd
pkgver=1.2.4
pkgrel=1
pkgdesc="An asyncio based SMTP server"
arch=('any')
url="https://github.com/aio-libs/aiosmtpd"
license=('Apache')
depends=('python-atpublic')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('dc6041e284daceb89f8ca83b5cd715a5811eb4cf76504c23112e24c97d6c88480667c713ce094c2dcbc5b12a80605e551b6aadc4b1ce499089be68c7c4e7df8c')
b2sums=('accecbc3fe66efc748d0bc3c85504d731005b907435fada5b3c7ae0f7778e89f19d7c15a7edb194a7ba6310a450132728557a441b3fb2958d3658b0a15d3ccf5')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
