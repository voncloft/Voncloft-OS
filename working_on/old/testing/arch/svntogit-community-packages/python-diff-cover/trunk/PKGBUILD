# Maintainer: David Runge <dvzrv@archlinux.org>

_name=diff_cover
pkgname=python-diff-cover
pkgver=4.1.1
pkgrel=1
pkgdesc="Automatically find diff lines that need test coverage"
arch=('any')
url="https://github.com/Bachmann1234/diff_cover"
license=('Apache')
depends=('python-jinja' 'python-jinja_pluralize' 'python-pluggy'
'python-pygments' 'python-setuptools')
checkdepends=('flake8' 'python-pytest' 'python-mock' 'python-pytest-cov'
'python-pycodestyle' 'python-pyflakes' 'python-pylint' 'python-pydocstyle')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('f69aa84b670dbbd548e355e95a7f5d0d0ed067036e810da993074702121e453395b7d376634d0d7cf0ae3d76cc16c45480daf72364aad3ed1d2185e818c3df69'
            'SKIP')
b2sums=('e884d9e64d6112784a98945a4108cf52588e270c15072a0329e6324dbefe39e81bc57d50554ebb9b183acbe73161a1b64d67d65e9151fa3dc90a741c14589a63'
        'SKIP')
validpgpkeys=('54CAEABCAC2956D407348256972401BDE60128CB') # Matt Bachmann <matt.bachmann@lola.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 {CHANGELOG,NOTICE,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
