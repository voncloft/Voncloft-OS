# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=elementary-wallpapers
pkgver=5.5.0
pkgrel=1
pkgdesc='Collection of wallpapers for elementary OS'
arch=(any)
url=https://github.com/elementary/wallpapers
license=(GPL3)
groups=(pantheon)
makedepends=(
  git
  meson
)
source=(
  elementary-wallpapers::git+https://github.com/elementary/wallpapers.git#tag=47b25c2268ff011af74628dedc00d66e73fe051e
  elementary-wallpapers-install-path.patch
)
sha256sums=('SKIP'
            'c6d599f649a35ae386b422c2fa061d441495999f664da31f22021d6dcea133aa')

pkgver() {
  cd elementary-wallpapers

  git describe --tags
}

prepare() {
  cd elementary-wallpapers

  patch -Np1 -i ../elementary-wallpapers-install-path.patch
}

build() {
  arch-meson elementary-wallpapers build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
