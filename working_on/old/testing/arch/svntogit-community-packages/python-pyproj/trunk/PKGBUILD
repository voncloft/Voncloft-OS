# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=python-pyproj
pkgver=2.5.0
pkgrel=3
pkgdesc="Python interfaces to PROJ.4 library"
arch=('x86_64')
url="https://github.com/pyproj4/pyproj"
license=('custom')
depends=('proj' 'python')
makedepends=('python-setuptools' 'cython')
source=($pkgname-$pkgver.tar.gz::https://github.com/pyproj4/pyproj/archive/v${pkgver}rel.tar.gz)
sha256sums=('bd3b9a27e3e53e164d6ce4e19f6a81384bbe050829e213119dbabe09efd18ebe')

prepare() {
  cd "${srcdir}"

# Remove RPATH
  sed -i '/runtime_library_dirs=/d' pyproj-${pkgver}rel/setup.py
}

build() {
  cd "${srcdir}"

  export PROJ_DIR=/usr

  cd pyproj-${pkgver}rel
  python setup.py build
}

package() {
  cd "${srcdir}"/pyproj-${pkgver}rel
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE_proj4 "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
