# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sigal
pkgver=2.1.1
pkgrel=2
pkgdesc="Yet another simple static gallery generator"
arch=('any')
url="http://sigal.saimon.org/en/latest/"
license=('MIT')
depends=('ffmpeg' 'python-blinker' 'python-click' 'python-jinja'
'python-markdown' 'python-natsort' 'python-pillow' 'python-pilkit'
'python-setuptools')
checkdepends=('python-pytest-cov' 'python-boto' 'python-brotli'
'python-cryptography' 'python-feedgenerator' 'python-zopfli')
optdepends=('python-boto: upload to S3 plugin'
            'python-brotli: compress assets plugin'
            'python-cryptography: encrypt plugin'
            'python-feedgenerator: feed plugin'
            'python-zopfli: compress assets plugin')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('0cf84087202cdcf060a0f3b3e166871f7f98c4736f68aadbae3dd4b69e58fca09194f1b7a7170deb5b480516183844c9c16793f4d2775a9ea0039693c0232baf')
b2sums=('472e0957f0169a51e967fa2954f00f121b8431bc6c09c450443b06eac98cd1fa335afa9f202976955a23e44e6bfa2a3cad8c5667127cce26033a9fcaedbd04c8')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {AUTHORS,{CONTRIBUTING,README}.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
