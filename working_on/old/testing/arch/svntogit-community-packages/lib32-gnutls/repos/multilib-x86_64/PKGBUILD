# Maintainer: Florian Pritz <bluewind@xinu.at>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Christoph Vigano <mail at cvigano dot de>
# Contributor: Biru Ionut <ionut@archlinux.ro>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

_pkgbasename=gnutls
pkgname=lib32-$_pkgbasename
pkgver=3.7.0
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer (32-bit)"
arch=('x86_64')
license=('GPL3' 'LGPL2.1')
url="https://gnutls.org/"
depends=('lib32-zlib' 'lib32-nettle' 'lib32-p11-kit' 'lib32-libtasn1' 'lib32-libidn2' $_pkgbasename)
makedepends=('gcc-multilib')
source=(https://www.gnupg.org/ftp/gcrypt/gnutls/v${pkgver%.*}/${_pkgbasename}-${pkgver}.tar.xz{,.sig})
sha256sums=('49e2a22691d252c9f24a9829b293a8f359095bc5a818351f05f1c0a5188a1df8'
            'SKIP')
validpgpkeys=('0424D4EE81A0E3D119C6F835EDA21E94B565716F'  # "Simon Josefsson <simon@josefsson.org>"
              '1F42418905D8206AA754CCDC29EE58B996865171'  # "Nikos Mavrogiannopoulos <nmav@gnutls.org>
              '462225C3B46F34879FC8496CD605848ED7E69871') # "Daiki Ueno <ueno@unixuser.org>"

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${_pkgbasename}-${pkgver}

  # disable libidn for security reasons:
  # http://lists.gnupg.org/pipermail/gnutls-devel/2015-May/007582.html

  ./configure --prefix=/usr --libdir=/usr/lib32 --build=i686-pc-linux-gnu \
    --with-zlib \
    --with-included-unistring \
    --disable-static \
    --disable-guile \
    --disable-valgrind-tests
  make
}

check() {
  cd ${_pkgbasename}-${pkgver}
  #make -k check
}

package() {
  cd ${_pkgbasename}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
