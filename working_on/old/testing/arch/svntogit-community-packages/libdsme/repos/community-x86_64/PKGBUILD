# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libdsme
pkgver=0.66.3
pkgrel=1
pkgdesc="DSME dsmesock dynamic library"
arch=('x86_64')
url="https://git.merproject.org/mer-core/libdsme"
license=('LGPL')
depends=('glib2')
makedepends=('git' 'check')
source=("git+https://git.merproject.org/mer-core/libdsme.git#tag=$pkgver" libdsme-uio.patch)
sha512sums=('SKIP'
            'aba3ffda5cab681133bc2df12f91c6b572e5b11692f9485558be36a80bfbf43a6438df2bf3c003c4f6e3dc73afc345435885eedf7d6b17c0083f054008a84a33')

prepare() {
  cd libdsme
  patch -p1 -i ../libdsme-uio.patch # Fix build
}

build() {
  cd libdsme
  ./verify_version
  make
}

package() {
  cd libdsme
  make DESTDIR="${pkgdir}" install_main install_devel

  # Fix symlinks
  for name in libdsme libdsme_dbus_if libthermalmanager_dbus_if; do
    ln -sf $name.so.0.3.0 "$pkgdir"/usr/lib/$name.so.0
    ln -sf $name.so.0.3.0 "$pkgdir"/usr/lib/$name.so
  done
}
