# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=hm
pkgver=16.22
pkgrel=1
pkgdesc='HEVC Test Model - the reference software for HEVC'
arch=('x86_64')
url='https://hevc.hhi.fraunhofer.de/'
license=('BSD')
depends=('gcc-libs')
makedepends=('cmake' 'lsb-release' 'openmp')
source=("https://vcgit.hhi.fraunhofer.de/jct-vc/HM/-/archive/HM-${pkgver}/HM-HM-${pkgver}.tar.bz2")
sha256sums=('b2863d480860a48472f6fe55f7488ccdaeaaac1a1ccc65b946ba643e0b6c8637')

prepare() {
    cp -a "HM-HM-${pkgver}" "HM-HM-${pkgver}-highbit"
}

build() {
    cmake -B build -S "HM-HM-${pkgver}" \
        -DCMAKE_BUILD_TYPE:STRING='Release' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DHIGH_BITDEPTH:BOOL='OFF' \
        -Wno-dev
    make -C build
    
    cmake -B build-highbit -S "HM-HM-${pkgver}-highbit" \
        -DCMAKE_BUILD_TYPE:STRING='Release' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DHIGH_BITDEPTH:BOOL='ON' \
        -Wno-dev
    make -C build-highbit
}

package() {
    local _file
    while read -r -d '' _file
    do
        install -D -m755 "$_file" -t "${pkgdir}/usr/bin"
    done < <(find "HM-HM-${pkgver}/bin" -maxdepth 1 -type f -executable -print0)
    
    while read -r -d '' _file
    do
        install -D -m755 "HM-HM-${pkgver}-highbit/bin/${_file}" "${pkgdir}/usr/bin/${_file/Static/HighBitDepthStatic}"
    done < <(find "HM-HM-${pkgver}-highbit/bin" -maxdepth 1 -type f -executable -print0 | sed -z "s|HM\-HM\-${pkgver}\-highbit/bin||")
    
    install -D -m644 "HM-HM-${pkgver}/README"    -t "${pkgdir}/usr/share/doc/${pkgname}"
    install -D -m644 "HM-HM-${pkgver}/doc"/*.pdf -t "${pkgdir}/usr/share/doc/${pkgname}"
    install -D -m644 "HM-HM-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
