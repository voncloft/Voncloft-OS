# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jack_utils
pkgver=0.0.1
pkgrel=1
pkgdesc="Useful command line programs for JACK"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/downloads/index.html"
license=('GPL2')
groups=('pro-audio')
depends=('glibc')
makedepends=('jack')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('599c53ea07d6cf760357583c00489a361adc8894a7b6baff9e58676b0be2295650822e52457b882bab85a89b90adb9a876ddd63d1e3785ec35ec66ac24f472f8')

prepare() {
  cd "$pkgname-$pkgver"
  # remove -march=native
  sed -e '/native/d' -i source/Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make PREFIX='/usr' -C source
}

package() {
  depends+=('libjack.so')
  cd "$pkgname-$pkgver"
  make PREFIX='/usr' DESTDIR="$pkgdir/" install -C source
  install -vDm 644 {AUTHORS,README} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
