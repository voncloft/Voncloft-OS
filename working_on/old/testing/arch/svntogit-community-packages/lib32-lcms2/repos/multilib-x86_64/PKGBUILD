# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

_pkgbasename=lcms2
pkgname=lib32-lcms2
pkgver=2.11
pkgrel=1
pkgdesc="Small-footprint color management engine, version 2 (32-bit)"
arch=('x86_64')
license=('MIT')
depends=('lib32-libtiff' ${_pkgbasename})
makedepends=('gcc-multilib')
url="https://www.littlecms.com"
source=("https://github.com/mm2/Little-CMS/archive/${pkgver}/${_pkgbasename}-${pkgver}.tar.gz")
sha512sums=('dce2bcda29b3e7200b748d4f2345c55ae5d20ba00e1966886bbeaf7fb5d134420963ee1b3fd4e72297f42b0e131146ac72d45579f0871f2dc47385ba5cad1491')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd Little-CMS-${pkgver}

  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

check() {
  cd Little-CMS-${pkgver}

  make check
}

package() {
  cd Little-CMS-${pkgver}

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -s ${_pkgbasename} "${pkgdir}/usr/share/licenses/${pkgname}"
}
