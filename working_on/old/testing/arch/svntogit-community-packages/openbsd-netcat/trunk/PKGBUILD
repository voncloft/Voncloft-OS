# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgname="openbsd-netcat"
pkgver=1.217_2
pkgrel=1
pkgdesc="TCP/IP swiss army knife. OpenBSD variant."
arch=('x86_64')
url="https://packages.debian.org/sid/netcat-openbsd"
license=('BSD')
depends=('libbsd')
makedepends=('gcc' 'make')
provides=('netcat')
conflicts=('gnu-netcat')
source=("https://deb.debian.org/debian/pool/main/n/netcat-openbsd/netcat-openbsd_${pkgver%_*}.orig.tar.gz"
	"https://deb.debian.org/debian/pool/main/n/netcat-openbsd/netcat-openbsd_${pkgver/_/-}.debian.tar.xz")
sha512sums=('99da59fa1d9b6e991531b45c0291ef43b064f2d54184cde8a2f508562d015b37cfc69391a4b0562b89b40dfe7307e8d38f48a63bb293af3b8f705848c689dd17'
            '4dcd95dddd77598a37fe7f4450be6d332058cd787d64454db347bfcfe8f2f809b22884df46925f96d020f8fbe6b411c3d6a19d7cd6e3903b15438ad7928f657a')

build() {
  cd "$srcdir"/netcat-openbsd-${pkgver%_*}
  for i in `cat ../debian/patches/series`; do
	echo "** patch $i" 1>&2
	cat "../debian/patches/$i"
  done | patch -p1
  make \
	CFLAGS="$CFLAGS -DDEBIAN_VERSION=\"\\\"${pkgrel}\\\"\" -I/usr/include/libbsd" \
	LDFLAGS="$LDFLAGS -lbsd"
}

package() {
  cd "$srcdir"/netcat-openbsd-${pkgver%_*}
  install -Dm0755 nc "$pkgdir"/usr/bin/nc
  ln -s nc "$pkgdir"/usr/bin/netcat
  install -Dm0644 nc.1 "$pkgdir"/usr/share/man/man1/nc.1
  install -dm0755 "$pkgdir"/usr/share/licenses/$pkgname/
  head -n28 netcat.c | tail -n+2 >"$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
