# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aniso8601
pkgver=8.1.1
pkgrel=1
pkgdesc="A library for parsing ISO 8601 strings."
url="https://bitbucket.org/nielsenb/aniso8601"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-dateutil' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://bitbucket.org/nielsenb/aniso8601/get/v$pkgver.tar.gz")
sha512sums=('1f622a0016c3fa991ffe76dc89eed77968f639e68a5d6ad45519cb6db85cf73856644dddfa713cd14eca3380a6996983f300459bcca4ea21dc047222e39273b2')

prepare() {
  mv nielsenb-aniso8601-* aniso8601-$pkgver
}

build() {
  cd "$srcdir"/aniso8601-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/aniso8601-$pkgver
  python setup.py pytest
}

package() {
  cd aniso8601-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
