# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=diskus
pkgver=0.6.0
pkgrel=2
pkgdesc="Minimal, fast alternative to 'du -sh'"
arch=('x86_64')
url=https://github.com/sharkdp/diskus
license=('Apache' 'MIT')
depends=('gcc-libs')
makedepends=('rust')
source=("diskus-$pkgver.tar.gz::https://static.crates.io/crates/diskus/diskus-$pkgver.crate")
sha512sums=('c532de84356f43109242805b47f6154b0f837c176e5aa016c5f2c221ff2b77da91511cc6e01351924f38cffd5f26c7e8740a4475def1f7cbcffc29501c3321b1')

build() {
  cd diskus-$pkgver
  cargo build --release --locked
}

check() {
  cd diskus-$pkgver
  cargo test --release --locked
}

package() {
  cd diskus-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/diskus
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 doc/diskus.1
  install -Dm644 -t "$pkgdir"/usr/share/licenses/diskus LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
