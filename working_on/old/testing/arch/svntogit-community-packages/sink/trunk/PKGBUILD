# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sink
pkgver=0.8.0
pkgrel=3
pkgdesc="An offline-caching, synchronization and indexing system for PIM data"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kcontacts kcalendarcore kasync kimap2 kdav2 lmdb libgit2 xapian-core curl)
makedepends=(extra-cmake-modules flatbuffers)
source=("https://download.kde.org/unstable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha256sums=('d75f825167433d577cf500bfb7a3f664288c7805640859de2f873611a9226ff4')

prepare() {
  mkdir -p build
  find -name CMakeLists.txt | xargs sed -e '/add_subdirectory(tests)/d' -i  # Don't build tests 
}

build() {
  cd build
  cmake ../$pkgname-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
