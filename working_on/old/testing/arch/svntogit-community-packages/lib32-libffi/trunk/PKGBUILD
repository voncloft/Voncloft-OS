# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-libffi
pkgver=3.3
pkgrel=2
pkgdesc="A portable, high level programming interface to various calling conventions (32-bit)"
arch=('x86_64')
license=('MIT')
url="https://sourceware.org/libffi/"
depends=('lib32-glibc' 'libffi')
checkdepends=('dejagnu')
source=(https://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz)
sha512sums=('61513801a156f11420f541d325de697131846487122d6bdcf5491b18b4da788589f5c0bb07e88e396495d3be5830d74e9135595e2b8ddbfe95c448d8597fbd6f')

build() {
  cd libffi-${pkgver}

  export CC="gcc -m32"

  ./configure --prefix=/usr \
    --libdir=/usr/lib32 --libexecdir=/usr/lib32 \
    --disable-static --enable-pax_emutramp

  make
}

check() {
  make -C libffi-${pkgver} check
}

package() {
  cd libffi-${pkgver}

  make DESTDIR="${pkgdir}" install

  rm -r "${pkgdir}"/usr/{include,share}
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
}
