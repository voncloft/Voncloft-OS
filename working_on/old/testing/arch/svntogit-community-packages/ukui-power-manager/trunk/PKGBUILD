# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-power-manager
pkgver=2.0.2
pkgrel=1
pkgdesc="Power management tool for the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-power-manager"
groups=('ukui')
depends=('dbus-glib' 'gtk3' 'libgnome-keyring' 'libnotify' 'upower')
makedepends=('intltool' 'mate-common' 'yelp-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-power-manager/archive/v$pkgver.tar.gz")
sha512sums=('12f5fea1e3d32f0a63be9f7445546346143621a9e75f2dd7ee9c209b7dd761b9a7492d80e152daf06194d4fafcedd8d612cb90c0bfce03d81abf679bf15423ce')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --sbindir=/usr/bin --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
