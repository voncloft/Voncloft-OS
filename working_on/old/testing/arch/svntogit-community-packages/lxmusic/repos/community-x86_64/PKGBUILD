# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Marcus Schulderinsky <mmaacceess at gmail dot com>

pkgbase=lxmusic
pkgname=(lxmusic lxmusic-gtk3)
pkgver=0.4.7
pkgrel=3
pkgdesc='Lightweight XMMS2 client'
arch=('x86_64')
url="https://lxde.org/"
license=('GPL')
depends=('gtk2' 'gtk3' 'libnotify' 'xmms2')
makedepends=('intltool')
source=(https://downloads.sourceforge.net/lxde/$pkgbase-$pkgver.tar.xz)
md5sums=('1bbd57f5b34e13ef31152c9ecb31ef68')

build() {
  # GTK+ 2 version
  [ -d gtk2 ] || cp -r $pkgbase-$pkgver gtk2
  cd gtk2
  ./configure --prefix=/usr
  make

  cd "$srcdir"
  # GTK+ 3 version
  [ -d gtk3 ] || cp -r $pkgbase-$pkgver gtk3
  cd gtk3
  ./configure --prefix=/usr --enable-gtk3
  make
}

package_lxmusic() {
  groups=('lxde')
  depends=('gtk2' 'libnotify' 'xmms2')

  cd gtk2
  make DESTDIR="$pkgdir" install
}

package_lxmusic-gtk3() {
  groups=('lxde-gtk3')
  pkgdesc+=' (GTK+ 3 version)'
  depends=('gtk3' 'libnotify' 'xmms2')
  conflicts=('lxmusic')

  cd gtk3
  make DESTDIR="$pkgdir" install
}
