# Maintainer: David Rungel <dvzrv@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

_name=libjpeg-turbo
pkgname=lib32-${_name}
pkgver=2.0.6
pkgrel=1
pkgdesc='JPEG image codec with accelerated baseline compression and decompression'
arch=('x86_64')
url="https://libjpeg-turbo.org/"
license=('BSD')
depends=('lib32-glibc' "${_name}=${pkgver}")
makedepends=('nasm' 'cmake')
provides=('lib32-libjpeg' 'libjpeg.so' 'libturbojpeg.so')
source=("https://sourceforge.net/projects/${_name}/files/${pkgver}/${_name}-${pkgver}.tar.gz"{,.sig})
sha512sums=('504062778224f2ed2ddb64c988fbf466a14247952aab0cf1d35d281942b6f0874eafd34b45914d2208e087f05ddd896c5ba87a67c944e8bb80323e187413038c'
            'SKIP')
b2sums=('1e003b147bf5bc1fed59df1eb42c8bd83f6548f267033969a6a26830afb24ae96c8ebba8407e4d2ded76da66a535ed59e249f64ed61218055771e5db9491514f'
        'SKIP')
validpgpkeys=('0338C8D8D9FDA62CF9C421BD7EC2DBB6F4DBF434') # The libjpeg-turbo Project (Signing key for official binaries) <information@libjpeg-turbo.org>

build() {
  cd "${_name}-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib32 \
        -DCMAKE_BUILD_TYPE='None' \
        -DWITH_JPEG8=ON \
        -W no-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "${_name}-$pkgver"
  make test -C build
}

package() {
  cd "${_name}-$pkgver"

  make VERBOSE=1 DESTDIR="${pkgdir}" \
       docdir="/usr/share/doc/${_name}" \
       exampledir="/usr/share/doc/${_name}" \
       install -C build
  # remove everything that is provided by libjpeg-turbo
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  # license
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
