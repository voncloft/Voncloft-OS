# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=krename
pkgver=5.0.1
pkgrel=2
pkgdesc="A very powerful batch file renamer"
arch=(x86_64)
url="https://www.krename.net"
license=(GPL)
depends=(kio kjs exiv2 podofo taglib hicolor-icon-theme)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('caac78afd3ba06613df47bbd1570bb900acf4185547354d6eb588e70656f6a7d'
            'SKIP')
validpgpkeys=(D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heirecka@exherbo.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
