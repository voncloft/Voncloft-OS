# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=mruby
pkgver=2.1.2
pkgrel=1
pkgdesc='An interpreter for the Ruby programming language with the intention of being lightweight and easily embeddable'
arch=(i686 x86_64)
url='https://github.com/mruby/mruby'
license=(MIT)
depends=(readline)
makedepends=(bison ruby ruby-rake)
source=(mruby-$pkgver.zip::https://github.com/mruby/mruby/archive/$pkgver.zip)
sha1sums=('3dc4b5ed859447f6e8432fc0c66e4e8a53e01503')

build() {
  cd mruby-$pkgver
  make
}

check() {
  cd mruby-$pkgver
  # make test disable due to https://github.com/mruby/mruby/issues/5026
}

package() {
  cd mruby-$pkgver

  install -d "$pkgdir/usr/bin" "$pkgdir/usr/lib" "$pkgdir/usr/share/licenses/mruby"
  cp build/host/bin/* "$pkgdir/usr/bin"
  cp build/host/lib/*.a "$pkgdir/usr/lib"
  cp -r include "$pkgdir/usr"

  cp LICENSE "$pkgdir/usr/share/licenses/mruby/LICENSE"
}
