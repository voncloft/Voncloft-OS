# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@phare.normalesup.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=gmp-ecm
pkgver=7.0.4
pkgrel=3
pkgdesc='Elliptic Curve Method for Integer Factorization'
url='https://gitlab.inria.fr/zimmerma/ecm'
license=(GPL LGPL)
depends=(gmp)
arch=(x86_64)
source=("https://gitlab.inria.fr/zimmerma/ecm/-/archive/$pkgver/ecm-$pkgver.tar.gz")
sha256sums=('6fc7147988205740e9339776396a581497ba3df9431a343bc1863859d47a8bb4')

prepare() {
  cd ecm-$pkgver
  autoreconf -vi
}

build() {
  cd ecm-$pkgver
  ./configure --prefix=/usr --enable-shared --enable-openmp
  make
}

check() {
  cd ecm-$pkgver
  make check
}

package() {
  cd ecm-$pkgver
  make DESTDIR="$pkgdir" install
}
