# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=indent
pkgver=2.2.12
pkgrel=1
pkgdesc="C language source code formatting program"
arch=('x86_64')
url="https://www.gnu.org/software/indent/"
license=('GPL3')
depends=('glibc')
makedepends=('texi2html')
source=("https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
validpgpkeys=(782130B4C9944247977B82FD6EA4D2311A2D268D) # andrewsh@debian.org
sha256sums=('b745a5dfc68f86a483d7f96dc1cda7aafd1e78ecba3c7d8ad304709e91e1defb'
            'SKIP')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" docdir=/usr/share/doc/indent install
}
