# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Lone_Wolf <lonewolf at xs4all dot nl>
# Contributor: Moritz Lipp <mlq@pwmt.org>

_pkgbasename=libunwind
pkgname=lib32-libunwind
pkgver=1.4.0
pkgrel=1
pkgdesc="Portable and efficient C programming interface (API) to determine the call-chain of a program (32-bit)"
arch=('x86_64')
url="https://www.nongnu.org/libunwind/"
license=('GPL')
makedepends=('gcc-multilib')
depends=('lib32-gcc-libs' 'libunwind' 'lib32-xz')
source=(https://download.savannah.gnu.org/releases/$_pkgbasename/$_pkgbasename-$pkgver.tar.gz{,.sig}
        fix-fno-common.patch)
sha512sums=('60bce1e840f071d3a48cfa8e2acff988977956f659c5c04ce83f00751348fbbeddbfc54e8c9bd6ebc9073423ca5ee1816655dc5aa59fefd0a8edc9c3f7f3b913'
            'SKIP'
            'd8b6e9208bd2fc351eb1d24a7b2534f4e6034982afeca9e4fb3cf6deed8e8445c46aaef8ff7e891167104feef9d07789b6253b02bf4099fa948aeb28a093dd9e')
validpgpkeys=('1675C8DA2EF907FB116EB709EC52B396E6874AF2')  # Dave Watson <davejwatson@fb.com> , project admin for unwind
validpgpkeys+=('75D2CFC56CC2E935A4143297015A268A17D55FA4') # Dave Watson

prepare() {
  cd $_pkgbasename-$pkgver

  # merged in v1.5.0
  # https://github.com/libunwind/libunwind/pull/166
  patch -Np1 -i ../fix-fno-common.patch
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  
  cd $_pkgbasename-$pkgver
  ./configure \
    --build=i686-pc-linux-gnu \
    --host=i686-pc-linux-gnu \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --disable-documentation 
  make

}

check() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  # This function is ``supposed'' to fail. Upstream know, but haven't fixed it.
  make check || :
}

package_lib32-libunwind() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install
  
  # this build only provides multilib binary for x86 on x86_64 , remove all includes for other architectures
  find $pkgdir/usr/include/*.h -not -name "*x86*" -exec rm -f {} \;
}

# vim:set ts=2 sw=2 et:
