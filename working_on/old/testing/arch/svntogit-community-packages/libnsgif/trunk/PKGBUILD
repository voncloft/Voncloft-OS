# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>

pkgname=libnsgif
pkgver=0.2.1
pkgrel=4
pkgdesc='Decoding library for the GIF image file format'
arch=(x86_64)
url='https://www.netsurf-browser.org/projects/libnsgif/'
license=(MIT)
makedepends=('netsurf-buildsystem>=1.9')
source=("https://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('9eaea534cd70b53c5aaf45317ae957701685a6b4a88dbe34ed26f4faae879a4b')

build() {
  export CFLAGS="$CFLAGS -fno-strict-aliasing -w"
  make -C $pkgname-$pkgver \
    INCLUDEDIR=include \
    LIBDIR=lib PREFIX=/usr \
    COMPONENT_TYPE="lib-shared"
}

package() {
  make -C $pkgname-$pkgver \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    install
  install -Dm644 $pkgname-$pkgver/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# getver: netsurf-browser.org/projects/libnsgif/
# vim: ts=2 sw=2 et:
