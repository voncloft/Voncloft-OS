# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Veeti Paananen <veeti.paananen@rojekti.fi>

pkgname=cmark-gfm
pkgver=0.29.0.gfm.0
pkgrel=2
pkgdesc="GitHub's fork of cmark, a CommonMark parsing and rendering library and program in C"
arch=('x86_64')
url="https://github.com/github/cmark"
license=('custom:BSD2')
depends=('glibc')
makedepends=('cmake' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/github/cmark/archive/$pkgver.tar.gz"
        "$pkgname-$pkgver-fix-tests-python-3.8.patch::https://github.com/github/cmark-gfm/commit/7f64038eebb0dcdeba4d3db8e2419586a1c0f9a5.patch")
sha512sums=('54e396e035a43e4d0c86fa7f1c48a6a1283c0caaabdbc56dfcecee92e89e69f6d2e016ae2d9cf4f40258a6455fba7b813c1c6e6d37e53d33a381088ccbc5673e'
            '604e0d1fb3b5b684f5c8b1176d48e5adcd406ccddd1f569c51162b71086009736a60cc5769a787b09f8069ce1041ba549c8833d9ae52265817a44278f71990f5')

prepare() {
  mkdir -p build

  cd cmark-gfm-$pkgver
  # Fix test suite for Python 3.8 (https://github.com/github/cmark-gfm/pull/184)
  patch -Np1 -i "$srcdir/$pkgname-$pkgver-fix-tests-python-3.8.patch"
}

build() {
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib ../cmark-gfm-$pkgver
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make install DESTDIR="$pkgdir"

  install -D -m 644 "$srcdir"/cmark-gfm-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
