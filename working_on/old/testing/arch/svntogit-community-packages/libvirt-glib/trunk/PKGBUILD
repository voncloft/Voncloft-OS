# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=libvirt-glib
pkgver=3.0.0
pkgrel=2
pkgdesc="GLib bindings for libvirt"
arch=('x86_64')
url="https://libvirt.org"
license=('LGPL2.1')
depends=('libvirt' 'libcap-ng')
makedepends=('intltool' 'gobject-introspection' 'vala' 'python')
optdepends=('python: support for python')
source=("https://libvirt.org/sources/glib/$pkgname-$pkgver.tar.gz"{,'.asc'})
sha512sums=('b9e7d782c25b5c3a9b2829f1be8162352438ab3505510a1ca7dd64d741eaab158dc9206fdd4b0e3e27b0c049e4bcfc47db0c9a7fa1a84888864e23d3c35c079e'
            'SKIP')
validpgpkeys=('DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF') # Daniel P. Berrange <dan@berrange.com>

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
