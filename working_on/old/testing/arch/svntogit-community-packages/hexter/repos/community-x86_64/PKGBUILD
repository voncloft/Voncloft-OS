# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=hexter
pkgver=1.1.1
pkgrel=1
pkgdesc="A Yamaha DX7 modeling software synthesizer for the DSSI Soft Synth Interface"
arch=('x86_64')
url="https://github.com/smbolton/hexter"
license=('GPL2')
groups=('dssi-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc')
makedepends=('alsa-lib' 'dssi' 'glib2' 'gtk2' 'ladspa' 'liblo')
optdepends=('dssi-host: for DSSI plugin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/smbolton/${pkgname}/archive/version_${pkgver}.tar.gz")
sha512sums=('3de1e75df453a79d4b7eda4cf503f436201db34837b7584ea683f57c4b62029ea994eb6bc6d461e10938183b995792ded975ac112f0b9639d06b5571c93f3100')
b2sums=('32bcb1f42f9971fd4ca2406e58b7a9f735510eaad5ccc92a0aa45200c0ca468fb02698d1e548f9a6a657bbae183eabe20a200352470270042ec7b99f0238b1db')

prepare() {
  mv -v "${pkgname}-version_${pkgver}" "${pkgname}-${pkgver}"
  cd "$pkgname-$pkgver"
  ./autogen.sh
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  depends+=('libasound.so' 'libgdk-x11-2.0.so' 'libglib-2.0.so'
  'libgobject-2.0.so' 'libgtk-x11-2.0.so' 'liblo.so' 'libpango-1.0.so'
  'libpangocairo-1.0.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
