# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-pod-parser
pkgver=1.63
pkgrel=1
pkgdesc="Modules for parsing/translating POD format documents"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Pod-Parser"
depends=('perl')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/M/MA/MAREKR/Pod-Parser-$pkgver.tar.gz")
sha512sums=('55f5741bb906701705f489b2314d3f11cd6d63ca5b64a60fdbda7eed0388945f2e68262735c7cc33b7089794630393049e619f9900e85c71bf9223243bc0984a')

build() {
  cd Pod-Parser-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Pod-Parser-$pkgver
  make test
}

package() {
  cd Pod-Parser-$pkgver
  make DESTDIR="$pkgdir" install
}
