# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Rafael Fontenelle <rafaelff@gnome.org>
# Contributor:  <qqqqqqqqq9 AT web DOT de>
# Contributor: Patrick Burroughs (Celti) <celti@celti.name>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: TDY <tdy@gmx.com>

pkgname=wdiff
pkgver=1.2.2
pkgrel=4
pkgdesc="A wordwise implementation of GNU diff"
arch=('x86_64')
url="https://www.gnu.org/software/$pkgname/"
license=('GPL3')
depends=('ncurses')
source=("https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz")
sha512sums=('9272c225f4f61612197fe151cf8c913107169ac9fcd6c9f8472a0f56d9124d78a060944cea1b51489ca34311220f23638d043d43c733a3252cdbb7cceb1085ed')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir"/$pkgname-$pkgver

  make -k check
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
