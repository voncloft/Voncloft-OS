# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: cyrant <cyrant at tuta dot io>

pkgname=opentoonz
pkgver=1.4.0
pkgrel=2
pkgdesc='Software for producing a 2D animation'
arch=(x86_64)
url='https://opentoonz.github.io/e/'
license=(BSD)
depends=(cblas ffmpeg freeglut glew hicolor-icon-theme libmypaint qt5-multimedia qt5-script qt5-svg superlu)
makedepends=(boost cmake qt5-tools)
source=("$pkgname-$pkgver.tar.gz::https://github.com/opentoonz/$pkgname/archive/v${pkgver}.tar.gz"
        fix-build.patch)
sha256sums=('176caca191bf2747964ecaf8cbb6be6a738fd04b464a2ba182b5aaf96ccaefa9'
            '14c5ad66485bbbc7665068f0ee623a2cf87bb2b20a24a636753997acd708b43e')

prepare() {
  [[ -d build ]] || mkdir build

  cd $pkgname-$pkgver

  # Fix build
  patch -Np1 -i ../fix-build.patch

  # Specify path for ffmpeg
  sed -i 's|"ffmpegPath", QMetaType::QString, ""|"ffmpegPath", QMetaType::QString, "/usr/bin"|' toonz/sources/toonzlib/preferences.cpp
}

build() {
  pushd $pkgname-$pkgver/thirdparty/tiff-4.0.3
  ./configure --with-pic --disable-jbig
  make
  popd

  cd build
  cmake -G "Unix Makefiles" ../$pkgname-$pkgver/toonz/sources \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=YES
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 ../$pkgname-$pkgver/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
