# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Tom Killian <tomk@runbox.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libnewt
pkgver=0.52.21
pkgrel=5
_tclver=8.6
pkgdesc="Not Erik's Windowing Toolkit - text mode windowing with slang"
arch=(x86_64)
url='https://pagure.io/newt'
license=(GPL)
depends=(gpm libxcrypt popt slang)
makedepends=("tcl>=$_tclver" 'python' 'python2')
optdepends=('tcl: whiptcl support'
            'python: libnewt support with the _snack module'
            'python2: libnewt support with the _snack module')
options=(!makeflags)
source=("https://releases.pagure.org/newt/newt-$pkgver.tar.gz")
sha256sums=('265eb46b55d7eaeb887fca7a1d51fe115658882dfe148164b6c49fccac5abb31')

prepare() {
  cd "newt-$pkgver"

  sed -i "s:tcl8.4:tcl$_tclver:" Makefile.in
  echo '#define USE_INTERP_RESULT 1' >> config.h
}

build() {
  cd "newt-$pkgver"

  ./configure --prefix=/usr --with-gpm-support
  make
}

package() {
  make -C "newt-$pkgver" prefix="$pkgdir/usr" install
}

# vim: ts=2 sw=2 et:
