# Contributor: Paulo Matias <matias.archlinux-br.org>
# Contributor: Leslie P. Polzer <polzer.gnu.org>
# Contributor: erm67 <erm67.yahoo.it>
# Contributor: Daniel J Griffiths
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=djview
pkgver=4.10.6
pkgrel=3
pkgdesc='Portable DjVu viewer and browser plugin'
url='http://djvu.sourceforge.net/djview4.html'
license=('GPL')
arch=('x86_64')
makedepends=('qt5-tools')
depends=('qt5-base' 'djvulibre'
         'libxkbcommon-x11' 'libsm')
source=("https://downloads.sourceforge.net/djvu/${pkgname}-${pkgver}.tar.gz")
sha256sums=('8446f3cd692238421a342f12baa365528445637bffb96899f319fe762fda7c21')

conflicts=('djview4')
provides=('djview4')
replaces=('djview4')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export QMAKE=qmake-qt5
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	ln -s djview "${pkgdir}"/usr/bin/djview4
}
