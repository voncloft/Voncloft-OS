# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=liquid-dsp-sse4.1
_pkgname=liquid-dsp
pkgver=1.3.1
pkgrel=1
pkgdesc='Open-source signal processing library for software-defined radios'
arch=('x86_64')
url='http://liquidsdr.org'
license=('MIT')
depends=('glibc')
optdepends=('fftw: efficient FFT')
provides=('liquid-dsp')
conflicts=('liquid-dsp')
source=("$url/downloads/$_pkgname-$pkgver.tar.gz")
sha512sums=('6da76030fc346e3fadfe70e72be14d1f2ff198d3e9bb5ffb5f4d9b89945686d2474d3bb4911a2b923aad22feabebf2744d47afbffabab87737fe46a24af68917')

prepare() {
    sed -i 's|ax_cv_have_sse41_ext=.*|ax_cv_have_sse41_ext=yes|' $_pkgname-$pkgver/scripts/ax_ext.m4
}

build() {
    cd $_pkgname-$pkgver

    autoreconf -vif
    ./configure --prefix=/usr

    make
}

check() {
    cd $_pkgname-$pkgver

    make check
}

package() {
    cd $_pkgname-$pkgver

    make DESTDIR="$pkgdir" install

    install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$_pkgname/LICENSE
}

