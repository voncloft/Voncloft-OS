# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Santiago Torres-Arias <santiago[at]archlinux[dot]org>
# Contributor: Aaron McDaniel (mcd1992) <'aur' at the domain 'fgthou.se'>

pkgname=r2cutter
_gitcommit=410dd8bf3614ab9a191294373b37193b4726f0e7
pkgver=1.12.0
epoch=1
pkgrel=1
pkgdesc='Qt and C++ GUI for radare2 reverse engineering framework'
url='https://github.com/radareorg/r2cutter'
arch=('x86_64')
license=('GPL3')
depends=('radare2' 'capstone' 'qt5-base' 'qt5-svg' 'qt5-webengine' 'icu' 'python' 'jupyter'
         'pyside2' 'python-shiboken2' 'graphviz' 'gcc-libs')
makedepends=('git' 'cmake' 'shiboken2' 'qt5-tools')
optdepends=('r2ghidra: ghidra decompiler plugin')
replaces=('radare2-cutter')
source=("git+https://github.com/radareorg/r2cutter#commit=${_gitcommit}")
sha512sums=('SKIP')
b2sums=('SKIP')

pkgver() {
  cd r2cutter
  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd r2cutter/src
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCUTTER_ENABLE_PYTHON=ON \
    -DCUTTER_ENABLE_PYTHON_BINDINGS=ON \
    -DCUTTER_USE_BUNDLED_RADARE2=OFF \
    -DCUTTER_USE_ADDITIONAL_RADARE2_PATHS=OFF \
    -DCUTTER_ENABLE_CRASH_REPORTS=OFF \
    -DCUTTER_ENABLE_GRAPHVIZ=ON
  make -C build VERBOSE=1
}

package() {
  cd r2cutter
  install -Dm 755 src/build/Cutter -t "${pkgdir}/usr/bin"
  install -Dm 644 src/org.radare.Cutter.desktop -t "${pkgdir}/usr/share/applications"
  install -Dm 644 src/img/cutter.svg -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a docs/* "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
