# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-sshj
pkgver=1.0.68.202012201616
pkgrel=1
pkgdesc='DBeaver plugin for SSHv2 tunnels with Bouncy Castle algorithm'
arch=('any')
url='https://dbeaver.io/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver-plugin-sshj-lib')
source=("https://dbeaver.io/update/ce/latest/plugins/org.jkiss.dbeaver.net.ssh.sshj_${pkgver}.jar"
        "https://dbeaver.io/update/ce/latest/features/org.jkiss.dbeaver.net.sshj.feature_${pkgver}.jar"
        "${pkgname}.info")
sha256sums=('7d08f288b34bb8cc8937b076ac3e3912e94c3c1443664a648bf8a79606b26451'
            '3abce454eb50b03aafc69b0914c6596d5c12456f39df78e8a2ee681401736864'
            '5cb9980e9fe2d339ab005f296202ef40b7fdd5f0a9682f192c254b7b77d1b4e0')
noextract=("org.jkiss.dbeaver.net.ssh.sshj_${pkgver}.jar"
           "org.jkiss.dbeaver.net.sshj.feature_${pkgver}.jar")

package() {
  # Install plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins" \
    "org.jkiss.dbeaver.net.ssh.sshj_${pkgver}.jar"

  # Extract feature
  install -m 755 -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.net.sshj.feature_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.dbeaver.net.sshj.feature_${pkgver}.jar" \
        -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.net.sshj.feature_${pkgver}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
