# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Steffen Weber <-boenki-gmx-de->

pkgbase=otter-browser
pkgname=(otter-browser{,-nowebengine})
pkgver=1.0.02
pkgrel=1
pkgdesc="Web browser aiming to recreate the best aspects of the classic Opera (12.x) UI using Qt5"
arch=('x86_64')
url="https://otter-browser.org/"
license=('GPL3')
depends=('hunspell' 'qt5-multimedia' 'qt5-svg' 'qt5-webengine' 'qt5-webkit' 'qt5-xmlpatterns')
makedepends=('cmake')
source=($pkgbase-$pkgver.tar.gz::https://github.com/OtterBrowser/$pkgbase/archive/v$pkgver.tar.gz)
sha256sums=('d1e090a80fa736cd128f594184817078a08cac31614e85e7838ff1b64511d62d')

prepare() {
  cd $pkgname-$pkgver
  # Fix metainfo file
  # https://github.com/OtterBrowser/otter-browser/issues/1649
  sed -i 's/org.otter_browser.Otter_Browser.desktop/otter-browser.desktop/' packaging/$pkgbase.appdata.xml

  mkdir ../build{,-nowebengine}
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver -DCMAKE_INSTALL_PREFIX="/usr"
  make

  cd ../build-nowebengine
  cmake ../$pkgbase-$pkgver -DCMAKE_INSTALL_PREFIX="/usr" -DENABLE_QTWEBENGINE=OFF
  make
}

package_otter-browser() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 ../$pkgbase-$pkgver/packaging/$pkgbase.appdata.xml "$pkgdir/usr/share/metainfo/$pkgbase.appdata.xml"
}

package_otter-browser-nowebengine() {
  pkgdesc+=' without WebEngine support'
  depends=('hicolor-icon-theme' 'hunspell' 'qt5-multimedia' 'qt5-svg' 'qt5-webkit' 'qt5-xmlpatterns')
  conflicts=($pkgbase)
  provides=($pkgbase=$pkgver)

  cd build-nowebengine
  make DESTDIR="$pkgdir" install

  # Ignore package by AppStream to avoid duplicated IDs
  sed -i '/^Actions=/a X-AppStream-Ignore=true' "$pkgdir/usr/share/applications/$pkgbase.desktop"
}
