# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=permlib
pkgver=0.2.9
pkgrel=1
pkgdesc="A C++ library implementing permutation group algorithms"
arch=(any)
url="http://www.math.uni-rostock.de/~rehn/software/permlib.html"
license=(GPL2)
source=($pkgname-$pkgver::"https://github.com/tremlin/PermLib/archive/v$pkgver.tar.gz"
        permlib-boost.patch)
depends=(boost)
sha256sums=('40b9c03df57d73412d75ee4098937706d95e252b4f40d091cc13633a0c56d20e'
            '03005c01596a1a6c2ec64431b109550e7891a11e0273d0142b20816063c006f1')

prepare() {
  cd PermLib-$pkgver
  patch -p0 -i ../permlib-boost.patch # Fix build with recent boost
}

package() {
  cd PermLib-$pkgver
  mkdir -p "$pkgdir"/usr/include
  cp -r include/permlib "$pkgdir"/usr/include
}
