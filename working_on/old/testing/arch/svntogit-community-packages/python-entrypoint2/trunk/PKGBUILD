# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=entrypoint2
pkgname=python-${_pkg}
pkgver=0.2
pkgrel=3
pkgdesc="Easy to use command-line interface for python modules"
arch=(any)
url="https://github.com/ponty/entrypoint2"
license=(BSD)
depends=(python-argparse python-decorator)
makedepends=(python-setuptools)
checkdepends=(python-nose python-path.py python-easyprocess)
#PyPi tarball does not have tests, see https://github.com/ponty/entrypoint2/pull/5
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('a49b160c452914b830135201e6db466c6bb39759ccd0c301cbb7943ab57802fd')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}/tests
  PYTHONPATH="../build/lib/" nosetests -vv
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
