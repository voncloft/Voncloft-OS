# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Chad Harp <chad dot harp at mac dot com>
# Contributor: FadeMind <fademind at gmail dot com>
# Contributor: TDY <tdy at archlinux dot info>
# Contributor: Sven Salzwedel <sven_salzwedel at web dot de>
# Contributor: Olaf Leidinger <leidola at newcon dot de>

pkgname=perl-pdf-api2
_perl_namespace=PDF
_perl_module=API2
pkgver=2.037
pkgrel=1
pkgdesc="Faciliates the creation and modification of PDF files"
arch=('any')
url="https://metacpan.org/release/${_perl_namespace}-${_perl_module}"
license=('LGPL')
depends=('perl' 'perl-font-ttf')
checkdepends=('perl-test-exception' 'perl-test-memory-cycle')
source=("https://www.cpan.org/modules/by-module/${_perl_namespace}/${_perl_namespace}-${_perl_module}-${pkgver}.tar.gz")
sha512sums=('9b7f8a99416482c6b01346b8e8476e192b8426a353333f3bc516ba707b223841e822afcbcead52e13ba6074a3b4f98d6fcf770d052d75c4f7a6c78d07c0a18d9')
options=('!emptydirs')

build() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make pure_install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  # Delete unuseful files
  find "${pkgdir}" -name '.packlist' -delete
}
