# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bruno Galeotti <bgaleotti at gmail dot com>

_pkgname=requests-aws4auth
pkgname=python-requests-aws4auth
pkgver=1.0.1
pkgrel=1
pkgdesc='Amazon Web Services version 4 authentication for the Python Requests module'
url='https://github.com/sam-washington/requests-aws4auth'
arch=('x86_64')
license=('MIT')
depends=('python' 'python-requests')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=(https://github.com/sam-washington/requests-aws4auth/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('99fd0a125f2808fd8871876674004370e558b22d311925203cdc59d11ab9503f423d7adbe1dd849ed2d2c3969cb5bc5e4fb40f0e4d44f9164416837915742fd2')
b2sums=('b86eca51d01aa44d3d16bc879499876939302f9e51fc0c35684e9e2fb3805f761bfa5db5ac5ca2007f8820bfd229f86caa7c477ac4a524fc019c538c42136735')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed "s|'test/requests_aws4auth_test.py',||" -i setup.py
}

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python -m pytest -k 'not test_headers_amz_example'
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
