# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: svoufff <svoufff at gmail dot com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=gigedit
pkgver=1.1.1
pkgrel=2
pkgdesc="Gigasampler instrument editor"
arch=('x86_64')
url="https://www.linuxsampler.org/"
license=('GPL2')
groups=('pro-audio')
depends=('linuxsampler' 'gtkmm3')
makedepends=('gendesk' 'intltool' 'libxslt' 'linuxsampler')
source=("https://download.linuxsampler.org/packages/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('223702628e83e12a292f902e32baa355aca31ee82f1f8b9fed586ec9c0aaec56956d7bdc958238ec64e79b2f4c5ef9246face669b2b3645046e51d8c8289a964')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
  # creating a ld.so.conf entry for gigedit.so
  echo "/usr/lib/${pkgname}" > "${pkgname}.conf"
  # generate XDG desktop file
  gendesk -n \
          --pkgname "${pkgname}" \
          --name "${pkgname}" \
          --genericname "Instrument Editor" \
          --categories "AudioVideo;Audio"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # xdg desktop
  install -vDm 644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,NEWS,README}
  # adding gigedit.so library path to ld.so.conf (required for dependants during
  # runtime)
  install -vDm 644 "${pkgname}.conf" -t "${pkgdir}/etc/ld.so.conf.d/"
}

# vim:set ts=2 sw=2 et:
