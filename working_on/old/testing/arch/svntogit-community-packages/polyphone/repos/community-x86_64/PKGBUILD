# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=polyphone
pkgver=2.2.0
pkgrel=2
pkgdesc="A soundfont editor for quickly designing musical instruments"
arch=('x86_64')
url="https://polyphone-soundfonts.com/en/"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'openssl' 'qt5-base' 'qt5-svg'
'rtmidi' 'zlib')
makedepends=('flac' 'jack' 'libogg' 'libvorbis' 'portaudio' 'qcustomplot'
'qt5-tools' 'stk')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/davy7125/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-2.2.0-qt5.15.patch::https://github.com/davy7125/polyphone/commit/7cfa7fe96533e402dd37e533b63c3105af36cd34.patch")
sha512sums=('88b4f0d951f75ed517e0942e75d543eca28c92fe8f2f99aee1d4be69f7d4fd74ee2219d7779b68b918befc8554cac5dc3f93ac9055bd8a94327e777a0725e5e3'
            'a412b16b304ea53665ae3d8a8769898e16297c9b5a57d16cc68a71e8fbd35ee938a364de5226d9cccbff4fd0b87c9d4d10f809a8fd75325a39701d895958f220')
b2sums=('973bcc812e1e6d774ab81cc092d7018738386235b550f8493550e33b2a7e02dbbdc4ad4f6267442e1019be85393561bfbb2eaaeb9feba84b8e70020b68f5a7a0'
        'd1d72bebdbdae372154b64efa55133b9314088be28ad1a5fc7641d9f66c91b58e51decf70e0f1ac5c62be4c5322161aca984f5d7412f8cf8f0dc5cedd118051d')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix missing include required to build against qt >= 5.15.0
  patch -Np1 -i "../${pkgname}-2.2.0-qt5.15.patch"
}

build() {
  cd "${pkgname}-${pkgver}/sources"
  qmake-qt5 "${pkgname}.pro" PREFIX=/usr
  make
}

package() {
  depends+=('libFLAC.so' 'libjack.so' 'libogg.so' 'libportaudio.so'
  'libqcustomplot.so' 'libstk-4.6.1.so' 'libvorbis.so' 'libvorbisenc.so'
  'libvorbisfile.so')
  cd "${pkgname}-${pkgver}/sources"
  # doesn't have an install target: https://github.com/davy7125/polyphone/issues/62
  install -vDm 755 "bin/${pkgname}" -t "${pkgdir}/usr/bin/"
  install -vDm 644 "resources/logo.svg" \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  install -vDm 644 "resources/${pkgname}.png" \
    -t "${pkgdir}/usr/share/icons/hicolor/512x512/apps"
  install -vDm 644 "contrib/${pkgname}."*1 \
    -t "${pkgdir}/usr/share/man/man1/"
  install -vDm 644 "contrib/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 "contrib/${pkgname}.xml" \
    -t "${pkgdir}/usr/share/mime/packages/"
  install -vDm 644 {../README.md,changelog} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
