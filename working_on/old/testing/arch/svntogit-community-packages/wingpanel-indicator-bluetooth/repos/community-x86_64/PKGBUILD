# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-bluetooth
pkgver=2.1.6
pkgrel=1
pkgdesc='Wingpanel Bluetooth Indicator'
arch=(x86_64)
url=https://github.com/elementary/wingpanel-indicator-bluetooth
license=(GPL3)
groups=(pantheon)
depends=(
  bluez
  glib2
  gtk3
  libgranite.so
  libwingpanel-2.0.so
)
makedepends=(
  git
  meson
  vala
  wingpanel
)
_tag=3766c1b5e7eb29b8445194f1bd84b0286ad8e256
source=(git+https://github.com/elementary/wingpanel-indicator-bluetooth.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd wingpanel-indicator-bluetooth

  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-bluetooth build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
