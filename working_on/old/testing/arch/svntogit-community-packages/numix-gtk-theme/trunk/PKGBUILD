# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Diego <cdprincipe@gmail.com>

pkgname=numix-gtk-theme
pkgver=2.6.7
pkgrel=1
pkgdesc='A flat and light theme with a modern look (GNOME, Openbox, Unity, Xfce)'
arch=('any')
url='https://github.com/numixproject/numix-gtk-theme'
license=('GPL3')
makedepends=('gdk-pixbuf2' 'ruby-bundler')
optdepends=('gtk-engine-murrine: for gtk2 theme')
replaces=('numix-themes')
_commit='f9b4a5ba3cc08eb649053e9e0d9ce0f2026675ff'
source=("numix-gtk-theme-${pkgver}.tar.gz::https://github.com/numixproject/numix-gtk-theme/archive/${pkgver}.tar.gz"
        "Gemfile")
sha256sums=('4590c6077828d73aecbc2521acf5b8d4669aacff1e9d09575888be67f2dc0d1b'
            'b1061ba7ce09c9eb3e41f59362aa8c21d27073b0658f13fb5eabdaea654afe2c')

build() {
  cd numix-gtk-theme-${pkgver}

  bundle install --path .
  make SASS="bundle exec sass"
}

package() {
  cd numix-gtk-theme-${pkgver}

  make SASS="bundle exec sass" DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
