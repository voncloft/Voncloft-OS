# Maintainer: kpcyrd <git@rxv.cc>

pkgname=diesel-cli
pkgver=1.4.5
pkgrel=1
pkgdesc="CLI for the Diesel crate"
arch=('x86_64')
url="https://diesel.rs/"
license=('MIT' 'Apache')
replaces=('diesel_cli')
depends=('sqlite' 'postgresql-libs' 'libmariadbclient')
makedepends=('cargo')
source=(https://github.com/diesel-rs/diesel/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('4b4b35e89e8dae8f88c4b071951d904c40139390319740af6fd751de0b6c2cfd8179787572b965dfa9588536c8f3016272aacaf77e7283761659c53d415ab74e')
b2sums=('78b559b3c84a3d3d6b60b310bda9d8061ef3f229c532b01ef78887398af4b40035349b773910b369260d49fc924d42711ec026d3f14185b411d5613aa7354eb1')

build() {
  cd "diesel-${pkgver}/diesel_cli"
  # --locked is broken
  RUSTFLAGS="--cap-lints allow" cargo build --release
}

check() {
  cd "diesel-${pkgver}/diesel_cli"
  # tests require a debug build to be present
  RUSTFLAGS="--cap-lints allow" cargo test --no-default-features --features sqlite
}

package() {
  cd "diesel-${pkgver}"
  install -Dm755 "target/release/diesel" "${pkgdir}/usr/bin/diesel"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/diesel" completions bash > "${pkgdir}/usr/share/bash-completion/completions/diesel"
  "${pkgdir}/usr/bin/diesel" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_diesel"
  "${pkgdir}/usr/bin/diesel" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/diesel.fish"

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
