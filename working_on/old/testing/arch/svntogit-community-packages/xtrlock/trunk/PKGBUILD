# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Michal Hybner <dta081@gmail.com>

pkgname=xtrlock
pkgver=2.14
pkgrel=1
pkgdesc='Minimal X display lock program'
url='https://packages.debian.org/sid/xtrlock'
arch=('x86_64')
license=('GPL3')
depends=('glibc' 'libx11' 'libxcrypt' 'libcrypt.so')
source=(http://ftp.debian.org/debian/pool/main/x/xtrlock/xtrlock_${pkgver}.tar.xz)
sha512sums=('d46e2cf72e607ec57b6a4a22425b96aff28bcf089bc7612d5f45d7852830b2ee2a36eb8739560faa15d3baeb21c545e68e4f1b3a0cdc6bcb0a33948900e47e73')
b2sums=('625784059921725d5519c4bac4a9c455a896d14ad185aa2e39db863ebaf8c3d1111053e34d1f900335df6eea758b830d4681e88231e926800ad79d811bc879e9')

build() {
  cd ${pkgname}-${pkgver}
  make -f Makefile.noimake \
    CFLAGS="${CFLAGS} -DSHADOW_PWD" \
    LDLIBS="-lX11 -lcrypt" \
    xtrlock
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 4755 xtrlock -t "${pkgdir}/usr/bin"
  install -Dm 644 xtrlock.man "${pkgdir}/usr/share/man/man1/xtrlock.1"
}

# vim: ts=2 sw=2 et:
