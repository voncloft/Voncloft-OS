# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Jonathan Conder <jonno dot conder at gmail dot com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Nathan Jones <nathanj@insightbb.com>
# Contributor: Javier "Phrodo_00" Aravena <phrodo.00 at gmail dot com>
# Contributor: angvp <angvp at archlinux dot us>
# Contributor: Allan <mcrae_allan at hotmail dot com>
# Contributor: w0rm <w0rmtux at gmail dot com>
# Contributor: vEX <vex at niechift dot com>
# Contributor: Asher256 <achrafcherti at gmail dot com>

pkgname=desmume
pkgver=0.9.11
pkgrel=7
pkgdesc="Nintendo DS emulator"
arch=('x86_64')
url="https://desmume.org/"
license=('GPL')
depends=('desktop-file-utils' 'libpcap' 'soundtouch' 'alsa-lib' 'glu' 'sdl' 'gtk2' 'zziplib')
makedepends=('intltool' 'mesa' 'clang')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        gcc6_fixes.patch
        gcc7_fixes.patch)

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix build
  patch -Np1 -i ../gcc6_fixes.patch
  patch -Np1 -i ../gcc7_fixes.patch

 
  # See https://bugs.archlinux.org/task/35086 and
  # https://sourceforge.net/tracker/?func=detail&aid=3612768&group_id=164579&atid=832291
  sed -i 's/@GETTEXT_PACKAGE@/desmume/' po/Makefile.in.in
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --enable-wifi --enable-openal
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

md5sums=('269b5d4ddc5715720469a9d0efc53044'
         '7887bff9ca494999f27cbd08bf54ce41'
         '02bfa83f96cee77d8a349dfd8ce56d5a')
