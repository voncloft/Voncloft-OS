# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>

pkgname=qjackctl
pkgver=0.9.1
pkgrel=1
pkgdesc="A Qt front-end for the JACK low-latency audio server"
url="https://qjackctl.sourceforge.io"
arch=('x86_64')
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('alsa-lib' 'cmake' 'jack2' 'jack2-dbus' 'qt5-tools' 'portaudio')
optdepends=('jack2-dbus: for JACK dbus support')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('7d3a6efdeedff9335897f9d943dd51d13bf7c48d55ee2bf31d53fe685cef88e281133c164cf6cdb95669b9fe394eb06d3544a78b9d760f4420a9acb3c800c612')
b2sums=('1ae68d3b61591bb31f7c8c31f35f8a42f0048cf34c49ff0eadca9155c1a770d9880728582f6b7abdaf27a28ccb3d6a20ca87e3a6178b090579b6ead811f091f0')


build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libasound.so' 'libjack.so' 'libportaudio.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO,TRANSLATORS}
}
