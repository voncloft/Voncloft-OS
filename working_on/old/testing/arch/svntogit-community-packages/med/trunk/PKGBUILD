# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Guilhem Saurel <guilhem@saurel.me>
# Contributor: Michel Zou
# Contributor: Oliver Goethel

pkgname=med
pkgver=4.1.0
pkgrel=3
pkgdesc='Generic pre- and post-processing platform for numerical simulation'
url="https://www.salome-platform.org/downloads"
license=(LGPL)
arch=(x86_64)
depends=(hdf5 python tk)
makedepends=(cmake gcc-fortran ninja swig)
source=("https://files.salome-platform.org/Salome/other/$pkgname-$pkgver.tar.gz"
        'https://gist.githubusercontent.com/jedbrown/527ef81ff59a0dccf833da40fdd15a47/raw/7482f9a42fdd277f5f9cf0167ea7a3381032ca7a/hdf5-1.12.patch')
sha256sums=('847db5d6fbc9ce6924cb4aea86362812c9a5ef6b9684377e4dd6879627651fce'
            '82d28b6ae2ee6ceb3c6de9acb861a49741e2e759c7c556217f7d284216788206')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../hdf5-1.12.patch
  sed -i 's/if H5_VERS_MINOR > 10/if 0/g' src/ci/MEDfileCompatibility.c
}

build() {
  mkdir build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMEDFILE_BUILD_PYTHON=ON \
    -DMEDFILE_BUILD_TESTS=OFF \
    -DMEDFILE_INSTALL_DOC=OFF \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
