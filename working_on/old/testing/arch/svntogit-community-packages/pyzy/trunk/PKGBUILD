# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kerrick Staley <mail@kerrickstaley.com>

pkgname=pyzy
pkgver=1.0
_commit="d7747466562cb8b4bc2934708e29b7643c7bedbc"
pkgrel=5
pkgdesc='The Chinese PinYin and Bopomofo conversion library'
arch=('x86_64')
url='https://github.com/pyzy/pyzy'
license=('LGPL')
depends=('glib2' 'sqlite' 'util-linux')
makedepends=('git' 'doxygen' 'gnome-common' 'python2')
source=("git://github.com/pyzy/pyzy.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd $pkgname

  # replace python with python2; see https://github.com/hsumita/libpyzy/issues/1
  find . -name '*.py' -exec sed -ri '1s#(bin/|env )python#\1python2#' '{}' \;

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname"

  make DESTDIR="$pkgdir" install
}
