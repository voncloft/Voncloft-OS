# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=shim
pkgver=15
pkgrel=5
pkgdesc="EFI preloader (unsigned EFI binaries)"
arch=('any')
url="https://github.com/rhboot/shim"
license=('BSD')
makedepends=('git' 'gnu-efi')
source=("git+https://github.com/rhboot/shim#tag=${pkgver}?signed")
md5sums=('SKIP')
validpgpkeys=('B00B48BC731AA8840FED9FB0EED266B70F4FEF10') # Peter Jones <pjones@redhat.com>

prepare() {
  cd "$pkgname"
  # -Werror, not even once
  sed -e 's/-Werror //g' -i Makefile Make.defaults
}

build() {
  cd "$pkgname"
  # TODO: evaluate setting DEFAULT_LOADER to e.g. loader.efi:
  # this would require changing grub's default, but would allow us to unify
  make EFI_PATH='/usr/lib/' EFIDIR='ARCH' ENABLE_HTTPBOOT='1'
}

package() {
  cd "$pkgname"
  make DATATARGETDIR="/usr/share/${pkgname}" DESTDIR="$pkgdir/" install-as-data
  install -vDm 644 COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {BUILDING,README,README.{fallback,tpm},TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
