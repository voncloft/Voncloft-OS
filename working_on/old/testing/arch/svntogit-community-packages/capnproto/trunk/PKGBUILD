# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributors: Dave Reisner <dreisner@archlinux.org>
#               Matthias Blaicher <matthias@blaicher.com>
#               Severen Redwood <severen@shrike.me>

pkgname=capnproto
pkgver=0.8.0
pkgrel=1
pkgdesc="Cap'n Proto serialization/RPC system"
arch=('x86_64')
url='https://capnproto.org/'
license=('MIT')
depends=('gcc-libs' 'glibc' 'zlib')
makedepends=('cmake')
provides=('libcapnp.so' 'libcapnp-json.so' 'libcapnp-rpc.so' 'libcapnpc.so'
'libkj-async.so' 'libkj-gzip.so' 'libkj-http.so' 'libkj-test.so' 'libkj.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('a3b87d47f4c160c3c72304539c3d2a6836488a50d1014c9f0eb95c112dc1e0f85b3158d26c248eff464205388ea61733c45ba10dc5441e5dd04064ba2c7970e6')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_SHARED_LIBS=ON \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CONTRIBUTORS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
# vim:set ts=2 sw=2 et:
