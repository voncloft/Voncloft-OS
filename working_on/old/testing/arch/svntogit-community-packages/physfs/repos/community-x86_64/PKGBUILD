# Maintainer:
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=physfs
pkgver=3.0.2
pkgrel=2
pkgdesc="A library to provide abstract access to various archives"
arch=('x86_64')
url="https://icculus.org/physfs/"
license=('ZLIB')
depends=('zlib')
makedepends=('cmake' 'doxygen')
source=(https://icculus.org/physfs/downloads/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('6a15c458898d0570101d8f173201cde9ced78df7')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPHYSFS_BUILD_TEST=OFF
  make -C build all docs
}

package() {
  make DESTDIR="${pkgdir}" install -C build
  install -d "${pkgdir}"/usr/share/{doc/physfs,man/man3}
  install -m644 build/docs/html/* "${pkgdir}/usr/share/doc/physfs"
  install -m644 build/docs/man/man3/* "${pkgdir}/usr/share/man/man3"

  for i in author Deinit deprecated description extension Free Init major Malloc minor opaque patch Realloc remove url ; do
    mv "${pkgdir}"/usr/share/man/man3/{,PHYSFS_}$i.3
  done

  install -D -m644 ${pkgname}-${pkgver}/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
