# Maintainer: David Runge <dvzrv@archlinux.org>

_name=geographiclib
pkgname=python-geographiclib
pkgver=1.50
pkgrel=3
pkgdesc="Python implementation of the geodesic routines in GeographicLib"
arch=('any')
url="https://geographiclib.sourceforge.io/1.50/python/"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
# author contacted via mail about missing LICENSE file in pypi sdist
# (will hopefully be included in upcoming version)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"
        "https://geographiclib.sourceforge.io/html/LICENSE.txt")
sha512sums=('134553b0b2fec49004b0ae1cc54a0f8cb923f5c396e4d2b6b1e35c40aa9627e633abed7e994270dd6a0f22f89f0387fd7badd8c121bb1ad7ce4455f65a6ec864'
            'b21d3a82056d8dbb55565cce737d4021092c17c2d924e0462147101e8a2a9e2f357dc77cb6091820fb0ab2f2135130dc3946a512c46e6914f6eaa88b4754b2a8')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 ../LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
