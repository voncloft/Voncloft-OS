# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Eric Bélanger <eric@archlinux.org>

pkgname=bluefish
pkgver=2.2.12
pkgrel=2
pkgdesc='A powerful HTML editor for experienced web designers and programmers'
arch=('x86_64')
url='http://bluefish.openoffice.nl/'
license=('GPL3')
depends=('gtk3' 'enchant' 'python')
makedepends=('gucharmap' 'intltool')
optdepends=('gucharmap: for using the charmap plugin')
install=$pkgname.install
source=("https://www.bennewitz.com/$pkgname/stable/source/$pkgname-$pkgver.tar.bz2"{,.sig})
validpgpkeys=(64979277BAFF2D4CB637AC3B291C63A6B78DFBA1) # Olivier Sessink <olivier@bluefish.openoffice.nl>
sha512sums=('f0eb6496d371db8a9b4aee33ddf67e5e87bcc86ff283704cac115be7f00609b1ccbd47ff2d8f8b0bc30f234a339069cc1f7cb1f8809d97f94f0b0e09e4f16754'
            'SKIP')

prepare() {
  cd $pkgname-$pkgver

  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-spell-check \
    --disable-update-databases \
    --disable-xml-catalog-update

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
