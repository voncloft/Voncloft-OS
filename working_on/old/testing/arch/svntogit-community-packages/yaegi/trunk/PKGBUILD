# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: navigaid <navigaid@gmail.com>
pkgname=yaegi
pkgver=0.9.6
pkgrel=1
pkgdesc='Yet another Elegant Go Interpreter'
arch=('x86_64')
url='https://github.com/containous/yaegi'
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/containous/yaegi/archive/v${pkgver}.tar.gz")
sha512sums=('b3bd7b82dd5605205d991e1f7fa10541420e98845b0b9cd773fe70acfb5db6eb76d4cec97783eac9ca5ad36c15da2ad73d36533055da94af910e149dad1cc109')

prepare() {
  export GOPATH="${srcdir}"
  mkdir -p src/github.com/containous "${GOPATH}/bin"
  mv "${pkgname}-${pkgver}" "src/github.com/containous/${pkgname}"
  export PACKAGE_ROOT="${GOPATH}/src/github.com/containous/${pkgname}"
}

build() {
  cd "${PACKAGE_ROOT}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build ./cmd/yaegi
}

#check() {
#  cd "${PACKAGE_ROOT}"
#  go test ./...
#}

package() {
  cd "${PACKAGE_ROOT}"
  install -Dm755 yaegi "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
