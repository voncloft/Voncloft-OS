# Maintainer: David Runge <dave@sleepmap.de>
pkgname=jconvolver
pkgver=1.0.3
pkgrel=2
pkgdesc="A real-time convolution engine"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL2')
groups=('pro-audio')
depends=('clthreads' 'jack' 'libsndfile' 'zita-convolver')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2"
        "https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-reverbs.tar.bz2")
sha512sums=('9b537e2537cccb1d637fa677b098e2939a61c142d48c63006454fc2b4a27fb65e8b228b743411a2861583fd737286e4bffc62910e07157a394e9512808b8b462'
            'aa79dab5cb5326e6eddbf99a3cfae97cf5052db9d75c74346bf70f1b210db5576e31cff70c3d6dc8ff88b8eff74e4163a770fd537db978c2b955855f8b2dfc7b')

prepare() {
  cd "$pkgname-$pkgver"
  # removing optimization
  sed -e '/march=native/d' -i source/Makefile
}

build() {
  cd "$pkgname-$pkgver/source"
  make
}

package() {
  cd "$pkgname-$pkgver/source"
  make DESTDIR="$pkgdir/" PREFIX=/usr install
  # docs
  install -vDm 644 ../{AUTHORS,README,README.CONFIG} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # configs and examples
  cd ../config-files
  for folder in $(ls |grep -vE "(.conf|.wav)"); do
    install -vDm 644 "$folder/"* \
      -t "${pkgdir}/usr/share/doc/${pkgname}/examples/${folder}/"
  done
  install -vDm 644 *.conf -t "${pkgdir}/usr/share/doc/${pkgname}/examples/"
  # reverbs
  cd "${srcdir}/reverbs"
  for folder in $(ls |grep -vE "(.conf|.wav)"); do
    install -vDm 644 "$folder/"* \
      -t "${pkgdir}/usr/share/doc/${pkgname}/examples/${folder}/"
  done
  install -vDm 644 *.wav \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples/demo-reverbs/"
}
