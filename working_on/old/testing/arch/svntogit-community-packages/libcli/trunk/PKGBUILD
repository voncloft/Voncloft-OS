# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Rudy Matela <rudy@matela.com.br>
# Contributor: Mike Roberts <noodlesgc@gmail.com>

pkgname=libcli
pkgver=1.10.4
pkgrel=1
pkgdesc='Library for including a Cisco-like command-line interface into other software'
url='https://github.com/dparrish/libcli'
arch=('x86_64')
license=('LGPL2.1')
depends=('glibc' 'libxcrypt' 'libcrypt.so')
provides=('libcli.so')
source=(https://github.com/dparrish/libcli/archive/V${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('cd278f8ac5ac2594fe19c89309398ee2acbb6bf6db66d22accc3e640d1dfd548e8e6b6cfd373148a595dcd556f5adcdab83bb2e5bd0d35cc188ff614d14a600b')
b2sums=('ce834e5d06edcc50d11a2d69f1937b34dd59ff811f356452f3055c72307783d61b4bbd498a687d1bc5c9a5fd081e4f0826c62a55807696463a03b068277d6f44')

prepare() {
  sed -i 's/-Werror//' ${pkgname}-${pkgver}/Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
}

# vim: ts=2 sw=2 et:
