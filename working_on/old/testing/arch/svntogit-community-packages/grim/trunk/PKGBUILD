# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: emersion <contact@emersion.fr>

pkgname=grim
pkgver=1.3.1
pkgrel=1
pkgdesc='Screenshot utility for Wayland'
arch=(x86_64)
url='https://github.com/emersion/grim'
license=(MIT)
depends=(libjpeg-turbo pango wayland wayland-protocols)
makedepends=(git meson ninja scdoc)
source=("git+$url#commit=fe8993533ed245c468b5122d7394154066d02c86") # tag: v1.3.1
sha256sums=('SKIP')

build() {
  arch-meson build $pkgname
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname/LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/emersion/grim/releases/latest
# vim: ts=2 sw=2 et
