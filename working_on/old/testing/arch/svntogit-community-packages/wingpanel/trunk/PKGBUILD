# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel
pkgver=2.3.2
pkgrel=2
pkgdesc='The Pantheon Top Panel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgala.so
  libgee
  libgranite.so
  mutter6
)
makedepends=(
  cmake
  git
  gala
  meson
  vala
)
provides=(libwingpanel-2.0.so)
_tag=4c359a3e0d418d097daa57487b399d977292ba87
source=(git+https://github.com/elementary/wingpanel.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd wingpanel

  git describe --tags
}

build() {
  arch-meson wingpanel build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
