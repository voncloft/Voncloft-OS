# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: DrZaius <lou[at]fakeoutdoorsman.com>

pkgname=libltc
pkgver=1.3.1
pkgrel=3
pkgdesc="Linear/Logitudinal Time Code (LTC) Library"
arch=('x86_64')
url="https://x42.github.io/libltc/"
license=('GPL3')
depends=('glibc')
makedepends=('doxygen')
provides=('libltc.so')
source=("https://github.com/x42/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e4ba96560f04155d022d2c975fa36d4bb77efb9a877344041e1bd12bd5e7d04dccb452f3a4e061aba961bd0c3c0d1d53dc98ac469e04e61a934feeedc20e09b3')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
  doxygen
}

check(){
  cd "${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -vDm 644 {AUTHORS,ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 doc/html/*.{css,html,js,png} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/html"
}
