# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-deferredimport
pkgver=4.3.1
pkgrel=6
pkgdesc="Allows you to perform imports names that will only be resolved when used in the code"
arch=('any')
url="https://github.com/zopefoundation/zope.deferredimport"
license=('ZPL')
depends=('python-setuptools' 'python-zope-proxy')
checkdepends=('python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.deferredimport/archive/$pkgver.tar.gz")
sha512sums=('1c367def232ec79ad10a9d8f1e068598d2fdb49b067c2c3f44c9ad25c198afbbf2d52659c3795aad76f8c82d212e49086b81ab1c1dcc6aac2e3ce66c35ff5750')

build() {
  cd "$srcdir"/zope.deferredimport-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.deferredimport-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.deferredimport-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
