# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-device-modem
pkgver=1.57
pkgrel=4
pkgdesc="Perl extension to talk to modem devices connected via serial port"
arch=('any')
url="https://search.cpan.org/dist/Device-Modem/"
license=('GPL' 'PerlArtistic')
depends=('perl-device-serialport')
makedepends=()
provides=('perl-device-modem-log-file=2.10.0' 'perl-device-modem-log-syslog=2.10.0'
	  'perl-device-modem-usrobotics=1.50.0')
options=(!emptydirs)
source=("https://search.cpan.org/CPAN/authors/id/C/CO/COSIMO/Device-Modem-$pkgver.tar.gz")
md5sums=('3477a29915990b6aad8c977d57e4a068')

build() {
  cd  "$srcdir"/Device-Modem-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/Device-Modem-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
