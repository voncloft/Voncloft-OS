# Maintainer:
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: K. Piche <kpiche@rogers.com>

pkgname=avfs
pkgver=1.1.3
pkgrel=1
pkgdesc='Virtual filesystem that allows browsing compressed files'
arch=('x86_64')
license=('GPL' 'LGPL')
url='http://avf.sourceforge.net/'
depends=('fuse2' 'perl' 'xz' 'zstd')
options=('!emptydirs')
source=("https://downloads.sourceforge.net/sourceforge/avf/$pkgname-$pkgver.tar.bz2")
sha512sums=('8e87655cbdb64f0cbbcc399142957ea9e04a998953cc64def9c8bb338b46abffc85a35e09b5fa52bb9ddcf56e27c4f62503050d32c8201eaad61c54e22bdd005')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --enable-library --enable-fuse --disable-static
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
