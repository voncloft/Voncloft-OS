# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Moritz Lipp <mlq@pwmt.org>
# Contributor: fordprefect <fordprefect@dukun.de>
# Contributor: Thor77 <thor77@thor77.org>

pkgname=radicale
_name=Radicale
pkgver=3.0.6
pkgrel=2
pkgdesc='Simple calendar (CalDAV) and contact (CardDAV) server'
arch=('any')
url="https://radicale.org/"
license=('GPL3')
depends=('python-bcrypt' 'python-dateutil' 'python-defusedxml' 'python-passlib'
'python-setuptools' 'python-vobject')
checkdepends=('python-pytest' 'python-pytest-runner' 'python-waitress')
backup=('etc/radicale/config'
        'etc/radicale/rights'
        'etc/radicale/users')
install="${pkgname}.install"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Kozea/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-sysusers.conf"
        "${pkgname}-tmpfiles.conf"
        "${pkgname}.service")
sha512sums=('e9741547395fae8886ad84b6807422ebb196f5293d484f5f6136498058576cff697e28c117216f151d56494af83593347ceee40a6efa21272b803d0f301a396d'
            '56dffb66e018cfbf158dc5d8fe638b3cb31229945f659aae5623f219bcd1d68ddc375f1633fa8e857a9b2f50c9e05a06efce165370137d6e116a4f187466637f'
            '9d0dd88e4a34e9f97abda1785698e4b2a5e8202063deeb91b84e13c05e00b07e45b8d4d9eca09b9241b1138bbbfdc999dba0135c18f5bc0c08d65b0cd83b367b'
            '6f411daf18fbeeb7cc8626652f4b87ac6ec5e4ec1212821c426de711c907be41ab995d5b35be4ff0d663edb1028f99d6c07a53158acf519e7560e230c022c986')
b2sums=('fa4b70c9920d518df6c939395eca857c237a75218b90ada45564f2d84266d65df12898e4efbb52905829948061e10e72b5442943fd5061a28447330ae8d491e8'
        'b3af60e144ef857e42ec672e806e9600265ab7d2ea4a75011de9ab56918a008437afdacb301df210b54424fb7ff1e9a332831c67b2e58fd6bc0a0aa1eebe8909'
        '41916d62f5e3f1060bd21db0722abe837754a4cb915af218c904dafac4b06794f8fde2e34486fb7392777b4738502f3df4c1390b835050045337585b064e23bb'
        '5ae0e87d4235a864ca482a6701e0631cdaccc3dceef71237d5bd708be08c3b7e1890793d01f8c51eaa108a097cfefbb31abb71cf69b195c0f50f95720965391f')

prepare() {
  mv -v ${_name}-${pkgver} ${pkgname}-${pkgver}
  cd ${pkgname}-${pkgver}
  # removing flake8, isort and coverage
  sed -e '/addopts/d' -i setup.cfg
  touch "users"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd "${pkgname}-${pkgver}"
  mkdir -vp testdir
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="testdir"
  export PYTHONPATH="testdir/usr/lib/python${python_version}/site-packages:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  # config
  install -vDm 644 {config,rights,users} -t "${pkgdir}/etc/${pkgname}/"
  # wsgi
  install -vDm 644 "${pkgname}.wsgi" \
    -t "${pkgdir}/usr/share/${pkgname}/"
  # systemd service
  install -vDm 644 "${srcdir}/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  # sysusers.d
  install -vDm 644 "${srcdir}/${pkgname}-sysusers.conf" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  # tmpfiles.d
  install -vDm 644 "${srcdir}/${pkgname}-tmpfiles.conf" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  # docs
  install -vDm 644 {NEWS,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
