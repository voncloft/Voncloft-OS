# Maintainer: David Runge <dave@sleepmap.de>
pkgname=blop.lv2
pkgver=1.0.0
pkgrel=5
pkgdesc="A port of the BLOP LADSPA plugins by Mike Rawes to LV2."
arch=('x86_64')
url="https://git.drobilla.net/cgit.cgi/blop.lv2.git/about/"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs')
makedepends=('lv2' 'waf')
source=("https://git.drobilla.net/cgit.cgi/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.bz2"
        "autowaf.py::https://git.drobilla.net/cgit.cgi/autowaf/plain/extras/autowaf.py?id=8280f9de69f93624896b8875caf039066cac0314"
        "wscript::https://git.drobilla.net/cgit.cgi/blop.lv2.git/plain/wscript?id=e76724d58c39a52e3f47dbff40c011c54d9a0fdb"
        "lv2.py::https://git.drobilla.net/cgit.cgi/autowaf/plain/extras/lv2.py?id=29d4d293d23759b39e152241ab3669583afbdeca")
sha512sums=('f1e6a1aadf733f86faa57e827002dcab6afea428258ba20bc8ea62f5f53ce4a1341e3e534b9b3f169df1de1b39f992f40f30d18447dc344e593af6a733da013a'
            '8b657954fc435b1c8768c877e9f5ffb5a8ef3cbfa1b63ad841f1ae1442da97b10557740b73f736bb57c05b0e85592b3f866ca1b456c66fbc9092e23bdcd58756'
            '3450104b12d0752bb39732bf95aa2eeed40971fd7d8a555d3b79ccfc41006182a86d00ee2bce22679b181e5af3bb83f29fc3c8a6421542fd05d47fbef9bf2532'
            '3824eb314ccb5dd8ea9bb4fb6dfabf033b2ccb97593000d1968ee5ff7bc7654ab682c45f5d89f535eb68ccfd0da8026fa7928b4a6b56228b6365d4592d40d040')

prepare() {
  cd "$pkgname-$pkgver"
  # copy more current versions of custom waf scripts in place
  mkdir -pv tools
  cp -av ../{autowaf,lv2}.py tools/
  touch __init__.py
  # copy more current version of wscript in place
  cp -av ../wscript .
  # modify wscript to use current scripts and fix version
  sed -e 's/waflib.extras/tools/' \
      -e "s/load('lv2'/load('lv2', tooldir='tools'/" \
      -e "s/load('autowaf'/load('autowaf', tooldir='tools'/" -i wscript
  sed -E "s|(BLOP_VERSION = ) .+|\1 '${pkgver}'|" -i wscript
}

build() {
  cd "$pkgname-$pkgver"
  waf configure --prefix=/usr
  waf
}

package() {
  cd "$pkgname-$pkgver"
  waf install --destdir="$pkgdir/"
  install -vDm 644 {AUTHORS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
