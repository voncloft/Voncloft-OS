# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=cpulimit
pkgver=0.2
pkgrel=2
epoch=1
pkgdesc="Limit cpu usage of a process in percentage. Actually sends SIGSTOP/SIGCONT POSIX signals to processes"
arch=('x86_64')
url="https://github.com/opsengine/cpulimit"
license=('GPL')
depends=('glibc')
source=($pkgname-$pkgver.tar.gz::https://github.com/opsengine/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('64312f9ac569ddcadb615593cd002c94b76e93a0d4625d3ce1abb49e08e2c2da')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/#ifdef __APPLE__ || __FREEBSD__/#if defined(__APPLE__) || defined(__FREEBSD__)/' \
    src/cpulimit.c tests/process_iterator_test.c
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 src/cpulimit "$pkgdir/usr/bin/cpulimit"
}
