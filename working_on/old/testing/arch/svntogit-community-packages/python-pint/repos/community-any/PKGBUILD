# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Matthew Sloan matthew@sloan.cc

pkgname=python-pint
pkgver=0.16.1
pkgrel=3
pkgdesc="A unit library for Python"
arch=('any')
license=('BSD')
url="https://pint.readthedocs.org"
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-runner' 'python-numpy' 'python-uncertainties')
source=("https://pypi.io/packages/source/P/Pint/Pint-$pkgver.tar.gz")
sha512sums=('7d18ff7a7e85ff98350347a487be62513b100e4adba701a8e5f27f6db89618931d0f62b67be42bda4af097f5f01830baa5a6b50a537924287214d0d954da5322')

prepare() {
  cd Pint-$pkgver
  sed -i 's/pkg_resources.extern.//' pint/quantity.py
}

check() {
  cd Pint-$pkgver
  python setup.py pytest
}

package() {
  cd Pint-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
