# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=aj-snapshot
pkgver=0.9.9
pkgrel=2
pkgdesc="Command line utility to store/restore ALSA and/or JACK connections to/from an XML file"
arch=('x86_64')
url="http://aj-snapshot.sourceforge.net/"
license=('GPL3')
groups=('pro-audio')
depends=('jack' 'mxml')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('551810bdce2679441264347aea9347a7980cd4fefeed5d6e19fbc9811da3249484429cd44eba7c7bfbbcf97a59a83fb2d983101d488a82ddf2caa432f0eb31f2')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,ChangeLog,README}
}

# vim:set ts=2 sw=2 et:
