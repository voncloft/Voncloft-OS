# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Mantas MikulÄ—nas <grawity@gmail.com>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Ryan Corder <ryanc@greengrey.org>

pkgname=pam-krb5
pkgver=4.9
pkgrel=1
pkgdesc="PAM module providing Kerberos v5 support"
arch=('x86_64')
url="https://www.eyrie.org/~eagle/software/pam-krb5/"
license=('custom')
depends=('krb5' 'pam')
source=(https://archives.eyrie.org/software/kerberos/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('c5936d7beb8f98a38f6c03467aae71e4200a3ff39b20b3e70da7f03f6535ff4f'
            'SKIP')
validpgpkeys=('E784364E8DDE7BB370FBD9EAD15D313882004173') # Russ Allbery <eagle@eyrie.org>

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-reduced-depends --libdir=/usr/lib

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
