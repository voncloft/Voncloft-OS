# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-alecthomas-units
pkgver=0.0.20151022
_commit=2efee857e7cfd4f3d0138cc3cbb1b4966962b93a
pkgrel=2
pkgdesc='Helpful unit multipliers and functions for Go'
arch=('any')
url="https://github.com/alecthomas/units"
license=('MIT')
depends=('go' 'golang-github-stretchr-testify')
checkdepends=('git')
source=("$pkgname-$_commit.tar.gz::https://github.com/alecthomas/units/archive/$_commit.tar.gz")
sha512sums=('855f15eeb83bdc16981694f522aa1e200a3857ed11ae87fca9b0cc70734eac76f5082bc3ed92f843b818da14b58dc921dbb1aed4ea35de056c087a64b7e25fe4')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/alecthomas
  cp -a "$srcdir"/units-$_commit "$srcdir"/build/src/github.com/alecthomas/units
  cd "$srcdir"/build/src/github.com/alecthomas/units
  go get gopkg.in/yaml.v3
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/alecthomas
  cp -a "$srcdir"/units-$_commit "$pkgdir"/usr/share/gocode/src/github.com/alecthomas/units

  install -Dm644 units-$_commit/COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
