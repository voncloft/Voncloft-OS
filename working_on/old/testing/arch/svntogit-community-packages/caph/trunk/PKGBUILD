# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: jose <jose1711 [at] gmail (dot) com>

pkgname=caph
arch=('x86_64')
pkgver=1.1
pkgrel=7
pkgdesc='Sandbox game based on physics which uses doodles as a primary gameplay element'
license=('GPL')
url='http://sourceforge.net/projects/caphgame/'
depends=('sdl' 'libgl' 'libpng')
makedepends=('mesa')
source=("https://sourceforge.net/projects/caphgame/files/caph/caphgame-${pkgver}/caph-${pkgver}.tar.bz2"
        "caph.desktop")
sha512sums=('44c325b5684210e091e416f2914531e72ce66bfd81303bfc696e9e35fb0bf0c73042e9cc3141146ffbfa0de81b6347fcda8ca37cab5b97119bf4cdb99b55cea7'
            '95e6e41abe9ae95c6083fb0bf64e30fc8e0bef9c270b02ff7b9366e0f791f25ab499b46e49ffa931a20a35ae4d9b3d220cb023462e7f1643212733dcfaec0ccd')

build() {
  cd caph-${pkgver}/src
  ./confg
  ./build
}

package() {
  cd caph-${pkgver}/src
  install -Dm755 ./caph "$pkgdir"/usr/bin/caph
  install -Dm644 "$srcdir"/caph.desktop "$pkgdir"/usr/share/applications/caph.desktop
  install -Dm644 ../share/caph/brush.png "$pkgdir"/usr/share/pixmaps/caph.png
  cp -r ../share/caph "$pkgdir"/usr/share
  cp -r ../doc "$pkgdir"/usr/share/doc
}

# vim: sw=2:ts=2 et:
