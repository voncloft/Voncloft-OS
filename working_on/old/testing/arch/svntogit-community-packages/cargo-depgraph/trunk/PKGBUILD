# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Vlad Frolov <frolvlad@gmail.com>

pkgname=cargo-depgraph
pkgver=1.2.2
pkgrel=2
pkgdesc="Creates dependency graphs for cargo projects using cargo metadata and graphviz"
arch=('x86_64')
url="https://sr.ht/~jplatte/cargo-depgraph"
license=('GPL3')
depends=('gcc-libs')
makedepends=('rust')
source=(${pkgname}-${pkgver}.tar.gz::https://git.sr.ht/~jplatte/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('c67624ed4889705a8a2b29492379ee95d10309c679ac2915c91813ae08a850c5')

build() {
  cd "$pkgname-v$pkgver"
  cargo build --release --locked
}

check() {
  cd "$pkgname-v$pkgver"
  cargo test --release --locked
}

package() {
  cd "$pkgname-v$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
