# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=synthv1
pkgver=0.9.19
pkgrel=1
pkgdesc="Old-school all-digital 4-oscillator subtractive polyphonic synthesizer with stereo fx"
arch=('x86_64')
url="https://synthv1.sourceforge.io/synthv1-index.html"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('alsa-lib' 'cmake' 'jack' 'liblo' 'lv2' 'qt5-tools')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'liblo: for standalone application'
            'new-session-manager: for session management using the standalone application')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('65f5229e8f42dfbc6ea2b992b2e36a76fec24c11d3373a421e8f73df5889e0d6e186785110ab40099ef24e9ed08bc985b939613e1da0fdffb3b93c8f99eef449')
b2sums=('c0a3b205ca000aed171a51fe7d558af9f3f22006d5bf2eaf7ff7493a074c838c0bcf07107b46c70c2ac86ac5ad95a7b62452a48c50391118769d9338769042d4')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
