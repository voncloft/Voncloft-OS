# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

_name=pyliblo
pkgname=python-pyliblo
pkgver=0.10.0
pkgrel=7
pkgdesc='Python wrapper for the liblo OSC library'
arch=('x86_64')
url="http://das.nasophon.de/pyliblo/"
license=('LGPL2.1')
depends=('python' 'liblo')
makedepends=('cython')
source=("http://das.nasophon.de/download/${_name}-${pkgver}.tar.gz")
sha512sums=('d2a3d47f6c52d310104256d33fbd5d652ae6bc8cde32c2d3b2f3f407b8a4aba99028e57cd466804d266f63468a6c792760f28a43dbabcfa3330e0a852ee4d4e3')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  python setup.py test
}

package_python-pyliblo() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  # docs
  install -vDm 644 {NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
