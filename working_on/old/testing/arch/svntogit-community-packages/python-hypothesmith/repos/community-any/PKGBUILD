# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hypothesmith
pkgver=0.1.8
pkgrel=1
pkgdesc="Hypothesis strategies for generating Python programs, something like CSmith"
url="https://github.com/Zac-HD/hypothesmith"
license=('MPL')
arch=('any')
depends=('python-hypothesis' 'python-lark-parser' 'python-libcst')
makedepends=('python-setuptools')
checkdepends=('python-black' 'python-parso' 'python-pytest-cov')
source=("https://pypi.io/packages/source/h/hypothesmith/hypothesmith-$pkgver.tar.gz")
sha512sums=('5cae94e56f8ba2c404eade44d3dbf1baf9f15ed6f75e00e2b7cf3569dabb0b904b88c4d70109b149919e658d6661d618680c286b202e0d03fa6eaa04970995a4')

build() {
  cd hypothesmith-$pkgver
  python setup.py build
}

check() {
  cd hypothesmith-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd hypothesmith-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
