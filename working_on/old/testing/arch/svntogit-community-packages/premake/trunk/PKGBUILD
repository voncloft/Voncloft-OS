# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: revel <revelΘmuub·net>
# Contributor: Marcos J. S. Magalhaes <mjsmagalhaes ^dot^ insc _at_ gmail ~dot~ com>

pkgname=premake
pkgver=5.0a15
_pkgver=5.0.0-alpha15
pkgrel=1
pkgdesc="A simple build configuration and project generation tool"
arch=('x86_64')
url="https://premake.github.io/"
license=('BSD')
depends=('glibc')
source=("https://github.com/premake/premake-core/releases/download/v${_pkgver}/premake-${_pkgver}-src.zip")
sha512sums=('1d9e89f77224d1fc191fec55226e13bf3a5c07d310fa79a4d90b284872ec7c52fb68f8e51874c7f9a97e44bc9af35b9fd781b7b06b9268015c5448c49506e9a7')

build() {
  cd "premake-$_pkgver/build/gmake2.unix"

  make config=release
}

package() {
  cd "premake-$_pkgver"

  install -Dm755 "bin/release/premake5" "${pkgdir}/usr/bin/premake5"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
