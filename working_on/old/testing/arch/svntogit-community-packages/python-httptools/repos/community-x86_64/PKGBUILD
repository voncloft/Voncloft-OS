# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=httptools
pkgname=python-$_pkgname
pkgver=0.1.1
pkgrel=4
pkgdesc='Fast HTTP parser'
arch=('x86_64')
url='https://github.com/MagicStack/httptools'
license=('MIT')
depends=('python' 'http-parser')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('afee301be87ec01a1cf64ab4593a99e5c6c8366238f4a7660d2f40dd87c3c2ed2b82da4b047ef736546fe1450ac747322521cbd67f2a430ce31b4415be3a8251')

prepare() {
  cd $_pkgname-$pkgver

  sed -i 's|../../vendor/http-parser/http_parser.h|/usr/include/http_parser.h|' $_pkgname/parser/cparser.pxd
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build build_ext --use-system-http-parser
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
