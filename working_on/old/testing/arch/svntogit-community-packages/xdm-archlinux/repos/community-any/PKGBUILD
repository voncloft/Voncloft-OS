# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Truls Becken <becken@stud.ntnu.no>
# Contributor: whompus

pkgname=xdm-archlinux
pkgver=0.7
pkgrel=2
pkgdesc="An XDM setup that looks better than the defaults"
arch=(any)
license=('GPL')
url="https://xorg.freedesktop.org"
depends=('xorg-xdm' 'bash'
	 'xorg-xmessage' 'xorg-xclock' 'xorg-xsetroot' 'xorg-xwininfo' 'xorg-xkill'
	 'ttf-bitstream-vera' 'terminus-font')
backup=(etc/X11/xdm/archlinux/Xsetup
	etc/X11/xdm/archlinux/Xresources)
install=xdm-archlinux.install
source=(xdm-config
	Xsetup
	Xstartup
	Xresources
	buttons
	arch.xpm.gz
	xdm-archlinux.service)
sha256sums=('c79853a4fb674c505b8d542b19320eb6fcb0ec9e836b6fd986ba70691c93e370'
            '12720450ec5430e8c8875f75b8045b88cc8155e03fc97841c28eca5d278e102d'
            '7f9d62c29425f03f098d42b68d6d8bbd2335be6235a83f6142cfa4e9f8a4727a'
            'a94c88acd9d277da093e910d6aebf6eccbf2aae4248fd9170fb1c4239fb87cf7'
            '9edbf630b1748d188544117938f604de08173ea865d7e485de8ac99ec9d38ac0'
            '625087946ff746d87fde41da490dbd28bdc9c23c6ff25a915d2ea4b5ef08ebfd'
            'eb79a6bcb03c3909afea88ac63aba0464e6f07ae32ca29130b016dc0207f5a6a')

package() {
  mkdir -p "$pkgdir"/etc/X11/xdm/archlinux
  cp arch.xpm buttons xdm-config Xresources Xsetup Xstartup "$pkgdir"/etc/X11/xdm/archlinux
  chmod 0755 "$pkgdir"/etc/X11/xdm/archlinux/{Xsetup,Xstartup,buttons}
  install -Dm0644 "$srcdir"/xdm-archlinux.service "$pkgdir"/usr/lib/systemd/system/xdm-archlinux.service
}
