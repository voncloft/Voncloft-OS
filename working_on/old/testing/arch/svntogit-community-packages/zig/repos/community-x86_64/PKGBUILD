# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Contributor: Marc Tiehuis <marctiehuis@gmail.com>

pkgname=zig
pkgver=0.7.1
pkgrel=1
pkgdesc='a general-purpose programming language and toolchain for maintaining robust, optimal, and reusable software'
arch=('x86_64')
url='https://ziglang.org/'
license=('MIT')
depends=('clang' 'llvm-libs' 'lld')
makedepends=('cmake' 'llvm')
source=("https://ziglang.org/download/$pkgver/zig-$pkgver.tar.xz"
        "resolve_DNS.patch")
sha256sums=('2db3b944ab368d955b48743d9f7c963b8f96de1a441ba5a35e197237cc6dae44'
            'c2b7e709dcc229f0e58e2529db900bcfb9a1cfb73da765c5c76ab1e6d0a333cc')

prepare() {
    cd "$pkgname-$pkgver"
    # https://github.com/ziglang/zig/issues/6898
    patch -p1 < ../resolve_DNS.patch
}

build() {
    cd "$pkgname-$pkgver"

    mkdir -p build
    cd build
    # The zig CMakeLists uses build type Debug if not set
    # override it back to None so makepkg env vars are respected
    cmake \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DZIG_PREFER_CLANG_CPP_DYLIB=ON \
        ..
    make
}

check() {
    cd "$pkgname-$pkgver/build"

    ./zig build test
}

package() {
    cd "$pkgname-$pkgver"

    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    make -C build DESTDIR="$pkgdir" install
}
