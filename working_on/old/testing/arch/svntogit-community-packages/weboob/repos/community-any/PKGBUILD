# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=weboob
pkgver=2.0
pkgrel=2
pkgdesc="Core library and modules for Web Outside of Browsers"
arch=(any)
url="https://weboob.org/"
license=(LGPL3)
depends=(
    python-lxml
    python-cssselect
    python-requests
    python-dateutil
    python-yaml
    python-html2text
    python-six
    python-unidecode
    python-pillow
    python-babel
    python-simplejson
)
optdepends=(
    'python-prettytable: CLI output formatting'
    'python-feedparser: required by some modules'
)
makedepends=(python-setuptools)
replaces=(weboob-headless)
provides=(weboob-headless)
conflicts=(weboob-headless)
source=("https://files.pythonhosted.org/packages/source/w/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('fc8be1f77ad3a53285cef8b20a8b747960c163fad729c56838043d8ddcdfc9b0')

build() {
    cd ${pkgname}-${pkgver}
    python setup.py build
}

package() {
    cd ${pkgname}-${pkgver}
    python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
}
