# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Dan Serban
# Contributor: Federico Chiacchiaretta

pkgname=transmageddon
pkgver=1.5
pkgrel=7
pkgdesc="Simple python application for transcoding video into formats supported by GStreamer"
arch=('any')
url="http://www.linuxrising.org/"
license=('LGPL')
depends=('python-gobject' 'libnotify' 'gtk3' 'gst-plugins-base')
makedepends=('intltool')
optdepends=('gst-plugins-good: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'            
            'gst-libav: Extra media codecs')
source=(http://www.linuxrising.org/files/$pkgname-$pkgver.tar.xz
        0001-Fix-AppStream-metadata-validation.patch)
sha256sums=('ca24369a94ce4edc992c1c7b3e2d09d7cd82794ce4c1b862a1a83ea40121e296'
            'ca16192de33f8694a74220c3790a230d32c5576c663a34db050e0c2db9b0e002')

prepare() {
  cd $pkgname-$pkgver

  # Fix AppStream metadata validation
  patch -Np1 -i ../0001-Fix-AppStream-metadata-validation.patch
  sed -i 's|${datadir}/appdata|${datadir}/metainfo|' configure
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
