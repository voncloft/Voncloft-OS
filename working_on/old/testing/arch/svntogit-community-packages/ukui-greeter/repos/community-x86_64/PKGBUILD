# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-greeter
pkgver=3.0.1
pkgrel=1
pkgdesc="Lightdm greeter for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-greeter"
groups=('ukui')
depends=('imlib2' 'liblightdm-qt5' 'libxrandr' 'libxtst' 'qt5-svg' 'qt5-x11extras')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-greeter/archive/v$pkgver.tar.gz")
sha512sums=('3882c9739f693b2cba9a23db4709b4a718afa02529111020e1862534293a7d20b275b1c8f97589406be4d4fee6f85866fee25a49594384fea2e5a60bc13b0a9c')

prepare() {
  cd ukui-greeter-$pkgver
  sed -i 's|target.path = /usr/sbin/|target.path = /usr/bin/|' ukui-greeter/ukui-greeter.pro
}

build() {
  cd ukui-greeter-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-greeter-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
