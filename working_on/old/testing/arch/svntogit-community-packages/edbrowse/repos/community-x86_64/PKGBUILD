# Maintainer: Alad Wenter <alad (at) mailbox.org>
# Contributor: Chris Brannon <cmbrannon (at) cox.net>
# Contributor: joyfulgirl <joyfulgirl (at) archlinux.us>
# Contributor: Chuck Boucher <jofywyxaxukyvofa (at) tempomail.fr>
# Contributor: aksr <aksr at t-com dot me>
pkgname=edbrowse
pkgver=3.7.6
pkgrel=2
_commit=c8c71daa7f3a32803c336e0b2e75a3dcc8b198f8 # 3.7.6
pkgdesc='Line-oriented editor, browser and mail client'
arch=('x86_64')
url='https://edbrowse.org'
license=('GPL' 'MPL')
depends=('duktape' 'tidy' 'curl' 'pcre')
makedepends=('git')
source=("$pkgname-$pkgver::git+https://github.com/CMB/edbrowse#commit=$_commit")
sha256sums=('SKIP')

build() {
    cd "$pkgname-$pkgver"
    make CFLAGS="$CFLAGS -I/usr/include/tidy -fcommon" all
}

package() {
    cd "$pkgname-$pkgver"
    make -C src DESTDIR="$pkgdir" prefix=/usr install

    install -Dm644 README CHANGES -t "$pkgdir/usr/share/doc/$pkgname/README"
    install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname"

    install -Dm644 doc/usersguide.html doc/sample.ebrc -t "$pkgdir/usr/share/doc/edbrowse"
    install -Dm644 doc/man-edbrowse-debian.1 -T "$pkgdir/usr/share/man/man1/edbrowse.1"
}
