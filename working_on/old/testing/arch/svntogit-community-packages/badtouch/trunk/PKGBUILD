# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: kpcyrd <git@rxv.cc>

pkgname=badtouch
pkgver=0.7.3
pkgrel=1
pkgdesc='Scriptable network authentication cracker'
url='https://github.com/kpcyrd/badtouch'
depends=('openssl' 'lua52' 'libcap' 'zlib')
makedepends=('cargo')
arch=('x86_64')
license=('GPL3')
install=badtouch.install
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha256sums=('cb09ae83bbe37e17ce1d5e86ba1c9f8b8dd1dc1b30e3f7d69d7cfd8db7ae5547'
            'SKIP')
sha512sums=('e667192c9271720b94304a0af3f9eaa9e1408a0709dc005436270cc667e84dbcdd2d6cf924c0077f4782a786070f64e3b396e3d78f126777f2f077da9c05805e'
            'SKIP')
validpgpkeys=('64B13F7117D6E07D661BBCE0FE763A64F5E54FD6')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/badtouch" completions bash > "${pkgdir}/usr/share/bash-completion/completions/badtouch"
  "${pkgdir}/usr/bin/badtouch" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_badtouch"
  "${pkgdir}/usr/bin/badtouch" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/badtouch.fish"

  install -Dm 644 docs/badtouch.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 scripts/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
