# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-rencode
pkgver=1.0.6
pkgrel=5
pkgdesc="A Module similar to bencode from the BitTorrent project"
url="https://github.com/aresch/rencode"
license=('GPL')
arch=('x86_64')
depends=('python')
makedepends=('cython')
checkdepends=('python-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/aresch/rencode/archive/v$pkgver.tar.gz")
sha512sums=('f04de4d3a83aed916bdc4b7a8ca772655e9324d0531ee882cb7d2a1e92b397bdcec8497bf02939e0a806886ecc62ed2e7319dfee054dad1b69052157f0959e8f')

build() {
  cd "$srcdir"/rencode-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/rencode-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.9" py.test
}

package() {
  cd rencode-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
