# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=cereal
pkgver=1.3.0
pkgrel=1
pkgdesc="C++11 library for serialization"
arch=(any)
url="https://github.com/USCiLab/cereal"
license=(BSD)
makedepends=(boost cmake git)
_tag=34eb6f6bd6783018354c7043d5d6aa2eec4e4dbe # git rev-parse v${pkgver}
source=(git+https://github.com/USCiLab/cereal.git#tag=${_tag})
sha256sums=(SKIP)

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../${pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSKIP_PORTABILITY_TEST=ON \
    -DTHREAD_SAFE=ON \
    -DWITH_WERROR=OFF
  make
}

package() {
  make DESTDIR="${pkgdir}" -C build install
  install -Dm644 ${pkgname}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
