# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-phonenumbers
pkgver=8.12.17
pkgrel=1
pkgdesc="Python version of Google's common library for parsing, formatting, storing and validating international phone numbers"
arch=('any')
url="https://github.com/daviddrysdale/python-phonenumbers"
license=('Apache')
depends=('python')
makedepends=("python-setuptools")
source=("$pkgname-$pkgver.tar.gz::https://github.com/daviddrysdale/python-phonenumbers/archive/v$pkgver.tar.gz")
sha512sums=('144b9e6812e4d58f1b0bc75781d2a0da73e28b69561b84f281fe4fa074da3e7bdf81b488f01ff9d96e80716d26295370148e814df89b287b016824050ac6db5a')

build() {
  cd python-phonenumbers-$pkgver
  python setup.py build
}

check() {
  cd python-phonenumbers-$pkgver
  python setup.py test
}

package() {
  cd python-phonenumbers-$pkgver
  python setup.py install -O1 --root "$pkgdir"
}
