# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Simon Perry <aur [at] sanxion [dot] net>
# Contributor: Jonas Nyrén <jonas.nyren*mindkiller.com>

pkgname=libsidplayfp
pkgver=2.0.2
pkgrel=1
pkgdesc='Library to play Commodore 64 music, derived from libsidplay2'
url='https://sourceforge.net/projects/sidplay-residfp/'
arch=(x86_64)
license=(GPL)
makedepends=(vice xa)
optdepends=('vice: better SID support')
source=("https://downloads.sourceforge.net/project/sidplay-residfp/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c3611cb71a4b98819d25c45272d11b0f9379f65a334291c0120af508d4401cb1')

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr
	make
}

package() {
	DESTDIR="$pkgdir" make -C $pkgname-$pkgver install
	install -d "$pkgdir/usr/share/sidplayfp"
	ln -t "$pkgdir/usr/share/sidplayfp" -s /usr/lib/vice/C64/{kernal,basic,chargen}
}

# getver: sf.net/p/sidplay-residfp/code
# vim: ts=2 sw=2 et:
