# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: dhead666 <myfoolishgames@gmail.com>

pkgname=tinyxml2
pkgver=7.1.0
pkgrel=1
pkgdesc='Simple XML parser made for easy integration'
arch=('x86_64')
url='http://www.grinninglizard.com/tinyxml2'
license=('zlib')
makedepends=('cmake' 'ninja' 'setconf')
source=($pkgname-$pkgver.tar.gz::https://github.com/leethomason/$pkgname/archive/$pkgver.tar.gz)
sha256sums=('68ebd396a4220d5a9b5a621c6e9c66349c5cfdf5efaea3f16e3bb92e45f4e2a3')

prepare() {
  grep License -A20 "$pkgname-$pkgver/readme.md" > LICENSE
}

build() {
  mkdir -p build
  cd build
  cmake "../$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/leethomason/tinyxml2
# vim: ts=2 sw=2 et:
