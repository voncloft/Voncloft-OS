# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ppix-quotelike
pkgver=0.015
pkgrel=1
pkgdesc="Parse Perl string literals and string-literal-like things"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/PPIx-QuoteLike"
depends=('perl-ppi')
makedepends=('perl-ppix-regexp' 'perl-readonly')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/W/WY/WYANT/PPIx-QuoteLike-$pkgver.tar.gz")
sha512sums=('b4bca17f2a33d7a3eabda4c6eb2b6f663015f0977af207bf01f5a24ac784e65dbcc05a70573429f9a277d3707544d4ced032c914ad0e0f70e47df39fcebdd85b')

build() {
  cd PPIx-QuoteLike-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd PPIx-QuoteLike-$pkgver
  make test
}

package() {
  cd PPIx-QuoteLike-$pkgver
  make DESTDIR="$pkgdir" install
}
