# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Piotr Rogo≈ºa <rogoza dot piotr at gmail dot com>

pkgbase=gimp-help
pkgver=2.10.0
pkgrel=3
arch=('any')
url='https://docs.gimp.org/'
license=('GPL' 'LGPL')
makedepends=('python2' 'docbook-xsl')
options=(!strip !zipman)
source=("https://ftp.gimp.org/pub/gimp/help/${pkgbase}-${pkgver}.tar.bz2")
sha256sums=('03804fed071b49e5810edd8327868659dfd9932fbf34d34189d56bd0ad539118')

_languages=(
  'ca     "Catalan"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en     "English"'
  'en_GB  "English (United Kingdom)"'
  'es     "Spanish"'
  'fi     "Finnish"'
  'fr     "French"'
  'it     "Italian"'
  'ja     "Japanese"'
  'ko     "Korean"'
  'nl     "Dutch"'
  'nn     "Norwegian"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'zh_CN  "Chinese (simplified)"'
)

_package() {
  _locale="$1"
  _language="$2"
  pkgdesc="${_language} help files for Gimp"
  install -dm755 "${pkgdir}/usr/share/gimp/2.0/help/${_locale}"
  cp -rL "${srcdir}/${pkgbase}-${pkgver}/html/${_locale}" \
    "${pkgdir}/usr/share/gimp/2.0/help"
}

prepare() {
  cd "${pkgbase}-${pkgver}"
  sed -e '1s/python$/python2/' -i "tools/xml2po.py"
}

build(){
  cd "${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --without-gimp
  make
}

for _lang in "${_languages[@]}"
do
  _locale=${_lang%% *}
  _pkgname=${pkgbase}-${_locale,,}

  pkgname+=(${_pkgname})
  eval "package_${_pkgname}() {
    _package ${_lang}
  }"
done
