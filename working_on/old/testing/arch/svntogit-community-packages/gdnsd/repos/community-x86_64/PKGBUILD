# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>

pkgname=gdnsd
pkgver=3.5.0
pkgrel=2
pkgdesc="an Authoritative-only DNS server which does geographic balancing, redirection, weighting, and service-state-conscious failover at the DNS layer"
arch=('x86_64')
url="https://gdnsd.org/"
license=('GPL3')
depends=('libev' 'libmaxminddb' 'libsodium' 'libunwind' 'liburcu')
makedepends=('ragel')
checkdepends=('perl-socket6' 'perl-io-socket-inet6' 'perl-http-daemon' 'perl-libwww' 'perl-net-dns')
source=("https://github.com/$pkgname/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"
        gdnsd.sysusers)
md5sums=('0e8711f5b526384c2d8c02829046aaca'
         '046488b1a1b36294a2a559aacbe71782')

prepare() {
    cd "$pkgname-$pkgver"
    autoreconf -vif
}

build() {
    cd "$pkgname-$pkgver"
    ./configure \
       --prefix=/usr \
      --sbindir=/usr/bin \
      --localstatedir=/ \
      --libexecdir=/usr/lib \
      --sysconfdir=/etc \
      --with-systemdsystemunitdir=/usr/lib/systemd/system
      
    make
}

check() {
    cd "$pkgname-$pkgver"
    make -k check
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
    rm -rf "$pkgdir"/{lib,run}

    install -Dm644 ../gdnsd.sysusers "$pkgdir/usr/lib/sysusers.d/gdnsd.conf"
}
