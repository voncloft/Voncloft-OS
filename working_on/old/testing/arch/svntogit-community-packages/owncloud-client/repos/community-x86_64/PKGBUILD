# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

pkgname=owncloud-client
pkgver=2.7.5.3180
pkgrel=1
pkgdesc='ownCloud client based on mirall'
arch=(x86_64)
url='http://owncloud.org/'
license=(GPL2)
depends=(qtkeychain libcloudproviders hicolor-icon-theme)
makedepends=(extra-cmake-modules python-sphinx qt5-tools doxygen kio)
optdepends=('python-nautilus: integration with Nautilus' 'nemo-python: integration with Nemo' 'python-caja: integration with Caja')
backup=('etc/ownCloud/sync-exclude.lst')
source=(https://download.owncloud.com/desktop/ownCloud/stable/$pkgver/source/ownCloud-$pkgver.tar.xz{,.asc})
sha256sums=('37cf19853b32a7227ad497c9baf320f96dd333a747dfe906e729c591679d748f'
            'SKIP')
validpgpkeys=(F05F7DD7953A07DF36579DAA498C45EBE94E7B37) # ownCloud Client Team (Signing Key) <info@owncloud.com>

build() {
  cmake -B build -S ownCloud-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
