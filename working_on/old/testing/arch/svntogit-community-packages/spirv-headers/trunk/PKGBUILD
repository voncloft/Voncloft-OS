# Maintainer: Felix Yan <felixonmars@archlinux.org

pkgname=spirv-headers
pkgver=1.5.4
pkgrel=1
pkgdesc='SPIR-V Headers'
arch=('any')
url='https://www.khronos.org/registry/spir-v/'
license=('MIT')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/KhronosGroup/SPIRV-Headers/archive/$pkgver.tar.gz")
sha512sums=('050ae8ded345bbef3d94e91f22dd72860fdeed3072f943bafaa37f86f2d522e59d7c4ff2e6aa5bddb3c3df55f124200394a91e6d99eb95933463e7b8ce360aad')

build() {
  cd SPIRV-Headers-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd SPIRV-Headers-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
