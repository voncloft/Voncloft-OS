# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-datetime
pkgver=2.2.5
pkgrel=2
pkgdesc='Date & Time indicator for Wingpanel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel-indicator-datetime
license=(GPL3)
groups=(pantheon)
depends=(
  cairo
  evolution-data-server
  glib2
  gtk3
  libgee
  libgranite.so
  libical
  libwingpanel-2.0.so
)
makedepends=(
  git
  meson
  vala
  wingpanel
)
_tag=c64a8882492c9d8f25bb3e0520ad26b19fc4f849
source=(git+https://github.com/elementary/wingpanel-indicator-datetime.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd wingpanel-indicator-datetime

  git describe --tags
}

prepare() {
  cd wingpanel-indicator-datetime

  git cherry-pick -n 3ccd05d611e6dd5274a03f061ba1b5e13d6fe0cf
}

build() {
  arch-meson wingpanel-indicator-datetime build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
