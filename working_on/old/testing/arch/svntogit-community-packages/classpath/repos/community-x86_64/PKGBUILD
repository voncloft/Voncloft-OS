# $Id$
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tardo <tardo@nagi-fanboi.net>
# Contributor: Sean Gillespie <Sean.D.Gillespie@gmail.com>

pkgname=classpath
pkgver=0.99
pkgrel=4
pkgdesc="A free replacement for Sun's proprietary core Java class libraries."
arch=('x86_64')
url="http://www.gnu.org/software/classpath/"
license=('GPL')
depends=('file' 'gtk2' 'libsm' 'libxtst')
makedepends=('java-environment' 'antlr2')
options=('!emptydirs')
source=(ftp://ftp.gnu.org/gnu/classpath/$pkgname-$pkgver.tar.gz
	freetype2_include.patch)
md5sums=('0ae1571249172acd82488724a3b8acb4'
         '55dafc301c5f2e497696d01e9c1dab9c')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver

  patch -p0 Makefile.in <<EOF
298,299c298,299
< SUBDIRS = lib doc external include native resource scripts \$(TOOLSDIR) \$(EXAMPLESDIR)
< DIST_SUBDIRS = lib doc external include native resource scripts tools examples
---
> SUBDIRS = lib doc external include native resource scripts
> DIST_SUBDIRS = lib doc external include native resource scripts
EOF

  # fix freetype2 include path 
  patch -Np1 -i "$srcdir"/freetype2_include.patch

  # fix build
  sed -i 's/-source 1\.5 -target 1\.5/-source 1.6 -target 1.6/' configure m4/ac_prog_javac_works.m4 {examples,tools,lib}/Makefile.{am,in}
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr --disable-Werror --disable-plugin --disable-gconf-peer \
    --with-antlr-jar=/usr/share/java/antlr2.jar
  make EXAMPLESDIR=
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make EXAMPLESDIR= DESTDIR="$pkgdir" install

  rm -f "$pkgdir"/usr/bin/gappletviewer
  rm -f "$pkgdir"/usr/bin/gjar
  rm -f "$pkgdir"/usr/bin/gjarsigner
  rm -f "$pkgdir"/usr/bin/gjavah
  rm -f "$pkgdir"/usr/bin/gkeytool
  rm -f "$pkgdir"/usr/bin/gnative2ascii
  rm -f "$pkgdir"/usr/bin/gorbd
  rm -f "$pkgdir"/usr/bin/grmic
  rm -f "$pkgdir"/usr/bin/grmid
  rm -f "$pkgdir"/usr/bin/grmiregistry
  rm -f "$pkgdir"/usr/bin/gserialver
  rm -f "$pkgdir"/usr/bin/gtnameserv
  rm -f "$pkgdir"/usr/lib/logging.properties
  rm -f "$pkgdir"/usr/lib/security/classpath.security
  rm -f "$pkgdir"/usr/share/man/man1/gappletviewer.1*
  rm -f "$pkgdir"/usr/share/man/man1/gcjh.1*
  rm -f "$pkgdir"/usr/share/man/man1/gjar.1*
  rm -f "$pkgdir"/usr/share/man/man1/gjarsigner.1*
  rm -f "$pkgdir"/usr/share/man/man1/gjavah.1*
  rm -f "$pkgdir"/usr/share/man/man1/gkeytool.1*
  rm -f "$pkgdir"/usr/share/man/man1/gnative2ascii.1*
  rm -f "$pkgdir"/usr/share/man/man1/gorbd.1*
  rm -f "$pkgdir"/usr/share/man/man1/grmid.1*
  rm -f "$pkgdir"/usr/share/man/man1/grmiregistry.1*
  rm -f "$pkgdir"/usr/share/man/man1/gserialver.1*
  rm -f "$pkgdir"/usr/share/man/man1/gtnameserv.1*
  rm -f "$pkgdir"/usr/share/man/man1/gjdoc.1*
  rm -f "$pkgdir"/usr/share/info/dir
}
