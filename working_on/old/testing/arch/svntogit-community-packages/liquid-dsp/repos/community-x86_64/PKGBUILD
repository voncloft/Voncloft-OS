# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgbase=liquid-dsp
pkgname=($pkgbase $pkgbase-sse4.1)
pkgver=1.3.2
pkgrel=2
pkgdesc='Open-source signal processing library for software-defined radios'
arch=('x86_64')
url='https://liquidsdr.org'
license=('MIT')
depends=('glibc')
optdepends=('fftw: efficient FFT')
source=("$url/downloads/$pkgbase-$pkgver.tar.gz")
sha512sums=('47d412b660dca87b8c7f4895d55ba71f2adaff555e796e837727c0f8414ee5a0b7f3184c3c5b07a895fd45078c55934571904ed2d166df8a86b7f6eebf709768')

prepare() {
    mkdir $pkgbase-sse4.1-$pkgver
    cp -r $pkgbase-$pkgver/* $pkgbase-sse4.1-$pkgver

    sed -i 's|ax_cv_have_sse41_ext=.*|ax_cv_have_sse41_ext=yes|' $pkgbase-sse4.1-$pkgver/scripts/ax_ext.m4
}

build() {
    echo 'Building liquid-dsp...'
    cd "$srcdir"/$pkgbase-$pkgver

    autoreconf -vif
    enable_simdoverride='' ./configure --prefix=/usr

    make

    echo 'Building liquid-dsp-sse4.1...'
    cd "$srcdir"/$pkgbase-sse4.1-$pkgver

    autoreconf -vif
    ./configure --prefix=/usr

    make
}

check() {
    echo 'Checking liquid-dsp...'
    cd "$srcdir"/$pkgbase-$pkgver

    make check

    echo 'Checking liquid-dsp-sse4.1...'
    cd "$srcdir"/$pkgbase-sse4.1-$pkgver

    make check
}

package_liquid-dsp() {
    cd $pkgbase-$pkgver

    make DESTDIR="$pkgdir" install

    install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_liquid-dsp-sse4.1() {
    pkgdesc="$pkgdesc (with SSE 4.1 extensions)"
    provides=('liquid-dsp')
    conflicts=('liquid-dsp')

    cd $pkgbase-sse4.1-$pkgver

    make DESTDIR="$pkgdir" install

    install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

