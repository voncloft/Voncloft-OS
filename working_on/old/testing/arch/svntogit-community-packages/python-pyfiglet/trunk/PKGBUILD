# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Victor HÃ¤ggqvist <aur@snilius.com>

_name=pyfiglet
pkgname=python-pyfiglet
pkgver=0.8.post1
pkgrel=3
pkgdesc='Pure-python FIGlet implementation'
url=https://github.com/pwaller/pyfiglet
license=('GPL')
arch=('any')
depends=('python-setuptools')
checkdepends=('figlet')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('c6c2321755d09267b438ec7b936825a4910fec696292139e664ca8670e103639')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  PYTHONPATH=. python pyfiglet/test.py
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
