# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-www-mechanize
pkgver=1.96
pkgrel=1
pkgdesc="Automates web page form & link interaction"
arch=(any)
url="https://search.cpan.org/dist/WWW-Mechanize"
license=('GPL' 'PerlArtistic')
depends=('perl-libwww' 'perl-html-parser' 'perl-uri' 'perl-http-response-encoding'
	 'perl-http-server-simple' 'perl-test-warn' 'perl-html-form')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/O/OA/OALDERS/WWW-Mechanize-$pkgver.tar.gz)
sha256sums=('a79a613452287433a88e689195b09951a06c2df6d7fd40c15aa556452de9ab04')

build() {
  cd  "$srcdir"/WWW-Mechanize-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/WWW-Mechanize-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
