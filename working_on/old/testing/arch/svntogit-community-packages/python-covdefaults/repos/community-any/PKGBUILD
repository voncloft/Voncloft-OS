# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=covdefaults
pkgname=python-$_pkgname
pkgver=1.2.0
pkgrel=2
pkgdesc='A coverage plugin to provide sensible default settings'
arch=('any')
url='https://github.com/asottile/covdefaults'
license=('MIT')
depends=('python-coverage')
makedepends=('python-setuptools')
checkdepends=('python-tox-current-env' 'python-pytest')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('2ac214acc79905ed8c57f766d8ca7c163db56819db4cac95d0d9ecf21e3f455d7865c00d64a4398bd2c63f5250217f53a9a3569d8ba1219d9152034f8c9628e8')

prepare() {
  cd $_pkgname-$pkgver

  # remove coverage usage as it wants the plugin we are building
  sed -i -e's|coverage run|python|' -e '/coverage .*/d' tox.ini
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  tox -e py39 --current-env
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
