# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Redistribution information:
# https://software.intel.com/en-us/mkl
# https://software.intel.com/en-us/articles/intel-math-kernel-library-license-faq
pkgbase=intel-mkl
pkgname=(intel-mkl intel-mkl-static)
pkgver=2020.4.304
_pkgver=${pkgver/%./-/}
_weirdnum=16917
pkgrel=1
pkgdesc="Intel Math Kernel Library"
arch=('x86_64')
depends=('gcc-libs')
url="https://software.intel.com/en-us/mkl"
license=("custom:ISSL")
source=("http://registrationcenter-download.intel.com/akdlm/irc_nas/tec/$_weirdnum/l_mkl_$pkgver.tgz"
        mklvars.sh
        intel-mkl.sh
        intel-mkl.conf)
options=(!strip staticlibs)
sha512sums=('9c4ff7710484a1c0dd3e6ba7401eb6cb599f771651006b6a570c45b9abf1f43e8e400940d859a656c6892aa81e634c4d74eefe88e8287fdbb19a513f332326b7'
            '827d90c756d5e8e81092683dd459e10b06f58dc93567f9cb1eee6fdc65fe52a84c45eb0b015857b30d41ab8c57a3c2e95ffcca3fa9e80393797c7616ddffbfd5'
            '1268685ff3ccd47733084dedcd4ea0d393f4ed3f6d00cd92125765ae5c8e14e7fe49b82692b3f667bfea931ad3c284a1c1c37235b474db09b5350c0046566c3c'
            'c7181ee34a8225eefcd66f937e58b29184181c1caa1f24ce264b6fb4c0415fcde743aff3c1a2b165c2af1920dda6c50f5e470a94fe1e11ea61b4ceb9388c3ce4')

prepare() {
  cd l_mkl_$pkgver/rpm
  for rpm in *.rpm; do
    bsdtar -xf $rpm
  done

  sed -i \
    -e 's|^prefix=.*|prefix=/opt/intel/mkl|g' \
    -e 's|${MKLROOT}|/opt/intel/mkl|g' \
    -e 's|^omplibdir=.*|omplibdir=/opt/intel/mkl/lib/intel64|g' \
    opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/bin/pkgconfig/*.pc
}

package_intel-mkl() {
  cd l_mkl_$pkgver/rpm

  # Instead of the original mklvars.sh which is basically just a fairly botchy pkg-config replacement by Intel,
  # we'll ship a file which just exports some static values. The reason is that some software expects this file.
  install -Dm755 "${srcdir}"/mklvars.sh "${pkgdir}"/usr/bin/mklvars.sh

  mkdir -p "${pkgdir}/usr/bin"
  install -Dm755 opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/bin/mkl_link_tool "${pkgdir}"/opt/intel/mkl/bin/mkl_link_tool
  ln -s /opt/intel/mkl/bin/mkl_link_tool "${pkgdir}"/usr/bin/mkl_link_tool

  mkdir -p "${pkgdir}/usr/lib/pkgconfig"
  cp opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/bin/pkgconfig/mkl-dynamic* "${pkgdir}/usr/lib/pkgconfig/"

  mkdir -p "${pkgdir}"/opt/intel/mkl/lib/intel64
  ln -s intel64 "${pkgdir}"/opt/intel/mkl/lib/intel64_lin

  # Sadly Intel seems to expect a certain structure for MKL
  # See https://software.intel.com/en-us/mkl-linux-developer-guide-high-level-directory-structure
  # We'll try to follow that as per upstream suggestion.
  cp opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/lib/intel64_lin/*.so "${pkgdir}"/opt/intel/mkl/lib/intel64
  cp opt/intel/compilers_and_libraries_${pkgver}/linux/compiler/lib/intel64_lin/*.so "${pkgdir}"/opt/intel/mkl/lib/intel64
  cp -r opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/include "${pkgdir}"/opt/intel/mkl/include
  cp -r opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/lib/intel64_lin/locale "${pkgdir}"/opt/intel/mkl/lib/intel64
  install -Dm644 opt/intel/compilers_and_libraries_${pkgver}/licensing/mkl/en/license.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  # Install profile and ld.so.config files
  # See https://software.intel.com/en-us/mkl-linux-developer-guide-scripts-to-set-environment-variables
  install -Dm755 "${srcdir}/intel-mkl.sh" "${pkgdir}/etc/profile.d/intel-mkl.sh"
  install -Dm644 "${srcdir}/intel-mkl.conf" "${pkgdir}/etc/ld.so.conf.d/intel-mkl.conf"
}

package_intel-mkl-static() {
  pkgdesc="Intel Math Kernel Library (static libraries)"
  depends=('intel-mkl')

  cd l_mkl_$pkgver/rpm

  mkdir -p "${pkgdir}/usr/lib/pkgconfig"
  cp opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/bin/pkgconfig/mkl-static* "${pkgdir}/usr/lib/pkgconfig/"

  mkdir -p "${pkgdir}"/opt/intel/mkl/lib/intel64
  cp opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/lib/intel64_lin/*.a "${pkgdir}"/opt/intel/mkl/lib/intel64
  cp opt/intel/compilers_and_libraries_${pkgver}/linux/compiler/lib/intel64_lin/*.a "${pkgdir}"/opt/intel/mkl/lib/intel64

  chmod 644 "${pkgdir}"/opt/intel/mkl/lib/intel64/*.a
}
# vim:set ts=2 sw=2 et:
