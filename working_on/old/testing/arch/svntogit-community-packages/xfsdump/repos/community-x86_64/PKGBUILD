# Maintainer: Jiachen YANG <farseerfc@gmail.com>
# Contributor: Allen Wild <allenwild93@gmail.com>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: dkaylor <dpkaylor@gmail.com>
# Contributor: Pablo Lezaeta <prflr88@gmail.com>

pkgname=xfsdump
pkgver=3.1.9
pkgrel=1
pkgdesc="Additional XFS filesystem utilities"
arch=(x86_64)
url="http://xfs.org"
license=('GPL')
depends=('xfsprogs' 'attr' 'acl' 'dmapi')
source=(https://kernel.org/pub/linux/utils/fs/xfs/xfsdump/xfsdump-${pkgver}.tar.{xz,sign})
# Eric R. Sandeen <sandeen@sandeen.net>
validpgpkeys=('2B8185919E8D248981869DED20AE1692E13DDEE0')
sha256sums=('55aeede6232ddce6c9e79e2af88d6f808534df1552eb2bfaf7fb85b92add6dd1'
            'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sbindir=/usr/bin INSTALL_USER=root INSTALL_GROUP=root
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # root /sbin directory can't be overridden properly with configure,
  # so move files manually
  mv -vf "${pkgdir}/sbin/"* "${pkgdir}/usr/bin/"
  rmdir "${pkgdir}/sbin"
}
