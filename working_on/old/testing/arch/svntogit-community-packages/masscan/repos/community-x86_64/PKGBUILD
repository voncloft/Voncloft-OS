# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=masscan
pkgver=1.3.1
pkgrel=1
pkgdesc='TCP port scanner, spews SYN packets asynchronously, scanning entire Internet in under 5 minutes'
url='https://github.com/robertdavidgraham/masscan'
arch=('x86_64')
license=('AGPL3')
depends=('glibc' 'libpcap')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('bc43b0c41313b400d55f5da2c777c94c3db31e087c87dc8dc883cd319a6eb45da1e8d7de107f52748e415d75c104c6e564f72bf8d7677c9be84e9297d02fa466')
b2sums=('cb18c6665bdd106dd6b9d69c904d465988b0727cfb62b3845b8b68e9f0db8d2fe4dd91b6bc0eb0ddb91a143a6d0656e774fb4dd9d7b19623261f94ace3c0b3e0')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|(CFLAGS )=|\1+=|g' -i Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  export CFLAGS+=" ${CPPFLAGS}"
  make CC=gcc
}

check() {
  cd ${pkgname}-${pkgver}
  make regress
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 doc/masscan.8 -t "${pkgdir}/usr/share/man/man8"
  install -Dm 644 README.md doc/masscan.8.markdown -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
