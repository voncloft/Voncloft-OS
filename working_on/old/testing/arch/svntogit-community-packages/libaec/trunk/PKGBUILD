# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libaec
pkgver=1.0.4
pkgrel=1
pkgdesc="Adaptive Entropy Coding library"
arch=(x86_64)
url="https://gitlab.dkrz.de/k202009/libaec/"
license=(custom)
depends=(glibc)
makedepends=(cmake)
_md5=ea0b7d197a950b0c110da8dfdecbb71f
source=("${url}/uploads/${_md5}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('f2b1b232083bd8beaf8a54a024225de3dd72a673a9bcdf8c3ba96c39483f4309')

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
    install -Dm644 ../${pkgname}-${pkgver}/Copyright.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
