# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cordova
pkgver=10.0.0
pkgrel=1
pkgdesc='Cordova command line interface tool'
arch=('any')
url='https://cordova.apache.org'
license=('Apache')
depends=('npm' 'semver')
install=cordova.install
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('d34c0c723dd7f482e12adbd11b6884c0edaa972e01faf8171611f85a155ea568365156c3d68a65d6af63499fa3d80688fefb01596f1ee8cd8ce450076ac9ee5b')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"

  # Delete npm installed pyc files
  find "$pkgdir" -name "*.pyc" -delete

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in semver; do
    rm -r $dep;
  done
}
