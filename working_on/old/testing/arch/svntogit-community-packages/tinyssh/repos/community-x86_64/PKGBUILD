# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
pkgname=tinyssh
pkgver=20190101
pkgrel=3
pkgdesc='Small SSH server using NaCl / TweetNaCl'
url='https://tinyssh.org'
arch=('x86_64')
license=('custom:Public Domain')
depends=('glibc')
optdepends=('tinyssh-convert: For converting OpenSSH keys')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/janmojzis/${pkgname}/archive/${pkgver}.tar.gz"
	tinyssh@.socket tinyssh@.service tinysshgenkeys.service
        "${pkgname}-${pkgver}.tar.gz.asc::https://github.com/janmojzis/${pkgname}/releases/download/${pkgver}/${pkgver}.tar.gz.asc")
sha512sums=('bf3829a71c29e8b0d5c2a145be9dfaf05efb1b98932f34f19f76e07871da4be10022bdde4c57ca49557b69931077b451bc2efbeab38d8ef24151fb73990f1012'
            '9fbc5ae3b6d1df6d78eedf812fdd8fe115e81fc0811f4799d541e37239d53db22203e691026ed479a38c9f17043e52d43b44054be51ae03c2f12f3f235c5b83a'
            '9c9ba78c5b61f64f42f59cfe556519d9ab2c3e4d45f36d6f78c5e7728b050c494673239630dae66398006f3f20fb3a26f22899cd0728ed17e61c655b372236b3'
            '415cdfe649eedcbb11a263938c0a66bef42432210baa2e2c9e428d5e8e486fb4dedad53b9fdb9086bf97ca506909d3b3054aaca56f02c063e0b8d5fab4e56198'
            'SKIP')
validpgpkeys=('AADF2EDF5529F1702772C8A2DEC4D246931EF49B')

build () {
	cd "${srcdir}/${pkgname}-${pkgver//./}"
	make
}

package () {
	cd "${srcdir}/${pkgname}-${pkgver//./}"
	make install DESTDIR="${pkgdir}"

	# Nowadays /usr/sbin is a symlink to /usr/bin
	mv "${pkgdir}"/usr/{s,}bin

        # Multi-call binary
        ln -sf tinysshd "${pkgdir}"/usr/bin/tinysshd-makekey
        ln -sf tinysshd "${pkgdir}"/usr/bin/tinysshd-printkey
        
        install -m 755 -d "${pkgdir}/etc/tinyssh"

        install -m 755 -d "${pkgdir}/usr/lib/systemd/system"
	install -m 644 -t "${pkgdir}/usr/lib/systemd/system" \
		"${srcdir}/tinysshgenkeys.service" \
		"${srcdir}/tinyssh@.service" \
		"${srcdir}/tinyssh@.socket"

        install -Dm644 "$srcdir/$pkgname-$pkgver/LICENCE" "$pkgdir/usr/share/licenses/$pkgname/LICENCE"
}
