# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=scrot
pkgver=1.5
pkgrel=1
pkgdesc='Simple command-line screenshot utility for X'
url='https://github.com/resurrecting-open-source-projects/scrot'
arch=('x86_64')
license=('MIT')
depends=('giblib' 'libxfixes' 'libxcomposite')
makedepends=('autoconf-archive')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('371e1f602e9049998b054b7d27422fd105ed93047f1014a7e5df04ac697a1aa5d6186121f443f112fadb861d301f760bd32011133b1785c7313a80cdb908025f')
b2sums=('142748568b59dbea883fe57a5794431ca94c75a97f8b2b640bcbf4368602b0084ce83a52ea63534252ee8e58013db54b1436ac898ed7b78372ccd34aaa50801b')

prepare(){
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README.md ChangeLog -t "${pkgdir}"/usr/share/doc/${pkgname}
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
