# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Cookie Engineer <cookiengineer@protonmail.ch>

pkgname=('dark-reader' 'firefox-dark-reader' 'thunderbird-dark-reader')
pkgbase=dark-reader
pkgver=4.9.28
pkgrel=1
pkgdesc='Inverts brightness of web pages and aims to reduce eyestrain while browsing the web'
url=https://darkreader.org/
arch=('any')
license=('MIT')
makedepends=('npm')
source=("https://github.com/darkreader/darkreader/archive/v$pkgver/darkreader-v$pkgver.tar.gz")
b2sums=('d0b9e7d916dc3b81a6b37bbe43fa4ee5b3c810ccc927226f8a7535ded142d696bec7eba2585a98e94af2aefde05a9af3b9dca11fdb169836f1e26b08a25f83a2')

build() {
  cd darkreader-$pkgver
  npm install --frozen-lockfile
  npm run build
}

check() {
  cd darkreader-$pkgver
  npm test
}

package_dark-reader() {
  pkgdesc+=' (unpacked webextension)'
  cd darkreader-$pkgver
  install -d "$pkgdir"/usr/lib/$pkgbase
  cp -r build/* "$pkgdir"/usr/lib/$pkgbase
}

package_firefox-dark-reader() {
  groups=('firefox-addons')
  cd darkreader-$pkgver
  install -Dm644 build-firefox.xpi \
    "$pkgdir"/usr/lib/firefox/browser/extensions/addon@darkreader.org.xpi
}

package_thunderbird-dark-reader() {
  cd darkreader-$pkgver
  install -Dm644 build-thunderbird.xpi \
    "$pkgdir"/usr/lib/thunderbird/extensions/addon@darkreader.org.xpi
}

# vim:set ts=2 sw=2 et:
