# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=alembic
pkgver=1.7.16
pkgrel=1
pkgdesc="An open framework for storing and sharing scene data"
arch=('x86_64')
url="http://www.alembic.io/"
license=('BSD')
depends=('openexr' 'boost-libs' 'hdf5')
makedepends=('cmake' 'boost' 'ninja')
source=($pkgname-$pkgver.tar.gz::https://github.com/alembic/alembic/archive/${pkgver}.tar.gz)
sha512sums=('bd4777dc4abf15bfb8307b00fd2d67671e89944d0dd8a74a657f4aee49e6a1cd0ab1fe5bb7f9afc63abcce2123466bb8a9a3886d41724ed2c242bc61d7d5b3a1')

build() {
  cd "${pkgname}-${pkgver}"

  cmake . \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_HDF5=ON

  ninja -C build
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  DESTDIR="${pkgdir}/" ninja -C build install
}

# vim:set ts=2 sw=2 et:
