# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: orbisvicis

pkgname=zita-convolver
pkgver=4.0.3
pkgrel=2
pkgdesc="A fast, partitioned convolution engine library."
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL3')
depends=('fftw' 'gcc-libs' 'glibc')
provides=('libzita-convolver.so')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('62d7841757f10c094e43ed755e187f947c5743f302ed2a1ee6064a850c18921466f4505d8a2a7b3ad23619db7f1ad7307e1dfb2e8a1e7685e60ece2ffff4f6ca')

prepare() {
  cd "${pkgname}-${pkgver}"
  # FS#36248 - strip -march=native
  # call ldconfig in local context
  sed -e '/native/d' \
      -e 's/ldconfig/& -N $(DESTDIR)\/$(LIBDIR)/' \
      -i source/Makefile
}

build() {
  cd "${pkgname}-${pkgver}"
  make -C source
}

package() {
  cd "${pkgname}-${pkgver}"
  make SUFFIX="" PREFIX=/usr DESTDIR="${pkgdir}/" install -C source
  # docs
  install -vDm 644 {AUTHORS,README} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
