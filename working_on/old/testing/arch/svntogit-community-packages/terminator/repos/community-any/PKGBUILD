# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: fancris3 <fancris3 at gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=terminator
pkgver=2.0.1
pkgrel=1
pkgdesc='Terminal emulator that supports tabs and grids'
arch=('any')
url='https://github.com/gnome-terminator/terminator'
license=('GPL2')
depends=('gsettings-desktop-schemas' 'libkeybinder3' 'libnotify' 'python-cairo' 'python-configobj'
         'python-dbus' 'python-psutil' 'python-gobject' 'vte3' 'xdg-utils')
makedepends=('intltool' 'python-pip' 'python-setuptools' 'python-wheel')
source=(${url}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc}
        0001-fixed-validate-module-import-for-newer-version-of-co.patch)

validpgpkeys=('6FBDB41587919A0F4BA96EBA9A358BAB04A4ED27'  #Markus Frosch <markus@lazyfrosch.de>
              'D11A7596F61705480C711598F2FAC7C7BAE930A5') #Matt Rose <mattrose@folkwolf.net>
md5sums=('080d3e2c7628aa4e8d7844ad6fd77473'
         'SKIP'
         '585159c653affe58d99bb4858360f21b')
sha512sums=('bc8094944952bec96b6dc92ca25453fe19076daa23f0b672edc664e9ecdf83a58c5a2a161a26c1a44280a01601827cb728766ec9d20bc3d303072ae040ea2352'
            'SKIP'
            'da609bbb1f2fce64bd345ec43c05f6b8d76046cfbd1be764d6fcd555d306dfca1dd5786b0fc65c0984e49a85454c9eeabf5a1f8c960412c14a97c5350912f8a9')

prepare () {
    cd "${srcdir}/${pkgname}-${pkgver}"

    #Fix for python configobj validate
    patch -Np1 < $srcdir/0001-fixed-validate-module-import-for-newer-version-of-co.patch
}
build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    python setup.py build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    python setup.py install --skip-build --root="${pkgdir}" --optimize=1

    rm -f "${pkgdir}/usr/share/icons/hicolor/icon-theme.cache"
}
