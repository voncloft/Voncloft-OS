# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=("lua-luassert" "lua53-luassert" "lua52-luassert" "lua51-luassert")
pkgver=1.8.0
pkgrel=7
pkgdesc='Assertion library for Lua'
arch=('any')
url="https://github.com/Olivine-Labs/luassert"
license=('MIT')
makedepends=('luarocks'
             'lua51' 'lua52' 'lua53' 'lua' # https://github.com/luarocks/luarocks/issues/1275
            )
depends=()
source=("$pkgname-$pkgver.tar.gz::https://github.com/Olivine-Labs/luassert/archive/v$pkgver.tar.gz")
sha256sums=('5955234a63fdc208674314b2e97982bb551459b85b8ff11611a50421c6a29c76')

build() {
  cd "luassert-$pkgver"
  for v in 5.1 5.2 5.3 5.4; do
    mkdir -p "$v/"
    luarocks make --lua-version="$v" \
      --pack-binary-rock --deps-mode=none \
      luassert-"$pkgver"-0.rockspec
    mv luassert-"$pkgver"-0.*.rock "$v/"
  done
}

package_lua-luassert() {
  pkgdesc="$pkgdesc 5.4"
  depends+=('lua-say')

  cd "luassert-$pkgver"
  luarocks install --lua-version=5.4 --tree="$pkgdir/usr/" --deps-mode=none --no-manifest 5.4/*.rock
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua53-luassert() {
  pkgdesc="$pkgdesc 5.3"
  depends+=('lua53-say')

  cd "luassert-$pkgver"
  luarocks install --lua-version=5.3 --tree="$pkgdir/usr/" --deps-mode=none --no-manifest 5.3/*.rock
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua52-luassert() {
  pkgdesc="$pkgdesc 5.2"
  depends+=('lua52-say')

  cd "luassert-$pkgver"
  luarocks install --lua-version=5.2 --tree="$pkgdir/usr/" --deps-mode=none --no-manifest 5.2/*.rock
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua51-luassert() {
  pkgdesc="$pkgdesc 5.1"
  depends+=('lua51-say')

  cd "luassert-$pkgver"
  luarocks install --lua-version=5.1 --tree="$pkgdir/usr/" --deps-mode=none --no-manifest 5.1/*.rock
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
