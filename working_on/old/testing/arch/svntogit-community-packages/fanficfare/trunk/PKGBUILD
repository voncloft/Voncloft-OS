# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=FanFicFare
pkgname=fanficfare
pkgver=3.29.0
pkgrel=1
pkgdesc="A tool for downloading fanfiction to eBook formats"
arch=('any')
url="https://github.com/JimmXinu/${_pkgname}"
license=('Apache')
changelog=changelog.txt
depends=('python' 'python-beautifulsoup4' 'python-chardet' 'python-cloudscraper' 'python-html5lib'
         'python-html2text')
optdepends=('python-pillow: support for converting/resizing story images and covers')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('7165364dfcb99bb84a23547252653d7dcfeba52b57dc167b3d6948dad8c67ad4')
b2sums=('1063e4530bd600cf9ab61efbe21c172bb2ee0dec03506aaed999f3c6c6595f6c9a7971bf01cf2613cb804730213694ae73a75d1a9d355a4893ead2accf2c2945')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    python setup.py build
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
