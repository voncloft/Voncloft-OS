# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Rob Til Freedmen <rob.til.freedman@gmail.com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>

pkgname=hydrogen
pkgver=1.0.1
pkgrel=1
pkgdesc="An advanced drum machine"
arch=('x86_64')
license=('GPL2')
groups=('pro-audio')
url="http://www.hydrogen-music.org/"
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base' 'qt5-xmlpatterns')
makedepends=('alsa-lib' 'cmake' 'jack' 'ladspa' 'libarchive' 'liblo' 'liblrdf'
'libpulse' 'libsndfile' 'portaudio' 'portmidi' 'python' 'qt5-tools')
checkdepends=('cppunit')
optdepends=('new-session-manager: for session management')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}-music/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('66f2db3986a00832558432d17aa6c459a099f314daed23a8eeef34d83d894dfec71d96ceb09113d22be58e4bd47265c105cf57024c43fb5d925b18cabfb146f0')
b2sums=('eb5ac2b80abcdd8e4a6ad72c41b3575c9682e193727714bb13d684bbf79edb1cffde8c18f23ee9ff02de5ad3e15845b8a0fc1600fb8bdb33f90b89fd0973b32c')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -DWANT_DEBUG=OFF \
        -DWANT_CPPUNIT=ON \
        -DWANT_LRDF=ON \
        -DWANT_PORTAUDIO=ON \
        -DWANT_PORTMIDI=ON \
        -DWANT_PULSEAUDIO=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
  # update translations
  (
    cd data/i18n
    ./updateTranslations.sh
  )
}

package() {
  depends+=('libarchive.so' 'libasound.so' 'liblo.so' 'libjack.so' 'liblrdf.so'
  'liblo.so' 'libportaudio.so' 'libportmidi.so' 'libpulse.so' 'libsndfile.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install -C build
  # docs
  install -vDm 644 {ChangeLog,DEVELOPERS,INSTALL.md,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
