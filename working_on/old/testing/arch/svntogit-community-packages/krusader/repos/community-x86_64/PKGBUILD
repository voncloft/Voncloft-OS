# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=krusader
pkgver=2.7.2
pkgrel=4
pkgdesc="Advanced twin panel (commander style) file manager"
arch=(x86_64)
url="https://krusader.org/"
license=(GPL)
depends=(kparts hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('xz: LZMA and XZ archive support'
            'unzip: ZIP decompression support'
            'zip: ZIP archive support'
            'lhasa: LHA archive support'
            'cpio: cpio archive support'
            'unrar: RAR decompression support'
            'arj: ARJ archive support'
            'unarj: ARJ decompression support'
            'unace: ACE decompression support'
            'p7zip: 7z archive support'
            # additional apps
            'keditbookmarks: Manage Bookmarks functionality'
            'kde-cli-tools: manage file associations, root mode'
            'kompare: file contents comparison'
            'kdiff3: file contents comparison'
            'krename: advanced rename tool'
            'konsole: terminal'
            'ktexteditor: file editing support')
source=("https://download.kde.org/stable/krusader/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('41a39a43b3c42dd1d1ecaea86df30caff6a061fecc1d66f60859b2a3ca976109')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DKDESU_PATH="/usr/lib/kf5/kdesu"
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
