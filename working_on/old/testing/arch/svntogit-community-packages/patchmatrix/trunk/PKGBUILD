# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=patchmatrix
pkgver=0.22.0
pkgrel=1
pkgdesc="A JACK patchbay in flow matrix style"
arch=('x86_64')
url="https://git.open-music-kontrollers.ch/lad/patchmatrix/about/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'hicolor-icon-theme' 'libglvnd' 'libx11')
makedepends=('glew' 'glu' 'jack' 'lv2' 'meson')
source=("https://git.open-music-kontrollers.ch/lad/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('d2a35ef752cdccee6dbd9a145016768c48cb522f7b513ef55305ab1c20760aa37248a7f788334b50ff4b40b597eb253c37ea92da888bb31690bf946e19343088'
            'SKIP')
b2sums=('b15e8d14554936e5ed14a80590d0261a3f36cdf417d772e2157e0fcac07ecf5539ec62034655e192af6ce14aae56bd8d347bf8d1a09ddf17f202fbef7042922a'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr \
        --libexecdir lib \
        --sbindir bin \
        --buildtype plain \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  meson test -C build
}

package() {
  depends+=('libGLEW.so' 'libjack.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
