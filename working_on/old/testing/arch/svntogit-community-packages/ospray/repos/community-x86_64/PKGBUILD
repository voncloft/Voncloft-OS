# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ospray
pkgver=1.8.5
pkgrel=1
pkgdesc="A Ray Tracing Based Rendering Engine for High-Fidelity Visualization"
arch=(x86_64)
url="https://www.ospray.org/"
license=(Apache)
depends=(intel-tbb embree ispc libgl glfw glu openmpi)
makedepends=(cmake)
source=("https://github.com/ospray/OSPRay/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('6d85e103280aa4c8d0032a2cc3082f08a6021a79d22cf4a8e38b09f152f35f53')

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DOSPRAY_BUILD_ISA=ALL \
        -DOSPRAY_ENABLE_TESTING=ON \
        -DOSPRAY_MODULE_MPI=ON
    make
}

check() {
    cd build
    make test
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
    # Remove tests
    rm "${pkgdir}"/usr/bin/test_*
    rm -r "${pkgdir}"/usr/scripts
}
