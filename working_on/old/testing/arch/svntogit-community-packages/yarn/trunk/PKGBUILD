# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Jan Koppe <post@jankoppe.de>

pkgname=yarn
pkgver=1.22.10
pkgrel=1
pkgdesc='Fast, reliable, and secure dependency management'
arch=(any)
url='https://classic.yarnpkg.com/'
license=('BSD')
depends=('nodejs')
source=("https://github.com/yarnpkg/yarn/releases/download/v$pkgver/yarn-v$pkgver.tar.gz")
sha512sums=('d75dccd318f280abcbfd8d78b63546e11e7a8459c5dd737d0c8da2742cd5d7e3d7a5e668ce680234b0c09e71d128787777061d24c6e3ae4b6626086461e3ce25')

package() {
  cd yarn-v${pkgver}
  install -dm755  "$pkgdir"/usr/lib/node_modules/yarn
  cp -R * "$pkgdir"/usr/lib/node_modules/yarn

  install -dm755 "$pkgdir"/usr/bin
  ln -s /usr/lib/node_modules/yarn/bin/yarn.js "$pkgdir"/usr/bin/yarn
  ln -s /usr/lib/node_modules/yarn/bin/yarn.js "$pkgdir"/usr/bin/yarnpkg

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
