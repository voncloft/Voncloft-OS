# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-json-any
pkgver=1.39
pkgrel=5

pkgdesc="(DEPRECATED) Wrapper Class for the various JSON classes"
url='http://search.cpan.org/dist/JSON-Any'
arch=('any')
license=('GPL' 'PerlArtistic')

depends=('perl-json>=2.02' 'perl-json-xs>=2.3' 'perl-yaml-syck' 'perl-namespace-clean')
makedepends=('perl-test-fatal' 'perl-test-requires' 'perl-test-warnings>=0.009' 'perl-test-without-module')

source=("https://cpan.metacpan.org/authors/id/E/ET/ETHER/JSON-Any-$pkgver.tar.gz")

sha1sums=('2c7e404fc4a398359693d62e9c74994f9273dd4c')

options=('!emptydirs')

build() {
	cd JSON-Any-"$pkgver"

	# Setting these env variables overwrites any command-line-options we don't want...
	export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
	       PERL_MM_OPT="" PERL_MB_OPT="" MODULEBUILDRC=/dev/null

	perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd JSON-Any-"$pkgver"
	make test
}

package() {
	cd JSON-Any-"$pkgver"
	make install DESTDIR="$pkgdir"
	find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
