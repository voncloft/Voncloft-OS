# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=byuu
pkgver=4
pkgrel=1
pkgdesc='A multi-system emulator focused on performance, features, and ease of use'
arch=(x86_64)
url=https://byuu.org/
license=(GPL3)
depends=(
  gcc-libs
  glibc
  libao
  libasound.so
  libgl
  libpulse-simple.so
  libpulse.so
  libudev.so
  libx11
  libxext
  libxrandr
  libxv
  openal
  qt5-base
  sdl2
)
makedepends=(
  git
)
source=(
  git+https://github.com/byuu/byuu.git#tag=3455d1f5bd3dd72797660384f4a35c8b6dfeed64
  git+https://github.com/byuu/bsnes.git#tag=8e80d2f8a43e34a82931e25143b279e5fbcfaedc
  byuu-flags.patch
  byuu-paths.patch
)
sha256sums=('SKIP'
            'SKIP'
            '514bce6f87f8ffc2476d0a5e5948fc79ab4470d0514d089cd8ac2386f8532730'
            '5596804071813a5745d38eda21a9b2df6470f24db27c1d99c7284cbc97f27fda')

pkgver() {
  cd byuu

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd byuu

  patch -Np1 -i ../byuu-flags.patch
  patch -Np1 -i ../byuu-paths.patch
  sed 's|/usr/local/lib/qt5/bin/moc|moc|' -i hiro/GNUmakefile
}

build() {
  cd byuu

  make -C higan hiro=qt5
}

package() {
  install -dm 755 "${pkgdir}"/usr/share/byuu

  install -Dm 755 byuu/higan/out/byuu -t "${pkgdir}"/usr/bin/
  install -Dm 644 byuu/higan/target-byuu/resource/byuu.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm 644 byuu/higan/target-byuu/resource/byuu.png -t "${pkgdir}"/usr/share/pixmaps/
  cp -dr --no-preserve='ownership' bsnes/bsnes/Database "${pkgdir}"/usr/share/byuu/
  cp -dr --no-preserve='ownership' bsnes/shaders "${pkgdir}"/usr/share/byuu/Shaders
}

# vim: ts=2 sw=2 et:
