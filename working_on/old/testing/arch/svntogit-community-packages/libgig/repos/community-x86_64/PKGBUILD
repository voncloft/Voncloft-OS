# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: svoufff <svoufff at gmail dot com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=libgig
pkgver=4.2.0
pkgrel=3
pkgdesc="C++ library for loading, modifying and creating .gig, .ksf, .kmp, .sf2 and DLS files"
arch=('x86_64')
url="https://www.linuxsampler.org/libgig/"
license=('GPL2')
depends=('util-linux' 'libsndfile')
provides=('libgig.so')
source=("https://download.linuxsampler.org/packages/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('52c8c749c7852e229c76c17211398dbf5dd5891a1ee9d8e8bc61d55b825c5676595b20b26014e739b9bfd387db36895152b380519609a928a8fa68f4a20279f6')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
  # creating a ld.so.conf entry for libgig.so
  echo "/usr/lib/${pkgname}" > "${pkgname}.conf"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  # adding libgig.so library path to ld.so.conf (required for dependants during
  # runtime)
  install -vDm 644 "${pkgname}.conf" -t "${pkgdir}/etc/ld.so.conf.d/"
}
# vim:set ts=2 sw=2 et:
