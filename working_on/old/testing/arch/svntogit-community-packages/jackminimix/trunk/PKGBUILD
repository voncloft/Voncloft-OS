# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jackminimix
pkgver=0.2.1
pkgrel=2
pkgdesc="A simple mixer for the Jack Audio Connection Kit with an OSC based control interface"
arch=('x86_64')
url="https://www.aelius.com/njh/jackminimix/"
license=('GPL2')
groups=('pro-audio')
depends=('glibc' 'libjack.so' 'liblo.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/njh/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('f99bd0de60624b185a8f6d9c254f52385a74efbbad172ffcb5fe0225648e04bac28d75883c1fc5bba47457ce87ef819cbf1b57d3e758fd07048cc227f57024eb')
validpgpkeys=()

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,{NEWS,README}.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
