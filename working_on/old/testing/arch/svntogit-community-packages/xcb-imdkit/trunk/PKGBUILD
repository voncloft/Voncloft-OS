# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=xcb-imdkit
pkgver=1.0.2
pkgrel=1
pkgdesc="Input method development support for xcb"
arch=('x86_64')
url="https://github.com/fcitx/xcb-imdkit"
license=('GPL')
depends=('xcb-util' 'xcb-util-keysyms')
makedepends=('extra-cmake-modules' 'ninja' 'uthash' 'xorgproto')
source=("https://download.fcitx-im.org/fcitx5/xcb-imdkit/xcb-imdkit-$pkgver.tar.xz"{,.sig})
sha512sums=('fc34c4d021fe1e3afb0fd140849c102c2c3d3040429dfef2ffe6032c3e87b7212abcc87224a0a638bff7e5ce17c2a1e770e2e501bca7c336af16aa4f31c530b3'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build() {
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

check() {
  cd $pkgname-$pkgver
  ninja test
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
