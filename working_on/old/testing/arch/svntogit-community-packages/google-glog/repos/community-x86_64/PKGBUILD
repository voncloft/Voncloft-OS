# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ian Yang <doit.ian@gmail.com>

pkgname=google-glog
pkgver=0.4.0
pkgrel=2
pkgdesc="Logging library for C++"
arch=('x86_64')
license=('custom:BSD3')
url='https://github.com/google/glog'
depends=('gcc-libs')
source=("glog-$pkgver.tar.gz::https://github.com/google/glog/archive/v$pkgver.tar.gz")
sha512sums=('b585f1819ade2075f6b61dc5aaca5c3f9d25601dba2bd08b6c49b96ac5f79db23c6b7f2042df003f7130497dd7241fcaa8b107d1f97385cb66ce52d3c554b176')

build() {
  cd glog-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd glog-$pkgver

  make DESTDIR="$pkgdir" install

  # Lazy way of dealing with conflicting man and info pages...
  rm -rf "$pkgdir"/usr/share

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
