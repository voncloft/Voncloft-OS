# Maintainer: Konstantin Gizdov <arch@kge.pw>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgbase=opencv-gcc8
_pkgbase=opencv
pkgname=(opencv-gcc8 opencv-gcc8-samples)
_pkgname=${_pkgbase}
pkgver=4.1.1
pkgrel=1
pkgdesc="Open Source Computer Vision Library compiled with GCC 8"
arch=(x86_64)
license=(BSD)
url="https://opencv.org/"
depends=(intel-tbb openexr gst-plugins-base libdc1394 cblas lapack libgphoto2 jasper ffmpeg gcc8 gcc8-libs)
makedepends=(cmake python-numpy python-setuptools mesa eigen hdf5 lapacke gtk3 vtk glew ant java-environment)
optdepends=('opencv-samples: samples'
            'gtk3: for the HighGUI module and the Python bindings'
            'vtk: for the viz module'
            'hdf5: for the HDF5 module and the Python bindings'
            'opencl-icd-loader: For coding with OpenCL'
            'python-numpy: Python bindings'
            'java-runtime: Java interface')
source=("${_pkgbase}-$pkgver.tar.gz::https://github.com/opencv/opencv/archive/$pkgver.zip"
        "opencv_contrib-$pkgver.tar.gz::https://github.com/opencv/opencv_contrib/archive/$pkgver.tar.gz"
        opencv-includedir.patch)
sha256sums=('d34985c7b4283519c032e3585b30846644f56c81acba35693295ff8930f080aa'
            '9f85d380758498d800fec26307e389620cde8b1a2e86ab51cddc5200fbe37102'
            'a96e35c9592e655b21a62cfe04e864a10e21535ad900e5de67356b9e9f40ca10')

prepare() {
  mkdir -p build

  cd ${_pkgname}-$pkgver
  patch -p1 -i ../opencv-includedir.patch # Fix wrong include patch in pkgconfig file
}

build() {
  cd build
  export JAVA_HOME="/usr/lib/jvm/default"
  # cmake's FindLAPACK doesn't add cblas to LAPACK_LIBRARIES, so we need to specify them manually
  _pythonpath=`python -c "from sysconfig import get_path; print(get_path('platlib'))"`
  CC=/usr/bin/gcc-8 \
  CXX=/usr/bin/g++-8 \
  cmake ../${_pkgname}-$pkgver \
    -DWITH_OPENCL=ON \
    -DWITH_OPENGL=ON \
    -DWITH_TBB=ON \
    -DOpenGL_GL_PREFERENCE=GLVND \
    -DBUILD_WITH_DEBUG_INFO=OFF \
    -DBUILD_TESTS=OFF \
    -DBUILD_PERF_TESTS=OFF \
    -DBUILD_EXAMPLES=ON \
    -DINSTALL_C_EXAMPLES=ON \
    -DINSTALL_PYTHON_EXAMPLES=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCPU_BASELINE_DISABLE=SSE3 \
    -DCPU_BASELINE_REQUIRE=SSE2 \
    -DOPENCV_EXTRA_MODULES_PATH="$srcdir/opencv_contrib-$pkgver/modules" \
    -DOPENCV_SKIP_PYTHON_LOADER=ON \
    -DOPENCV_PYTHON3_INSTALL_PATH=$_pythonpath \
    -DLAPACK_LIBRARIES="/usr/lib/liblapack.so;/usr/lib/libblas.so;/usr/lib/libcblas.so" \
    -DLAPACK_CBLAS_H="/usr/include/cblas.h" \
    -DLAPACK_LAPACKE_H="/usr/include/lapacke.h" \
    -DOPENCV_GENERATE_PKGCONFIG=ON \
    -DOPENCV_ENABLE_NONFREE=ON \
    -DOPENCV_JNI_INSTALL_PATH=lib \
    -DOPENCV_GENERATE_SETUPVARS=OFF \
    -DEIGEN_INCLUDE_PATH=/usr/include/eigen3
  make
}

package_opencv-gcc8() {
  provides=('opencv')
  conflicts=('opencv')
  cd build
  make DESTDIR="$pkgdir" install

  # install license file
  install -Dm644 "$srcdir"/${_pkgname}-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/${_pkgname}

  # separate samples package
  cd "$pkgdir"/usr/share
  mv opencv4/samples "$srcdir"/${_pkgname}-samples
}

package_opencv-gcc8-samples() {
  pkgdesc+=" (samples)"
  depends=("opencv-gcc8=$pkgver")
  provides=("opencv-samples=$pkgver")
  conflicts=("opencv-samples=$pkgver")
  unset optdepends

  mkdir -p "$pkgdir"/usr/share/opencv
  cp -r "$srcdir"/opencv-samples "$pkgdir"/usr/share/opencv/samples
  # fix permissions
  chmod 755 "$pkgdir"/usr/share/opencv/samples/*

  # install license file
  install -Dm644 "$srcdir"/opencv-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/${_pkgname}
}
