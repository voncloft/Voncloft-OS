# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=gnokii
pkgver=0.6.31
pkgrel=19
pkgdesc="Tools and user space driver for use with mobile phones"
arch=('x86_64')
url="https://www.gnokii.org/"
license=('GPL')
depends=('libusb-compat' 'libxpm' 'bluez-libs' 'libical' 'sqlite' 'systemd')
makedepends=('gtk2' 'mariadb-libs' 'postgresql-libs' 'intltool')
optdepends=('dialog: sendsms tool'
            'gtk2: xgnokii GUI'
            'mariadb-libs: smsd mysql backend'
            'postgresql-libs: smsd postgresql backend')
backup=('etc/gnokiirc')
install=$pkgname.install
source=("https://www.gnokii.org/download/$pkgname/$pkgname-$pkgver.tar.bz2"
        'gnokii-config.patch'
        'gnokii-lock.patch'
        'gnokii-gcc5.patch'
        'gnokii-gcc7.patch')
md5sums=('d9627f4a1152d3ea7806df4532850d5f'
         'b2961b52ac1f770c4704ccc50b64fde2'
         '17b629db85a6bb2b98ca59941aa95295'
         '5bcf2dadd31a826d1f80b53bb37d9bd0'
         '338e376185ff5323c5fdd3538890f776')

prepare() {
  cd $pkgname-$pkgver

  # Set bindir location
  patch -Np1 -i "$srcdir/gnokii-config.patch"

  # Set lock path
  patch -Np1 -i "$srcdir/gnokii-lock.patch"

  # Fix build with GCC 5 (Fedora patch)
  patch -Np1 -i "$srcdir/gnokii-gcc5.patch"

  # Fix build with GCC 7
  patch -Np1 -i "$srcdir/gnokii-gcc7.patch"
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin \
              --enable-security
  make
  pushd xgnokii
  make
  popd
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  pushd xgnokii
  make DESTDIR="$pkgdir" install
  popd

  # Install initial config file
  install -Dm644 Docs/sample/gnokiirc "$pkgdir/etc/gnokiirc"

  # Fix file permission
  chmod 755 "$pkgdir/usr/bin/sendsms"

  # Setup lock directory
  install -dm755 "$pkgdir"/usr/lib/tmpfiles.d
  echo 'd /run/lock/lockdev 0775 root lock -' > "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
