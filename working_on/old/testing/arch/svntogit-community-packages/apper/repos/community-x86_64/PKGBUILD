# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: BlackIkeEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Valeriy Lyasotskiy <onestep@ukr.net>
# Contributor: Zom <zom@eevul.org>

pkgname=apper
pkgver=1.0.0
pkgrel=4
pkgdesc="An application and package manager using PackageKit"
arch=(x86_64)
url="https://www.kde.org/applications/system/apper/"
license=(GPL)
depends=(packagekit-qt5 kcmutils appstream-qt qt5-xmlpatterns)
makedepends=(extra-cmake-modules kdoctools plasma-workspace)
optdepends=('plasma-workspace: Updates daemon')
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig} apper-rpath.patch
        apper-qt5.11.patch::"https://cgit.kde.org/apper.git/patch/?id=d8960ea9"
        apper-qt5.14.patch::"https://cgit.kde.org/apper.git/patch/?id=1815d19f"
        kdebug-401461.patch::"https://cgit.kde.org/apper.git/patch/?id=d486706f")
sha256sums=('1a30be92aab8bd258c2a8824f533c5646b934e06b4268edbd11724ea450f4923'
            'SKIP'
            '17ce82a3f9ba8888febc5d2b2dc63e5c73d7be4b216e9110f0ca73050be0010d'
            '05d9f54d788256ec658cadec6571f3ec649ab3e82adb8daa2e7209aa9c1ff062'
            '1275d8af4d61d50e3d39a6507395bb4142572720937e9ebfe9679c69b53ce402'
            '2f66e12f40c68fb1b8c7a1d4fcf71adfdd4dceb9ecf0806c636fbf20d55e2154')
validpgpkeys=(70C26659D184ABC01FFAF45228DDEDC6E4480AD0) # Daniel Nicoletti <dantti12@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../apper-rpath.patch # Fix RPATH of apper binary
  patch -p1 -i ../apper-qt5.11.patch # fix build with Qt 5.11
  patch -p1 -i ../apper-qt5.14.patch # fix build with Qt 5.14
  patch -p1 -i ../kdebug-401461.patch # fix crash
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DAPPSTREAM=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

# fix conflicts with gnome-software
  mv "$pkgdir"/usr/share/dbus-1/services/{,kde_}org.freedesktop.PackageKit.service
}
