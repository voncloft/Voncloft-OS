# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Maintainer: Jason Chu <jason@archlinux.org>

pkgname=procstatd
pkgver=1.4.2
pkgrel=10
pkgdesc="A daemon that can be run either from userspace or inetd that can be queried via a simple API to parse various files in /proc"
arch=('x86_64')
license=('GPL')
url="http://www.phy.duke.edu/brahma/"
source=(https://downloads.sourceforge.net/herdtools/$pkgname.tar.gz)
sha512sums=('02f10a87f9325f0252786a598433596ad1dc2c6412ff8b6fe1f8e19a6e1425497bc0fcf0be085ed7be9434642c7a1f22d4f8018787ab2f6dffe0d1aa566dfcc5')

build() {
  cd "$srcdir"/$pkgname
  # Get rid of the funky make install forever bug
  sed -i 's/make install/echo/;s/cd watchman/echo/;s@$(INSTDIR)/sbin@$(INSTDIR)/bin@' Makefile
  make
}

package() {
  cd "$srcdir"/$pkgname
  make INSTDIR="$pkgdir"/usr install
  mkdir "$pkgdir"/usr/share
  mv "$pkgdir"/usr/man "$pkgdir"/usr/share/man
}
