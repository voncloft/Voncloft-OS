# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Kevin Azzam <aur at kevin.azz.am>
# Contributor: Ye Shu <shuye02 at outlook.com>

pkgname=onionshare
pkgver=2.2
pkgrel=4
pkgdesc='Share a file over Tor Hidden Services anonymously and securely'
url='https://github.com/micahflee/onionshare'
arch=('any')
license=('GPL3')
depends=('python' 'python-setuptools' 'hicolor-icon-theme' 'tor' 'stem' 'python-requests' 'python-pysocks' 'python-flask' 'python-flask-httpauth' 'python-pycryptodome' 'python-pyqt5')
optdepends=(
  'python-nautilus: enable Nautilus right-click extension'
  'obfs4proxy: tor bridge support'
)
checkdepends=('python-pytest')
source=(https://github.com/micahflee/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/micahflee/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha512sums=('fbb457971213b49c535c471e87ca4e5f9493da06a22a457b016517a3094ac594f4b059ec8ea8ce557234ba59960718c489ed42c587f008f92b85e44b74ce6b07'
            'SKIP')
b2sums=('91fa39c116d0eae0b22e1efcfbe636a97b4059553af39a3723f323e89d4080b2da9798bbc3cc6ad2849875806920bc4fb84e6784cec2102aca8bdbb9235499ea'
        'SKIP')

validpgpkeys=('927F419D7EC82C2F149C1BD1403C2657CD994F73')

build() {
  cd ${pkgname}
  python setup.py build
}

check() {
  cd ${pkgname}
  pytest tests/
}

package() {
  cd ${pkgname}
  python setup.py install --skip-build -O1 --root="${pkgdir}"
}

# vim: ts=2 sw=2 et:
