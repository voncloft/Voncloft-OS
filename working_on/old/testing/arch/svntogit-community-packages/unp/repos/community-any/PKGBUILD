# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Michael DÃ¼ll <mail@akurei.me> PGP-Key: AAAEE882
# Contributor: TDY <tdy@gmx.com>
# Contributor: Sergio Rubio <rubiojr.ml@gmail.com>
# Contributor: Hannes Rist <cowider@gmail.com>
 
pkgname=unp
pkgver=2.0~pre7+nmu1
pkgrel=4
pkgdesc='A script for unpacking a wide variety of archive formats'
arch=(any)
url='https://packages.qa.debian.org/u/unp.html'
license=(GPL)
depends=(perl)
optdepends=(
  'unarj: arj support'
  'unrar: rar support'
  'unzip: zip support'
  'bzip2: bzip2 support'
  'p7zip: p7zip support'
  'unace: ace support'
  'cpio: cpio support'
  'sharutils: sharutils'
  'cabextract: cab support'
  'rpmextract: rpm support'
)
source=(http://ftp.debian.org/debian/pool/main/u/unp/unp_$pkgver.tar.bz2)
sha1sums=('b91f4cbc4720b3aace147652ac2043cf74668244')
 
build() {
  cd unp-$pkgver/po
  make
}
 
package() {
  cd unp-$pkgver
  install -Dm755 unp "$pkgdir"/usr/bin/unp
  install -Dm755 ucat "$pkgdir"/usr/bin/ucat
  install -Dm644 debian/unp.1 "$pkgdir"/usr/share/man/man1/unp.1
  install -Dm755 bash_completion.d/unp "$pkgdir"/usr/share/bash-completion/completions/unp

  cd po
  make DESTDIR="$pkgdir" install
}
