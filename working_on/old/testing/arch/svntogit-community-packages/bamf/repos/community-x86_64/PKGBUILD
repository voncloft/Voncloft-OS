# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Balló György <ballogyor+arch@gmail.com>

pkgname=bamf
pkgver=0.5.4
pkgrel=2
pkgdesc='Application matching framework'
arch=(x86_64)
url=https://launchpad.net/bamf
license=(GPL)
depends=(
  libgtop
  libwnck3
)
makedepends=(
  git
  gnome-common
  gobject-introspection
  vala
)
source=(
  git+https://git.launchpad.net/bamf?signed#tag=75326c28cd519891dbd9d6f1e96be9e528746052
  bamf-no-gtester2xunit.patch
  bamf-no-werror.patch
)
validpgpkeys=(45B1103FB93ACBD90296DBCAE83D089481836EBF) # Marco Trevisan
sha256sums=('SKIP'
            'a330601f7bd332e96088b318374ca35515b6687b5bbe2c9c51f483bb7286a59b'
            '3e93850927e2f70bc8af96587307dfa38759c850bf89ef9d2775c7432e7f2041')

pkgver() {
  cd bamf

  git describe --tags
}

prepare() {
  cd bamf

  patch -Np1 -i ../bamf-no-gtester2xunit.patch
  patch -Np1 -i ../bamf-no-werror.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd bamf

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make DESTDIR="${pkgdir}" -C bamf install
}

# vim: ts=2 sw=2 et:
