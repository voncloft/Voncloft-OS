# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=spring
pkgver=104.0
pkgrel=11
pkgdesc='A free 3D real-time-strategy (RTS) game engine'
arch=('x86_64')
url="http://springrts.com/"
license=('GPL')
depends=('openal' 'glew' 'boost-libs' 'freetype2' 'devil' 'libvorbis' 'sdl2' 'libunwind'
         'libxcursor' 'curl' 'shared-mime-info' 'desktop-file-utils' 'libx11')
makedepends=('boost' 'cmake' 'zip' 'xz' 'p7zip' 'python' 'jdk8-openjdk' 'mesa')
optdepends=('python: python-based bots'
            'java-runtime: java-based bots')
source=(https://downloads.sourceforge.net/sourceforge/springrts/${pkgname}_${pkgver}_src.tar.lzma
        fix-6358-compile-error-because-of-new-openal-version.patch
        fix-reinterpret_cast-in-constexpr.patch
        fix-include.patch)
sha512sums=('be8922e798d7c959c248501494ab61cff625154c109debd324875a4e2a7149748224b6fcb428948f2b4d66cc9e484bc002a63810f55c5f25a499c34a01dfc225'
            '76cf4c596f87f8306ad19ae82d68a36db250fbf7e2e3656c05453be761d13fd1a5cbc8e6499c24b46af993566ed624da744b9e21b240627c214ae4b505f0701b'
            'ba8a6f3031c7507983f2ae10de9f19b27434cd9159df6ce431d1f83b21b37974e8add4bea8bc0d738f88f5bfebdbeeacaeb3964b9db31b5181d12bed66ff2109'
            '0fd54748e3dfa3792ff66b6cbe2c482f1d553033dc48f6279863ad0b07aaff15069669b1d1d85be11e89fdad2faba5f16e4b9c99af718da0152ac9aba7b0f75a')

prepare() {
  cd spring_$pkgver

  # https://springrts.com/mantis/view.php?id=5781
  # https://github.com/spring/spring/commit/6104061fe3
  patch -Np1 -i ../fix-reinterpret_cast-in-constexpr.patch

  # https://springrts.com/mantis/view.php?id=6358
  patch -Np1 -i ../fix-6358-compile-error-because-of-new-openal-version.patch

  patch -Np1 -i ../fix-include.patch
}

build() {
  cd spring_${pkgver}

  cmake	. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDATADIR=share/spring \
    -DJAVA_HOME=/usr/lib/jvm/java-8-openjdk \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd spring_${pkgver}

  DESTDIR="$pkgdir" make install

  install -d "$pkgdir/etc/spring"
  echo '$HOME/.spring' > "$pkgdir/etc/spring/datadir"
}

# vim sw=2:ts=2 et:
