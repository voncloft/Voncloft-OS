# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-html-tree
pkgver=5.07
pkgrel=2
pkgdesc="Make parse trees out of HTML source"
arch=('any')
url="https://search.cpan.org/dist/HTML-Tree"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0' 'perl-html-tagset>=3.02' 'perl-html-parser>=3.46')
makedepends=('perl-test-fatal' 'perl-module-build')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/K/KE/KENTNL/HTML-Tree-$pkgver.tar.gz)
sha256sums=('f0374db84731c204b86c1d5b90975fef0d30a86bd9def919343e554e31a9dbbf')

build() {
  cd  "$srcdir"/HTML-Tree-$pkgver
  perl Build.PL installdirs=vendor destdir="$pkgdir/"
  perl Build
}

package() {
  cd  "$srcdir"/HTML-Tree-$pkgver
  perl Build install
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
