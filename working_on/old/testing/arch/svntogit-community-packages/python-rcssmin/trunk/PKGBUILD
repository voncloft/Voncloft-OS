# Maintainer: David Runge <dvzrv@archlinux.org>

_name=rcssmin
pkgname=python-rcssmin
pkgver=1.0.6
pkgrel=4
pkgdesc="Fast CSS minifier for Python"
arch=('x86_64')
url="https://github.com/ndparker/rcssmin"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('5129a6007755a5b1949256bba7ad3616a08034d7bfcf06a1f9d8143897dddc5d2f6b8a0ebde7abb2e8b12e5b1aee2e46260e59101a78f35026e15f9f6503db77')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # in the next release hopefully plain pytest -v
  python run_tests.py -q tests
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  mv -v "${pkgdir}/usr/share/doc/"{${_name},${pkgname}}
}
