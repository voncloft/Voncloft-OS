# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-sniffio
pkgver=1.2.0
pkgrel=3
pkgdesc='Sniff out which async library your code is running under'
arch=(any)
url=https://github.com/python-trio/sniffio
license=(MIT)
depends=(python)
makedepends=(
  git
  python-curio
  python-pytest
  python-setuptools
)
_tag=b93038b1a8b76607363f2f261bcf8e7ad4c53807
source=(git+https://github.com/python-trio/sniffio.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd sniffio

  git describe --tags | sed 's/^v//'
}

build() {
  cd sniffio

  python setup.py build
}

check() {
  cd sniffio

  python setup.py test
}

package() {
  cd sniffio

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-sniffio/
}

# vim: ts=2 sw=2 et:
