# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Philip Nilsson <pnilsson@nullref.se>

pkgname=alex
pkgver=3.2.6
pkgrel=2
pkgdesc='Lexical analyser generator for Haskell'
arch=(x86_64)
url='https://hackage.haskell.org/package/alex'
license=(BSD)
depends=(ghc-libs)
makedepends=(ghc happy)
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('05b6e55e9a1c348aea599d1130780a3d2a0a7c5f1db00b2c98bd34737cac9d48842fb1b1f98947a99d99013f94e39a6ffee1af8546f02fb2d32fcf362ff0f83c')

build() {
  cd $pkgname-$pkgver
  runghc Setup.hs configure \
    --prefix=/usr \
    --datasubdir=$pkgname \
    --enable-executable-dynamic \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'
  runghc Setup.hs build
}

package() {
  cd $pkgname-$pkgver
  runghc Setup.hs copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/alex/BSD3"

  # Doc only contains the license
  rm -rf "$pkgdir/usr/share/doc"
}

# getver: hackage.haskell.org/package/alex/changelog
# vim: ts=2 sw=2 et:
