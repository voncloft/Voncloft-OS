# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Peter Mattern <pmattern at arcor dot de>
# Contributor: Michael Straube <straubem at gmx dot de>
# Contributor: Ofer Kashayov <oferkv at gmail dot com>

pkgname=phototonic
pkgver=2.1
pkgrel=2
pkgdesc='Image viewer and organizer built with Qt and Exiv2'
arch=(x86_64)
url='https://github.com/oferkv/phototonic/'
license=(GPL3)
depends=(exiv2 hicolor-icon-theme qt5-imageformats qt5-svg)
source=(https://github.com/oferkv/phototonic/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        phototonic.appdata.xml)
sha256sums=('a8b2dbc81750efabb65a4732b1704641afd4a4f772ef9c90817871ca2a52f5ef'
            '42a79f018c2909651c8d65a0008fc94350e72f95ec6c76bd4f6c821d37c17e1b')

prepare() {
  cd $pkgname-$pkgver
  sed -i '/INSTALLS +=/ s/ iconPixmaps//' phototonic.pro
}

build() {
  cd $pkgname-$pkgver
  qmake PREFIX=/usr phototonic.pro
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 ../$pkgname.appdata.xml "$pkgdir/usr/share/metainfo/$pkgname.appdata.xml"
}
