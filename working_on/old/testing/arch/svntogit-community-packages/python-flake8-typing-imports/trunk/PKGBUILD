# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=flake8-typing-imports
pkgname=python-$_pkgname
pkgver=1.10.1
pkgrel=1
pkgdesc='flake8 plugin which checks that typing imports are properly guarded'
arch=('any')
url='https://github.com/asottile/flake8-typing-imports'
license=('MIT')
depends=('flake8')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'flake8' 'python-pyflakes' 'python-pycodestyle' 'python-mccabe')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('c369af01e7157c9dec8b47506a15ad565541c6ccabb48fb0c0c37d8bfd62739e92d3e4d20b971a7eca16b07f56ce62f43dbcb4b4860a73eebea7bdd2ca5c7b1c')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
