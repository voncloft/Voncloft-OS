# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=monadplus
pkgname=haskell-monadplus
pkgver=1.4.2
pkgrel=20
pkgdesc="Haskell98 partial maps and filters over MonadPlus."
url="https://hackage.haskell.org/package/${_hkgname}"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        https://github.com/asr/monadplus/commit/c4231e1423e5c81f2779acf4462eea8872f6a4e6.patch)
sha512sums=('839a35b3de1226e177c07e30b86e841ddd19075d3ce29fa7154fefb371d9bef8aa85847d7c139faad93713d5b7889979498097f69c6e3bccfcee2fbbf7bf6539'
            'a3cd839c84c6c1c6ce827f75b3e98159da998d9693044e9af0ef50f3aac8c0e30b0c32305c09a3df881c826bc39f8a8e5839238c3f55df35d32f6edcc6d07d96')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../c4231e1423e5c81f2779acf4462eea8872f6a4e6.patch
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
