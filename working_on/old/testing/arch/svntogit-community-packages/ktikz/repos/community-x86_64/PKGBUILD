# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgbase=ktikz
pkgname=('ktikz' 'qtikz')
pkgver=0.12
pkgrel=3
pkgdesc="A small application helping you to create TikZ diagrams (from the LaTeX pgf package)"
arch=('x86_64')
url="https://github.com/fhackenberger/ktikz"
license=('GPL')
depends=('poppler-qt5')
makedepends=('cmake' 'extra-cmake-modules' 'qt5-base' 'qt5-tools'
             'kdoctools' 'kxmlgui' 'ktexteditor' 'kparts' 'kiconthemes'
             'poppler' 'poppler-qt5')
source=(${pkgbase}-${pkgver}.tar.gz::"${url}/archive/${pkgver}.tar.gz"
        reldir.patch::"${url}/commit/cfb52b1fe840aba8f9124d479df48edb61e943b8.patch"
        docbook.patch::"${url}/commit/3b137b2a4f12f74a6bc68f3a6ac093994cc418a0.patch"
        appdata.patch::"${url}/commit/bab16b2864888586c5d9044986e4891917addd08.patch"
        icons.patch::"${url}/commit/d0ee62bf61650dafbb056ee3181ad162c2ba0894.patch"
        ktikz-qt5.15.patch::"${url}/commit/ebe4dfb72ac8a137b475ef688b9f7ac3e5c7f242.patch")
sha256sums=('ef15fe70f4e057eab74ed738886f8e4a53d5b1bcdf1ee329930bf06e22f56e9d'
            'f420866c08c70119d597ffd36fac20d577700b921766a3168cd1b992ee84e2b6'
            '6176ed492f228495256954b56c696904f6a66f8f12769801289d806dbaeb2003'
            '0887348e7195e1c4e56b6cdcb6ae55a3928e75a0f07701472b53b8382d45ed20'
            '3ffa029eab77bf7198e504a90c7e72d9d7922f9d83d6abcd65069f649abbfbbf'
            '889734826f99d5af3703a4d039e515d0bd6533287c446c3800b29d82635a84f9')

prepare() {
    mkdir build{,qt}
    cd ${pkgbase}-${pkgver}
    patch -p1 -i ../reldir.patch
    patch -p1 -i ../docbook.patch
    patch -p1 -i ../appdata.patch
    patch -p1 -i ../icons.patch
    patch -p1 -i ../ktikz-qt5.15.patch # Fix build with Qt 5.15
}

build() {
    # Build ktikz
    cd build
    cmake ../${pkgbase}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None
    make
    # Build qtikz
    cd ../buildqt
    qmake ../${pkgbase}-${pkgver}/qtikz.pro
    make
}

package_ktikz() {
    pkgdesc+=" – KDE version"
    depends+=('kparts')
    cd build
    make DESTDIR="${pkgdir}" install
}

package_qtikz() {
    pkgdesc+=" – Qt version"
    cd buildqt
    make INSTALL_ROOT="${pkgdir}" install
    # Fix icons path
    sed -i 's:Icon=/usr/share/qtikz/qtikz-128.png:Icon=qtikz:' "${pkgdir}"/usr/share/applications/qtikz.desktop
    rm "${pkgdir}"/usr/share/qtikz/qtikz-128.png
    cd ../${pkgbase}-${pkgver}/app/icons
    install -Dm644 qtikz.svg "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/qtikz.svg
    install -Dm644 qtikz-22.png "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/qtikz.png
    install -Dm644 qtikz-128.png "${pkgdir}"/usr/share/icons/hicolor/128x128/apps/qtikz.png
}
