# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cldr-emoji-annotation
pkgver=37.0.13.0.0.2
_tag=37.0_13.0_0_2
pkgrel=1
pkgdesc="Emoji annotation files in CLDR"
arch=('any')
url="https://github.com/fujiwarat/cldr-emoji-annotation"
license=('custom')
source=("$pkgname-$pkgver.tar.gz::https://github.com/fujiwarat/cldr-emoji-annotation/archive/$_tag.tar.gz")
sha512sums=('9c863c6f6494fdedd7b8db20f42dd803388e4f8165d2827ef358330862c52b6c84d98b55e87054fcb01bab906cc3fe0dd351d9d8063f74b988ca686e5c9d2fa1')

build() {
  cd cldr-emoji-annotation-$_tag
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd cldr-emoji-annotation-$_tag
  make DESTDIR="$pkgdir" install

  install -Dm644 unicode-license.txt "$pkgdir"/usr/share/licenses/$pkgname/unicode-license.txt
}
