# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: derfenix <derfenix@gmail.com>

pkgname=neochat
pkgver=1.0.1
pkgrel=1
pkgdesc='A client for matrix, the decentralized communication protocol'
license=(GPL3)
arch=(x86_64)
url='https://apps.kde.org/neochat'
depends=(kirigami2 kdbusaddons ki18n knotifications libquotient kquickimageeditor kitemmodels qt5-svg qtkeychain-qt5 cmark hicolor-icon-theme)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('f432b780368335674aa019043090c921e0abc7f06c4097153bcaa79754ed141f'
            'SKIP')
validpgpkeys=(B3ED0BAE371BEDA00717921D18F4795C1CE7042B) # Carl Schwan <carl@carlschwan.eu>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
