# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=python-pygit2
pkgver=1.5.0
pkgrel=1
pkgdesc='Python bindings for libgit2'
arch=('x86_64')
url="https://github.com/libgit2/pygit2"
license=('GPL2')
depends=('libgit2>=1:0.27.0' 'python-cffi' 'python-six' 'python-cached-property')
makedepends=('python-cffi' 'python-setuptools' 'git' 'python-six')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz"::https://github.com/libgit2/pygit2/archive/v$pkgver.tar.gz)
sha256sums=('27f9918fe47b189387378923884928d69b18b8aacc54000edc0669f8741de2e4')

build() {
	cd "pygit2-$pkgver"
	python setup.py build
}

check() {
	cd "pygit2-$pkgver"
	python setup.py pytest
}

package() {
	cd "pygit2-$pkgver"
	LANG=en_US.UTF8 python setup.py install --root="${pkgdir}" --optimize=1
}
