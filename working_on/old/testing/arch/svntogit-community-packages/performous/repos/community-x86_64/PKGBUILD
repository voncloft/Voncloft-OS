# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=performous
pkgver=1.1
pkgrel=28
pkgdesc='A free game like "Singstar", "Rockband" or "Stepmania"'
arch=('x86_64')
url="https://performous.org/"
license=('GPL')
depends=('boost-libs' 'libepoxy' 'libxml++2.6' 'portaudio' 'portmidi' 'librsvg' 'ffmpeg' 'opencv' 'sdl2')
makedepends=('cmake' 'pkgconfig' 'help2man' 'boost' 'mesa')
optdepends=('performous-freesongs: free songs for performous')
source=($pkgname-$pkgver.tar.gz::https://github.com/performous/${pkgname}/archive/${pkgver}.tar.gz
        boost-1.67.patch
        performous-libswresample.patch
        performous-opencv4.patch
        performous-boost-1.69.patch
        performous-boost-1.70.patch
        performous-boost-1.73.patch)
sha512sums=('6659aa03960e68d7af65b072e179e42b122d2a39d25229c6daf765146379a115a52e51c1dc2bc69081ea17fe7a22f1d7007b7cf9e09bc32d134c3ad8e3444dd1'
            'dde4ac66d92a5a269237abb27fc5140bf87fd07b1d1e333d0e47c13b05a66b08347883380a575f0635c871236519710e791d99d3bd06700dd48341f5e50fd8b1'
            'f85b9a5c1b1bf0f1f5df7fb13945856cbca19bc5e49e3bb6f3cff302110b08275e97496ec70f23a8e9c77246b92ddde826520000888d6a4c2e888a8bae311f17'
            '854d969de951a5f380ddd1523b3d66639ef19b16af161466dfef3c697280c95884a3e34258dc86bb7eef1143ca24de44ae2dfccb5ce76fd333784b62f0762f56'
            '6e2d619ba55294ad901f84e3d3a30fe08f092521b4e19041ce4eb19dddc3d8021dfd5697947489a8312246c71ff7b544af4562fa4dfc744f9c27c7bc59b8e9bd'
            '7a4a11f0150930ea0527067519487629e714bc732eb111c1d4ca9b13cd8d0775182b22f8a96093223f7aab8524f63bdf901886169e6559adcd5e6b6a9b14edb4'
            'd291621b191c0e55a47232e63a75d4059e2a983c78f9ad7d3a9709293fe22486f307d641ccecf2f0a56ff784b8a3cb6be35a531cb8adf5d35ff2ee3d333d5c40')

prepare() {
  cd performous-${pkgver}
  patch -Np1 -i ../boost-1.67.patch
  patch -Np1 -i ../performous-libswresample.patch
  patch -p1 -i ../performous-opencv4.patch # Fix build with openCV 4
  patch -p1 -i ../performous-boost-1.69.patch # Fix build with boost 1.69
  patch -p1 -i ../performous-boost-1.70.patch # Fix build with boost 1.70
  patch -p1 -i ../performous-boost-1.73.patch # Fix build with boost 1.73
}

build() {
  cd performous-${pkgver}

  mkdir -p build
  cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -I/usr/include/harfbuzz" # Workaround build failure with pango 1.44
  make
}

package() {
  cd performous-${pkgver}/build

  make DESTDIR="$pkgdir" install
}
