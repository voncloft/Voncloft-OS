# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python2-importlib-metadata
pkgver=2.1.1
pkgrel=1
pkgdesc='importlib_metadata is a library to access the metadata for a Python package'
url='https://importlib-metadata.readthedocs.io'
license=('Apache')
arch=('any')
depends=('python2-zipp' 'python2-configparser' 'python2-contextlib2' 'python2-pathlib2')
makedepends=('python2-setuptools-scm')
checkdepends=('python2-pytest-runner' 'python2-importlib_resources' 'python2-wheel')
source=("$pkgname-$pkgver.tar.gz::https://github.com/python/importlib_metadata/archive/v$pkgver.tar.gz")
sha512sums=('28b15b15c8675f55f805915802a1f08023e107e4eb1d7a53c1f25d36396168dd1a52b2b6ebc7e7b3c30fbaa005f4678b648c57928868cb3a7029b6a5de81880e')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd importlib_metadata-$pkgver

  python2 setup.py build
}

package() {
  cd importlib_metadata-$pkgver

  python2 setup.py install --root="$pkgdir" --optimize=1
}
