# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Arkham <arkham@archlinux.us>
# Contributor: dirty_d <andrew2085@gmail.com>

pkgname=joyutils
pkgver=1.6.1
pkgrel=2
pkgdesc='jscal, jstest, and jsattach utilities for the Linux joystick driver'
arch=(x86_64)
license=(GPL)
url='https://packages.debian.org/sid/joystick'
conflicts=(linuxconsole)
depends=(sdl)
source=("https://deb.debian.org/debian/pool/main/j/joystick/joystick_$pkgver.orig.tar.bz2")
sha256sums=('e9d6b05a6c57c7eb3ec7f9f017f46b20e4b55d5a02f930eff1823a13921c5934')

prepare() {
  sed -i 's,DESTDIR)/lib,DESTDIR)$(PREFIX)/lib,g' \
    linuxconsoletools-$pkgver/utils/Makefile
}

build() {
  make -C linuxconsoletools-$pkgver
}

package() {
  DESTDIR="$pkgdir" PREFIX=/usr make -C linuxconsoletools-$pkgver install
}

# vim: ts=2 sw=2 et:
