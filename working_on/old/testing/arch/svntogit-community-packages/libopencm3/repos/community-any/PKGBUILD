# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=libopencm3
_commit=90753950bbe10e87b8caabfb0bd6e1d195bb24b8
pkgver=r2639.90753950
pkgrel=1
pkgdesc='Open Source firmware library for various ARM Cortex microcontrollers'
arch=('any')
url='http://www.libopencm3.org'
license=('GPL')
depends=('arm-none-eabi-newlib')
makedepends=('git' 'arm-none-eabi-gcc' 'python-yaml')
options=(!strip staticlibs !buildflags)
source=("git://github.com/libopencm3/libopencm3.git#commit=$_commit"
        '0001-Revert-Drop-install-target-and-documentation.patch')
sha512sums=('SKIP'
            '133e599bf8ea66e64225cf16873672ef86a66821ba3d735d7bae5cd356205d4b2f850121e93647c644be0a840764b71bfaa19b25ac8b59d678a45604b8a56540')

pkgver() {
  cd $pkgname

  printf 'r%s.%s' $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  patch -p1 < ../0001-Revert-Drop-install-target-and-documentation.patch
}

build() {
  cd $pkgname

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" PREFIX=/usr/arm-none-eabi install
}

