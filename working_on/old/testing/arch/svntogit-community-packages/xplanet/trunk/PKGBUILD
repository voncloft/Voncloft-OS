# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: dorphell <dorphell@archlinux.org>
# Committer: Judd Vinet <jvinet@zeroflux.org>

pkgname=xplanet
pkgver=1.3.1
pkgrel=4
pkgdesc="An Xearth wannabe"
arch=(x86_64)
url="http://xplanet.sourceforge.net/"
license=('GPL')
depends=('pango' 'giflib' 'libtiff' 'libxss')
source=(https://downloads.sourceforge.net/project/xplanet/xplanet/$pkgver/xplanet-$pkgver.tar.gz
	giflib6.patch xplanet-c++11.patch)
md5sums=('9797dbd9697d10205ca1671f728ea30d'
         '4ccddbd3edfae97a8d4cf36ea571282f'
         '71ea4c0fddcf0643db97ede1044c681d')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -p1 <"$srcdir"/giflib6.patch
  patch -p1 -i ../xplanet-c++11.patch # Fix build with C++11 (Gentoo)
  sed -e 's|freetype-config|pkg-config freetype2|g' -i acinclude.m4
  autoreconf -vif
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr --with-freetype
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make prefix="$pkgdir"/usr install
}
