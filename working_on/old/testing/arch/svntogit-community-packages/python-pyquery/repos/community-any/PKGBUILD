# Maintainer : Felix Yan <felixonmars@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Arnau Sanchez <tokland {at} gmail {dot} com>

pkgname=python-pyquery
pkgver=1.4.3
pkgrel=1
pkgdesc="A jquery-like library for python."
arch=('any')
url="https://github.com/gawel/pyquery/"
license=('BSD')
depends=('python-lxml' 'python-cssselect')
optdepends=('python-webob: query wsgi app')
makedepends=('python-setuptools')
checkdepends=('python-requests' 'python-webob' 'python-nose' 'python-webtest')
source=("https://github.com/gawel/pyquery/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ab5110232d79209fb861fa3ac61c8803352c28c8372ba5d96b3fe731ebdb0b732c0c1642719c9cebbd0030a27edd2a962a7c368c99a7c966bf0ea77f7ae89a37')

build() {
  cd pyquery-$pkgver
  python setup.py build
}

check() {
  cd pyquery-$pkgver
  nosetests3
}

package() {
  cd pyquery-$pkgver
  python setup.py install -O1 --root="$pkgdir"

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
