# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=midi_matrix.lv2
pkgver=0.28.0
pkgrel=1
pkgdesc="A plugin for MIDI channel filtering, multiplication and rerouting"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/midi_matrix/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'libglvnd' 'libx11')
makedepends=('glew' 'glu' 'lv2' 'meson' 'sord')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('89b6806347f2617834374194064e83bfc76e6c78654b18eaf73489b060416905ebf42178859525eb90595492120c524bd278fc0ee045885dfeb006cf821ec2dc'
            'SKIP')
b2sums=('6329d07bffb236a5e2faab5a58c900ba3c12e20f3af05d52d1086c6f39f399b1473c9f8fe96a56badb5018ee473ea8b65b670f017204bf842f3bc2084c18362b'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr \
        --libdir=lib \
        --buildtype plain \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  meson test -C build
  lv2lint -Mpack -I "build/" "http://open-music-kontrollers.ch/lv2/midi_matrix#channel_filter"
}


package() {
  depends+=('libGLEW.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
