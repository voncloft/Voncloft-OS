# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python-plyvel
pkgver=1.2.0
pkgrel=3
pkgdesc="A fast and feature-rich Python interface to LevelDB"
arch=('x86_64')
license=('BSD')
url="https://github.com/wbolster/plyvel"
depends=('leveldb' 'python')
makedepends=('cython' 'python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/p/plyvel/plyvel-$pkgver.tar.gz")
sha512sums=('00b3424e248d1ed17f6443c9862c9f35c94ad76707df5adfe625d7a387620252ff71b3e1f7e903dda1c5cfcd53cb23883c1049e31df541dfe566b8e849c0a96d')

prepare() {
  # Shipped egg-info has wrong permission etc
  cd plyvel-$pkgver
  make clean
}

build() {
  cd plyvel-$pkgver
  make cython
  python setup.py build
}

check() {
  cd plyvel-$pkgver
  python setup.py pytest
}

package() {
  cd plyvel-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}
