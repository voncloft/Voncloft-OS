# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-biometric-auth
pkgver=1.2.1
pkgrel=1
pkgdesc="Biometric authentication for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-biometric-auth"
groups=('ukui')
depends=('polkit-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-biometric-auth/archive/v$pkgver.tar.gz")
sha512sums=('bc5dde2252120e0389ace6995fe207d64a0c8595cea3cce38ae49c7e3360f8b4b0d7a9499d6eeaaaa0c6f1ce4e7a291d1bc34c1dd343c3211f9013de24e5e99c')

prepare() {
  mkdir -p build
  sed -i 's|qt5/QtWidgets/||' $pkgname-$pkgver/polkit-agent/src/PolkitListener.cpp
  sed -i 's|/lib/security|/usr/lib/security|' $pkgname-$pkgver/pam-biometric/CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
