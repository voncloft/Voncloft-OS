# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=coin-or-osi
pkgver=0.108.6
pkgrel=1
pkgdesc="COIN-OR Open Solver Interface"
arch=(x86_64)
url="https://projects.coin-or.org/Osi"
license=(EPL)
groups=(coin-or)
depends=(coin-or-coinutils)
makedepends=(gcc-fortran)
source=("https://www.coin-or.org/download/source/Osi/Osi-${pkgver}.tgz")
sha256sums=('57ef3f0c97995bac647504aee0ed34d31f79033ece04cd2cb86b4645f0a552d8')

build() {
  cd Osi-$pkgver
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr --enable-dependency-linking \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/"
  make
}

package() {
  cd Osi-$pkgver
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
  make DESTDIR="$pkgdir" install
}
