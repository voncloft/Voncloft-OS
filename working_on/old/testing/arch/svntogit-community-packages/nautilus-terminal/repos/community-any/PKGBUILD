# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=nautilus-terminal
pkgver=3.4.2
pkgrel=1
pkgdesc="Embedded terminal for Nautilus"
arch=(any)
url="https://github.com/flozz/nautilus-terminal"
license=(GPL3)
depends=(python-nautilus python-psutil vte3)
makedepends=(git python-setuptools)
_commit=44f3a15897c104d672ad45863da17a3b37a2895b  # tags/v3.4.2
source=("git+https://github.com/flozz/nautilus-terminal#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd $pkgname
  python3 setup.py build
}

package() {
  cd $pkgname
  python3 setup.py install --root="$pkgdir" --optimize=1
}
