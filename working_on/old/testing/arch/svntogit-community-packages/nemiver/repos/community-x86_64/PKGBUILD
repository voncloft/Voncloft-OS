# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: jordz <jordz@archlinux.us>

pkgname=nemiver
pkgver=0.9.6
pkgrel=8
pkgdesc='C/C++ debugger for GNOME'
arch=('x86_64')
license=('GPL')
url='https://projects.gnome.org/nemiver/'
depends=('gdlmm' 'gdb' 'gtksourceviewmm' 'libgtop' 'vte3' 'ghex' 'gsettings-desktop-schemas')
makedepends=('boost' 'intltool' 'gnome-common' 'yelp-tools' 'git')
source=("git+https://git.gnome.org/browse/nemiver#commit=1ef54607bfb463")
options=('!emptydirs')
sha256sums=('SKIP')

prepare() {
  cd "$pkgname"

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$pkgname"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --with-boost \
    --enable-symsvis=yes \
    --enable-memoryview=yes \
    --enable-gsettings=yes \
    --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" install
}

# getver: git.gnome.org/browse/nemiver/plain/configure.ac
# vim:set ts=2 sw=2 et:
