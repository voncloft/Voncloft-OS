# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=danmaq
pkgdesc="A small client side Qt program to play danmaku on any screen"
pkgver=0.2.3
pkgrel=2
arch=("x86_64")
license=('GPL')
url="https://github.com/tuna/danmaQ"
depends=('qt5-x11extras')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tuna/danmaQ/archive/v$pkgver.tar.gz")
sha512sums=('b9f5b63b9fa84861ec9c73df5fbfe54b13e748c982e48afda1d70c189884568fc9ea01de0ba8707d2ff647442bc04c83d3a25f91f67e5341b4d676e2bdf22f2f')

prepare() {
  mkdir -p build
  sed -i '/include <X11.extensions.shape.h>/a #undef None' danmaQ-$pkgver/src/DMCanvas.cpp
}

build() {
  cd build
  cmake ../danmaQ-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
