# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Baptiste Jonglez
# Contributor: Leen Jewel <leenjewel@gmail.com>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=msgpack-c
pkgver=3.3.0
pkgrel=2
pkgdesc='An efficient object serialization library'
arch=("x86_64")
url='https://msgpack.org/'
license=("Apache")
depends=("glibc")
checkdepends=("gtest")
makedepends=("cmake")
source=("https://github.com/msgpack/msgpack-c/releases/download/cpp-${pkgver}/msgpack-${pkgver}.tar.gz")
sha256sums=('6e114d12a5ddb8cb11f669f83f32246e484a8addd0ce93f274996f1941c1f07b')

build() {
  cd "msgpack-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DBUILD_SHARED_LIBS=ON \
        -DMSGPACK_ENABLE_STATIC=OFF \
        -DMSGPACK_CXX11=ON \
        -DMSGPACK_BUILD_EXAMPLES=OFF \
        .
  make
}

check() {
  cd "msgpack-${pkgver}"
  make test 
}

package() {
  cd "msgpack-${pkgver}"
  make DESTDIR="$pkgdir" install
}
