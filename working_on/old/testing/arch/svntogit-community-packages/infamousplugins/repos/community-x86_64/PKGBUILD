# Maintainer: David Runge <dvzrv@archlinux.org>

_name=infamousPlugins
pkgname=infamousplugins
pkgver=0.3.0
pkgrel=3
pkgdesc="A collection of open-source LV2 plugins"
arch=('x86_64')
url="https://ssj71.github.io/infamousPlugins/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc')
makedepends=('cmake' 'fftw' 'lv2' 'ntk' 'zita-resampler')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ssj71/${_name}/archive/v${pkgver}.tar.gz"
        "${pkgname}-0.3.0-lv2-1.18.0.patch::https://github.com/ssj71/infamousPlugins/pull/39/commits/06dd967b4736ea886dc1dc07f882cb1563961582.patch")
sha512sums=('54a691bd5a1e0d5274c74d3de3c1dc77c6998bd588a315b6b39de67a78651c6fcb412c527b7a6083666b2ba8aad39e3b32b6efbd6bb12c96653ddbe76524dbbb'
            'a61d53f51950b5c0e8ccc37734fee5cf96f6abb6c9bdbfa34e06bce3806aad94a896e152754ddc92d600691b1240f4ef5dcb5fec85552b9adf01de0c5eae1db1')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "../${pkgname}-0.3.0-lv2-1.18.0.patch"
}

build() {
  export CFLAGS+=" ${CPPFLAGS}"
  export CXXFLAGS+=" ${CPPFLAGS}"
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -Wno-dev \
        -Bbuild \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libfftw3f.so' 'libntk.so' 'libzita-resampler.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install -C build
  install -vDm 644 {CHANGELOG,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
