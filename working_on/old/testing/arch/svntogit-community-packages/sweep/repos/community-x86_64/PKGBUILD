# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributer: Jason Chu <jchu@xentac.net>

pkgname=sweep
pkgver=0.9.3
pkgrel=5
pkgdesc="Sound editing and mixing program"
arch=('x86_64')
url="http://www.metadecks.org/software/sweep/"
license=('GPL2')
groups=('pro-audio')
depends=('gtk2' 'libsamplerate' 'libmad' 'speex')
source=("https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
sha512sums=('e1e2018ae46cb77cc3aa8faf8f83fc50e937600e6103c9ebb1bfee5a43c30aa1710fd8876c1f59285356a9d0517607deda8c03463ad7a6dcfe72bbff486b6a46')

build() {
  cd $pkgname-$pkgver
  # this is a general hack for similar linking errors
  # TODO: report upstream
  export LDFLAGS="$LDFLAGS -lgmodule-2.0"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir/usr" sysconfdir="$pkgdir/etc" localstatedir="$pkgdir/var" install
}
