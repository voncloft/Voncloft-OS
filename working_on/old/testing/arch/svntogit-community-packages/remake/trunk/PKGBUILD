# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Based on extra/make

pkgname=remake
_remakever="dbg-1.5"
pkgver=4.3+dbg1.5
pkgrel=1
pkgdesc="Enhanced GNU Make - tracing, error reporting, debugging, profiling and more"
arch=('x86_64')
url="http://bashdb.sourceforge.net/remake/"
license=('GPL3')
depends=('glibc' 'guile')
source=("https://sourceforge.net/projects/bashdb/files/remake/4.3%2B${_remakever}/remake-4.3%2B${_remakever}.tar.gz")
sha256sums=('2e6eb709f3e6b85893f14f15e34b4c9b754aceaef0b92bb6ca3a025f10119d76')

prepare() {
  cd "${pkgname}-4.3+${_remakever}"
  autoreconf -fi
}

build() {
  cd "${pkgname}-4.3+${_remakever}"
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-4.3+${_remakever}"
  make DESTDIR="${pkgdir}" install
  rm "$pkgdir"/usr/share/info/make.info{,-1,-2}
}
