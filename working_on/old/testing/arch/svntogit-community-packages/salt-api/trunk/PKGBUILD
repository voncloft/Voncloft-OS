# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Christer Edwards <christer.edwards@gmail.com>

pkgname=salt-api
pkgver=0.8.4.1
pkgrel=1
pkgdesc="Salt API is used to expose the fundamental aspects of Salt control to external sources."
arch=(any)
url="https://github.com/saltstack/salt-api"
license=("APACHE")
depends=('salt'
         'python2-requests')
makedepends=('python2-setuptools')
optdepends=('python2-cherrypy: rest_cherrypy interface')

source=("http://pypi.python.org/packages/source/s/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        salt-api.service)

sha256sums=('b733534a1de80602d89938be9c328cd83b9cf27b69bc0ffa661e17369b7c5dd6'
            'b28b93928a5210a3340ad420f8b9b372fb22888b3765238796331f59c6d681c3')

prepare(){
  find $srcdir/$pkgname-$pkgver -type f -name '*.py' | xargs sed -i '1s/python$/&2/'
}


build() {
	cd "$srcdir/$pkgname-$pkgver"
	python2 setup.py build
	chmod a+r doc/man/*
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	python2 setup.py install --skip-build -O1 --root="$pkgdir"
	install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm0644 "$srcdir/salt-api.service" "$pkgdir/usr/lib/systemd/system/salt-api.service"
}
