# Maintainer: Anatol Pomozov

pkgname=aws-sdk-cpp
pkgver=1.8.129
pkgrel=2
pkgdesc='AWS SDK for C++'
arch=(x86_64)
url='https://github.com/aws/aws-sdk-cpp'
license=(Apache)
depends=(openssl curl zlib util-linux-libs aws-c-common aws-c-event-stream libpulse)
makedepends=(cmake)
source=(aws-sdk-cpp-$pkgver.zip::https://github.com/aws/aws-sdk-cpp/archive/$pkgver.zip)
sha256sums=('ac57e83a5a0ae93a3fe784a91a0658e46e484b3a49567d3a3ab4b3fb335df4af')

# AWS C++ SDK is a huge project. Install size of the whole project is ~500MiB. To reduce it
# we build only a small subset of components used by other packages. Add more components to
# this list if you need it.
_components="core;ec2;kinesis;firehose;sts;s3"
# TOTHINK: it would be interesting to explore an idea of breaking sdk into set of small
# per-component packages "aws-sdk-cpp-ec2, aws-sdk-cpp-kinesis, ...".

build() {
  cd aws-sdk-cpp-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DCUSTOM_MEMORY_MANAGEMENT=OFF -DENABLE_TESTING=OFF -DBUILD_DEPS=OFF -DBUILD_ONLY="$_components" -S . -B build
  cmake --build build
}

package() {
  cd aws-sdk-cpp-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
