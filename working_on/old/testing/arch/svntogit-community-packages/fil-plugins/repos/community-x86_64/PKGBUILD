# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=fil-plugins
pkgver=0.3.0
pkgrel=7
pkgdesc="LADSPA four-band parametric equaliser plugins"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/ladspa/"
license=('GPL2')
groups=('ladspa-plugins' 'pro-audio')
depends=('gcc-libs')
makedepends=('ladspa')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/FIL-plugins-$pkgver.tar.bz2")
sha512sums=('9054d2bdbceb2a55d7772958f3bdcc03fb1bd15109de931bee07f3ab55f437c4076c5a740187252a406142c9438febe269c5b25364eddab3191de75ca56a81b1')

prepare() {
  mv -v "FIL-plugins-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  # libs
  install -vDm 755 filters.so "${pkgdir}/usr/lib/ladspa/filters.so"
  # docs
  install -vDm 644 {AUTHORS,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
