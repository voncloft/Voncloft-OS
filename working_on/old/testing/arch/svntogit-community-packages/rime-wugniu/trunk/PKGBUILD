# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-wugniu
pkgver=0.0.0.20190120
_commit=abd1ee98efbf170258fcf43875c21a4259e00b61
pkgrel=1
pkgdesc="Wugniu input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-wugniu"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-luna-pinyin')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-wugniu/archive/$_commit/rime-wugniu-$_commit.tar.gz")
sha512sums=('257d4bd8d62dda5bce4beb39879fc46b4981590ce16d347ad3e2441f1b9bacabfe3772d8e0cf134a4a70b7dd5490e6bb6b6526e0e45d58d22730217e23407095')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
