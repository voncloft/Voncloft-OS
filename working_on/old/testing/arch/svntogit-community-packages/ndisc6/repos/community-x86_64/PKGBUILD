# Maintainer: Sébastien Luttringer
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor: Mark Smith <markzzzsmith@yahoo.com.au>

pkgname=ndisc6
pkgver=1.0.4
pkgrel=2
pkgdesc='Collection of IPv6 networking utilities'
arch=('x86_64')
url='https://www.remlab.net/ndisc6/'
license=('GPL')
depends=('glibc' 'perl')
makedepends=('glibc' 'gcc' 'systemd')
options=('!emptydirs')
install=$pkgname.install
source=("https://www.remlab.net/files/ndisc6/$pkgname-$pkgver.tar.bz2"{,.asc}
	      'rdnssd@.service'
        'resolvconf-hook')
validpgpkeys=('9480583353E334D2F03FE80CC3EC6DBEDD6D12BD') # Rémi Denis-Courmont <remi@remlab.net>
md5sums=('a796350c1629aa3e438a2bb55be1b792'
         'SKIP'
         '58b44cbe6d38082fb299eb136754bf31'
         '48d0a77ed9536cd0d4642e1f1ca0b255')

prepare() {
  cd $pkgname-$pkgver
  # apply patch from the source array (should be a pacman feature)
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
  # patch invalid path
  sed -ri 's,^PATH=.*,PATH=/usr/local/sbin:/usr/local/bin:/usr/bin,' rdnss/merge-hook.in
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --sysconfdir=/etc
  make
}

package() {
  pushd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  popd

  # service
  install -Dm644 "$srcdir/rdnssd@.service" "$pkgdir/usr/lib/systemd/system/rdnssd@.service"

  # tmpfiles
  install -Dm644 /dev/null "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  echo 'd /run/rdnssd 0755 nobody root' > "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  # resolvconf merge hook
  install -Dm755 "$srcdir/resolvconf-hook" "$pkgdir/etc/rdnssd/resolvconf-hook"
}

# vim:set ts=2 sw=2 et:
