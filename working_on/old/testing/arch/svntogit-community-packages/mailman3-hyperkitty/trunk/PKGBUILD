# Maintainer: David Runge <dvzrv@archlinux.org>

_name=mailman-hyperkitty
pkgname=mailman3-hyperkitty
pkgver=1.1.0
pkgrel=5
pkgdesc="Mailman plugin to archive emails with HyperKitty"
arch=('any')
url="https://gitlab.com/mailman/mailman-hyperkitty"
license=('GPL3')
depends=('mailman3' 'python-requests' 'python-zope-interface')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-nose2')
replaces=('python-mailman-hyperkitty-plugin')
backup=('etc/mailman-hyperkitty.cfg')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"
        "https://gitlab.com/mailman/mailman-hyperkitty/raw/84e05811fb71aa105fd85fd14399bff813ed744d/mailman_hyperkitty/tests/test_archiver.py"
        "${pkgname}.tmpfiles")
sha512sums=('d1325b5f88b5f46fb9d772d70a68a6fb1202e195356a0079df414074113d8efcaf3e07dd06dc8e7b1433d5da014fbe2a5344d787288d959c7ce192a34102e3dd'
            '0b90900e4060fc654ea1e69d2be52f2357d62284fa0d6f470f5663b5bbe9ed5c1b4fa4606ae8a8622e9a31c95d457646390d1ba8094fa8474b85136d3c39c398'
            'bc97105a6208d951cbb884a18fea9f1aeb94cb1dab5966ef707c4af526d6bcf869c6c75321fa834f66ca20d315f73a071bb62aba9353c320d7ba84ba935cce6b')
b2sums=('62dba12861ff43f59d521675e1805a8cb3dfc572fda2aea2aba1134bcdc71ee8f400d4550595a04a38ecce08978ced5c1e0da7be52b6de7e7f5654b392185f3d'
        'f889918bd4991759a6beba8fbccd301b5aeced10e86c7a4dcf60953484723cb3fd0f4e98a229623923a3bda7870ac2c822c040a1b17ab3781ab4dda2e8af8f72'
        '973b3ef36a3707fdefb36a94a7b5d4a2556f1dfd0d1c70fc648b90226203d33e488d8b79526b837a99ab0acbd4562894b8b62fe84021485180e03f9a874e66bf')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # test_archiver.py is broken with python >= 3.7:
  # https://gitlab.com/mailman/mailman-hyperkitty/issues/17
  cp -v ../test_archiver.py mailman_hyperkitty/tests/
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  nose2 -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 640 "${_name}.cfg" -t "${pkgdir}/etc/"
  install -vDm 644 "../${pkgname}.tmpfiles" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
