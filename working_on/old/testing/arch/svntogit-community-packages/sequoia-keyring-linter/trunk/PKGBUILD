# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=sequoia-keyring-linter
pkgver=0.4.0
pkgrel=1
pkgdesc='OpenPGP certificate linter'
url='https://sequoia-pgp.org/'
arch=('x86_64')
license=('GPL2')
depends=('glibc' 'nettle' 'libhogweed.so' 'libnettle.so' 'gcc-libs' 'gmp')
makedepends=('git' 'cargo' 'llvm' 'clang')
source=(https://gitlab.com/sequoia-pgp/keyring-linter/-/archive/v${pkgver}/keyring-linter-v${pkgver}.tar.gz)
sha512sums=('eac6481fe583603db406accee1f0dbcd5941b240f0e70c3da25f50958caa83dd45a7b2457376982f434c892797b5643f0891db278a8edb31d96812e3860cd9e2')
b2sums=('ea075425449966c2855670e2dd93aced8138d50598c62b9c14cd7d2e2c9b4d3d03c41fb56bba7f74a89491fc2859f69e8f01a1e9e35181603e8e7671cb5f5cc9')

prepare() {
  cd keyring-linter-v${pkgver}
  sed 's/debug/release/g' -i -- Makefile
}

build() {
  cd keyring-linter-v${pkgver}
  cargo build --release
  make
}

check() {
  cd keyring-linter-v${pkgver}
  cargo test --release
}

package() {
  cd keyring-linter-v${pkgver}
  install -Dm 755 target/release/sq-keyring-linter -t "${pkgdir}/usr/bin"
  install -Dm 644 sq-keyring-linter.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
