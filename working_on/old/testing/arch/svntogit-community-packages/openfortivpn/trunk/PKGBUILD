# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=openfortivpn
pkgver=1.15.0
pkgrel=1
pkgdesc="An open implementation of Fortinet's proprietary PPP+SSL VPN solution"
arch=(x86_64)
url="https://github.com/adrienverge/openfortivpn"
license=(GPL3)
depends=(openssl ppp)
backup=(etc/openfortivpn/config)
source=($pkgname-$pkgver.tar.gz::https://github.com/adrienverge/openfortivpn/archive/v$pkgver.tar.gz)
sha256sums=('5279dbd3da779b818d201bbd9243cff9421a7e790fd2190438610a03c88641f8')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
	./configure --prefix=/usr --sysconfdir=/etc
	make
}

package() {
  cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
}
