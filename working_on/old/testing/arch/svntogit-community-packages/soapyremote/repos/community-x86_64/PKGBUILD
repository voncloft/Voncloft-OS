# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=soapyremote
_pkgname=SoapyRemote
pkgver=0.5.1
_gitver=soapy-remote-$pkgver
pkgrel=1
pkgdesc='Use any Soapy SDR remotely'
arch=('x86_64')
url='https://github.com/pothosware/SoapyRemote'
license=('custom:Boost')
depends=('soapysdr' 'avahi')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_gitver.tar.gz")
sha512sums=('39941ff01326377957c4b089763d6ada313f4eacee50e963378d3060d1f53ce95add29ad6f832d483b268854b09f34b1cb2c5e9c55dab2928c8604a1e96b63af')

build() {
  mkdir -p $_pkgname-$_gitver/build
  cd $_pkgname-$_gitver/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd $_pkgname-$_gitver/build

  make DESTDIR="$pkgdir" install

  install -Dm 644 "$srcdir"/$_pkgname-$_gitver/LICENSE_1_0.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
