# Maintainer: David Runge <dvzrv@archlinux.org>

_name=GxPlugins
pkgname=gxplugins.lv2
pkgver=0.8
pkgrel=1
pkgdesc="A set of extra lv2 plugins from the guitarix project"
arch=('x86_64')
url="https://github.com/brummer10/gxplugins.lv2"
license=('GPL3')
groups=('lv2-plugins')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11')
makedepends=('lv2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/brummer10/${pkgname}/releases/download/v${pkgver}/${_name}_${pkgver}.tar.gz")
sha512sums=('0fb15dcda8fcca351c7b218c6eec8fcd44f23ac972b91ab03126a37f0d421679c598064d5a136337b01760af222a30667461fbd4b81b891163bb3e5ef0c55aba')

prepare() {
  mv -v "${_name}_$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
