# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=lib32-cdparanoia
pkgver=10.2
pkgrel=3
pkgdesc='Compact Disc Digital Audio extraction tool'
arch=('x86_64')
url='http://www.xiph.org/paranoia/'
license=('GPL')
depends=('cdparanoia' 'lib32-glibc')
makedepends=('gcc-multilib')
options=('!makeflags')
source=("http://downloads.xiph.org/releases/cdparanoia/cdparanoia-III-${pkgver}.src.tgz"
        'gcc.patch')
sha512sums=('4ab0a0f5ef44d56c1af72d1fc1035566a1a89c4eeddb9e8baea675fe51c06138d913342afc8bed167d9fa55672fa25a2763ce21f7e24c1232e4739aff20733a7'
            '4d86fccc0967dd8d568b97a5b5b92dbe830a7227bee3167371ffc8e7d7f0b99aa0ad4116539c3414f2cc4a600c9bc3fcf06b5d8e4d2fe531dd9d6781736bd1a8')

prepare() {
  cd cdparanoia-III-${pkgver}

  patch -Np0 -i ../gcc.patch
}

build() {
  cd cdparanoia-III-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  autoreconf
  ./configure \
    --prefix="${pkgdir}/usr" \
    --libdir="${pkgdir}/usr/lib32"
  make
}

package() {
  cd cdparanoia-III-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
