# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mephisto.lv2
pkgver=0.14.0
pkgrel=1
pkgdesc="A Just-in-Time FAUST compiler embedded in an LV2 plugin"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/mephisto/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'libglvnd' 'libvterm' 'libx11' 'ttf-fira-code')
makedepends=('faust' 'fontconfig' 'glew' 'glu' 'lv2' 'meson')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('8fe192ce5d9108cc736437998b8a3cc44eeda8ace55bb781c69c257122bd2a8fac730f3a00bb6ac212b903063b5832ce036a8f6dedf3ffc3ec991f364f973370'
            'SKIP')
b2sums=('4b0c22285a4188c63d7c57988c27cca69573052d0cf196e400fcf9a2a9e1e3334a10af3d51998b1922660cf094cea9f29cad3d713bb7783dec96b419befac1c4'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr \
        --libexecdir lib \
        --sbindir bin \
        --buildtype plain \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        build
  ninja -C build
}

check() {
  local _names=('audio_1x1' 'audio_2x2' 'audio_4x4' 'audio_8x8' 'cv_1x1'
  'cv_2x2' 'cv_4x4' 'cv_8x8')
  local _name

  cd "$pkgname-$pkgver"
  for _name in ${_plugin_names[@]}; do
    lv2lint -Mpack -I "build/" "http://open-music-kontrollers.ch/lv2/mephisto#${_plugin}"
  done
}

package() {
  depends+=('libGLEW.so' 'libfaust.so' 'libfontconfig.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" ninja -C build install
  # devendoring ttf-fira-code
  rm -vf "${pkgdir}/usr/lib/lv2/${pkgname}/"*.ttf
  for font_type in {Bold,Light,Medium,Regular}; do
    ln -svf "/usr/share/fonts/TTF/FiraCode-${font_type}.ttf" \
      "${pkgdir}/usr/lib/lv2/${pkgname}/"
  done
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
