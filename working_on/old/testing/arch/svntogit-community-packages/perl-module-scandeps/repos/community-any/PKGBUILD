# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: xRemaLx <anton.komolov@gmail.com>

pkgname=perl-module-scandeps
_pkgname=Module-ScanDeps
pkgver=1.30
pkgrel=1
pkgdesc="Scan file prerequisites"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-test-requires')
url="https://search.cpan.org/dist/Module-ScanDeps"
source=("https://search.cpan.org/CPAN/authors/id/R/RS/RSCHUPP/Module-ScanDeps-$pkgver.tar.gz")
sha512sums=('4813dd38e6defc2576b802f5fda2712f06cc6c763f06aa82115957f2b6eed36a14cd835669c46acc5ec9afd5a74c750b3f0f9a574471017e7a58fefede5311ac')

build() {
  cd Module-ScanDeps-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Module-ScanDeps-$pkgver
  make test
}

package() {
  cd Module-ScanDeps-$pkgver
  make DESTDIR="$pkgdir" install
}
