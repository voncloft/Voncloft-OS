# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

pkgname=python-webtest
pkgver=2.0.35
pkgrel=4
pkgdesc="Helper to test WSGI applications"
arch=('any')
url='http://webtest.pythonpaste.org/'
license=('MIT')
depends=('python-webob' 'python-six' 'python-beautifulsoup4' 'python-waitress')
optdepends=('python-pyquery: for response.pyquery'
            'python-lxml: for response.lxml')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-mock' 'python-pyquery'  'python-pastedeploy' 'python-wsgiproxy2'
              'python-pylons-sphinx-themes')
source=("https://pypi.io/packages/source/W/WebTest/WebTest-$pkgver.tar.gz")
sha512sums=('ecad031702a9d2031d4e2230dc8213aaa6b1a3e9e6add296ee796aa2ef77d81c992dc24e481f721bc34711d1e33f3932d56cec4be3d62ee82a71fb405942c339')

build() {
  cd WebTest-$pkgver
  python setup.py build
}

check() {
  cd WebTest-$pkgver
  PYTHONPATH=".:$PYTHONPATH" nosetests3
}

package() {
  cd WebTest-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 license.rst "$pkgdir"/usr/share/licenses/$pkgname/license.rst
}
