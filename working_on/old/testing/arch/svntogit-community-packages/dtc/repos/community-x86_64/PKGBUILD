# Maintainer: Anatol Pomozov
# Contributor: Frederic Bezies <fredbezies at gmail dot com>

pkgname=dtc
pkgver=1.6.0
pkgrel=3
pkgdesc='Device Tree Compiler'
url='https://www.devicetree.org/'
arch=(x86_64)
license=(GPL2)
makedepends=(swig python)
checkdepends=(valgrind)
source=(https://www.kernel.org/pub/software/utils/dtc/dtc-$pkgver.tar.xz)
sha256sums=('10503b0217e1b07933e29e8d347a00015b2431bea5f59afe0bed3af30340c82d')

prepare() {
  cd dtc-$pkgver
  sed -i 's/-Werror//' Makefile
}

build() {
  cd dtc-$pkgver
  make
}

check() {
  cd dtc-$pkgver
  make check
}

package() {
  cd dtc-$pkgver
  DESTDIR="$pkgdir" make SETUP_PREFIX="$pkgdir/usr" PREFIX="$pkgdir/usr" install
}
