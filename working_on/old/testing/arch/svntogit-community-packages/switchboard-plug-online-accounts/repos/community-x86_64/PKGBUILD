# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-online-accounts
pkgver=2.0.1
pkgrel=2
pkgdesc='Switchboard Online Accounts Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-onlineaccounts
license=(GPL2)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk3
  json-glib
  libaccounts-glib
  libgee
  libgranite.so
  libsignon-glib
  libswitchboard-2.0.so
  rest
  webkit2gtk
)
makedepends=(
  git
  meson
  vala
)
source=(switchboard-plug-online-accounts::git+https://github.com/elementary/switchboard-plug-onlineaccounts.git#tag=${pkgver})
sha256sums=(SKIP)

prepare() {
  cd $pkgname
  # fix build
  git cherry-pick -n 5fa2882f765076d20c6ef4886198a34a05159f8a
  git cherry-pick -n 34164a9fc2044380c82d2cc29b7b1a8583c4fd9c
}

build() {
  arch-meson switchboard-plug-online-accounts build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
