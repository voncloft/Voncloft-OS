# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-twisted
pkgver=1.13.2
pkgrel=3
pkgdesc='A twisted plugin for py.test'
arch=('any')
license=('BSD')
url='https://github.com/pytest-dev/pytest-twisted'
depends=('python-pytest' 'python-greenlet' 'python-decorator' 'python-twisted')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-twisted/archive/v$pkgver.tar.gz")
sha512sums=('8682cd4c457a4c1447089b80e9f24f75642f019d070060a3ee898276564b5506f0c88257c51cac02447fda717d598b661b1d220c997a1e0bccaa884dfa313433')

build() {
  cd "$srcdir"/pytest-twisted-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-twisted-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  PYTHONPATH="$PWD/tmp_install/${site_packages}:$PYTHONPATH" py.test
}

package() {
  cd pytest-twisted-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
