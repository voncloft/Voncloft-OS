# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Tomislav Ivek <tomislav.ivek@gmail.com>

pkgname=python-fasteners
pkgver=0.15
pkgrel=5
pkgdesc="A python package that provides useful locks."
arch=('any')
url="https://github.com/harlowja/fasteners"
license=('Apache')
depends=('python-six' 'python-monotonic')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/harlowja/fasteners/archive/$pkgver.tar.gz")
sha512sums=('0c77c7061c8b76d0d018b07a3d33fc71701842f5b686456ebf31069694baa27a6cc47196267f24bc296078e7503ab0709fa28c9f0088c3cf777ea0e133bb5832')

build() {
  cd "$srcdir/fasteners-$pkgver"
  python setup.py build
}

package() {
  cd "$srcdir/fasteners-$pkgver"
  python setup.py install --optimize=1 --root="$pkgdir"
}
