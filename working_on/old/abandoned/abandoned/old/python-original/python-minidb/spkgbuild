# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

_name=minidb
pkgname=python-minidb
pkgver=2.0.4
pkgrel=3
pkgdesc='Simple SQLite3 store for Python objects'
arch=('any')
url=https://thp.io/2010/minidb/
license=('ISC')
depends=('python')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('c1c62be076a1e4bb9dd880504dd81a3e7e027b7657e8a54a83c5a2727f09520a')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  mkdir -p temp
  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --skip-build --root=temp
  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$sitepackages" pytest
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -d "$pkgdir"/usr/share/licenses/$pkgname
  sed -n '/# Copyright/,/OF THIS SOFTWARE./p' minidb.py > "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
