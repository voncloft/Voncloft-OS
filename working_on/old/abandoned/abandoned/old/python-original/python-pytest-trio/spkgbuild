# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-pytest-trio
pkgver=0.7.0
pkgrel=3
pkgdesc='Pytest plugin for Trio'
arch=(any)
url=https://github.com/python-trio/pytest-trio
license=(
  APACHE
  MIT
)
depends=(
  python
  python-async_generator
  python-pytest
  python-trio
)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-hypothesis
  python-pytest-cov
  python-trio-asyncio
)
_tag=51035fd839f873b470b33bac8bac6b7c1434b5b1
source=(git+https://github.com/python-trio/pytest-trio.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pytest-trio

  git describe --tags | sed 's/^v//'
}

build() {
  cd pytest-trio
  sed -i '/pytest.RemovedInPytest4Warning/d' pytest_trio/_tests/conftest.py

  python setup.py build
}

check() {
  cd pytest-trio

  python setup.py test
}

package() {
  cd pytest-trio

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-pytest-trio/
  install -Dm 644 LICENSE.MIT -t "${pkgdir}"/usr/share/licenses/python-pytest-trio/
}

# vim: ts=2 sw=2 et:
