# Maintainer: David Runge <dvzrv@archlinux.org>

_name=lazr.delegates
pkgname=python-lazr.delegates
pkgver=2.0.4
pkgrel=5
pkgdesc="Easily write objects that delegate behavior"
arch=('any')
url="https://launchpad.net/lazr.delegates"
license=('GPL')
depends=('python-nose' 'python-setuptools' 'python-zope-interface')
checkdepends=('python-pytest')
provides=('python-lazr-delegates')
replaces=('python-lazr-delegates')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('0c0b32a4fa4c132646d601870be0f0c1f1856aceb91bf979faa9a675fcbf94f76c7c6f548978a402842633cf4e53275c052cff7dfd776a4246691c067eabe501'
            'SKIP')
validpgpkeys=('AC0A4FF12611B6FCCF01C111393587D97D86500B')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {HACKING,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
