# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-cliff
pkgver=3.6.0
pkgrel=1
pkgdesc="Command Line Interface Formulation Framework"
arch=('any')
url="https://docs.openstack.org/cliff/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-cmd2' 'python-prettytable' 'python-pyparsing'
         'python-stevedore' 'python-yaml')
makedepends=('git')
checkdepends=('python-stestr' 'python-mock' 'python-sphinx' 'python-testscenarios')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/cliff/archive/$pkgver.tar.gz")
sha512sums=('7b5f0ad55c42b6782897046a4037248b8d5fd39398c43efee1fc0acc4f23d3d1c7ae41aa2e316937eeb39b9684a4a0dcf57b2528118f7e6cece1d907e31bea3a')

export PBR_VERSION=$pkgver

build() {
  cd cliff-$pkgver
  python setup.py build
}

check() {
  cd cliff-$pkgver
  stestr run
}

package() {
  cd cliff-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
