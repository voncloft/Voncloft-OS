# Maintainer: David Runge <dvzrv@archlinux.org>

_name=mailmanclient
pkgname=python-mailmanclient
pkgver=3.3.2
pkgrel=1
pkgdesc="Official Python bindings for the GNU Mailman 3 REST API"
arch=('any')
url="https://gitlab.com/mailman/mailmanclient"
license=('LGPL3')
depends=('python-requests')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-vcr' 'python-pytest-services'
'mailman3' 'python-falcon')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('87498df04ba9c7471f3a1c462dccb11af3af93669e2b0bef0d7f88fd756a860845b17503ea53b5b6eb894fb53f42816ea798fe8658ac78c119db9e795f805755'
            'SKIP')
b2sums=('4578adc3a65b778fa9c24665e025b04d69e79253b851887712895eefc15c9a50eea135f131e9bded203a14573d40c74baa8a1f0e029eedf666a030701393d0e4'
        'SKIP')
validpgpkeys=('541EA0448453394FF77A0ECC9D9B2BA061D0A67C') # Abhilash Raj <raj.abhilash1@gmail.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v -k 'not using.rst'
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
