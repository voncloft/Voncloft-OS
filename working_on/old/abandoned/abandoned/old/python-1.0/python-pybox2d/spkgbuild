# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

_name=pybox2d
name=python-pybox2d
version=2.3.2
release=6
epoch=1
pkgdesc='Python wrapper for Box2D'
url='https://github.com/pybox2d/pybox2d'
depends=('python')
makedepends=('python-setuptools' 'swig')
source=("$name-$version.tar.gz::https://github.com/pybox2d/$_name/archive/$version.tar.gz")
sha256sums=('ca7111557df3ecda3cb48b6c5fb3290fa8b45b6226a34867d2a85cbee5747266')

prepare() {
	# Generate py library properly
	rm $_name-$version/library/Box2D/Box2D.py
}

build() {
	cd $_name-$version
	swig -python -c++ -IBox2D -small -O -includeall -ignoremissing -w201 -globals b2Globals -outdir library/Box2D -keyword -w511 -D_SWIG_KWARGS -o Box2D/Box2D_wrap.cpp Box2D/Box2D.i
	python setup.py build
}

check() {
	cd $_name-$version
	python setup.py test
}

package() {
	cd $_name-$version
	python setup.py install --root "$pkgdir" --optimize=1 --skip-build
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$name/LICENSE"
}
