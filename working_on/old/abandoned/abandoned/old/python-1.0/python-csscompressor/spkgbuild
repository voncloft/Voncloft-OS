# Maintainer: David Runge <dvzrv@archlinux.org>

_name=csscompressor
name=python-csscompressor
version=0.9.5
release=3
pkgdesc="Port of YUI CSS Compressor to Python"
url="https://github.com/sprymix/csscompressor"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${version}.tar.gz")
sha512sums=('aee16b5af8b1b780a0fef10d359864a5a21a90b7196740c8b7efef25d1ba4d8197e9fce03483d0e2331abc736290cc6b862e43ee65d1b76260310aea65fc6eb6')

prepare() {
  mv -v "${_name}-${version}" "$name-$version"
}

build() {
  cd "$name-$version"
  python setup.py build
}

check() {
  cd "$name-$version"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$name-$version"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${name}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${name}"
}
