# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

name=python-bleach
version=3.3.0
release=1

pkgdesc="An easy whitelist-based HTML-sanitizing tool"
url="https://pypi.org/project/bleach/"
depends=('python-packaging'
         'python-six'
         'python-webencodings')
makedepends=('python'
             'python-setuptools')
checkdepends=('python-pytest')

source=(https://pypi.org/packages/source/b/bleach/bleach-$version.tar.gz
        $name-py39.patch::https://github.com/mozilla/bleach/commit/9ba905729f17.patch)

sha256sums=('98b3170739e5e83dd9dc19633f074727ad848cbedb6026708c8ac2d3b697a433'
            '5b47567a3d47c8e4464b63d5d50abc38da55f2a67d2fa2f7c65041191ea6c156')

prepare() {
	cd "$srcdir"/bleach-$version
	# https://github.com/mozilla/bleach/pull/565
	patch -Np1 -i ../$name-py39.patch
}

build() {
	cd "$srcdir"/bleach-$version
	python setup.py build
}

# Upstream broke the test suite.
check() {
	cd "$srcdir"/bleach-$version
	python -m pytest
}

package() {
	cd "$srcdir"/bleach-$version
	export PYTHONHASHSEED=0
	python setup.py install --skip-build --optimize=1 --root="$pkgdir"
}
