# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: examon <examon.mail[at]gmail[dot]com>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Dwight Schauer <dschauer@ti.com>

_name=pyudev
name=python-$_name
version=0.22
release=5
url='https://github.com/pyudev/pyudev'
pkgdesc='Python bindings to libudev'
depends=('python-six' 'systemd')
makedepends=('python-setuptools' 'python-sphinx' 'python-mock')
checkdepends=('python-pytest-runner' 'python-docutils' 'python-mock' 'python-hypothesis')
optdepends=('python-pyqt5: PyQt integration'
            'pyside2: PySide integration'
            'python-wxpython: WX integration')
source=("$_name-$version.tar.gz::$url/archive/v$version.tar.gz")
sha512sums=('b509f4442e1c11eea9755fbae867b0aee97221db60d78ed2b8da40c39fb11015982b2e9b423680ed44b3f9db2c84b3a0a57a1e8aad4fb939061e33646638d9e3')

prepare() {
  cd $_name-$version

  # pytest-runner doesn't understand norecursedirs
  sed -i '/norecursedirs/d' setup.cfg
  echo -e '[pytest]\nnorecursedirs = .* _* build' > pytest.ini

  # Remove failing tests (we can't test udev inside makepkg)
  rm tests/test_{util,discover,device,monitor,enumerate,observer}.py

  # Fix documentation build
  sed -i "s|os.path.join(doc_directory, os.pardir)|os.path.join(doc_directory, os.pardir, 'src')|
          s|b'autodoc-process-docstring'|'autodoc-process-docstring'|" doc/conf.py
}

build() {
  cd $_name-$version

  python setup.py build

  # Generate documentation
  sphinx-apidoc -f -e -o doc src/pyudev
  sphinx-build -a -b html doc doc/html
}

check() {
  cd $_name-$version

  python setup.py pytest
}

package() {
  cd $_name-$version

  python setup.py install --root "$pkgdir" --skip-build -O1

  # Install documentation
  install -dm 755 "$pkgdir"/usr/share/doc/$name
  cp -r -a --no-preserve=ownership doc/html "$pkgdir"/usr/share/doc/$name
  rm -rf "$pkgdir"/usr/share/doc/$name/html/.doctrees
}
