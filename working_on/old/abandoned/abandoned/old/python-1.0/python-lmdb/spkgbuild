# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=py-lmdb
name=python-lmdb
version=1.0.0
release=3
pkgdesc="Universal Python binding for the LMDB Lightning Database"
url="https://github.com/jnwatson/py-lmdb/"
depends=(python python-cffi lmdb)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=(${url}/archive/${_pkg}_${version}.tar.gz)
sha256sums=('e01bc4d62679b6e8c8962f1110f910cdfbdb2d750b145f02fae8b8a9d787f2f9')

build() {
  cd ${_pkg}-${_pkg}_${version}
  LMDB_FORCE_SYSTEM=1 python setup.py build
}

check() {
  cd ${_pkg}-${_pkg}_${version}
  local _pyver
  _pyver="$(python -c 'import sys; print("%s.%s" %sys.version_info[0:2])')"
  export LMDB_FORCE_SYSTEM=1
  export PYTHONPATH="$(pwd)/build/lib.linux-${CARCH}-${_pyver}"
  pytest
}

package() {
  cd ${_pkg}-${_pkg}_${version}
  LMDB_FORCE_SYSTEM=1 python setup.py install --root="$pkgdir" --skip-build --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${name}
}
