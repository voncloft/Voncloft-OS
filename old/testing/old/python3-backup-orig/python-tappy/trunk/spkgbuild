# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=python-tappy
_pyname=tap.py
pkgver=2.5
pkgrel=3
pkgdesc="Test Anything Protocol (TAP) tools for Python"
arch=(any)
url="https://pypi.python.org/pypi/tap.py"
license=('BSD')
depends=('python' 'python-babel' 'python-pyaml' 'python-more-itertools')
makedepends=('python-mock')
options=(!emptydirs)
source=(https://files.pythonhosted.org/packages/source/t/${_pyname}/${_pyname}-${pkgver}.tar.gz
        https://github.com/python-tap/tappy/commit/83b5335f6f104cde2396dbf9dce5ceb3dd5049c3.patch)
sha256sums=('8ad62ba6898fcef4913c67d468d0c4beae3109b74c03363538145e31b1840b29'
            '28ac7dd6577641decdbf7ec689f7d920f34b4451e299af517e1fefb81d35a1fa')

prepare() {
	cd "$_pyname-$pkgver"
	patch -p1 -i ../83b5335f6f104cde2396dbf9dce5ceb3dd5049c3.patch
}

check() {
	cd "$_pyname-$pkgver"
	python setup.py test
}

package() {
	cd "$_pyname-$pkgver"
	python setup.py install --root="$pkgdir/" --optimize=1
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
