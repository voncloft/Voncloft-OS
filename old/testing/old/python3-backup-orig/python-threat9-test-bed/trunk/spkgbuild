# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-threat9-test-bed
_pyname=threat9-test-bed
_gitcommit=1ed61b34d0b138711e3f8267259cdb29787fe349
pkgver=0.6.0+2+g1ed61b3
pkgrel=4
pkgdesc='Threat9 Test Bed'
url='https://github.com/threat9/threat9-test-bed'
arch=('any')
license=('custom')
depends=('python' 'python-setuptools' 'python-click' 'python-faker' 'python-flask'
         'gunicorn' 'python-pyopenssl' 'python-requests')
makedepends=('git' 'python-setuptools-scm')
checkdepends=('python-pytest')
source=(${pkgname}::"git+https://github.com/threat9/threat9-test-bed#commit=${_gitcommit}")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  git describe --always --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd ${pkgname}
  python setup.py build
}

check() {
  cd ${pkgname}
  python -m pytest
}

package() {
  cd ${pkgname}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
