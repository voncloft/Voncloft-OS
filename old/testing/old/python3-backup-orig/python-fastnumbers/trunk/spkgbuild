# Maintainer: David Runge <dvzrv@archlinux.org>

_name=fastnumbers
pkgname=python-fastnumbers
pkgver=3.1.0
pkgrel=2
pkgdesc="Super-fast and clean conversions to numbers"
arch=('x86_64')
url="https://github.com/SethMMorton/fastnumbers"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-hypothesis')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('b58b6cac12a04f038707533040bff178eeb010cb3c1bc19ab9ddad534553c6aa8bb4bfea666a6f7fb9867b41044a62ada6c2be6a8237abdcc4fa49118acfd9fe')
b2sums=('2069866b60a60b5c807b0d878f1b0c691a81ffaf73c5723fc89ffa2b3eb6e4027a8716b2002eeb1e785874e8992fb7554563a229d1457314db19f32f0bd019a8')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  local _py3_ver=$(python --version | cut -d " " -f2)
  export PYTHONPATH="build/lib.linux-$CARCH-${_py3_ver%"."*}:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
