# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-filetype
pkgver=1.0.7
pkgrel=3
pkgdesc="Infer file type and MIME type of any file/buffer"
url="https://github.com/h2non/filetype.py"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/h2non/filetype.py/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f6d861a9108ffb796858bfdfb26d1bdf6aa9704794fa86340cc36a2920b4f11e12faf4271d19bca9a89be76d8fcff43e8859c8def2525ddbadd52a18c7f68cac')

build() {
  cd filetype.py-$pkgver
  python setup.py build
}

check() {
  cd filetype.py-$pkgver
  python -m unittest discover
}

package() {
  cd filetype.py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
