# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-h2
pkgver=4.0.0
pkgrel=1
pkgdesc="HTTP/2 State-Machine based protocol implementation"
arch=('any')
license=('MIT')
url="https://hyper.rtfd.org"
depends=('python-hpack' 'python-hyperframe')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-hypothesis')
source=("$pkgname-$pkgver.tar.gz::https://github.com/python-hyper/hyper-h2/archive/v$pkgver.tar.gz")
sha512sums=('84578174a13782897f9286c62d450eaf11aa71c01c96b8660179cd2df77618dc1a6c4b354b2bc5731cd21c4709c8c0a8282a859a98c4a62a7295b6dfd1b5cfb2')

build() {
  cd hyper-h2-$pkgver
  python setup.py build
}

check() {
  cd hyper-h2-$pkgver
  python setup.py pytest
}

package() {
  cd hyper-h2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
