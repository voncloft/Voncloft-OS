# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-colorlog
pkgver=4.7.2
pkgrel=1
pkgdesc='A colored formatter for the python logging module'
arch=('any')
url='https://github.com/borntyping/python-colorlog'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('0bb833faeb643962dd8b7c23e83e109368c303c53ecf59083f951867ecd94f5f94320754a1184c83807dc594db7df4abb786e79a441535fcce6639b1280c9a25')

build() {
  cd $pkgname-$pkgver

  python setup.py build
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
