# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=QDarkStyle
pkgname=python-${_pkg,,}
pkgver=2.8.1
pkgrel=3
pkgdesc="Dark style sheet for QtWidgets application"
arch=(any)
url="https://github.com/ColinDuquesnoy/QDarkStyleSheet"
license=(MIT CCPL)
depends=(python-helpdev python-qtpy)
makedepends=(python-setuptools)
checkdepends=(python-pyqt5 pyside2)
# Pypi tarballs do not provide tests files anymore
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('bc2f8d2b3644ed660c1267cba314df80a08ddaf28c1b7f701f6cdd6f44f70fdb')

build() {
  cd ${_pkg}Sheet-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}Sheet-${pkgver}
  # PyQt5 tests
  python example/example.py --qt_from=pyqt5 --test
  python example/example.py --qt_from=pyqt5 --test --no_dark
  # PySide2 tests
  python example/example.py --qt_from=pyside2 --test
  python example/example.py --qt_from=pyside2 --test --no_dark
}

package() {
  cd ${_pkg}Sheet-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE.rst -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
