# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pytest-rerunfailures
pkgname=python-pytest-rerunfailures
pkgver=9.1.1
pkgrel=3
pkgdesc="A plugin for py.test that re-runs failed tests to eliminate intermittent failures"
arch=('any')
url="https://github.com/pytest-dev/pytest-rerunfailures/"
license=('MPL2')
depends=('python-pytest')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('875591306ab010494ccbb865ea6445ab37422fce9105260d9495215af44057210564aca4fed3b4765b5a30c1cbef120de716d15d10a6db91dcb7d9f5b4f3b102')
b2sums=('b2118fe9460ebb750c72b05762d70cf53703e4c9982c6343ef02ebcec6630598ac98998ff994268b9108bd6850e71fd5b9cde1d0abb727d33fad238e9b26fa61')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build/lib:$PYTHONPATH"
  pytest -v
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGES.rst,README.rst}
}
