# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pkgname=uproot-methods
pkgname=python-uproot-methods
pkgver=0.7.4
pkgrel=3
pkgdesc="Pythonic mix-ins for ROOT classes"
arch=('any')
makedepends=('python-setuptools')
depends=('python-awkward' 'python-numpy')
url="https://github.com/scikit-hep/uproot-methods"
license=('BSD')

source=("${url}/archive/${pkgver}.zip")
sha256sums=('7bfb74516ac6fffb887c23d69d121c6fef5e04adcf32ccc259b2c36004546c03')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py build
}

check() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py test
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    python setup.py install --root="${pkgdir}/" --optimize=1

    install -D LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    install -D README.rst "${pkgdir}/usr/share/${pkgname}/README.rst"
}
