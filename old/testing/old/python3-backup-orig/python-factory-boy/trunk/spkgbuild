# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-factory-boy
pkgver=3.2.0
pkgrel=1
pkgdesc="A versatile test fixtures replacement based on thoughtbot's factory_bot for Ruby"
url="https://github.com/FactoryBoy/factory_boy"
license=('MIT')
arch=('any')
depends=('python-faker')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-flask' 'python-django' 'python-sqlalchemy'
              'python-flask-sqlalchemy' 'python-mongoengine' 'python-mongomock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/FactoryBoy/factory_boy/archive/$pkgver.tar.gz")
sha512sums=('1e9736491229aa1648cfe9956c157738f5b9ecfd43e39a2f1d299e3c018763518f04b9bde7b19c1c7dbef48e5a93db818f1010068be541b02453313f71bc37c0')

prepare() {
  cd factory_boy-$pkgver
  # Use mongomock
  sed -i '/serverselectiontimeoutms/a is_mock=True' tests/test_mongoengine.py
}

build() {
  cd factory_boy-$pkgver
  python setup.py build
}

check() {
  cd factory_boy-$pkgver
  python setup.py pytest
}

package() {
  cd factory_boy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
