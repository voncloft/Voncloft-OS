# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=netCDF4
_mpi=openmpi
pkgname=python-${_pkg,,}-${_mpi}
pkgver=1.5.5.1
pkgrel=1
pkgdesc="Python/NumPy interface to the netCDF C library (${_mpi} version)"
arch=(x86_64)
url="https://unidata.github.io/${_pkg,,}-python"
license=(MIT)
depends=(python-numpy python-cftime python-mpi4py "netcdf-${_mpi}")
makedepends=(cython python-setuptools)
conflicts=(python-netcdf4)
provides=(python-netcdf4)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('d957e55a667d1fc651ddef22fea10ded0f142b7d9dbbf4d08c0012d32f445abd')

build() {
    cd ${_pkg}-${pkgver}
    USE_NCCONFIG=1 python setup.py build
}

check() {
    cd ${_pkg}-${pkgver}/test
    local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
    PYTHONPATH="../build/lib.linux-${CARCH}-${python_version}" python -B ./run_all.py
}

package() {
    cd ${_pkg}-${pkgver}
    USE_NCCONFIG=1 python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=2

    install -Dm644 docs/netCDF4/* -t "${pkgdir}"/usr/share/doc/${pkgname}/docs/netCDF4
#   https://github.com/Unidata/netcdf4-python/pull/1060
#    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
