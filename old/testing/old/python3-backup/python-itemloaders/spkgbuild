# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-itemloaders
pkgver=1.0.4
pkgrel=1
pkgdesc="Library to populate items using XPath and CSS with a convenient API"
url="https://github.com/scrapy/itemloaders"
license=('BSD')
arch=('any')
depends=('python-w3lib' 'python-parsel' 'python-jmespath' 'python-itemadapter')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/scrapy/itemloaders/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b3f59c99db8dbf9757a304c55bd6b333347fdddc984edbeacd8314baaf8e319897b33801bc481d192330a573e75a6aa586d3da5bb11318287709723221bcbde6')

build() {
  cd itemloaders-$pkgver
  python setup.py build
}

check() {
  cd itemloaders-$pkgver
  python -m pytest
}

package() {
  cd itemloaders-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
