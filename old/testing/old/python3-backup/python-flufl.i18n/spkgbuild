# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.i18n
pkgname=python-flufl.i18n
pkgver=3.1.4
pkgrel=1
pkgdesc="A high level API for internationalization"
arch=('any')
url="https://gitlab.com/warsaw/flufl.i18n"
license=('Apache')
depends=('python-atpublic')
makedepends=('python-setuptools')
checkdepends=('python-sybil' 'python-pytest')
provides=('python-flufl-i18n')
replaces=('python-flufl-i18n')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('02de410e1ca911e2c452b5dd48868c8c409817b867c58dbebdda6aff6dfe4d09b2f7ac4c293cbc1306eb5caa17c1f5a63e9d8c0ee59e8e41058ce0a4517257ea')
b2sums=('1652ea9d0ff7b853298b3647da0fa4c692b9f227f42873194dcff00b119316f425012c534bbd5df87125517bf12cdd7a6d5eaa807f1bacf7c6c82967e8880adb')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we don't need code coverage during tests
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
