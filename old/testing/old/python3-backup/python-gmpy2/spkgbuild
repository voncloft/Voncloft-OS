# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Omar Sandoval <osandov at cs dot washington dot edu>
# Contributor: Alexander Slesarev <slesarew at gmail dot com>

pkgname=python-gmpy2
pkgver=2.1.0b5
pkgrel=3
pkgdesc="Provides C-coded Python modules for fast multiple-precision arithmetic."
arch=(x86_64)
url="https://github.com/aleaxit/gmpy"
license=(LGPL2.1)
depends=(python libmpc)
makedepends=(python-setuptools)
source=(https://pypi.io/packages/source/g/gmpy2/gmpy2-$pkgver.tar.gz)
sha256sums=('8951bcfc61c0f40102b92a4777daf9eb85445b537c4d09086deb0e097190bef0')

build() {
  cd gmpy2-$pkgver
  python setup.py build
}

package() {
  cd gmpy2-$pkgver 
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
 
  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}
