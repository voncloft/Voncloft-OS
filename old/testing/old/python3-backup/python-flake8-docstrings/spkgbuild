# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Pieter Goetschalckx <3.14.e.ter <at> gmail <dot> com>

_name=flake8-docstrings
pkgname=python-flake8-docstrings
pkgver=1.5.0
pkgrel=3
pkgdesc='Plugin to Flake8 to include checks provided by pydocstyle'
arch=('any')
url=https://gitlab.com/pycqa/flake8-docstrings
license=('MIT')
depends=('flake8' 'python-pydocstyle' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('3d5a31c7ec6b7367ea6506a87ec293b94a0a46c0bce2bb4975b7f1d09b6f3717')
b2sums=('f78d3604ee842f45637e53d6420910f1cdc611607883af8888918a753faa6e4ee020d4248c896fafda1f6d0515a2a3b33ab32ea159618f8524c03cd92ab55f17')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  mkdir -p temp
  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --skip-build --root=temp
  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$sitepackages" flake8 --exit-zero flake8_docstrings.py
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
