# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-parver
pkgver=0.3.1
pkgrel=3
pkgdesc="Parse and manipulate version numbers"
url="https://github.com/RazerM/parver"
license=('MIT')
arch=('any')
depends=('python-arpeggio' 'python-attrs' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-hypothesis' 'python-pretend')
source=("$pkgname-$pkgver.tar.gz::https://github.com/RazerM/parver/archive/$pkgver.tar.gz")
sha512sums=('2ea7eb7be0c58657f5261d7b4729da6caaf2fac67db3fc3f36d29627bdbeba45f6ecb0e4bb251575b621908eb1ff5fa5ede5fc2ef0e041156dcc00548fd34360')

build() {
  cd parver-$pkgver
  python setup.py build
}

check() {
  cd parver-$pkgver
  python setup.py pytest
}

package() {
  cd parver-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
