# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-soupsieve
pkgver=2.1.0
pkgrel=1
pkgdesc="A CSS4 selector implementation for Beautiful Soup"
url="https://github.com/facelessuser/soupsieve"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-beautifulsoup4' 'python-html5lib' 'python-lxml')
source=("https://github.com/facelessuser/soupsieve/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('19cd4ee596d189a001bbaf9f047e6c9eff569e2149ef5424bfe55c9b4325d856c957de87d55510335f748aab322dbb847b06b48931fbdeadbcc3790259c69eb1')

build() {
  cd soupsieve-$pkgver
  python setup.py build
}

check() {
  cd soupsieve-$pkgver
  pytest
}

package() {
  cd soupsieve-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
