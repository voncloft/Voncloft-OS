# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_name=ifaddr
pkgname=python-ifaddr
pkgver=0.1.7
pkgrel=3
pkgdesc="Enumerates all IP addresses on all network adapters of the system"
arch=('any')
url="https://github.com/pydron/ifaddr"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=(https://github.com/pydron/ifaddr/archive/${pkgver}.tar.gz)
sha512sums=('648d05c1aa91b81bfbda81c9753c3f35c7f451e9842eef9b46545db7817e4fdbe9b8f8f30f552cdef9522998960d7b07a4465dd49733fd784702f48aa875238d')

build() {
  cd "${_name}-${pkgver}"
  python3 setup.py build
}

package() {
  cd "${_name}-${pkgver}"

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

}

check() {
  cd "${_name}-${pkgver}"
  nosetests ${_name}/test_ifaddr.py
}

# vim:set ts=2 sw=2 et:
