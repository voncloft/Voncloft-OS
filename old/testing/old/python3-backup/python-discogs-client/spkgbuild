# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Guillaume Hayot <ghayot@postblue.info>
# Contributor: Arvedui <arvedui@posteo.de>
# Contributor: Marc Plano-Lesay <marc.planolesay@gmail.com>
# Contributor: Joost Bremmer <toost.b@gmail.com>

pkgname=python-discogs-client
pkgver=2.3.0
pkgrel=3
pkgdesc='Official Python Client for the Discogs API'
arch=(any)
url=https://github.com/discogs/discogs_client
license=(BSD)
depends=(
  python-six
  python-requests
  python-oauthlib
)
makedepends=(
  git
  python-setuptools
)
_tag=c159195a43ceb3def5a9be2acce43bbad71a2e32
source=(git+https://github.com/discogs/discogs_client.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd discogs_client
  git describe --tags | sed 's/^v//'
}

build() {
  cd discogs_client
  python setup.py build
}

package() {
  cd discogs_client
  python setup.py install --root=$pkgdir --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-discogs-client
}

# vim: set ts=2 sw=2 et:
