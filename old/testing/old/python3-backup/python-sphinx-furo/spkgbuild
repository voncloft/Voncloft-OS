# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=furo
pkgname=python-sphinx-$_pkgname
pkgver=2020.11.10.beta15
pkgrel=3
pkgdesc='A clean customizable documentation theme for Sphinx'
arch=('any')
url='https://github.com/pradyunsg/furo'
license=('MIT')
depends=('python-sphinx' 'python-beautifulsoup4')
makedepends=('npm' 'gulp' 'python2' 'python-setuptools' 'python-dephell' 'python-myst-parser' 'python-sphinx-inline-tabs')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('cadb1be5982b71f944397428515fc9821bd6874db6ce67a66f725342d109992ba2ad9d2de7029d17de4e6a2c5fb1746f3b4b5df92ba4d1fba66ac2b662746ea2')

prepare() {
  cd $_pkgname-$pkgver

  npm install
}

build() {
  cd $_pkgname-$pkgver

  gulp build
  rm -rf node_modules

  # this should be in prepare() but it evaluates the package data when we run dephell,
  # instead of using the glob module in setup.py or something similar, so we need to
  # build the data first with gulp and only then we can generate the setup.py
  dephell deps convert --from pyproject.toml --to setup.py

  python setup.py build

  # sphinx needs this theme installed because it is uses it.
  # simply injecting the package to sys.path (via PYTHONPATH or similar)
  # is not enough because sphinx looks for themes in the registered
  # entrypoints, this means we actually have to install the package.
  # for this we will create a venv with access to system packages and
  # install the theme there, then we will build the documentation.

  python -m venv --system-site-packages doc-env
  doc-env/bin/python setup.py install --skip-build

  doc-env/bin/python /usr/bin/sphinx-build -b dirhtml -v docs build/docs/html
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  cp -r -a --no-preserve=ownership build/docs/html "$pkgdir"/usr/share/doc/$pkgname
  rm -rf "$pkgdir"/usr/share/doc/$pkgname/html/.doctrees

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
