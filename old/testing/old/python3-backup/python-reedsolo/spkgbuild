# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=python-reedsolo
pkgver=1.5.4
pkgrel=1
pkgdesc="A pure-python universal errors-and-erasures Reed-Solomon Codec"
arch=('x86_64')
url="https://github.com/tomerfiliba/reedsolomon"
license=('MIT')
makedepends=('python' 'cython')
depends=('python')
source=($pkgname-$pkgver.tar.gz::https://github.com/tomerfiliba/reedsolomon/archive/v${pkgver}.tar.gz)
sha512sums=('c738f817c8f536ee86e5bcd3de73a4cf6a3e606ba196a359b6c09e7372dba31316ddb8b134d7c335100e4c2ff0158ff07caa35fee9bc20768bde23cd803e274a')

build() {
  cd "reedsolomon-${pkgver}"
  python3 setup.py build
}

check() {
  cd "reedsolomon-${pkgver}"
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  PYTHONPATH="$PWD/build/lib.linux-$CARCH-${python_version}" python -m unittest discover tests
}

package() {
  cd "reedsolomon-${pkgver}"
  python3 setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -D -m0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
