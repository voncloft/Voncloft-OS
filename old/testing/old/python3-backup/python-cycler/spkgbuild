# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cycler
pkgver=0.10.0
pkgrel=8
pkgdesc="Composable style cycles"
arch=('any')
license=('BSD')
url="https://github.com/matplotlib/cycler"
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-coverage')
source=("https://github.com/matplotlib/cycler/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('685231d455dfdde70eb71415dfe561e07c34579d7b31ef7197f4c1441301635b387fec903d610b35406b619264f4c0cc616562de8f02b8192414c29274b63c01')

build() {
  cd cycler-$pkgver
  python setup.py build
}

check() {
  cd cycler-$pkgver
  python run_tests.py
}

package() {
  cd cycler-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
