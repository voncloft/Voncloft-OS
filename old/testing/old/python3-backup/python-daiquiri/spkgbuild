# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-daiquiri
pkgver=3.0.0
pkgrel=3
pkgdesc='Library to configure Python logging easily'
arch=('any')
license=('Apache')
url='https://github.com/jd/daiquiri'
depends=('python-json-logger')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jd/daiquiri/archive/$pkgver.tar.gz")
sha512sums=('8e89ae4f4f84df35125687196921cda0136b039a189be54a0c184c596440ca80c00071c39af942e6e66e5abecb30c88bb31e73fe336958bea99f7a8c60e4db9f')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd daiquiri-$pkgver
  python setup.py build
}

check() {
  cd daiquiri-$pkgver
  python -m pytest
}

package() {
  cd daiquiri-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
