# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-purl
pkgver=1.5
pkgrel=6
pkgdesc="An immutable URL class for easy URL-building and manipulation"
arch=('any')
license=('MIT')
url="https://github.com/codeinthehole/purl"
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("https://github.com/codeinthehole/purl/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c034e563d25f4270d4aa9731e87df23203a1c6fc09d2df60fd6038e88c17341e2b30b04a9fba177a04a2552ce3ee0f2d9e34bf4aad6675eb23d2c7bd047d3813')

build() {
  cd purl-$pkgver
  python setup.py build
}

check() {
  cd purl-$pkgver
  python setup.py nosetests
}

package() {
  cd purl-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
