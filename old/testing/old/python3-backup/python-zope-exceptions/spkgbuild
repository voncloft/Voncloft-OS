# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-exceptions
pkgname=('python-zope-exceptions' 'python2-zope-exceptions')
pkgver=4.4
pkgrel=3
pkgdesc="Generic exceptions and implementations for Zope"
arch=('any')
url="https://github.com/zopefoundation/zope.exceptions"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface')
checkdepends=('python-zope-testrunner' 'python2-zope-testrunner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.exceptions/archive/$pkgver.tar.gz")
sha512sums=('31a2d5e3fc20b3e9923c8780b85d412adccd826a8d98a62baed116614190fc8a9991980db899781d09c910f11037cfaa2a5f788f08567b8be2bf0055bb6b4073')

prepare() {
  cp -a zope.exceptions-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.exceptions-$pkgver
  python setup.py build

  cd "$srcdir"/zope.exceptions-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.exceptions-$pkgver
  python setup.py test || :

  cd "$srcdir"/zope.exceptions-$pkgver-py2
  python2 setup.py test
}

package_python-zope-exceptions() {
  depends=('python-setuptools' 'python-zope-interface')

  cd zope.exceptions-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-exceptions() {
  depends=('python2-setuptools' 'python2-zope-interface')

  cd zope.exceptions-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
