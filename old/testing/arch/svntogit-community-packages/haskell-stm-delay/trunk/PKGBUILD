# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_pkgname=stm-delay
pkgname=haskell-stm-delay
pkgver=0.1.1.1
pkgrel=8
pkgdesc='Updatable one-shot timer polled with STM'
arch=(x86_64)
url="https://hackage.haskell.org/package/$_pkgname"
license=(BSD)
depends=(ghc-libs haskell-stm)
makedepends=(ghc)
source=("https://hackage.haskell.org/packages/archive/$_pkgname/$pkgver/$_pkgname-$pkgver.tar.gz")
sha512sums=('4ee1d8c109e5ecd9dec075d4b0516b05a80c417e46f62473087e1baa83182d31515b8052728db9e266e7d4f3849cb31549e3eff3bc998828ea33b167a7a96f2c')

build() {
  cd $_pkgname-$pkgver
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
      --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
      --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_pkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $_pkgname-$pkgver
  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et:
