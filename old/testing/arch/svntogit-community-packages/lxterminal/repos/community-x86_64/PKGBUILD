# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=lxterminal
pkgver=0.3.2
pkgrel=2
pkgdesc="VTE-based terminal emulator (part of LXDE)"
arch=('x86_64')
url="https://lxde.org/"
license=('GPL2')
groups=('lxde' 'lxde-gtk3')
depends=('vte3')
makedepends=('intltool')
conflicts=('lxterminal-gtk3')
replaces=('lxterminal-gtk3')
source=(https://downloads.sourceforge.net/lxde/${pkgname}-${pkgver}.tar.xz
        0001-Don-t-use-deprecated-vte_terminal_match_add_gregex.patch)
sha256sums=('3166b18493a8e55811b02aa0de825cbbea65e2b628e69006c1a65b98e1bb4484'
            'bae1370beff82d14eea14b684ace5033c70483184030ac46547cadba33a5d9a5')

prepare() {
  cd $pkgname-$pkgver
  # https://sourceforge.net/p/lxde/bugs/932/
  patch -Np1 -i ../0001-Don-t-use-deprecated-vte_terminal_match_add_gregex.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --sysconfdir=/etc --prefix=/usr --enable-gtk3
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
