# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: jason ryan <jasonwryan@gmail.com>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>

pkgbase=profanity
pkgname=('profanity' 'profanity-gtk')
pkgver=0.10.0
pkgrel=1
epoch=1
pkgdesc='Console based XMPP client'
url='http://www.profanity.im'
arch=('x86_64')
license=('GPL3')
makedepends=(
  'curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify' 'gpgme'
  'python' 'gtk2' 'autoconf-archive' 'libsignal-protocol-c' 'libgcrypt'
  'libx11' 'libxss' 'libxcrypt' 'glibc' 'freetype2' 'readline' 'libassuan'
  'harfbuzz' 'pango' 'gdk-pixbuf2' 'atk' 'cairo' 'libgpg-error')
checkdepends=('cmocka')
source=(https://github.com/profanity-im/profanity/archive/${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('9702085e8323a09c12c22b0471214689d70bf0a3b00e438201f14248bf85a1f2')
sha512sums=('998a190991eb20188a208ee659b6e90f4c15c37ccd2b9013850f20627c09df3af104ba1308a355055766b7e9e29fd5821c5d0df62b501549500af85ad03d3196')
b2sums=('c8a8d257658a0d892582848fec3b84a13f0b8a2ca4cbeaaf23c7d5e590c61645a989832a9e23e2c52044680edcf79706babec6d83183d7e88f15243f6402e0ee')

prepare() {
  cd ${pkgname}-${pkgver}
  mkdir -p m4
  autoreconf -fiv
  cp -a "${srcdir}"/${pkgname}-${pkgver}{,-gtk}
}

build() {
  echo "Building non-gtk variant..."
  (cd ${pkgname}-${pkgver}
    ./configure \
      --prefix=/usr \
      --disable-icons-and-clipboard \
      --without-xscreensaver \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-omemo \
      --enable-pgp
    make
  )
  echo "Building gtk variant..."
  (cd ${pkgname}-${pkgver}-gtk
    ./configure \
      --prefix=/usr \
      --enable-icons-and-clipboard \
      --with-xscreensaver \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-omemo \
      --enable-pgp
    make
  )
}

package_profanity() {
  depends=(
    'curl' 'libcurl.so' 'ncurses' 'libncursesw.so' 'expat' 'glib2' 'libotr'
    'libmesode' 'libmesode.so' 'libnotify' 'libnotify.so' 'libxcrypt' 'libcrypt.so'
    'gpgme' 'libgpgme.so' 'python' 'libgcrypt' 'libsignal-protocol-c'
    'glibc' 'readline' 'libreadline.so' 'libassuan' 'libgpg-error'
  )
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

package_profanity-gtk() {
  pkgdesc+=' (gtk support)'
  depends=(
    'curl' 'libcurl.so' 'ncurses' 'libncursesw.so' 'expat' 'glib2' 'libotr'
    'libmesode' 'libmesode.so' 'libnotify' 'libnotify.so' 'libxcrypt' 'libcrypt.so'
    'gpgme' 'libgpgme.so' 'python' 'libgcrypt' 'libsignal-protocol-c'
    'glibc' 'readline' 'libreadline.so' 'libassuan' 'libgpg-error'

    'gtk2' 'libxss' 'libx11' 'pango' 'libpango-1.0.so' 'libpangoft2-1.0.so'
    'freetype2' 'libfreetype.so' 'harfbuzz' 'libharfbuzz.so' 'gdk-pixbuf2'
    'atk' 'libatk-1.0.so' 'cairo'
  )
  provides=('profanity')
  conflicts=('profanity')
  cd ${pkgbase}-${pkgver}-gtk
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
