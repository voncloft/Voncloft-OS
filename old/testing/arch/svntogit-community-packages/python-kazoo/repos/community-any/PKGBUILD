# Maintainer: David Runge <dvzrv@archlinux.org>

_name=kazoo
pkgname=python-kazoo
pkgver=2.8.0
pkgrel=2
pkgdesc="A high-level Python library that makes it easier to use Apache Zookeeper"
arch=('any')
url="https://github.com/python-zk/kazoo"
license=('Apache')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'python-mock' 'python-nose' 'python-objgraph')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('ae244a70356274d6259d255e60a5dff00eb22bcf16d5b25f9d9a03178619dacb57414905b473801347b4ec1a19e5600be6c7040a5e53b67b81b440c304c95cc0')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # TODO: run zookeeper here
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGES,CONTRIBUTING,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
