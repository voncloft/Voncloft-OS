# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-videos
pkgver=2.7.2
pkgrel=1
pkgdesc='The Pantheon Video Player'
arch=(x86_64)
url='https://github.com/elementary/videos'
license=(GPL3)
groups=(pantheon)
depends=(
  clutter
  clutter-gst
  clutter-gtk
  gdk-pixbuf2
  glib2
  gst-plugins-base-libs
  gstreamer
  gtk3
  libgee
  libgranite.so
  libx11
)
makedepends=(
  git
  intltool
  meson
  vala
)
optdepends=(
  'gst-plugins-base: "Base" plugin libraries'
  'gst-plugins-good: "Good" plugin libraries'
  'gst-plugins-bad: "Bad" plugin libraries'
  'gst-plugins-ugly: "Ugly" plugin libraries'
)
_tag=0faf03232b236d4f823b2fcbb8a70149c69dd44e
source=(pantheon-videos::git+https://github.com/elementary/videos.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pantheon-videos

  git describe --tags
}

build() {
  arch-meson pantheon-videos build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
