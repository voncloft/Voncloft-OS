# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Pajaro

pkgname=dssi
pkgver=1.1.1
pkgrel=11
pkgdesc="An API for audio processing plugins & softsynths with UIs"
arch=('x86_64')
url="http://dssi.sourceforge.net/"
license=('BSD' 'LGPL')
depends=('liblo' 'jack')
makedepends=('ladspa')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('faf35ae851b889ad17a8ba624c713af8a5f33bb93db8e34842e8b778a0e36c3eab9ab712a40e4ca7ecd96954f3e70504d89a75b3826dd138332085e176c0d34d')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -vDm 644 COPYING \
    -t "$pkgdir/usr/share/licenses/${pkgname}/"
  install -vDm 644 {ChangeLog,README} \
    -t "$pkgdir/usr/share/doc/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
