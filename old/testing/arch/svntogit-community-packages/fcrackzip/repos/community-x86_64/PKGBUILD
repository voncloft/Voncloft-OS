# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Daenyth <Daenyth+Arch at gmail dot com>

pkgname=fcrackzip
pkgver=1.0
pkgrel=6
pkgdesc='Zip file password cracker'
arch=('x86_64')
url="http://oldhome.schmorp.de/marc/$pkgname.html"
license=('GPL')
depends=('glibc')
source=("http://oldhome.schmorp.de/marc/data/$pkgname-$pkgver.tar.gz")
sha256sums=('4a58c8cb98177514ba17ee30d28d4927918bf0bdc3c94d260adfee44d2d43850')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  mv "$pkgdir/usr/bin/zipinfo" "$pkgdir/usr/bin/fzipinfo"
}

# vim:set ts=2 sw=2 et:
