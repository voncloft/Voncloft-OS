# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>

pkgname=cm256cc
pkgver=1.0.5
pkgrel=2
pkgdesc='Fast GF(256) Cauchy MDS Block Erasure Codec in C++'
arch=('x86_64')
url='https://github.com/f4exb/cm256cc'
license=('GPL3')
makedepends=('cmake' 'boost')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('2bf86a155becf64f91430a1380f152716e20785e05de42791498dd577b9051a0d6f9e6818bab0f5b1f88141f70cc46390fabdb4fbdef1aff9a3e51c2085c1ebd')

build() {
  mkdir -p $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr

  make
}

check() {
  $pkgname-$pkgver/build/cm256_test
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install
}
