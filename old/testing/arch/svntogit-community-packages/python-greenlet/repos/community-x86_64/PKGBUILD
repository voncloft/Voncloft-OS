# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgbase=python-greenlet
pkgname=(python-greenlet python2-greenlet)
pkgver=1.0.0
pkgrel=1
pkgdesc="Lightweight in-process concurrent programming"
license=("MIT")
arch=('x86_64')
url="https://pypi.org/project/greenlet/"
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/g/greenlet/greenlet-${pkgver}.tar.gz")
sha512sums=('7b615792c340e0eb8ca61bd09bde48ed15110d97254910bd8598312e15adb7267ba795e9e6b52f0998db27d8413d2e1aa9e977511c0d09cd3a5583b37475f882')

prepare() {
    cp -a greenlet-$pkgver{,-py2}
}

build() {
	cd "$srcdir"/greenlet-$pkgver
	python setup.py build

    cd "$srcdir"/greenlet-$pkgver-py2
    python2 setup.py build
}

check() {
    cd "$srcdir"/greenlet-$pkgver
    PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.9" python -m unittest discover -v greenlet.tests

    cd "$srcdir"/greenlet-$pkgver-py2
    PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7" python2 -m unittest discover -v greenlet.tests
}

package_python-greenlet() {
    depends=('python')

    cd greenlet-$pkgver
    python setup.py install -O1 --root="$pkgdir"
    install -Dm0644 LICENSE.PSF "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.PSF
}

package_python2-greenlet() {
    depends=('python2')

	cd greenlet-$pkgver-py2
	python2 setup.py install -O1 --root="$pkgdir"
	install -Dm0644 LICENSE.PSF "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.PSF
}
