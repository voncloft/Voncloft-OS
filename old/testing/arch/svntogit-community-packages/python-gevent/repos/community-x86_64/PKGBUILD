# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgbase=python-gevent
pkgname=(python-gevent python2-gevent)
pkgver=21.1.2
pkgrel=2
arch=('x86_64')
pkgdesc="Python network library that uses greenlet and libev for easy and scalable concurrency"
license=("MIT")
url="http://www.gevent.org/"
makedepends=('python-greenlet' 'python2-greenlet' 'python-setuptools' 'python2-setuptools' 'cython'
             'cython2' 'c-ares' 'libev')
checkdepends=('lsof' 'python-perf' 'python2-perf' 'python2-virtualenv' 'python-objgraph'
              'python2-objgraph' 'python-zope-interface' 'python2-zope-interface' 'python-dnspython'
              'python2-dnspython' 'python-requests' 'python2-requests' 'python-zope-event'
              'python2-zope-event' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/gevent/gevent/archive/$pkgver.tar.gz")
sha512sums=('7dae1dc766bc3cd36312c2b3d11b9ff77a8258b54a39f79357296d4c69d54ec60d6dc4cd8b4299dda26e0f8b93f670e061f3c50e13da33bea5a43d6497d14c4a')

export EMBED=0

prepare() {
  cp -a gevent-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/gevent-$pkgver

  CYTHON=cython \
  PYTHON=python \
  python setup.py build

  cd "$srcdir"/gevent-$pkgver-py2

  CYTHON=cython2 \
  PYTHON=python2 \
  python2 setup.py build
}

check() {
  (
    cd "$srcdir"/gevent-$pkgver
    python -m venv "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    export PYTHONPATH="$PYTHONPATH:/usr/lib/python3.6/site-packages"
    python setup.py install
    python -m gevent.tests
  ) || echo "Tests failed"

  (
    cd "$srcdir"/gevent-$pkgver-py2
    virtualenv2 "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    export PYTHONPATH="$PYTHONPATH:/usr/lib/python2.7/site-packages"
    python setup.py install
    python2 -m gevent.tests
  ) || echo "Tests failed"
}

package_python-gevent() {
  depends=('c-ares' 'libev' 'python-greenlet' 'python-setuptools' 'python-zope-event' 'python-zope-interface')

  cd gevent-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-gevent() {
  depends=('c-ares' 'libev' 'python2-greenlet' 'python2-setuptools' 'python2-zope-event' 'python2-zope-interface')

  cd gevent-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
