# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-sortedcontainers
pkgver=2.3.0
pkgrel=3
pkgdesc="Python Sorted Container Types: SortedList, SortedDict, and SortedSet"
arch=('any')
license=('Apache')
url="http://www.grantjenks.com/docs/sortedcontainers/"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/grantjenks/python-sortedcontainers/archive/v$pkgver.tar.gz")
sha512sums=('0488e2f9f07ea7e4737ade69441f02614b63cfccb32a71af4e5953001bfa840d2e113a36a0a0a6d4c1e7fdd920ac3937b0baaea522d742547a4d81a99b8d1a6c')

prepare() {
  # Don't use tox
  sed -i '/tests_require/d' python-sortedcontainers-$pkgver/setup.py
}

build() {
  cd python-sortedcontainers-$pkgver
  python setup.py build
}

check() {
  cd python-sortedcontainers-$pkgver
  python setup.py pytest
}

package() {
  cd python-sortedcontainers-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
