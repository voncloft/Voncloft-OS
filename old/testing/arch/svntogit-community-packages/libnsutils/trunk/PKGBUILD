# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=libnsutils
pkgver=0.1.0
_netsurf=3.10
pkgrel=2
pkgdesc='NetSurf Utilities'
arch=(x86_64)
url='https://source.netsurf-browser.org/libnsutils.git/'
license=(MIT)
makedepends=(netsurf-buildsystem)
source=("https://download.netsurf-browser.org/netsurf/releases/source-full/netsurf-all-$_netsurf.tar.gz")
sha256sums=('495adf6b6614ce36fca6c605f7c321f9cb4a3df838043158122678ce2b3325b7')

build() {
  make -C netsurf-all-$_netsurf/$pkgname PREFIX=/usr INCLUDEDIR=include \
    LIBDIR=lib COMPONENT_TYPE=lib-shared
}

package() {
  cd netsurf-all-$_netsurf/$pkgname
  make PREFIX=/usr INCLUDEDIR=include LIBDIR=lib COMPONENT_TYPE=lib-shared \
    DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# getver: source.netsurf-browser.org/libnsutils.git/plain/Makefile
# vim: ts=2 sw=2 et:
