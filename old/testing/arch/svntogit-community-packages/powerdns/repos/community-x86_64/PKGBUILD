# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Remi Gacogne <rgacogne[at]archlinux[dot]org>
# Contributor: Pieter Lexis <pieter[dot]lexis[at]powerdns[dot]com>

pkgname=powerdns
pkgver=4.3.1
pkgrel=3
pkgdesc='Authoritative DNS server'
url='https://www.powerdns.com/'
arch=('x86_64')
license=('GPL2')
depends=('boost-libs' 'libcurl.so' 'libsodium' 'lmdb' 'luajit' 'openssl' 'protobuf'
         'sqlite' 'systemd-libs')
makedepends=('boost' 'geoip' 'libldap' 'mariadb-libs' 'curl' 'libmaxminddb'
             'postgresql-libs' 'systemd' 'tinycdb' 'unixodbc' 'yaml-cpp')
optdepends=('geoip: GeoIP backend'
            'mariadb-libs: MariaDB/MySQL and MyDNS backend'
            'libldap: LDAP backend'
            'libmaxminddb: MaxMindDB GeoIP2 database for the GeoIP backend'
            'postgresql-libs: PostgreSQL backend'
            'unixodbc: ODBC backend'
            'tinycdb: TinyDNS backend'
            'yaml-cpp: GeoIP backend')
provides=('pdns')
conflicts=('pdns')
backup=('etc/powerdns/pdns.conf')
source=(https://downloads.powerdns.com/releases/pdns-${pkgver}.tar.bz2{,.asc}
        boost-1.73.patch
        sysusers.conf)
sha512sums=('e61e23544f1e57c1ec3a85b4edbc3ff3e8e831410237afdc8896e9f34602774c1dc8bd02eb7440e765262fcc988f98e47b6f1b7945427b1f4a59dd6b84b9437e'
            'SKIP'
            'ba902584687f0243a5d44a7733ed817432c603c0cdfd4d80e416b7dfac76f6631e256c5b59507e14a171cc63e47c8d0c8066bb54da0578c66f2ae64abce51fcf'
            '522bde8b76367db6edd461fca8f81d50304a4d764addf33774b3e2c5c93a9e72d93993f54ddd4ee887ceeccf70280aceea3ffaf6370d26310e0a7ebbbf61f26a')
validpgpkeys=('B76CD4671C0968BAA87DE61C5E50715BF2FFE1A7'   # Winkels, Erik <erik.winkels@open-xchange.com>
              '16E12866B7738C73976A57436FFC33439B0D04DF'   # Peter van Dijk <peter.van.dijk@powerdns.com>
              'FBAE0323821C7706A5CA151BDCF513FA7EED19F3' ) # Pieter Lexis <pieter.lexis@powerdns.com>

prepare() {
  cd pdns-${pkgver}
  patch -Np1 -i ../boost-1.73.patch
  # Patch the Makefile.in's so /powerdns is used instead of /pdns (for e.g. $LIBDIR)
  # This allows for running pdns_server without setting `module-dir` in the config.
  find . -name 'Makefile.in' -exec sed -i 's,pkglibdir = \$(libdir)/@PACKAGE@,pkglibdir = $(libdir)/powerdns,' {} \;
}

build() {
  cd pdns-${pkgver}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/powerdns \
    --sbindir=/usr/bin \
    --with-modules='' \
    --with-dynmodules="bind geoip gmysql godbc gpgsql gsqlite3 ldap lmdb lua2 pipe random remote tinydns" \
    --docdir=/usr/share/doc/powerdns \
    --with-libsodium \
    --enable-tools \
    --enable-ixfrdist \
    --disable-dependency-tracking \
    --disable-silent-rules \
    --enable-reproducible \
    --enable-unit-tests \
    --enable-systemd \
    --with-service-user=powerdns \
    --with-service-group=powerdns
  make
}

check() {
  make -C pdns-${pkgver} check
}

package() {
  make -C pdns-${pkgver} DESTDIR="${pkgdir}" install
  install -Dm 644 sysusers.conf "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  mv "${pkgdir}/etc/powerdns/pdns.conf"{-dist,}
  # Ensure we run as the powerdns user
  sed -i "s,^# \(set.id=\),\1${pkgname}," "${pkgdir}/etc/powerdns/pdns.conf"
}

# vim: ts=2 sw=2 et:
