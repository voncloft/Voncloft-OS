# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Simon Legner <Simon.Legner@gmail.com>
# Contributor: Dmitri Goutnik <dg@syrec.org>
# Contributor: Lubomir 'Kuci' Kucera <kuci24-at-gmail-dot-com>

pkgname=gox
pkgver=1.0.1
pkgrel=4
pkgdesc="A simple, no-frills tool for Go cross compilation"
arch=('x86_64')
url="https://github.com/mitchellh/${pkgname}"
license=('MPL2')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('25aab55a4ba75653931be2a2b95e29216b54bd8fecc7931bd416efe49a388229')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build .
}

check() {
  cd $pkgname-$pkgver
  go test .
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
