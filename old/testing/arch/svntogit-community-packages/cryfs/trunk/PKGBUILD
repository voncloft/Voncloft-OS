# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: kaylyn
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=cryfs
pkgver=0.10.2
pkgrel=2
pkgdesc="Cryptographic filesystem for the cloud"
arch=(x86_64)
depends=(fuse2 curl)
makedepends=(cmake python boost)
url="https://www.cryfs.org"
license=(LGPL3)
source=("https://github.com/cryfs/cryfs/releases/download/$pkgver/cryfs-$pkgver.tar.xz"{,.asc})
sha256sums=('5531351b67ea23f849b71a1bc44474015c5718d1acce039cf101d321b27f03d5'
            'SKIP')
validpgpkeys=(C3111FE656732704EF6DF059017DC932549E65B2) # CryFS Team <messmer@cryfs.org>

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCRYFS_UPDATE_CHECKS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm 644 "$srcdir"/LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm 644 "$srcdir"/README.md -t "$pkgdir"/usr/share/doc/$pkgname
}
