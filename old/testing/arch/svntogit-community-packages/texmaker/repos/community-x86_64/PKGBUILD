# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Federico Cinelli <cinelli@aur.archlinux.org>

pkgname=texmaker
pkgver=5.0.4
pkgrel=1
pkgdesc='Free cross-platform LaTeX editor'
arch=(x86_64)
url="https://www.xm1math.net/texmaker/index.html"
license=(GPL)
depends=(qt5-script texlive-core)
optdepends=('ghostscript: LaTeX to HTML conversion command')
source=("https://www.xm1math.net/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('1de3ab3f6d6bee37a7d0b528ca1cbb7ce8e7aef5ec6a4405aff2caf9d12bd0e2')

build() {
  cd $pkgname-$pkgver
  qmake-qt5 PREFIX=/usr texmaker.pro
  make
}
package() {
  cd $pkgname-$pkgver

  install -dm755 "$pkgdir"/usr/share
  make INSTALL_PATH="$pkgdir" INSTALL_ROOT="$pkgdir" PREFIX="/usr" install
}
