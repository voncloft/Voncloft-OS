# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=shelly
pkgname=haskell-shelly
pkgver=1.9.0
pkgrel=44
pkgdesc="Shell-like (systems) programming in Haskell"
url="https://github.com/yesodweb/Shelly.hs"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-unix-compat' 'haskell-monad-control' 'haskell-lifted-base'
         'haskell-lifted-async' 'haskell-enclosed-exceptions'
         'haskell-async' 'haskell-transformers-base')
makedepends=('ghc' 'haskell-hunit' 'haskell-hspec' 'haskell-hspec-contrib')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('7c1eb25439e08095187746d0101fd92ad56ebb5a706820bad7330667cfc806066d4675c677e5d267440d4af7b517f36449e8f5e75332521780b564edc1586254')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-lifted -f-build-examples
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
