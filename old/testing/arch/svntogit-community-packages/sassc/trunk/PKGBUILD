# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Isaac Dupree <antispam@idupree.com>

pkgname=sassc
pkgver=3.6.1
pkgrel=2
pkgdesc="C implementation of Sass CSS preprocessor."
arch=("x86_64")
url="http://libsass.org/"
license=("MIT")
depends=("libsass")
source=("$pkgname-$pkgver.tar.gz::https://github.com/sass/$pkgname/archive/$pkgver.tar.gz")
sha256sums=("8cee391c49a102b4464f86fc40c4ceac3a2ada52a89c4c933d8348e3e4542a60")


build() {
	cd "$srcdir/$pkgname-$pkgver"

	autoreconf -i
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir/" install
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
