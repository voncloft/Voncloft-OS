# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-picker
pkgver=5.0.9
pkgrel=1
pkgdesc='Color picker tool for deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-picker"
license=('GPL3')
depends=('deepin-qt5integration' 'libxtst' 'qt5-svg')
makedepends=('qt5-tools' 'deepin-gettext-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-picker/archive/$pkgver.tar.gz")
sha512sums=('d27660a1d2dae04825b844e2eb262d1564f198ee1c9f06e86c48aa1b58138aeef50070f234e9e9a627d4887d01eeaf3854a8ba771487ff03f0f7111d3ab2cdc7')

prepare() {
  cd deepin-picker-$pkgver
  sed -i '/#include <QPainter>/a #include <QPainterPath>' src/animation.cpp src/picker.cpp
}

build() {
  cd deepin-picker-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-picker-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
