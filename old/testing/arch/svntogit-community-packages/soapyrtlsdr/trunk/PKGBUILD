# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=soapyrtlsdr
_pkgname=SoapyRTLSDR
pkgver=0.3.0
_gitver=soapy-rtlsdr-$pkgver
pkgrel=2
pkgdesc='Soapy SDR plugin for RTL-SDR devices'
arch=('x86_64')
url='https://github.com/pothosware/SoapyRTLSDR'
license=('MIT')
depends=('soapysdr' 'rtl-sdr')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_gitver.tar.gz")
sha512sums=('ccc0b3a5174bb1b57a79ecd0395ccaa5c3fada8ea7dce08d35a07992e84fef2cb45d92744fb0064857df36ad8120f495c3841b0fdca1870b9ed2328fb639ac21')

build() {
  mkdir -p $_pkgname-$_gitver/build
  cd $_pkgname-$_gitver/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd $_pkgname-$_gitver/build

  make DESTDIR="$pkgdir" install

  install -Dm 644 "$srcdir"/$_pkgname-$_gitver/LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
