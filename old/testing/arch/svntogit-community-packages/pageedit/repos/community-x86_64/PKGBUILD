# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

pkgname=pageedit
pkgver=1.4.0
pkgrel=1
pkgdesc="Simple Single Page XHTML Visual Editor, split from Sigil's BookView"
arch=('x86_64')
url="https://github.com/Sigil-Ebook/PageEdit"
license=('GPL3')
depends=('qt5-webengine')
makedepends=('cmake' 'qt5-tools')
source=("PageEdit-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "${url}/releases/download/${pkgver}/PageEdit-${pkgver}.tar.gz.sig")
sha256sums=('f14c1e998961edc260f8019f71dcc1d4314294d5fee6b7b27e7075a94d367e18'
            'SKIP')
b2sums=('4b14310a60de379b761a3c9b10b1192a6b5578731e04651bc6904dbf5a0695a65a5eb43051bc36ebfdd2a423e8f75a3c32fbd76e2e8d2281cc1a906537fb8465'
        'SKIP')
validpgpkeys=('B5A56206AB0FBC1A24EFAB8AA166D29A8FCDAC63') # Doug Massay <douglaslmassay@gmail.com>

build() {
    mkdir -p "${srcdir}"/PageEdit-${pkgver}/build
    cd "${srcdir}"/PageEdit-${pkgver}/build

    cmake -G "Unix Makefiles" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DINSTALL_BUNDLED_DICTS=0 \
        -DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
        -DCMAKE_CXX_FLAGS:STRING="${CXXFLAGS}" \
        ..
    make
}

package() {
    cd "${srcdir}"/PageEdit-${pkgver}/build

    make install DESTDIR="${pkgdir}"
}
