# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libfabric
pkgver=1.11.2
pkgrel=1
pkgdesc="User-space API for OpenFabrics Interfaces (OFI)"
arch=(x86_64)
url="https://ofiwg.github.io/libfabric/"
license=(GPL2)
depends=(glibc)
source=(https://github.com/ofiwg/libfabric/releases/download/v${pkgver}/libfabric-${pkgver}.tar.bz2)
sha512sums=('79d9ce52287ead57dfec20bcca91d4ca186e709379b55669612e596f40f02c83fee1c69601927e1894a01395eff128bd918c8f23ffb198f1bbba9b8d484780ef')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fvi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
