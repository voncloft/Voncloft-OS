# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_pkgbasename=flac
pkgname=lib32-$_pkgbasename
pkgver=1.3.3
pkgrel=1
pkgdesc='Free Lossless Audio Codec (32-bit)'
url='https://xiph.org/flac/'
arch=('x86_64')
license=('BSD' 'GPL')
depends=('lib32-gcc-libs' 'lib32-libogg' $_pkgbasename)
makedepends=('nasm')
source=(https://downloads.xiph.org/releases/flac/${_pkgbasename}-${pkgver}.tar.xz)
sha256sums=('213e82bd716c9de6db2f98bcadbc4c24c7e2efe8c75939a1a84e28539c4e1748')
sha512sums=('d6417e14fab0c41b2df369e5e39ce62a5f588e491af4d465b0162f74e171e5549b2f061867f344bfbf8aaccd246bf5f2acd697e532a2c7901c920c69429b1a28')

build() {
  cd ${_pkgbasename}-${pkgver}

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --disable-thorough-tests \
    --disable-sse
  make
}

check() {
  cd ${_pkgbasename}-${pkgver}
  make check
}

package() {
  provides=('libFLAC.so' 'libFLAC++.so')

  cd ${_pkgbasename}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
