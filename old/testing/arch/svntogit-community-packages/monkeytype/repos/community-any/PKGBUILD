# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=MonkeyType
pkgname=monkeytype
pkgver=20.5.0
pkgrel=2
pkgdesc='A system for Python that generates static type annotations by collecting runtime types'
arch=('any')
url='https://github.com/instagram/MonkeyType'
license=('BSD')
depends=('python-mypy_extensions' 'python-libcst')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-django')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        py3.9.patch)
sha512sums=('1fd0eb9bd83e7f27de5b7e75d06a89d851220b165d8becd7bac75c196361315669142c5f8838e85696e5dceaeaae6b64b71e3e46010546c65fa4f9d9b62f55d2'
            '81b37bf0ca06a65d10aa5efe7da200564b234247eb63c619a2ad3243ad243f9aebeaf244dfd69c4032341c47febd12e03c8ac5b4fe8dedf21492600c874ac4d3')

prepare() {
  cd $_pkgname-$pkgver

  patch -p1 -i ../py3.9.patch
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$_pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
