# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Kevin MacMartin <prurigro@gmail.com>
# Contributor: openfbt
# Contributor: Werecat
# Contributor: Xyne

pkgname=cjdns
pkgver=21
pkgrel=1
pkgdesc='Routing engine designed for security, scalability, speed and ease of use'
url='https://github.com/cjdelisle/cjdns'
arch=('x86_64')
license=('GPL3')
depends=('glibc' 'sh')
optdepends=('nodejs: optional utilities support')
makedepends=('nodejs' 'python')
install=cjdns.install
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/cjdelisle/${pkgname}/archive/cjdns-v${pkgver}.tar.gz)
sha512sums=('2c0b80c715c8895b573c09787df8be9fcc721d0903c90a32649e22f72e2d7b0b13d4254f31936c61f47cdd1fee8e3cf4a28f827571f94972557258e4a42f9eb2')
b2sums=('6118c8d0f43ae99c168d40da822f43fa3498bc4f88277f53362edcb7ba11e482b77e0bde1274303c905e02646c4c47af8d0534f256cd4fded35dcece16f30b2c')

build() {
  cd ${pkgname}-${pkgname}-v${pkgver}
  # Pretend to be cross compiling so -march=native is not set.
  CROSS="true" CJDNS_RELEASE_VERSION="${pkgver}" \
    node ./node_build/make.js
}

package() {
  cd ${pkgname}-${pkgname}-v${pkgver}
  install -Dm 755 cjdroute -t "${pkgdir}/usr/bin"
  install -Dm 644 contrib/systemd/{cjdns,cjdns-resume}.service \
    -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 doc/man/cjdroute.conf.5 -t "${pkgdir}/usr/share/man/man5"
  install -Dm 644 -t "${pkgdir}/usr/share/doc/${pkgname}" \
    README.md \
    doc/admin-api.md \
    doc/configure.md \
    doc/djc_layer_model.md \
    doc/nat-gateway.md \
    doc/network-services.md \
    doc/non-root-user.md \
    doc/security_specification.md \
    doc/shorewall_and_vpn_gateway_howto.md \
    doc/tunnel.md
  cp -a tools "${pkgdir}/usr/lib/${pkgname}"
  cp -a node_modules "${pkgdir}/usr/lib/${pkgname}/node_modules"
}

# vim: ts=2 sw=2 et:
