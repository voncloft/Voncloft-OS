# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgbase=cloud-utils
pkgname=(cloud-utils cloud-guest-utils cloud-image-utils)
pkgver=0.32
pkgrel=1
url="https://launchpad.net/cloud-utils"
arch=('any')
license=('GPL3')
source=("https://launchpad.net/cloud-utils/trunk/${pkgver}/+download/${pkgbase}-${pkgver}.tar.gz")
b2sums=('87233a0f2b62f6df15a8d8575ec9a176fa588b5ff42f4e13cc8a071d486b6192d53e1c989f13bb91e6ae84c0a2066c9e43554a8f0acf3026fcb641e4a512508c')

package_cloud-utils() {
  pkgdesc="Useful set of utilities for interacting with a cloud (meta package)"
  depends=('cloud-guest-utils' 'cloud-image-utils')
}

package_cloud-guest-utils() {
  pkgdesc="Useful set of utilities for interacting with a cloud (cloud guests)"
  depends=('python')

  cd "${pkgbase}-${pkgver}"

  for bin in ec2metadata growpart vcs-run; do
    install -Dm 755 "bin/${bin}" -t "${pkgdir}/usr/bin"
  done

  install -Dm 644 "man/growpart.1" -t "${pkgdir}/usr/share/man/man1"
}

package_cloud-image-utils() {
  pkgdesc="Useful set of utilities for interacting with a cloud (cloud images)"
  depends=('python' 'ca-certificates' 'cdrtools' 'qemu-headless' 'wget')

  cd "${pkgbase}-${pkgver}"

  for bin in cloud-localds mount-image-callback resize-part-image ubuntu-cloudimg-query write-mime-multipart; do
    install -Dm 755 "bin/${bin}" -t "${pkgdir}/usr/bin"
  done

  for man in cloud-localds resize-part-image write-mime-multipart; do
    install -Dm 644 "man/${man}.1" -t "${pkgdir}/usr/share/man/man1"
  done
}

# vim:set ts=2 sw=2 et:
