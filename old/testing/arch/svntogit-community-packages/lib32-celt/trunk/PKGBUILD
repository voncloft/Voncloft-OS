# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: see .contrib

pkgname=lib32-celt
pkgver=0.11.3
pkgrel=4
pkgdesc="Low-latency audio communication codec (32 bit)"
url="https://gitlab.xiph.org/xiph/celt"
arch=(x86_64)
license=(BSD)
depends=(lib32-glibc celt)
makedepends=(git libogg)
provides=(libcelt0.so)
_commit=6c28805c56d562bec56cc0b31c0d5905ebb5a6ac  # tags/v0.11.3
source=("git+$url.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd celt
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd celt
  ./autogen.sh
}

build() {
  cd celt

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  ./configure --prefix=/usr --enable-custom-modes --disable-static --libdir=/usr/lib32
  make -C libcelt
}

package() {
  cd celt
  make DESTDIR="$pkgdir" install-pkgconfigDATA
  make -C libcelt DESTDIR="$pkgdir" install
  rm -r "$pkgdir/usr/include"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}

# vim:set sw=2 et:
