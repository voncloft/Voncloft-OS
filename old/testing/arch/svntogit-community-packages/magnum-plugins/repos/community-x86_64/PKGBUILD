# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Mosra <mosra@centrum.cz>

pkgname=magnum-plugins
pkgver=2020.06
_basis_pkgver=2f43afcc97d0a5dafdb73b4e24e123cf9687a418
_meshoptimizer_pkgver=0.14
pkgrel=1
pkgdesc='Plugins for the Magnum C++11/C++14 graphics engine'
arch=(x86_64)
url='https://magnum.graphics'
license=(MIT)
depends=(assimp devil faad2 freetype2 libjpeg libpng magnum)
makedepends=(cmake git glfw ninja)
source=("git+https://github.com/mosra/magnum-plugins#commit=b0f2818b781eb0fb6a8a76095a6529fc85d442f3"
        "basis_universal.tar.gz::https://github.com/BinomialLLC/basis_universal/archive/${_basis_pkgver}.tar.gz"
        "meshoptimizer.tar.gz::https://github.com/zeux/meshoptimizer/archive/v${_meshoptimizer_pkgver}.tar.gz")
sha256sums=('SKIP'
            'f983ab5bfc5c32474914a87d9ca572c7fb970bf96f7bc68385c08772f633639f'
            '5a6e429ecf72325131b439a46297a690f0cab833c7563efdadab26c0a6cf8490')

build() {
  mkdir -p build
  cd build

  # Bundle Basis Universal and meshoptimizer
  ln -sf "$srcdir"/basis_universal-${_basis_pkgver}/ "$srcdir/${pkgname%-git}/src/external/basis-universal"
  ln -sf "$srcdir"/meshoptimizer-${_meshoptimizer_pkgver}/ "$srcdir/${pkgname%-git}/src/external/meshoptimizer"

  cmake "$srcdir/magnum-plugins" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_ASSIMPIMPORTER=ON \
    -DWITH_BASISIMAGECONVERTER=ON \
    -DWITH_BASISIMPORTER=ON \
    -DWITH_DDSIMPORTER=ON \
    -DWITH_DEVILIMAGEIMPORTER=ON \
    -DWITH_DRFLACAUDIOIMPORTER=ON \
    -DWITH_DRMP3AUDIOIMPORTER=ON \
    -DWITH_DRWAVAUDIOIMPORTER=ON \
    -DWITH_FAAD2AUDIOIMPORTER=ON \
    -DWITH_FREETYPEFONT=ON \
    -DWITH_ICOIMPORTER=ON \
    -DWITH_JPEGIMAGECONVERTER=ON \
    -DWITH_JPEGIMPORTER=ON \
    -DWITH_MESHOPTIMIZERSCENECONVERTER=ON \
    -DWITH_MINIEXRIMAGECONVERTER=ON \
    -DWITH_OPENGEXIMPORTER=ON \
    -DWITH_PNGIMAGECONVERTER=ON \
    -DWITH_PNGIMPORTER=ON \
    -DWITH_PRIMITIVEIMPORTER=ON \
    -DWITH_STANFORDIMPORTER=ON \
    -DWITH_STANFORDSCENECONVERTER=ON \
    -DWITH_STBIMAGECONVERTER=ON \
    -DWITH_STBIMAGEIMPORTER=ON \
    -DWITH_STBTRUETYPEFONT=ON \
    -DWITH_STBVORBISAUDIOIMPORTER=ON \
    -DWITH_STLIMPORTER=ON \
    -DWITH_TINYGLTFIMPORTER=ON \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# getver: github.com/mosra/magnum-plugins/releases/latest
# vim: ts=2 sw=2 et:
