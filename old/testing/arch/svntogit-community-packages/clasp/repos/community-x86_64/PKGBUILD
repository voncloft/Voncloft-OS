# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vincent Bernardoff <vb@luminar.eu.org>

pkgname=clasp
pkgver=3.3.5
pkgrel=2
pkgdesc='Answer set solver'
arch=('x86_64')
url='https://potassco.org/'
license=('GPL3')
makedepends=('cmake' 'git' 'ninja')
conflicts=('clingo')
source=("git+https://github.com/potassco/clasp.git#tag=v$pkgver")
md5sums=('SKIP')

build() {
  mkdir -p build
  cd build
  cmake "../$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# getver: github.com/potassco/clasp/releases
# vim: ts=2 sw=2 et:
