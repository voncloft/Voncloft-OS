# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=rogue
pkgver=5.4.4
pkgrel=7
pkgdesc="The original dungeon crawl game."
arch=('x86_64')
#url="http://rogue.rogueforge.net/rogue-5-4/"
url="http://www.roguebasin.com/index.php?title=Rogue"
license=('custom')
depends=('ncurses')
provides=('rouge')  # so the illiterate can find it in searches too
source=("$pkgname-$pkgver.tgz::https://github.com/phs/rogue/archive/v$pkgver.tar.gz")
md5sums=('26534fed4e4eed9662f6be620765e78e')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  # ncurses fix, FS#57731
  sed -i '262i TERMTYPE *tp = (TERMTYPE *) (cur_term);' mdport.c
  sed -i 's/cur_term->type.Strings/tp->Strings/' mdport.c
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 rogue "$pkgdir/usr/bin/rogue"
  install -Dm644 rogue.6 "$pkgdir/usr/share/man/man6/rogue.6"
  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE.TXT"
}
