# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lee.MaRS<leemars at gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-table-extraphrase
pkgver=1.3.9.20110826
pkgrel=4
pkgdesc="The extra phrases of tables engines for IBus"
arch=('x86_64')
url="https://code.google.com/archive/p/ibus/"
license=('LGPL')
depends=('ibus-table')
source=("https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/ibus/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5310cee7bd74455a732cd568a2a60887765588ef7fabdcd950f66fb2d5fab6dcdd059d2e460c0962117495c88656386a0c738dbd94f9ba553f3365fb3b42ce5d')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
