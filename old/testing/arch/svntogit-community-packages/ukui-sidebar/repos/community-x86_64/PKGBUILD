# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-sidebar
pkgver=3.0.1
pkgrel=1
pkgdesc="Parallels toolbox for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-sidebar"
groups=('ukui')
depends=('dconf' 'gsettings-qt' 'kwindowsystem' 'qt5-multimedia' 'qt5-svg')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-sidebar/archive/v$pkgver.tar.gz")
sha512sums=('cdc507e468443d44a53cc1bd2b2e394e65de8a4eaff501666a5ccb80f5f2de4d273f9280269545053842d0461506518fe789696d8776086b7ef721149200051b')

build() {
  cd ukui-sidebar-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-sidebar-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 src/data/ukui-sidebar.desktop -t "$pkgdir"/etc/xdg/autostart/
}
