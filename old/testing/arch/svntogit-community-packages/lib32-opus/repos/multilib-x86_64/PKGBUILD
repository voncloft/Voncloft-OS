# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: farwayer <farwayer [at] gmail [dot] com>

pkgname=lib32-opus
pkgver=1.3.1
pkgrel=1
pkgdesc="Totally open, royalty-free, highly versatile audio codec (32-bit)"
arch=(x86_64)
url="https://www.opus-codec.org/"
license=(BSD)
depends=(lib32-glibc opus)
source=(https://archive.mozilla.org/pub/opus/opus-$pkgver.tar.gz)
sha256sums=('65b58e1e25b2a114157014736a3d9dfeaad8d41be1c8179866f144a2fb44ff9d')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd opus-$pkgver
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32 --enable-custom-modes
  make
}

check() {
  cd opus-$pkgver
  make check
}

package() {
  cd opus-$pkgver
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{include,share}
  install -d "$pkgdir/usr/share/licenses"
  ln -s opus "$pkgdir/usr/share/licenses/$pkgname"
}
