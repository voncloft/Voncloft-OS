# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ghc-lib-parser-ex
pkgname=haskell-ghc-lib-parser-ex
pkgver=8.10.0.17
pkgrel=27
pkgdesc="Algorithms on GHC parse trees"
url="https://github.com/shayne-fletcher/ghc-lib-parser-ex"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-uniplate' 'haskell-ghc')
makedepends=('ghc' 'haskell-extra' 'haskell-tasty' 'haskell-tasty-hunit' 'haskell-uniplate')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('193cac08c898aed5c8e8f4f60ff140b2c5adf7fce84832e843b1a1bc2e2bf184972ad91f1baa47d744c7a4bdf3fa87cb6513b5ca3d02f79765d7c36574c8be9e')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
