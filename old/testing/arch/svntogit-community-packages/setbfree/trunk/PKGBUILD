# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Johannes Maibaum <jmaibaum at gmail dot com>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

_name=setBfree
pkgname=setbfree
pkgver=0.8.11
pkgrel=2
pkgdesc="DSP tonewheel organ"
arch=('x86_64')
url="http://setbfree.org"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'ftgl' 'glibc' 'libglvnd' 'libx11')
makedepends=('gendesk' 'glib2' 'jack' 'liblo' 'lv2' 'pango' 'ttf-dejavu')
optdepends=('jack: for standalone applications'
            'liblo: for standalone applications'
            'lv2-host: for LV2 plugin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pantherb/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('1db2ee90032efee3c24ee99071e11c064a2fbc82f1126346ec0667e1e91f09ffab781bcdb8fa766613711779cdfd85b486e0043f99073d8072ac92e214241c88')
b2sums=('63b89fb4865ef449d3e291c547f2e115b5fcb0e5de280353e58b3b25e9b9c4d7291df09b2c486d8819295e367a1f3128c0fb5a6bf3b144a999e8ae634eda5f4c')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  # XDG desktop files
  gendesk -n \
          --pkgname "${pkgname}" \
          --name "${_name}" \
          --exec "${_name}UI" \
          --icon "${_name}" \
          --genericname "${pkgdesc}" \
          --pkgdesc "${pkgdesc}" \
          --categories "AudioVideo;Audio"
  gendesk -n \
          --pkgname x42-whirl \
          --name x42-whirl \
          --genericname "Leslie speaker emulation" \
          --pkgdesc "Leslie speaker emulation as JACK client" \
          --categories "AudioVideo;Audio"
}

build() {
  cd "$pkgname-$pkgver"
  export FONTFILE=/usr/share/fonts/TTF/DejaVuSans-Bold.ttf
  make PREFIX=/usr
}

package() {
  depends+=('libgobject-2.0.so' 'libpango-1.0.so' 'libpangocairo-1.0.so')
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  # xdg desktop files
  install -vDm 644 ../*.desktop -t "${pkgdir}/usr/share/applications"
  # icons
  install -vDm 644 doc/{${_name},x42-whirl}.png \
    -t "${pkgdir}/usr/share/pixmaps/"
  # man
  install -vDm 644 doc/*.1 -t "${pkgdir}/usr/share/man/man1/"
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,README.md} doc/*.png \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
