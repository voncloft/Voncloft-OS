# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=psi-l10n
pkgver=1.4
pkgrel=2
pkgdesc="Language packs for Psi"
arch=('any')
url="https://psi-im.org"
license=('GPL2')
depends=('psi')
makedepends=('qt5-tools')
options=('!strip' '!zipman')
source=("$pkgname-$pkgver.tar.gz::https://github.com/psi-im/psi-l10n/archive/$pkgver.tar.gz")
sha256sums=('a3b228d89982f505eac81faab92ac98a792e91197c255736f390e862f9978a63')

build() {
	cd psi-l10n-$pkgver/translations
	for langfile in *.ts; do
		lrelease-qt5 "$langfile"
	done
}

package() {
	cd psi-l10n-$pkgver/translations
	install -v -dm755 "$pkgdir/usr/share/psi/translations"
	install -v -m644 *.qm "$pkgdir/usr/share/psi/translations"
}
