# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-djabberd
pkgver=0.85
pkgrel=7
pkgdesc="Jabber daemon written on Perl"
arch=('any')
url="https://search.cpan.org/~bradfitz/DJabberd/"
license=("GPL" "PerlArtistic")
depends=("perl-danga-socket" "perl-log-log4perl" "perl-unicode-stringprep"
	 "perl-net-dns" "perl-xml-libxml" "perl-net-ssleay")
source=("https://search.cpan.org/CPAN/authors/id/M/MA/MART/DJabberd-$pkgver.tar.gz")
md5sums=('120f7427034d707dd6666100374a180d')

build() {
  cd "$srcdir"/DJabberd-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/DJabberd-$pkgver
  make pure_install doc_install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
