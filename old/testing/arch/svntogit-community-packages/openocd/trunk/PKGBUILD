# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Matthias Bauch <matthias.bauch@gmail.com>
# Contributor: Laszlo Papp <djszapi2 at gmail com>
# Contributor: Samuel Tardieu <sam@rfc1149.net>

_features=(amtjtagaccel armjtagew buspirate ftdi gw16012 jlink oocd_trace opendous osbdm
           parport presto_libftdi remote-bitbang rlink stlink ti-icdi ulink usbprog vsllink
           aice cmsis-dap dummy jtag_vpi openjtag_ftdi usb-blaster-2 usb_blaster_libftdi)

pkgname=openocd
pkgver=0.10.0
pkgrel=4
epoch=1
pkgdesc='Debugging, in-system programming and boundary-scan testing for embedded target devices'
arch=('x86_64')
url='http://openocd.org'
license=('GPL')
depends=('libftdi-compat' 'libusb-compat' 'hidapi' 'libudev.so')
source=("https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2"
        'fix-gcc-10.patch::http://openocd.zylin.com/gitweb?p=openocd.git;a=patch;h=c60252ac2b636c4d99b766a574b9df0966151696')
sha512sums=('6c890c86c3339da49bd529e8674ca62bc7fce4a2a17798dcc5d70b4f7628b640d856651170a3f7e2e2a120989066e84d8439b56ff42ff2bd72446f9af0f28ba2'
            'dbb247f703d79bad9a9e3660dccebfc4b268dd020084e1e0b889a8388c6aba80dae2e1eb03e8f561a842aabb8ed4ea26ed321ced9a6ba50b511f5cd2bf1583e8')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i ../fix-gcc-10.patch

  sed -i 's|GROUP="plugdev", ||g' contrib/60-openocd.rules
}

build() {
  cd $pkgname-$pkgver

  libtoolize -vif
  autoreconf -vif
  ./configure \
  	--prefix=/usr \
  	--disable-werror \
  	${_features[@]/#/--enable-}

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm 644 contrib/60-openocd.rules "$pkgdir"/usr/lib/udev/rules.d/60-openocd.rules
}
