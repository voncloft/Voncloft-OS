# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Francois Boulogne <fboulogne@april.org>
# Contributor: TDY <tdy@gmx.com>

pkgname=spyder
pkgver=4.2.1
pkgrel=1
pkgdesc="The Scientific Python Development Environment"
arch=(any)
url="https://www.spyder-ide.org/"
license=(MIT)
makedepends=(python-sphinx)
depends=(
    python-atomicwrites
    python-chardet
    python-cloudpickle
    python-diff-match-patch
    python-intervaltree
    ipython
    python-jedi
    python-jsonschema
    python-keyring
    jupyter-nbconvert
    python-numpydoc
    python-parso
    python-pexpect
    python-pickleshare
    python-psutil
    python-pygments
    python-pylint
    python-pyqt5
    python-pyqtwebengine
    qt5-webkit
    python-language-server
    python-pyls-black
    python-pyls-spyder
    python-xdg
    python-pyzmq
    python-qdarkstyle
    python-qtawesome
    python-qtconsole
    python-qtpy
    python-setuptools
    python-sphinx
    python-spyder-kernels
    python-textdistance
    python-three-merge
    python-watchdog
)
optdepends=(
    'cython: run Cython files in the IPython Console'
    'python-matplotlib: 2D/3D plotting in the IPython Console'
    'python-numpy: support for N-dimensional arrays in the Variable Explorer'
    'python-pandas: support for DataFrames and Series in the Variable Explorer'
    'python-scipy: support for Matlab workspace in the Variable Explorer'
    'python-sympy: symbolic mathematics in the IPython Console'
)
checkdepends=(
    python-pytest
    python-pytest-qt
    python-pytest-mock
    python-pytest-cov
    python-pytest-xvfb
    python-pytest-ordering
    python-pytest-lazy-fixture
    python-mock
    python-flaky
    python-pandas
    python-scipy
    python-sympy
    python-pillow
    python-matplotlib
    cython
    git
    tk
)
source=(https://github.com/spyder-ide/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('0231a3b0ba0530817e53b5162ce0c3db8c98f071a0e4d4b6027721bf32039e2b')

prepare() {
  cd ${pkgname}-${pkgver}
  # Allow our jedi/parso/pyls versions
  sed -i "s|JEDI_REQVER = '=|JEDI_REQVER = '>=|" -i spyder/dependencies.py
  sed -i "s|PARSO_REQVER = '=|PARSO_REQVER = '>=|" -i spyder/dependencies.py
  sed -i "s|PYLS_REQVER = '>=0.36.2;<1.0.0'|PYLS_REQVER = '>=0.36.2'|" -i spyder/dependencies.py
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  # Required per above patching
  sed -i "s|jedi==|jedi>=|" -i setup.py
  sed -i "s|parso==|parso>=|" -i setup.py
  sed -i "s|python-language-server[all]>=0.36.2,<1.0.0|python-language-server[all]>=0.36.2|" -i setup.py
  sed -i "s|jedi =|jedi >=|" -i requirements/conda.txt
  sed -i "s|parso =|parso >=|" -i requirements/conda.txt
  sed -i "s|jedi =|jedi >=|" -i binder/environment.yml
  sed -i "s|parso =|parso >=|" -i binder/environment.yml
  sed -i "s|python-language-server >=0.36.2,<1.0.0|python-language-server >=0.36.2|" -i binder/environment.yml
  # Some tests failures but upstream does not support us
  python runtests.py || echo "Tests failed"
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  # Install license file
  install -Dm644 LICENSE.txt -t ${pkgdir}/usr/share/licenses/${pkgname}/
  # Install icon file
  install -Dm644 spyder/images/spyder.svg -t "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}"/usr/bin/spyder_win_post_install.py
}
