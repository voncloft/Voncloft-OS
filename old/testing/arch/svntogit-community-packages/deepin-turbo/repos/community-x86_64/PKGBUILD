# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-turbo
pkgver=0.0.3
pkgrel=2
pkgdesc='A daemon that helps to launch applications faster'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-turbo"
license=('GPL3')
depends=('qt5-base')
makedepends=('cmake')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-turbo/archive/$pkgver.tar.gz")
sha512sums=('c1f62263e259443b29018e704ee4e981feb7023c39d9ce33cb3c68b7834e39ed9f421d250e803aeaa30b56173909609ba99ccbcb1984400c3e11f7705dc6f0af')

build() {
  cd deepin-turbo-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd deepin-turbo-$pkgver
  make DESTDIR="$pkgdir" install
}
