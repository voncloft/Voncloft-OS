# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Eddie Lozon <almostlucky@attbi.com>

pkgname=pekwm
pkgver=0.1.18
pkgrel=1
pkgdesc='Tabbed X11 window manager'
arch=(x86_64)
license=(GPL)
url='https://github.com/pekdon/pekwm'
makedepends=(gendesk git)
depends=(libjpeg libxft libxinerama libxpm libxrandr)
optdepends=('lxappearance: feature-rich GTK+ theme switcher'
            'nitrogen: background browser and setter'
            'tint2: basic, good-looking task manager')
backup=(etc/pekwm/autoproperties
        etc/pekwm/autoproperties_typerules
        etc/pekwm/config
        etc/pekwm/config_system
        etc/pekwm/keys
        etc/pekwm/menu
        etc/pekwm/mouse
        etc/pekwm/mouse_click
        etc/pekwm/mouse_sloppy
        etc/pekwm/mouse_system
        etc/pekwm/start
        etc/pekwm/vars)
source=("git+$url#tag=release-$pkgver")
sha256sums=('SKIP')

prepare() {
  gendesk -f -n --pkgname $pkgname --pkgdesc "$pkgdesc" --name PekWM
  gendesk -f -n -wm --pkgname ${pkgname}_kdm --exec $pkgname --name PekWM
  cd $pkgname
  ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  make -C $pkgname DESTDIR="$pkgdir" install
  install -Dm644 $pkgname.desktop \
    "$pkgdir"/usr/share/xsessions/$pkgname.desktop
  install -Dm644 ${pkgname}_kdm.desktop \
    "$pkgdir"/usr/share/apps/kdm/sessions/$pkgname.desktop
}

# getver: repology.org/metapackage/pekwm/information
