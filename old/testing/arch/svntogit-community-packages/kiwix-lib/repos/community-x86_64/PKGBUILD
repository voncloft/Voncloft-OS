# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
pkgname=kiwix-lib
pkgver=9.4.1
pkgrel=2
pkgdesc="Library providing the Kiwix software core"
license=('GPL3')
arch=('x86_64')
depends=('libzim>=6.3.0' 'pugixml' 'mustache' 'xapian-core' 'aria2' 'libmicrohttpd' 'python' 'curl')
makedepends=('meson' 'gtest')
url='https://github.com/kiwix/kiwix-lib'
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('f76bfa90411c52d4d8c49ac5767928fc28d01ef26916d72df4856cf08b064202')

build() {
	cd "${pkgname}-${pkgver}"
	# We want to ignore xapian ABI version mismatches
	arch-meson build -Dwerror=false
	ninja -C build
}

check() {
	cd "${pkgname}-${pkgver}/build"
	ninja meson-test
}

package() {
	install -Dm644 "${pkgname}-${pkgver}/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
	cd "${pkgname}-${pkgver}/build"
	DESTDIR="${pkgdir}" ninja install
}
