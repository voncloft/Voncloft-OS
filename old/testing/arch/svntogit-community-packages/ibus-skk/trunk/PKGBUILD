# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: eagletmt <eagletmt@gmail.com>

pkgname=ibus-skk
pkgver=1.4.3
pkgrel=2
pkgdesc='Japanese input method SKK engine for IBus'
arch=('x86_64')
url='https://github.com/ueno/ibus-skk'
license=('GPL')
depends=('ibus' 'libskk')
makedepends=('intltool' 'gnome-common' 'vala')
source=("https://github.com/ueno/ibus-skk/archive/$pkgname-$pkgver.tar.gz")
sha512sums=('729de81ffd01b142fb29dbde8af4f9561e2481b68ba5853ae8425c75ff02fb5b911fc24910246b5b01a976fc9d5e70ce847150fb500901a8b7ccaf6b87de42b9')

build() {
  cd $pkgname-$pkgname-$pkgver

  ./autogen.sh --prefix=/usr --libexecdir=/usr/lib/ibus
  make
}

package() {
  cd $pkgname-$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
