# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=frei0r-plugins
pkgver=1.7.0
pkgrel=1
pkgdesc="Minimalistic plugin API for video effects"
arch=('x86_64')
url="https://frei0r.dyne.org/"
license=('GPL')
depends=('cairo' 'gavl')
makedepends=('cmake' 'doxygen') # opencv - not compatible with 4.0
#optdepends=('opencv: for facebl0r and facedetect plugins')
source=(https://files.dyne.org/frei0r/$pkgname-$pkgver.tar.gz{,.asc})
validpgpkeys=('6113D89CA825C5CEDD02C87273B35DA54ACB7D10') # Denis Roio (Jaromil)
sha256sums=('1b1ff8f0f9bc23eed724e94e9a7c1d8f0244bfe33424bb4fe68e6460c088523a'
            'SKIP')

build() {
  mkdir -p build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
