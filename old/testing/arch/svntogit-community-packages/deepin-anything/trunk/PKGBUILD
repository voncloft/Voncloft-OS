# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=deepin-anything
pkgname=(deepin-anything deepin-anything-dkms)
pkgver=5.0.7
_extramodules=extramodules-ARCH
pkgrel=2
pkgdesc="Deepin Anything file search tool"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-anything"
license=('GPL3')
makedepends=('dtkcore' 'udisks2-qt5')
source=("https://github.com/linuxdeepin/deepin-anything/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        deepin-anything-server.sysusers)
sha512sums=('51e8488b4d0652d24afd1b5bd74e88140e2754354bfa85639c668479deca25e8163f3ffcf8bc1c6629d97d1cea24de89c65a76b8c9902745911fd0ed9b12b770'
            '0ff6a6de1fbfb0c33eaac511b989da321a9e43ece92708af88aee34ad1a05e55572713b1290bc2705d70b91dc7bec4fb4abd3dc664a0abe01de27d88bd9e9c85')

prepare() {
  cd deepin-anything-$pkgver
  # https://github.com/linuxdeepin/deepin-anything/issues/36
  sed -i '454s/return;/return -1;/' kernelmod/vfs_kretprobes.c

  sed -i 's|^systemd_service.path.*|systemd_service.path = /usr/lib/systemd/system|' server/monitor/src/src.pro server/tool/tool.pro
}

build() {
  cd deepin-anything-$pkgver
  make VERSION=$pkgver
}

package_deepin-anything-dkms() {
  depends=('dkms')
  provides=('DEEPIN-ANYTHING-MODULE')
  conflicts=('DEEPIN-ANYTHING-MODULE')

  cd deepin-anything-$pkgver
  install -dm 755 "$pkgdir"/usr/src
  cp -r kernelmod "$pkgdir"/usr/src/deepin-anything-$pkgver
  install -m644 debian/deepin-anything-dkms.dkms "$pkgdir"/usr/src/deepin-anything-$pkgver/dkms.conf
}

package_deepin-anything() {
  depends=('DEEPIN-ANYTHING-MODULE' 'dtkcore' 'udisks2-qt5')
  groups=('deepin')

  cd deepin-anything-$pkgver
  make VERSION=$pkgver DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/src

  mv "$pkgdir"/etc/dbus-1/system.d "$pkgdir"/usr/share/dbus-1/system.d

  install -Dm644 ../deepin-anything-server.sysusers "$pkgdir/usr/lib/sysusers.d/deepin-anything-server.conf"
}
