# Maintainer: David Runge <dave@sleepmap.de>
pkgname=argtable
pkgver=2.13
pkgrel=5
pkgdesc="An ANSI C library for parsing GNU style command line options with a minimum of fuss"
arch=('x86_64')
url="http://argtable.sourceforge.net/"
license=('GPL2')
depends=('glibc')
replaces=('argtable-docs')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgname}-${pkgver}/${pkgname}${pkgver/./-}.tar.gz")
sha512sums=('3d8303f3ba529e3241d918c0127a16402ece951efb964d14a06a3a7d29a252812ad3c44e96da28798871e9923e73a2cfe7ebc84139c1397817d632cae25c4585')

prepare() {
  mv -v "${pkgname}${pkgver/./-}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # moving examples
  mv -v "${pkgdir}/usr/share/doc/${pkgname}2" \
    "${pkgdir}/usr/share/doc/${pkgname}"
  # removing unneeded pdf/ps: https://bugs.archlinux.org/task/59732
  rm -v "${pkgdir}/usr/share/doc/${pkgname}/"*.{ps,pdf}
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
