# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Rudy Matela <rudy@matela.com.br>
# Contributor: Bartek Piotrowski <barthalion@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>
# Contributor: pootzko <pootzko@gmail.com>
# Contributor: Jinx <jinxware@gmail.com>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname=hydra
pkgver=9.1
pkgrel=1
pkgdesc='Very fast network logon cracker which support many different services'
url='https://github.com/vanhauser-thc/thc-hydra'
arch=('x86_64')
license=('GPL3')
depends=('postgresql-libs' 'subversion' 'libssh' 'mariadb-libs' 'libidn'
         'libfbclient' 'afpfs-ng' 'zlib' 'openssl' 'ncurses' 'pcre'
         'freerdp' 'libmemcached' 'mongo-c-driver')
optdepends=('gtk2: graphical front-end (xhydra)')
makedepends=('gtk2')
source=(https://github.com/vanhauser-thc/thc-hydra/archive/v${pkgver}/thc-hydra-v${pkgver}.tar.gz)
sha512sums=('863176375fe1aae4c0a2b74c11aba297682d66e522c98c926c539348309b0af4e407e460b4cb803d2c83c008787c73f7f1e56ac6aca9791351e1b596c2e4fbb2')
b2sums=('9a5b6dc52b5f4069bf326e86728ebaf8c1fe018536f3943b83493de934703bff6770ae3f7a9a223ee00b5f52f7bd10ee4ddc272e1bbad00c399aa19cc0c083ba')


build() {
  cd thc-hydra-${pkgver}
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./configure --fhs
  make
}

package() {
  cd thc-hydra-${pkgver}
  make DESTDIR="${pkgdir}" MANDIR=/share/man/man1 DATADIR=/share/hydra install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
