# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=httpcore
pkgname=python-httpcore
pkgver=0.12.0
pkgrel=3
pkgdesc="A minimal HTTP client"
arch=('any')
url="https://github.com/encode/${_pkgname}"
license=('BSD')
depends=('python-h11' 'python-sniffio')
optdepends=('python-h2: for HTTP/2 support')
makedepends=('python-setuptools' 'python-h2')
checkdepends=('python-anyio' 'python-curio' 'python-pproxy' 'python-pytest-trio' 'uvicorn')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('173008b3a81957c1151c628356a399967872d2b184fce3226aa607977644aee12d0cd8dbceef87cf6271c45f283e444183ef7b547a4f00c5d1b234e76697ec14')
b2sums=('13a60577be926116a1b94c6397b0d35b35534204175dcb1d19743880dd57346d84e9d1cbe3b91dcc3b6d8442ec4cc2bcef1eeeda1b9b6ad121030b8f23124738')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # do not run coverage in unittests!
    sed -i '/^addopts/d' setup.cfg
}

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.md
}
