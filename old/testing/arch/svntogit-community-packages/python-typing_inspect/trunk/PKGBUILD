# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=typing_inspect
pkgname=python-$_pkgname
pkgver=0.6.0
pkgrel=4
pkgdesc='Runtime inspection utilities for Python typing module'
arch=('any')
url='https://github.com/ilevkivskyi/typing_inspect'
license=('MIT')
depends=('python-mypy_extensions' 'python-typing_extensions')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
        $pkgname-support-py39-generic-types.patch::https://github.com/thatch/typing_inspect/commit/67b455cf5a7e.patch)
sha512sums=('c08654d2d3887710094fa7374c2ce5fa6b79176543d478d15113d3904bb3c68cdeb08d8331b6eeb271858e96ee859fdaad18dd17237a0618072f630f34758177'
            '74800c6934fcac7198fda7a68237369452d83a7d8cb194015d114a966572faceef5d2aa511a0a37f2d95c3065e615d9de691f604d7d500570a96c7a18190c46d')

prepare() {
  cd $_pkgname-$pkgver

  # https://github.com/ilevkivskyi/typing_inspect/issues/60
  patch -Np1 -i ../$pkgname-support-py39-generic-types.patch

  sed -e '/res = tp.__args__/i \            if not hasattr(tp, "__args__"): return ()' \
      -i typing_inspect.py
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
