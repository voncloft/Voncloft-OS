# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libmodsecurity
pkgver=3.0.4
pkgrel=4
epoch=1

pkgdesc='ModSecurity v3 library component'
arch=('x86_64')
depends=('yajl' 'curl' 'libxml2' 'pcre' 'geoip' 'libmaxminddb' 'ssdeep' 'luajit')
makedepends=('doxygen')
url="https://github.com/SpiderLabs/ModSecurity/tree/v3/master"
license=('APACHE')
source=("https://github.com/SpiderLabs/ModSecurity/releases/download/v$pkgver/modsecurity-v$pkgver.tar.gz"{,.asc}
        "https://gist.githubusercontent.com/crsgists/0e1f6f7f1bd1f239ded64cecee46a11d/raw/181bc852065e9782367f1dc67c96d4d250e73a46/cve-2020-15598.patch")
sha256sums=('b4231177dd80b4e076b228e57d498670113b69d445bab86db25f65346c24db22'
            'SKIP'
            '461a418512043587c6d1e41ef2ccef63e2ff68a0c68b1bab2ae5c0f18082d028')
validpgpkeys=('190EFACCA1E9FA466A8ECD9CE6DFB08CE8B11277') # Felipe Zimmerle da Nobrega Costa <felipe@zimmerle.org>

prepare() {
	cd "$srcdir"/modsecurity-v$pkgver
	patch -p1 -i ../cve-2020-15598.patch
}

build() {
	cd "$srcdir"/modsecurity-v$pkgver
	./build.sh
	./configure \
		--prefix=/usr \
		--disable-examples
	make
}

package() {
	cd "$srcdir"/modsecurity-v$pkgver
	make DESTDIR="$pkgdir" install
}
