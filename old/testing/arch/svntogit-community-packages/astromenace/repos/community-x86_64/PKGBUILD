# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Ivan Bobrov <ibobrik at gmail dot com>
# Contributor: Black_Mage <vleon1 at gmail dot com>

pkgname=astromenace
pkgver=1.4.1
pkgrel=2
pkgdesc="Hardcore 3D space shooter with spaceship upgrade possibilities"
arch=('x86_64')
url="http://www.viewizard.com/astromenace/index_linux.php"
license=('GPL3')
depends=('sdl2' 'freealut' 'libjpeg' 'libvorbis' 'glu' 'freetype2' 'libxinerama')
makedepends=('cmake' 'mesa')
source=("$pkgname-$pkgver".tar.gz::https://github.com/viewizard/astromenace/archive/v${pkgver}.tar.gz)
sha512sums=('408fd8a9fcc51d0cdfd31d77bc1c05b079b3248622a252e73e1145db9715b3a331eef5fde295c3390143fb7bb2f88460d374eddea719b6452577bf5e83bd14c1')

prepare() {
  mkdir astromenace-$pkgver/build
}

build() {
  cd astromenace-$pkgver/build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DDATADIR=/usr/share/astromenace
  make
}

package() {
  cd astromenace-$pkgver

  install -Dm755 build/astromenace "$pkgdir"/usr/bin/astromenace
  install -Dm644 build/gamedata.vfs "$pkgdir"/usr/share/astromenace/gamedata.vfs
  install -Dm644 share/astromenace_128.png "$pkgdir"/usr/share/pixmaps/astromenace.png
  install -Dm644 share/astromenace.desktop "$pkgdir"/usr/share/applications/astromenace.desktop
  install -Dm644 share/astromenace.appdata.xml "$pkgdir"/usr/share/appdata/astromenace.appdata.xml
}
