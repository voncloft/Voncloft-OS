# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: <kaptoxic _at_ yahoo _dot_ com >
# Contributor: Peter Simons <simons@cryp.to>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=maude
pkgver=3.1
pkgrel=1
pkgdesc="High-level Specification Language"
arch=('x86_64')
url="http://maude.cs.illinois.edu"
license=('GPL')
depends=('buddy' 'cvc4' 'libtecla' 'gmp' 'libsigsegv')
makedepends=('flex' 'bison')
source=("http://maude.cs.illinois.edu/w/images/d/d3/Maude-$pkgver.tar.gz"
        "http://maude.cs.illinois.edu/w/images/0/0a/Full-Maude-$pkgver.zip"
        maude.sh)
sha512sums=('34dba3c3b7eed48d0c82c6d5e4dade08bd11dc57b436f04e94842000809b9b6086f8077bccbfac0313e4a4f361b03c3ef3d56e6df6242ae42427dd674566a3bc'
            'fb0c0e30d3df830b60ece67c2fc5655c5e3f1377a269ce8acfc45fdfa0d42f3fe3fbf4038758b35598d82e7f47af5f24507b3ebe2a30dcb8722e61e6315248c3'
            'f62e1709d035e996c9f9a887840492527415f157990cc2d52355d8a7fe9382a757c06bed95f97e298f8eea0dd2a846bf7efb8fb84987784a4a797820807be12e')

prepare() {
  cd "$srcdir/maude-$pkgver"

  # Always enabled in CVC4 1.8: https://github.com/CVC4/CVC4/pull/4519
  sed -i '/rewrite-divk/d' src/Mixfix/cvc4_Bindings.cc

  autoreconf -i
}

build() {
  cd "$srcdir/maude-$pkgver"
  ./configure --with-cvc4=yes --with-yices2=no --prefix=/usr --datadir=/usr/share/maude
  make -j1 all CVC4_LIB="-lcvc4 -lcln"
}

check() {
  cd "$srcdir/maude-$pkgver"
  make check
}

package() {
  cd "$srcdir/maude-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -D -m 444 "$srcdir/full-maude31.maude" \
    "$pkgdir/usr/share/maude/full-maude.maude"

  install -D -m 555 "$srcdir/maude.sh" "$pkgdir/etc/profile.d/maude.sh"
}
