# Maintainer: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Wijnand Modderman <wijanand+aur@archlinux.nl>

pkgname=cksfv
pkgver=1.3.14
pkgrel=5
pkgdesc="SFV (Simple File Verification) Checker"
arch=('x86_64')
url="http://zakalwe.fi/~shd/foss/cksfv"
license=('GPL')
depends=('glibc')
source=(http://zakalwe.fi/~shd/foss/cksfv/files/$pkgname-$pkgver.tar.gz
        http://zakalwe.fi/~shd/foss/cksfv/files/$pkgname-$pkgver.tar.gz.asc)
sha512sums=('1278c3e9c912dce9a5f24c42a3c14566bddbe2220e9ae3d6b94c59090efe39ab98f65db5881648d63d64ab6939881845084900177968e680c04e49bf47b4e58e'
            'SKIP')
validpgpkeys=(1902E24D7822120FD84D64A9AB13F56F8FBDC8CD)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make BINDIR="$pkgdir/usr/bin" MANDIR="$pkgdir/usr/share/man" install
  chmod 644 "$pkgdir/usr/share/man/man1/$pkgname.1"
}
