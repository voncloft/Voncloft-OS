# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=openvr
pkgver=1.14.15
pkgrel=3
pkgdesc="API and runtime that allows access to VR hardware from multiple vendors"
arch=(x86_64)
url="https://github.com/ValveSoftware/openvr"
license=(custom)
depends=(jsoncpp libgl)
makedepends=(cmake)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        openvr-fix-vsprintf_s.patch::${url}/pull/594.patch
        openvr-use-system-jsoncpp.patch::${url}/pull/1178.patch)
sha256sums=('40644e569f057e86c733d818a22d821c640307e5058d95958414422134d79584'
            '29715711e5b28e2ce2934ef21cb5e6cfd05eec7f9d47b3e9b674a6d72ca8623a'
            'a230a1f9fdf356766b27e7765fc2929344bd17edc3e7e67eb479cb016a3d8981')

prepare() {
  cd ${pkgname}-${pkgver}
  rm -rf thirdparty/jsoncpp
  patch -Np1 -i ../openvr-fix-vsprintf_s.patch
  patch -Np1 -i ../openvr-use-system-jsoncpp.patch
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED=1 \
    -DUSE_SYSTEM_JSONCPP=True
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
