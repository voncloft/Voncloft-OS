# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_name=alsa-lib
pkgname=lib32-alsa-lib
pkgver=1.2.4
pkgrel=2
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=('x86_64')
url="https://www.alsa-project.org"
license=('LGPL2.1')
depends=('lib32-glibc' "alsa-lib=${pkgver}")
provides=('libasound.so' 'libatopology.so')
source=("https://www.alsa-project.org/files/pub/lib/${_name}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('12086952dc8f16f1cb6946517858e17b1c3276aeda9ff5703a84bb38aa78eb4c4e9cb4485c5b3f21f174fdbd976b3bcbbc481e85cb2460652858490df51ae844'
            'SKIP')
b2sums=('e1d5d9afe21dac693645274ae4ec8a607c9fcc9915ba4ebccfdb539d44b9353ba07529e2c8eaca119884b926d4467285313c945c9015a788101fe532064442db'
        'SKIP')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

prepare() {
  cd "${_name}-$pkgver"
  autoreconf -vfi
}

build() {
  cd "${_name}-$pkgver"

  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --without-debug \
              --disable-python
  make
}

check() {
  cd "${_name}-$pkgver"
  export LD_LIBRARY_PATH="${PWD}/src/.libs/:${LD_LIBRARY_PATH}"
  make -k check
}

package() {
  cd "${_name}-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{bin,include,share}
}
