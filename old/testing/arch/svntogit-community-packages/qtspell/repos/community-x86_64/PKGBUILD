# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=qtspell
pkgver=0.9.0
pkgrel=1
url="https://github.com/manisandro/qtspell"
pkgdesc="Spell checking for Qt text widgets"
arch=(x86_64)
license=(GPL3)
depends=(enchant iso-codes qt5-base)
makedepends=(cmake qt5-tools)
source=($pkgname-$pkgver.tar.gz::https://github.com/manisandro/$pkgname/archive/$pkgver.tar.gz)
sha256sums=('49a5ab995c3150572c3c48989cd3e506b0c2d64b09d80fb5b30daa7325445635')

prepare() {
  [[ -d build ]] || mkdir build
  cd $pkgname-$pkgver

  # Fix location of translations
  sed -i 's|share/${QT_VER}/translations)|share/qt/translations)|' CMakeLists.txt
}

build() {
  cd build
  cmake -G "Unix Makefiles" ../$pkgname-$pkgver/ \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
