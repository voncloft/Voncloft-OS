# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tofe <chris.chapuis@gmail.com>
# Contributor: erm67 <erm67@yahoo.it>

pkgname=cairo-dock
pkgver=3.4.1
pkgrel=4
pkgdesc='Light eye-candy fully themable animated dock'
arch=('x86_64')
url='https://glx-dock.org/'
license=('GPL')
depends=('curl' 'dbus-glib' 'glu' 'gtk3')
makedepends=('bzr' 'cmake')
optdepends=('cairo-dock-plug-ins: Plugins for Cairo-Dock')
source=("cairo-dock-${pkgver}.tar.gz::https://github.com/Cairo-Dock/cairo-dock-core/archive/${pkgver}.tar.gz")
sha256sums=('ca9b5769f60165b09d5cf2cae3c55fcc62fed19a279e8a279a0619f4189b4e00')

build() {
  cd cairo-dock-core-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd cairo-dock-core-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
