# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Michael Schubert <mschu.dev at gmail>

pkgname=symengine
pkgver=0.6.0
pkgrel=2
pkgdesc="Fast symbolic manipulation library, written in C++"
url="http://sympy.org/"
arch=(x86_64)
license=(MIT)
depends=(arb libmpc gmp-ecm primesieve gperftools zlib)
makedepends=(cmake boost llvm)
source=(https://github.com/symengine/symengine/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz
        symengine-llvm11.patch::"https://github.com/symengine/symengine/commit/346697a7.patch")
sha256sums=('4d2caa86c03eaaa8ed004084d02f87b5c51b6229f8ba70d161227e22d6302f0a'
            '3800bc90874f3ccb7d019ee9fbbdb94cea6c2ff91fd6beafe710957c70305d57')

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../symengine-llvm11.patch # Fix build with LLVM 11
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_TCMALLOC=ON \
    -DWITH_PTHREAD=ON \
    -DWITH_SYMENGINE_THREAD_SAFE=ON \
    -DWITH_ARB=ON \
    -DWITH_ECM=ON \
    -DWITH_MPFR=ON \
    -DWITH_MPC=ON \
    -DWITH_LLVM=ON \
    -DWITH_CPP14=ON \
    -DWITH_PRIMESIEVE=ON \
    -DWITH_BOOST=ON \
    -DWITH_COTIRE=OFF
  cmake --build build
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
