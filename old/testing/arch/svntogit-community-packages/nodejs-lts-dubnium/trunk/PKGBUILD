# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=nodejs-lts-dubnium
pkgver=10.23.2
pkgrel=1
pkgdesc="Evented I/O for V8 javascript (LTS release: Dubnium)"
arch=(x86_64)
url="https://nodejs.org/"
license=(MIT)
depends=(openssl zlib icu libuv http-parser c-ares libnghttp2)
makedepends=(python2 procps-ng)
optdepends=('npm: nodejs package manager')
provides=("nodejs=$pkgver")
conflicts=(nodejs)
source=("${url}/dist/v${pkgver}/node-v${pkgver}.tar.xz"
        icu68.patch)
# https://nodejs.org/download/release/latest-dubnium/SHASUMS256.txt.asc
sha256sums=(05a5e00e5e4342840f73ea40176416161ce9eeab6a4e936fc57bffd24dd1dbc7
            e953185d2d7de61bf8d4ca168fe034cce7a968264c1e7c24d53421fe7d24aace)
validpgpkeys=(C4F0DFFF4E8C1A8236409D08E73BC641CC11F4C8  # Myles Borins <mylesborins@google.com>
              77984A986EBC2AA786BC0F66B01FBB92821C587A  # Gibson Fahnestock <gibfahn@gmail.com>
              B9AE9905FFD7803F25714661B63B535A4C206CA9  # Evan Lucas <evanlucas@me.com> 
              DD8F2338BAE7501E3DD5AC78C273792F7D83545D  # Rod Vagg <rod@vagg.org>
              B9E2F5981AA6E0CD28160D9FF13993A75599653C  # Shelley Vohr <shelley.vohr@gmail.com>
              4ED778F539E3634C779C87C6D7062848A1AB005C  # Beth Griggs <Bethany.Griggs@uk.ibm.com>
              C82FA3AE1CBEDC6BE46B9360C43CEC45C17AB93C) # Richard Lau <rlau@redhat.com> 

prepare() {
  cd node-v${pkgver}

  patch -Np0 -i ../icu68.patch

  echo 'Fixing for python2 name'
  find -type f -exec sed \
    -e 's_^#!/usr/bin/env python$_&2_' \
    -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
    -e 's_^#!/usr/bin/python$_&2_' \
    -e 's_^\( *exec \+\)python\( \+.*\)$_\1python2\2_'\
    -e 's_^\(.*\)python\( \+-c \+.*\)$_\1python2\2_'\
    -e "s_'python'_'python2'_" -i {} +
  find test/ -type f -exec sed 's_python _python2 _' -i {} +
}

build() {
  cd node-v${pkgver}

  export PYTHON=python2
  ./configure \
    --prefix=/usr \
    --with-intl=system-icu \
    --without-npm \
    --shared-openssl \
    --shared-zlib \
    --shared-libuv \
    --shared-http-parser \
    --shared-cares \
    --shared-nghttp2

  make
}

check() {
  cd node-v${pkgver}
  # One failing test due to our http-parser version
  make test-only || echo "Tests failed"
}

package() {
  cd node-v${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
