# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>

pkgname=libx86emu
pkgver=3.1
pkgrel=1
pkgdesc='x86 emulation library'
arch=(x86_64)
url='https://github.com/wfeldt/libx86emu'
license=(BSD)
makedepends=(git setconf)
source=("git+$url#commit=0aed125dcc2214ccf9e1a52e9925ae654cefbaf1") # 3.1
sha256sums=('SKIP')

prepare() {
  sed -i 's/-shared -Wl/-shared '"$LDFLAGS"'/g' "$pkgname/Makefile"
  setconf "$pkgname/Makefile" \
    CFLAGS="$CFLAGS -shared -fPIC -fvisibility=hidden -fomit-frame-pointer -w"
}

build() {
  make -C "$pkgname" LIBDIR=/usr/lib
}

package() {
  make -C "$pkgname" LIBDIR=/usr/lib DESTDIR="$pkgdir" install
  install -Dm644 "$pkgname/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/wfeldt/libx86emu/releases
# vim: ts=2 sw=2 et:
