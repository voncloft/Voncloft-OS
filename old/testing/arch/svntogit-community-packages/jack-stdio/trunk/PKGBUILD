# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jack-stdio
pkgver=1.6
pkgrel=1
pkgdesc="Unix pipe audio-data from and to JACK"
arch=('x86_64')
url="https://github.com/x42/jack-stdio"
license=('GPL2')
groups=('pro-audio')
depends=('glibc' 'libjack.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/x42/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('e5537968ef58a74c535a7cfd960e22923c20db5808803d194efe8e5de4ffe3954941cdc331626ba660f6edcff3dbab257321db78de043741dc5fe60b247d8199')
b2sums=('0650bf96edcdf085fc4ee5d9a8cceaf881ab77ddcc9a93e94351947581938ac2d68b1d233c49c8cbcaa40b37e7c7e07c6c3b1acaed9edd51644b068ee8b47cf0')

build() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,ChangeLog} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
