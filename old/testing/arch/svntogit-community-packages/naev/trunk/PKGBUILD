# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Johan Rehnberg <cleanrock@gmail.com>

pkgname=naev
pkgver=0.8.1
pkgrel=1
pkgdesc='2D action/rpg space game'
arch=('x86_64')
url="https://github.com/naev/naev"
license=('GPL3')
depends=('glu' 'openal' 'libvorbis' 'sdl2' 'sdl2_image' 'sdl2_mixer' 'libgl' 'libxml2' 'freetype2' 'libpng' 'libzip' 'luajit')
makedepends=('freeglut' 'zip' 'meson' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/naev/naev/archive/v${pkgver}.tar.gz")
sha512sums=('b506b9b1522cc4271a4ab9fa81606b339c854a5ab2881d448cc1fbbf574f8764d77776e5a1159f7cf88b5a42530161d99083871cb95d89f88777e4ecdf1d366d')

build() {
  cd "$pkgname-$pkgver"

  meson setup -Dprefix=/usr -Dndata_path=/usr/share/naev/ndata build .
  cd build
  meson compile
}

package() {
  cd "$pkgname-$pkgver/build"

  DESTDIR="$pkgdir" meson install
}
