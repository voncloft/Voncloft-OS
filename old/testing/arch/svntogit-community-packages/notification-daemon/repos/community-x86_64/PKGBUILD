# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Mark Rosenstand <mark@borkware.net>

pkgname=notification-daemon
pkgver=3.20.0
pkgrel=4
pkgdesc="Server implementation of the freedesktop.org desktop notification specification"
arch=(x86_64)
license=(GPL2)
url="https://wiki.gnome.org/Projects/NotificationDaemon"
depends=(gtk3)
makedepends=(intltool)
source=("https://download.gnome.org/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('dd13768f35fd8bba9c4920b0f8269f39270e5a3cfed1a34c3b940a492286ece5')

build() {
    cd "${pkgname}-${pkgver}"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --libexecdir=/usr/lib/notification-daemon-1.0 \
        --localstatedir=/var --disable-static
    make
}

check() {
    cd "${pkgname}-${pkgver}"
    make check
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
