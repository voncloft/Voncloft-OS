# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-positional
pkgver=1.2.1
pkgrel=5
pkgdesc="Library to enforce positional or key-word arguments"
arch=('any')
url="https://github.com/morganfainberg/positional"
license=('Apache')
depends=('python-pbr' 'python-wrapt')
checkdepends=('python-testrepository')
source=("$pkgname-$pkgver.tar.gz::https://github.com/morganfainberg/positional/archive/$pkgver.tar.gz")
sha512sums=('5356d30a9412503342e828ce49d5f9ef71920f086135d2320c710177fbe2be11309bd61d56693de05e6dca620ae3b04c0fdd730fadef6dedd46c96fd0336d6ca')

export PBR_VERSION=$pkgver

build() {
  cd positional-$pkgver
  python setup.py build
}

check() {
  cd positional-$pkgver
  python setup.py testr
}

package() {
  cd positional-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
