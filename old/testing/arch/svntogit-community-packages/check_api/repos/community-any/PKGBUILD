# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=check_api
pkgver=1.2.2
pkgrel=1
pkgdesc='A gate-keeper tool for the APIs.guru API collection'
arch=('any')
url='https://github.com/Mermade/check_api'
license=('BSD')
depends=('nodejs' 'semver')
makedepends=('npm')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('94a4c5c89dd4bfb9a55d6349e8433c24598df3d1a0d4a83b96fdcaec34a1ff2a32654b1d59b7ddec72a5778efb692e9db0b4defef81a218ea14e675ee60ba157')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Why 777? :/
  chmod -R u=rwX,go=rX "$pkgdir"

  rm -r "$pkgdir"/usr/lib/node_modules/check_api/node_modules/{,.bin/}semver

  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s ../../../lib/node_modules/$pkgname/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
