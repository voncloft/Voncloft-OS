# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tobias Powalowski <t.powa@gmx.de>

pkgname=pwgen
pkgver=2.08
pkgrel=2
pkgdesc='Password generator for creating easily memorable passwords'
arch=('x86_64')
url='https://sourceforge.net/projects/pwgen/'
license=('GPL')
depends=('glibc')
validpgpkeys=('3AB057B7E78D945C8C5591FBD36F769BC11804F0')
source=("https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('dab03dd30ad5a58e578c5581241a6e87e184a18eb2c3b2e0fffa8a9cf105c97b'
            'SKIP')

prepare() {
  cd "$pkgname-$pkgver"

  autoconf
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: sf.net/p/pwgen/code/ci/master/tree/Makefile.in?format=raw
# vim: ts=2 sw=2 et:
