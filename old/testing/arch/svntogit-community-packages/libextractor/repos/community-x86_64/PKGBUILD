# Maintainer: Sergej Pupykin <pupykin.s@gmail.com>
# Contributor: damir <damir@archlinux.org>

pkgname=libextractor
pkgver=1.9
pkgrel=3
pkgdesc="A library used to extract meta-data from files of arbitrary type"
arch=("x86_64")
license=('GPL')
url="https://www.gnu.org/software/libextractor/"
depends=('libltdl')
makedepends=('exiv2' 'ffmpeg' 'file' 'flac' 'gst-plugins-base-libs' 'libgsf' 'libmpeg2' 'libvorbis'
             'poppler' 'libsmf')
optdepends=('exiv2: metadata support'
            'ffmpeg: previewopus/thumbnailffmpeg support'
            'file: mime support'
            'flac: flac support'
            'gst-plugins-base-libs: gstreamer support'
            'libarchive: archive support'
            'libgsf: (optional) for ODF support'
            'libmpeg2: mpeg support'
            'libvorbis: ogg/vorbis support'
            'poppler: pdf support'
            'libsmf: midi support')
validpgpkeys=('19647543F7861D3BF4E64FF7BF60708B48426C7E'
              'D8423BCB326C7907033929C7939E6BE1E29FC3CC')
source=("https://ftp.gnu.org/gnu/libextractor/${pkgname}-${pkgver}.tar.gz"{,.sig}
        libextractor-exiv2-0.27.patch)
sha512sums=('c2539b144d026fb0e871c5776aee4deaad4a987a730350744a7e5e74fbe98a4abb635dbe206b93c3aa9cd676b8797ea0b97271de0c903dfb035e245ab42ea149'
            'SKIP'
            '6411df1498fd3594088b3488e490b25009e9243684b777d35fa26189c20f410b42496b5fb42f762c468df746a9bdcd6a310f645db5ab0bbe3bd121e82aa9d48c')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../libextractor-exiv2-0.27.patch # Fix build with exiv2 0.27 (Gentoo)
  sed -e 's/ CODEC_FLAG_QSCALE/ AV_CODEC_FLAG_QSCALE/' \
      -e 's/FF_INPUT_BUFFER_PADDING_SIZE/AV_INPUT_BUFFER_PADDING_SIZE/' \
      -i src/plugins/thumbnailffmpeg_extractor.c
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/share/info/dir
}
