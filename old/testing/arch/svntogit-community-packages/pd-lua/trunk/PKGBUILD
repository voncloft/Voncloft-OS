# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pd-lua
pkgver=0.10.1
pkgrel=1
pkgdesc="LUA embedding for pd"
arch=('x86_64')
url="https://github.com/agraef/pd-lua"
license=('GPL2')
groups=('pd-externals' 'pro-audio')
depends=('gcc-libs' 'glibc' 'lua' 'pd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/agraef/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('26e33d9fcba1ef1dc2c5f553db13de0e0bf7cc1aa1371815d17b6d8bdccb8ad93b5ba30bc5adcdf5c3f7632d95da98f6687c516614c45d10190373eb3ba7169b')
b2sums=('6854208570eed9557dceed6954bf1e0ef352c815476585990d5fe9ae2e8cdbbe812dc2914dc758dcee1dfb91fa70964ff9141ff50b1e00f6a0b44258406a03bb')

prepare() {
  cd "$pkgname-$pkgver"
  # not installing license and README (to wrong location)
  sed -e '/COPYING /d' \
      -e '/README /d' \
      -i Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make LDFLAGS="${LDFLAGS}" CFLAGS="${CFLAGS}"
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" prefix='/usr' install
  # docs
  install -vDm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
  mv -v "${pkgdir}/usr/lib/pd/extra/pdlua/doc/"* \
    "${pkgdir}/usr/share/doc/${pkgname}"
  rmdir -v "${pkgdir}/usr/lib/pd/extra/pdlua/doc/"
}
