# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Dmitriy Morozov <foxcub>

pkgname=cgal
pkgver=4.14.3
pkgrel=1
pkgdesc="Computational Geometry Algorithms Library"
arch=('x86_64')
url="https://www.cgal.org"
license=('GPL' 'LGPL')
source=("https://github.com/CGAL/cgal/releases/download/releases%2FCGAL-$pkgver/CGAL-$pkgver.tar.xz")
depends=('mpfr' 'gmp' 'mesa' 'glu' 'boost-libs')
optdepends=('qt5-svg: for CGAL_Qt5'
            'eigen: for some packages, see the CGAL manual')
makedepends=('cmake' 'qt5-svg' 'eigen' 'boost')
md5sums=('41c56ad048ce882aae6ff5b56256fe95')

# consider building with swig for python stuff
# https://gforge.inria.fr/frs/?group_id=52  for _pkgid

# 4.8 supposedly does not depend on boost?  good luck...

build() {
  cd "$srcdir/CGAL-$pkgver"
  mkdir -p build
  cd build
  cmake ../ -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd "$srcdir/CGAL-$pkgver/build"
  make install DESTDIR="$pkgdir"

  # The tarball still has all these licenses included
  for _license in LICENSE{,.FREE_USE,.GPL,.LGPL}; do
    install -D -m644 "$srcdir/CGAL-$pkgver/$_license" "$pkgdir/usr/share/licenses/$pkgname/$_license"
  done
}
