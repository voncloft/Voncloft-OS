# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Simon Lipp <aur@simon.lipp.name>
# Contributor: Daniel Grana <dangra@gmail.com>

pkgname=s3cmd
pkgver=2.1.0
pkgrel=4
pkgdesc="A command line client for Amazon S3"
arch=('any')
url='http://s3tools.org/s3cmd'
license=('GPL')
depends=('python' 'python-dateutil')
makedepends=('python-setuptools')
optdepends=('gnupg: encrypted file storage'
            'python-magic: determine mimetype based on contents')
source=(https://github.com/s3tools/s3cmd/releases/download/v${pkgver}/s3cmd-${pkgver}.tar.gz{,.asc}
        https://patch-diff.githubusercontent.com/raw/s3tools/s3cmd/pull/1144.patch
        https://patch-diff.githubusercontent.com/raw/s3tools/s3cmd/pull/1137.patch)
sha512sums=('98156218c4379e233185662d29fa4299b4a4e45a0fe143a145f30ff7d03362a4558b5fff5e5da0da850f5cce2c760fcd031bcf5ff7a6bd2c6352259e791d65e6'
            'SKIP'
            '72ccb5bbb8bf8e3b9e4c887d6e995f5dc209c94fcc3916340143b0004997e7822d491c07adae616a92bac6a47b957635ac2d8e42ec89506730efa31e903b09c5'
            '9cdfb70ee630242170fbdc16eac67e7fe95921a307861fdc6b4b60094dde3f5573aa2d7b9484714cf19721bf59c99af94b2d2a438e21b5f358ecfdc8cf08a8ce')
validpgpkeys=(0D37A8F4A5D183D5541D85D9241769189AC3D00B)

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "$srcdir"/1144.patch
  patch -Np1 -i "$srcdir"/1137.patch
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python setup.py install --root="$pkgdir"
}
