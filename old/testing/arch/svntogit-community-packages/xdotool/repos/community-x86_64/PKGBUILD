# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Rttommy <rttommy@gmail.com>

pkgname=xdotool
pkgver=3.20160805.1
pkgrel=3
pkgdesc="Command-line X11 automation tool"
arch=('x86_64')
url="https://www.semicomplete.com/projects/xdotool/"
license=('BSD')
depends=('libxtst' 'libxinerama' 'libxkbcommon')
source=(https://github.com/jordansissel/xdotool/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('35be5ff6edf0c620a0e16f09ea5e101d5173280161772fca18657d83f20fcca8')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make WITHOUT_RPATH_FIX=1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX="$pkgdir/usr" INSTALLMAN="$pkgdir/usr/share/man" install

  # Remove execute bit from header file
  chmod -x "$pkgdir/usr/include/xdo.h"

  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
}

# vim:set ts=2 sw=2 et:
