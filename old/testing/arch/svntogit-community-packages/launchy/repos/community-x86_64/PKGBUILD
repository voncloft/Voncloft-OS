# Contributor: Andrew Burkett <burkett.andrew@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=launchy
_pkgver=2.9.1-pre.1
pkgver=${_pkgver/-/}
pkgrel=2
pkgdesc="Indexes the programs in your start menu and can launch your documents, project files, folders, and bookmarks with just a few keystrokes!"
arch=('x86_64')
url="https://www.launchy.net/"
license=('GPL')
depends=('qt5-x11extras' 'qt5-declarative' 'qt5-svg')
makedepends=('qt5-tools' 'boost' 'cmake')
source=($pkgname-$pkgver.tar.gz::"https://github.com/Slesa/launchy/archive/v$_pkgver.tar.gz")
sha512sums=('54d27e39a86f4b9c3d7a2770d06ef6a6bcdc5dee770e523c9fcb9dd3c25c7de1705c9f0d0b5452a9b7d20b607a998ad7faa713aa6566dc3f406cacb14a84f8bd')

build() {
  cd ${pkgname}-${_pkgver}/src

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${pkgname}-${_pkgver}/src

  make DESTDIR="${pkgdir}" install
}
