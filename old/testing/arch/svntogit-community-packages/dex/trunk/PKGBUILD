# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=dex
pkgver=0.9.0
pkgrel=1
pkgdesc='Program to generate and execute DesktopEntry files of type Application'
url='https://github.com/jceb/dex'
arch=('any')
license=('GPL2')
depends=('python')
makedepends=('git' 'python-sphinx')
source=("https://github.com/jceb/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        fix-dex-version.patch)
sha512sums=('d68f5482cb0948f27a724437ddfc6de9a0f502bfd0d5c60c76fb85dda3c30e4c432013e530f6a91138c9ac9ff36b3824cd5e382e9d29bb9fb2ec2b9de4133094'
            '327135abd8c61068a9cbd76dc15ba81d5a7c4e96e8f3ceaa4e2db0f15e7f433aa81310ec80db02bbf2f5abadbc22bb03a71b88ba039a08254eea610d3dd7c7f6')
b2sums=('7abc01ccc6938e926840e4448eb6436db77703c1f8e1fef334d97196803a0b59af0f05fc507d1ac4337367120f7d9d9a211aecec6ac3644424edee3599067a89'
        'de60d2f1118a9b27f7a133b0058a780f9dd1e31cf9bbf99c5218b65ea4cde2c1f7a23a9f948ec0b32d2a4cea70a8a95cdab9bfaebabad27fda722d72947184db')

prepare() {
  cd "$pkgname-$pkgver"
  patch dex <"$srcdir"/fix-dex-version.patch
}

package() {
  cd "$pkgname-$pkgver"
  make install PREFIX=/usr MANPREFIX=/usr/share/man DESTDIR="$pkgdir"
  rm "$pkgdir/usr/share/doc/dex/LICENSE"
}

# vim:set ts=2 sw=2 et:
