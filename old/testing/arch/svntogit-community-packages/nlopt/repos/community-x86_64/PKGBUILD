# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=nlopt
pkgver=2.7.0
pkgrel=1
pkgdesc="Nonlinear optimization library"
arch=(x86_64)
url="https://nlopt.readthedocs.io/en/latest/"
license=(LGPL)
depends=(gcc-libs)
makedepends=(cmake python-numpy octave guile swig)
optdepends=('python-numpy: to use with python')
source=("https://github.com/stevengj/nlopt/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('b881cc2a5face5139f1c5a30caf26b7d3cb43d69d5e423c9d78392f99844499f')

build() {
    cmake -B build -S ${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    make -C build
}

check() {
    cd build
    ctest --output-on-failure
}

package() {
    make -C build DESTDIR="${pkgdir}" install
}
