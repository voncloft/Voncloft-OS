# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=sunpinyin-data
pkgver=2.0.3.20140820
epoch=1
_lm_sc_ver=20140820
_dict_ver=20131214
pkgrel=3
arch=('any')
pkgdesc="Statistical language model data from open-gram project for sunpinyin"
license=('APACHE')
provides=("sunpinyin-data-le=$pkgver")
url="https://sourceforge.net/projects/open-gram/"
makedepends=('sunpinyin' 'git')
source=("https://downloads.sourceforge.net/project/open-gram/lm_sc.3gm.arpa-${_lm_sc_ver}.tar.bz2"
        "https://downloads.sourceforge.net/project/open-gram/dict.utf8-${_dict_ver}.tar.bz2")
sha512sums=('ea3ede62eb993c4d21ca7cf339635f4686493a0cefd57df4a31f00a001e67502a13ff9612ff667b941b82b47de00be9f3c9689677d04195a7ef8716bac087519'
            'adaf52a27c6afdeb950912845f3fcb7a6799f997d50a2892ceecf76ed13e3644407338d734832c2ce4b09bce11fec4731774f1eb2dd4c30bea5490555915bd5b')

prepare() {
  mkdir raw && cd raw
  ln -s ../lm_sc.3gm.arpa
  ln -s ../dict.utf8

  ln -sf "/usr/share/doc/sunpinyin/SLM-inst.mk" ./Makefile
}

build() {
  cd raw
  make
}

package() {
  cd raw
  make DESTDIR="$pkgdir" install
}

