# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-magnumclient
pkgver=3.3.0
pkgrel=1
pkgdesc="Client library for Magnum API"
arch=('any')
url="https://docs.openstack.org/python-magnumclient/latest/"
license=('Apache')
depends=('python-pbr' 'python-babel' 'python-keystoneauth1' 'python-stevedore' 'python-requests'
         'python-oslo-i18n' 'python-oslo-log' 'python-oslo-serialization' 'python-oslo-utils'
         'python-os-client-config' 'python-osc-lib' 'python-prettytable' 'python-cryptography'
         'python-debtcollector')
checkdepends=('python-openstackclient' 'python-oslotest' 'python-osprofiler')
source=("https://github.com/openstack/python-magnumclient/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0c38bdc150e166e8f6209d82823e3f05d28ced32308b3dc634870f727c867016ae4fda5037760ccfc01be4b12fe699c62dfe9206770f1395e21ff990310a3cbd')

export PBR_VERSION=$pkgver

build() {
  cd python-magnumclient-$pkgver
  python setup.py build
}

check() {
  cd python-magnumclient-$pkgver
  stestr run
}

package() {
  cd python-magnumclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
