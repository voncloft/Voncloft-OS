# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=vmpk
pkgver=0.8.0
pkgrel=1
pkgdesc="Virtual MIDI Piano Keyboard"
arch=('x86_64')
url="http://vmpk.sourceforge.net/"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'libxcb' 'qt5-base' 'qt5-x11extras')
makedepends=('cmake' 'docbook-xsl' 'drumstick' 'qt5-tools')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver/a/}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('b3e1e40792661c1e4ce3a5274513b4b75742ca861a6233c2653d6df90b7a89e086d15a4196d59c7021c48e9e2aae83116ba29c3395960179d602f0cb06c1f608')
b2sums=('9fee766b71ad7f9cd96f9dfd859dc31434f14c913c75f5a8e76c66150522dbf04c6e28582269d1603c6d53d6db436e9e7f73cab1172c0d6b004cc71edccb21c8')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libdrumstick-rt.so' 'libdrumstick-widgets.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install -C build
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
