# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Gilles CHAUVIN <gcnweb at gmail dot com>

pkgname=recordmydesktop
pkgver=0.3.8.1
pkgrel=9
pkgdesc="Produces a OGG encapsulated Theora/Vorbis recording of your desktop"
arch=('x86_64')
url="http://recordmydesktop.sourceforge.net"
license=('GPL')
depends=('alsa-lib' 'zlib' 'libvorbis' 'libxdamage' 'libxext' 'libtheora' 'libsm' 'jack')
makedepends=('jack')
source=(https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
	shmstr-to-shmproto.patch
	recordmydesktop-bitrate.patch)
sha512sums=('a32df1257d7d8928123437b38abcbdf623a9a65d313df5bec9bc06ef7229ebe4664a3cbea1fe5f6f1f9b89efb939873adc020f4ac072477f6df9481a37e7c350'
            '22cc2cb91013466908858e0246643b80822e3935265e9d3d1653626bfadfdd798f78aa482addd7db1afd6c4d7f19e1f9917eed3096981cf994879027a2ff61cf'
            '0e26f5170c39b74c345a35d6c64a6745261a2457cd340d62c1181c90ead8d6a41ebdbbee9f115d8bfc1dea2cce021a6aadd3abf9df3b7ebcff9e59774204f560')

build() {
  cd "$srcdir"/${pkgname}-${pkgver}
  patch -p0 < "$srcdir"/shmstr-to-shmproto.patch
  patch -p1 < "$srcdir"/recordmydesktop-bitrate.patch
  ./configure --prefix=/usr --enable-jack=yes LIBS="`pkg-config --libs jack`" \
	CFLAGS="`pkg-config --cflags jack` -DHAVE_LIBJACK"
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
}
