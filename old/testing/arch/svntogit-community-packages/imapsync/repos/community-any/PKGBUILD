# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=imapsync
pkgver=1.977
pkgrel=1
pkgdesc='IMAP synchronisation, sync, copy or migration tool'
arch=('any')
url='https://github.com/imapsync/imapsync'
license=('custom:imapsync')
depends=('perl-cgi' 'perl-crypt-openssl-rsa' 'perl-data-uniqid' 'perl-date-manip' 'perl-encode-imaputf7'
        'perl-file-copy-recursive' 'perl-file-tail' 'perl-html-parser' 'perl-io-socket-inet6'
        'perl-io-socket-ssl' 'perl-io-tee' 'perl-json-webtoken' 'perl-libwww' 'perl-module-scandeps'
        'perl-mail-imapclient' 'perl-module-runtime' 'perl-ntlm' 'perl-package-stash'
        'perl-readonly' 'perl-regexp-common' 'perl-sys-meminfo'
        'perl-term-readkey' 'perl-unicode-string')
makedepends=('cpanminus' 'perl-par-packer')
checkdepends=('inetutils' 'lsb-release' 'time' 'procps-ng' 'perl-test-deep' 'perl-test-fatal'
              'perl-test-mock-guard' 'perl-test-mockobject' 'perl-test-pod' 'perl-test-requires'
              'perl-test-warn')
source=("https://github.com/imapsync/imapsync/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('ccbe678f331a59b2ee50150b4e6dd3b2a7fb7750f204f3d67c90beed600b83b38a712bfa723dc0a73d21fb9bc03a5013f287469afa1164962cfb0f2923d93910')

build() {
  cd ${pkgname}-${pkgver}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
