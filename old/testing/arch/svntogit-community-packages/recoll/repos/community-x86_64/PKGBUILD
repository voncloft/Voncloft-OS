# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Vladimir Chizhov <jagoterr@gmail.com>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>

pkgname=recoll
pkgver=1.28.5
pkgrel=1
pkgdesc="Full text search tool based on Xapian backend"
arch=('x86_64')
url="https://www.lesbonscomptes.com/recoll/"
license=('GPL')
depends=('xapian-core>=1.0.15-1' 'qt5-base' 'openssl' 'hicolor-icon-theme' 'qt5-webkit'
	 'chmlib')
makedepends=('python' 'python-setuptools')
optdepends=('libxslt: for XML based formats (fb2,etc)'
	    'unzip: for the OpenOffice.org documents'
	    'poppler: for pdf'
	    'pstotext: for postscipt'
	    'antiword: for msword'
	    'catdoc: for ms excel and powerpoint'
	    'unrtf: for RTF'
#	    'untex: for dvi support with dvips'
	    'djvulibre: for djvu'
	    'id3lib: for mp3 tags support with id3info'
	    'python-mutagen: Audio metadata'
	    'perl-image-exiftool: EXIF data from raw files'
	    'python-lxml: indexing spreadsheets'
            'python-pychm: CHM filter'
	    'aspell-en: English stemming support')
source=("https://www.lesbonscomptes.com/$pkgname/$pkgname-${pkgver/_/}.tar.gz")
sha512sums=('df4c7a5e624933ccc7ce257a767a4621728a4dbcdd5b551b47845ed0fcffdd5a6f7f617a466404c5e8d8b62c81e57986e1f21e01d643899755623a2dc597d712')

prepare() {
  cd "$srcdir/$pkgname-${pkgver/_/}"
  sed -i '1,1i#include <QAbstractItemView>' qtgui/ssearch_w.cpp
}

build() {
  cd "$srcdir/$pkgname-${pkgver/_/}"
  QMAKE=qmake-qt5 ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/}"
  make DESTDIR="$pkgdir" install
}
