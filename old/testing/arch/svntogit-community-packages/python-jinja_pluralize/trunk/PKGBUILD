# Maintainer: David Runge <dvzrv@archlinux.org>

_name=jinja2_pluralize
pkgname=python-jinja_pluralize
pkgver=0.2.1
pkgrel=3
pkgdesc="Jinja2 pluralize filters"
arch=('any')
url="https://github.com/audreyr/jinja2_pluralize"
license=('BSD')
depends=('python-inflect' 'python-jinja')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('61d6866cbd966a534788b947f949231d0e54e575d826a45953be850b470c00e76bd10faed076d548aef17ba29f81a6efd040c90b1d9a80bc709205879f9a266b')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {AUTHORS,CONTRIBUTING,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
