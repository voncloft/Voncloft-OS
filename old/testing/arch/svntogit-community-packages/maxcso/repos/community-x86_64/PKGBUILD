# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gabriel Rauter <rauter.gabriel@gmail.com>

pkgname=maxcso
pkgver=1.12.0
pkgrel=1
pkgdesc='A fast ISO to CSO compression program for use with PSP and PS2 emulators'
arch=(x86_64)
url=https://github.com/unknownbrackets/maxcso
license=(custom:ISCL)
depends=(
  gcc-libs
  glibc
  libuv
  libzopfli.so
  lz4
  zlib
)
makedepends=(git)
source=(
  git+https://github.com/unknownbrackets/maxcso.git#tag=d80e89540f75afc654ea191fa2d8c629189d90a2
  maxcso-system-flags.patch
  maxcso-system-zopfli.patch
)
sha256sums=('SKIP'
            '9500875013db8ad18350cc0a36751fec37de80cde06e91e2999a421e53af8416'
            'ceae42c12bc686dba1dae8b0df1f32e6063ba25d7edebfaaa94af85a9e8f24f7')

pkgver() {
  cd maxcso

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd maxcso

  patch -Np1 -i ../maxcso-system-flags.patch
  patch -Np1 -i ../maxcso-system-zopfli.patch
}

build() {
  make -C maxcso
}

package() {
  make DESTDIR="${pkgdir}" PREFIX=/usr -C maxcso install
  install -Dm 644 maxcso/LICENSE.md -t "${pkgdir}"/usr/share/licenses/maxcso/
}

#vim: ts=2 sw=2 et:
