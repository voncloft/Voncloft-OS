# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=mgard
pkgver=0.1.0
pkgrel=1
pkgdesc="MultiGrid Adaptive Reduction of Data"
arch=(x86_64)
url="https://github.com/CODARcode/MGARD"
license=(Apache)
depends=(glibc zlib zstd)
makedepends=(cmake)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('254cc77ca678c746c3808d7fb76c289fce17f2cbb84a1892d6cd2ea078be62f0')

build() {
  cmake -B build -S ${pkgname^^}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr
  make -C build
}

check() {
  cd build
  # https://github.com/CODARcode/MGARD/issues/105
  ctest -v || warning "Tests failed"
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
