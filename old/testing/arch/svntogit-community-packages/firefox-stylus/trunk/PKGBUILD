# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=firefox-stylus
pkgver=1.5.15
pkgrel=1
pkgdesc='Easily install themes and skins for many popular sites'
url=https://add0n.com/stylus.html
arch=('any')
license=('GPL3')
groups=('firefox-addons')
makedepends=('git' 'nodejs' 'npm')
source=("https://github.com/openstyles/stylus/archive/v$pkgver/stylus-v$pkgver.tar.gz")
b2sums=('92dbebdcf65b756d107daa49a7567fc671a58d0beaf82153adada9683477ae4f8a870b96d50fca2417fbe42492ce23d7b47cf2777f15b1c1023dc737b871f6b2')

build() {
  cd stylus-$pkgver
  npm install --frozen-lockfile
  npm run zip
}

package() {
  cd stylus-$pkgver
  install -Dm644 stylus.zip \
    "$pkgdir"/usr/lib/firefox/browser/extensions/{7a7a4a92-a2a0-41d1-9fd7-1e92480d612d}.xpi
}

# vim:set ts=2 sw=2 et:
