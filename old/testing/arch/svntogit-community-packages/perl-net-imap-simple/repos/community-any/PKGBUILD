# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-net-imap-simple
pkgver=1.2207
pkgrel=4
pkgdesc="Net::IMAP::Simple - a simple IMAP access library"
arch=(any)
license=('GPL' 'PerlArtistic')
url="https://search.cpan.org/dist/Net-IMAP-Simple/"
depends=('perl>=5.10.0')
options=(!emptydirs)
source=(https://search.cpan.org/CPAN/authors/id/J/JE/JETTERO/Net-IMAP-Simple-${pkgver}.tar.gz)
sha256sums=('7fb7fd25fdf62fd7f83d3a9401117e5f485116fc7653e943a93c6c5bbb1f4099')

build() {
  cd "$srcdir"/Net-IMAP-Simple-${pkgver}
  yes n | perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/Net-IMAP-Simple-${pkgver}
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name perllocal.pod -delete
  find "$pkgdir" -name .packlist -delete
}
