# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>

pkgname=yq
pkgver=2.12.0
pkgrel=1
pkgdesc="Command-line YAML/XML processor - jq wrapper for YAML/XML documents"
arch=('any')
url="https://github.com/kislyuk/yq"
license=('Apache')
depends=('jq' 'python-setuptools' 'python-yaml' 'python-xmltodict' 'python-toml'
         'python-argcomplete')
source=("https://files.pythonhosted.org/packages/source/y/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('1d2ad403504d306b5258b86c698f9856d7ad58b7bb17a2b875691a6a7b8c4c20')
b2sums=('a314b54628e1865df68dadcbec8e59e16c1e57205e2b9f2b364bb6bee91563dc3da30c1e3a7ac9e2656899ee8ad004600291a845f59fbe13356f0cfb69de3e79')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  python test/test.py
}

package() {
  cd $pkgname-$pkgver
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
