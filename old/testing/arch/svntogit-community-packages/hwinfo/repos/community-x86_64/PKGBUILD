# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Gergely Imreh <imrehg@gmail.com>
# Contributor: champus, madeye, ranguvar and ninja_pt

pkgname=hwinfo
pkgver=21.72
pkgrel=1
pkgdesc='Hardware detection tool from openSUSE'
arch=(x86_64)
url='https://github.com/openSUSE/hwinfo'
license=(GPL2)
depends=(libx86emu perl-xml-parser perl-xml-writer)
makedepends=(flex git sysfsutils)
options=(!emptydirs)
source=("$pkgname-$pkgver.tgz::https://github.com/openSUSE/$pkgname/archive/$pkgver.tar.gz"
        custom_ioctl.patch)
sha512sums=('28c9c15765f9631c678aab995a355b08add17303db982bcd831383b33e72ffe7abfa7133d5a36cc29beef9a9bb6ded4675caac509417eeea16b313d3f69fc113'
            '88134fb6a08b86841c6b560afc9b8bb009c45bda1674437ef1b36f3d67ea37056717567252a314812dc30f174f0ad2be8768fba23469df42afbf5ebdd4666a3c')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i ../custom_ioctl.patch
  echo 'touch changelog' > git2log
}

build() {
  make -C $pkgname-$pkgver -j1 \
    CFLAGS+="-fPIC -I$srcdir/$pkgname-$pkgver/src/hd" \
    HWINFO_VERSION="$pkgver" \
    LIBDIR=/usr/lib
}

package() {
  make -C $pkgname-$pkgver LIBDIR=/usr/lib DESTDIR="$pkgdir" install
  mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
}

# getver: github.com/openSUSE/hwinfo/releases
