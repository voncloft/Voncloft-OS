# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>
# Contributor: Sander Deryckere  <sanderd17@gmail.com>

pkgname=faience-icon-theme
pkgver=0.5.1
pkgrel=3
pkgdesc='An icon theme based on Faenza'
url='http://tiheum.deviantart.com/art/Faience-icon-theme-255099649'
license=('GPL3')
arch=('any')
depends=('faenza-icon-theme')
source=("http://ppa.launchpad.net/tiheum/equinox/ubuntu/pool/main/f/${pkgname}/${pkgname}_${pkgver}.tar.gz")
sha256sums=('da4e6e921c465caeb8904d4fc6a183b173c8000c08abce625b5b6739f5a0867a')

package() {
  cd ${pkgname}-${pkgver%.*}

  install -dm 755 "${pkgdir}"/usr/share/icons
  cp -dr --no-preserve=ownership Faience* "${pkgdir}"/usr/share/icons/

# Fix permissions
  find "${pkgdir}" -type d -exec chmod 755 {} +
  find "${pkgdir}" -type f -exec chmod 644 {} +
}

# vim: ts=2 sw=2 et:
