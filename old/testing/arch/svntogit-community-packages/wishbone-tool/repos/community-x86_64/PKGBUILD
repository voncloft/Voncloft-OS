# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=wishbone-utils
pkgver=0.4.8
pkgrel=1
pkgdesc='Utilities for working with a Wishbone bridge'
arch=(x86_64)
url="https://github.com/xobs/wishbone-utils"
license=(BSD)
depends=(glibc gcc-libs)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::https://github.com/xobs/wishbone-utils/archive/v$pkgver.tar.gz)
sha512sums=('48eb39487da99994831a7329df2bad38116fea1427f3dca6711656a81e22e0caceb996ba7afef703a2fad70fa98247864ef8787d9d9e6ca64c72bf79ec8f67d5')

build() {
  cd $pkgname-$pkgver/wishbone-tool
  cargo build --release --locked
}

check() {
  cd $pkgname-$pkgver/wishbone-tool
  cargo test --release --locked
}

package() {
  cd $pkgname-$pkgver
  install -Dm 755 wishbone-tool/target/release/wishbone-tool -t "${pkgdir}/usr/bin/"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
