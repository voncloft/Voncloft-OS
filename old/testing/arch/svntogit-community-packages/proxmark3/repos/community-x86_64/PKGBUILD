# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=proxmark3
pkgver=4.9237
pkgrel=1
pkgdesc='Software for the the Proxmark3, an RFID swiss-army tool'
arch=('x86_64')
url='https://github.com/RfidResearchGroup/proxmark3'
license=('GPL2')
depends=('qt5-base' 'bluez-libs')
makedepends=('arm-none-eabi-gcc' 'arm-none-eabi-newlib')
optdepends=('lua: lua scripts'
            'python: some scripts')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('c3ba93c3d456a1daf5001a37bc8030aa973eb1626ef4b31fc14df791375088763926637478df1bbb45f489743ec03490efe36accc22ef7301d7a032a445e551d')

prepare() {
  cp -r $pkgname-$pkgver{,-rdv4}
}

build() {
  cd "$srcdir"/$pkgname-$pkgver

  make PLATFORM=PM3OTHER all

  cd "$srcdir"/$pkgname-$pkgver-rdv4

  make PLATFORM=PM3RDV4 FWTAG=rdv4 common bootrom recovery fullimage
}

package() {
  export PREFIX=/usr
  export DESTDIR="$pkgdir"
  export UDEV_PREFIX=/usr/lib/udev/rules.d
  export INSTALLTOOLSRELPATH=bin

  cd "$srcdir"/$pkgname-$pkgver-rdv4

  make PLATFORM=PM3RDV4 FWTAG=rdv4 {bootrom,recovery,fullimage}/install

  cd "$srcdir"/$pkgname-$pkgver

  make PLATFORM=PM3OTHER install
}

