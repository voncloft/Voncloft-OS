# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Mario Bl√§ttermann <mario.blaettermann@gmail.com>
# Contributor: Benjamin Vialle <archlinux@vialle.io>
# Contributor: Rafael Fontenelle <rafaelff@gnome.org>

pkgbase=man-pages-l10n
pkgname=('man-pages-de'
         'man-pages-fr'
         'man-pages-nl'
         'man-pages-pl'
         'man-pages-pt_br'
         'man-pages-ro')
pkgver=4.2.0
pkgrel=1
pkgdesc="Translation of man pages"
arch=('any')
url="https://salsa.debian.org/manpages-l10n-team/manpages-l10n"
license=('GPL3')
depends=('man-db')
makedepends=('po4a')
changelog=$pkgbase.changelog
options=('!emptydirs')
source=(https://salsa.debian.org/manpages-l10n-team/manpages-l10n/-/archive/v$pkgver/manpages-l10n-v$pkgver.tar.bz2)
sha256sums=('0dbf2b879793e0e84d0cf61562019c11260b7e37d8151048dc068bfa719bcebd')

build() {
  cd "${srcdir}"/manpages-l10n-v$pkgver
  ./configure --prefix=/usr --enable-distribution=archlinux

  for _lang in de fr nl pl pt_BR ro; do
    make -C po/"${_lang}"
  done
}

check() {
  cd "${srcdir}"/manpages-l10n-v$pkgver

  for _lang in de fr nl pl pt_BR ro; do
    make check -k -C po/"${_lang}"
  done
}

package_man-pages-de() {
  pkgdesc="German man pages"
  depends=('man-db')

  cd "${srcdir}"/manpages-l10n-v$pkgver/po/de
  make DESTDIR="${pkgdir}" install
}

package_man-pages-fr() {
  pkgdesc="French man pages"
  depends=('man-db')

  cd "${srcdir}"/manpages-l10n-v$pkgver/po/fr
  make DESTDIR="${pkgdir}" install
}

package_man-pages-nl() {
  pkgdesc="Dutch man pages"
  depends=('man-db')

  cd "${srcdir}"/manpages-l10n-v$pkgver/po/nl
  make DESTDIR="${pkgdir}" install
}

package_man-pages-pl() {
  pkgdesc="Polish man pages"
  depends=('man-db')

  cd "${srcdir}"/manpages-l10n-v$pkgver/po/pl
  make DESTDIR="${pkgdir}" install
}

package_man-pages-pt_br() {
  pkgdesc="Brazilian Portuguese man pages"
  depends=('man-db')

  cd "${srcdir}"/manpages-l10n-v$pkgver/po/pt_BR
  make DESTDIR="${pkgdir}" install
}

package_man-pages-ro() {
  pkgdesc="Romanian man pages"
  depends=('man-db')

  cd "${srcdir}"/manpages-l10n-v$pkgver/po/ro
  make DESTDIR="${pkgdir}" install
}
