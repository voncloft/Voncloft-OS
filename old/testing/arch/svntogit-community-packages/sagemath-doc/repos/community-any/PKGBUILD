# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sagemath-doc
pkgver=9.2
pkgrel=1
pkgdesc="HTML documentation for SageMath"
arch=(any)
url="http://www.sagemath.org"
license=(GPL)
depends=(gap-doc python-pplpy)
makedepends=(sagemath python-pyzmq python-docutils python-jupyter_client python-pkgconfig cython python-ipywidgets thebe)
source=(sagemath-$pkgver.tar.gz::"https://github.com/sagemath/sage/archive/$pkgver.tar.gz"
        docbuild_main.patch)
sha256sums=('edc89776461247cf74a16473851378e70a2de867309978ca2346ef6f93af0f90'
            'ea5e17bb7a7cb36a22e5e3872fcc2585852bc971c4b139b0b2cd69a36c1b009b')
options=(!strip)

prepare() {
  cd sage-$pkgver

# fix relative imports
  patch -p0 -i ../docbuild_main.patch
# use sage_setup imports from sagemath source, but use system sage ones (which include compiled modules)
  mkdir -p local-python
  ln -sr src/sage_setup local-python

  SAGE_ROOT="$PWD" PATH="$PWD/build/bin:$PATH" src/doc/bootstrap
}

build() {
  cd sage-$pkgver/src

  export SAGE_LOCAL="/usr" \
         SAGE_ROOT="/usr" \
         SAGE_SRC="$PWD" \
         SAGE_DOC_SRC="$PWD"/doc \
         SAGE_DOC="$PWD"/doc \
         MATHJAX_DIR="/usr/share/mathjax2" \
         PYTHONPATH="$srcdir"/sage-$pkgver/local-python \
         SAGE_NUM_THREADS=10 \
         LANG=C
  python sage_setup/docbuild --no-pdf-links --mathjax all html
}

package() {
  cd sage-$pkgver/src/doc
 
  mkdir -p "$pkgdir"/usr/share/doc/sage
  cp -r common "$pkgdir"/usr/share/doc/sage
  cp -r html "$pkgdir"/usr/share/doc/sage

# Replace duplicated files by symlinks (Gentoo)
  cd "$pkgdir"/usr/share/doc/sage
  mv html/en/_static{,.tmp}
  for _dir in `find -name _static` ; do
    rm -r $_dir
    ln -s /usr/share/doc/sage/html/en/_static $_dir
  done
  mv html/en/_static{.tmp,}
}
