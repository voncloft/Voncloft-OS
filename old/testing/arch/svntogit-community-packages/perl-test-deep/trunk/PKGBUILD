# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-test-deep
pkgver=1.128
pkgrel=3
pkgdesc="Extremely flexible deep comparison "
arch=('any')
url="https://search.cpan.org/dist/Test-Deep"
license=('GPL' 'PerlArtistic')
depends=('perl-test-nowarnings' 'perl-test-simple')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/Test-Deep-$pkgver.tar.gz)
sha256sums=('852d7e836fba8269b0b755082051a24a1a309d015a8b76838790af9e3760092f')

build() {
  cd  "$srcdir"/Test-Deep-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/Test-Deep-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
