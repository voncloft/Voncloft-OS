# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rink
pkgver=0.5.1
pkgrel=1
pkgdesc='Unit conversion tool and library written in rust'
arch=('x86_64')
url=https://github.com/tiffany352/rink-rs
license=('GPL3' 'MPL2')
depends=('gcc-libs' 'gmp' 'openssl')
makedepends=('rust')
source=("rink-$pkgver.tar.gz::https://static.crates.io/crates/rink/rink-$pkgver.crate")
sha512sums=('d40c150867029f817b222a75e982f98ad83c4edaca701916c81971efba6d83b11aeeeb2b415f18c038ec3f9dbc7e39c0a5f411a8d78fe09aafe8f93d8f74d13c')

build() {
  cd rink-$pkgver
  cargo build --release --locked
}

check() {
  cd rink-$pkgver
  cargo test --release --locked
}

package() {
  cd rink-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/rink
}

# vim:set ts=2 sw=2 et:
