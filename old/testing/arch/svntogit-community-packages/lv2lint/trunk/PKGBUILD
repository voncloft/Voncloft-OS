# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=lv2lint
pkgver=0.10.0
pkgrel=1
pkgdesc="Check whether a given LV2 plugin is up to the specification"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/lv2lint/"
license=('Artistic2.0')
depends=('glibc' 'libelf')
makedepends=('curl' 'lilv' 'lv2' 'meson')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('a537394eca38da4f407cbcf05bb883b1d519a88f28273f4c89900dd879e26379ed55b44e67bfcedf9e56ecfb525489fc63b81c00cb5c1a92e9fc2ee037d86b53'
            'SKIP')
b2sums=('c5f90a64d7f2382d5bc3b2753d9bc1f5355d03c30e7cc23dca79508c3e5e00a87f83ec8dda569d3db451d3dab9ab11c1794ebe15631177bd7fdd6b95b49fca49'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr \
        --libexecdir lib \
        --sbindir bin \
        --buildtype plain \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        -Donline-tests=true \
        -Delf-tests=true \
        build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build test
}

package() {
  depends+=('libcurl.so' 'liblilv-0.so')
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir/" ninja -C build install
  install -vDm 644 {ChangeLog,README.md,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
