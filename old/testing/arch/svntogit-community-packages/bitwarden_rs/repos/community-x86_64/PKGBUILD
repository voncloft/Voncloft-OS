# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>
# Contributor: Timoth√©e Ravier <tim@siosm.fr

pkgname=bitwarden_rs
pkgver=1.19.0
pkgrel=1
pkgdesc='Unofficial Bitwarden compatible server written in Rust'
arch=('x86_64')
url=https://github.com/dani-garcia/bitwarden_rs
license=('GPL3')
depends=('mariadb-libs' 'openssl' 'postgresql-libs' 'sqlite')
makedepends=('rustup')
optdepends=('bitwarden_rs-web: for the web app')
backup=('etc/bitwarden_rs.env')
install=$pkgname.install
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname.service"
        "$pkgname.sysusers.conf"
        "$pkgname.tmpfiles")
b2sums=('a44f70525c02c12b0998e870f02663c8a1b9a9b899770a5b769fd77ad59820d171e40028ccd1183fb7cec5ce68a71ceee4029a8b491098c4103bf7e02f4eb1a4'
        '22bc90b8c49b6a26610ec840f04481fe3c7fcffc82434df3ee6adba4555787ba44c7b58dfcb181a28d4a6c943db6e7f7f439b67995c5aa06c23035002765de09'
        'c44af94e19724ba23a11cec3ccc46ff9db307a058564d539dc533308e75ff43cfb5e42515bd49fdeb86e02cbc7575dc87c3b132d9d28d49f7e8fedab598c06f5'
        'a2a6a128a405b4dbd06eb84c25b1971a5dcab4b918d6fec74da317b76485eda6b4b16ad972a85d9c8267b0a848787761fae75cd6bbb81d970a8cbc8683a2fc42')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's,# DATA_FOLDER=data,DATA_FOLDER=/var/lib/bitwarden_rs,
  s,web-vault/,/usr/share/bitwarden_rs-web,
  s,# WEB_VAULT_ENABLED=true,WEB_VAULT_ENABLED=false,
  s,/path/to/log,/var/log/bitwarden_rs.log,
  /^# ROCKET_TLS/a ROCKET_LIMITS={json=10485760}' .env.template
}

build() {
  cd $pkgname-$pkgver
  rustup set profile minimal
  BWRS_VERSION="$pkgver" cargo build --release --locked --features sqlite,mysql,postgresql
}

check() {
  cd $pkgname-$pkgver
  cargo test --release --locked --features sqlite,mysql,postgresql
}

package() {
  cd $pkgname-$pkgver
  install -Dm644 .env.template "$pkgdir"/etc/bitwarden_rs.env
  install -Dt "$pkgdir"/usr/bin target/release/bitwarden_rs
  install -Dm644 -t "$pkgdir"/usr/lib/systemd/system ../bitwarden_rs.service
  install -Dm644 ../$pkgname.sysusers.conf "$pkgdir"/usr/lib/sysusers.d/bitwarden_rs.conf
  install -Dm644 ../bitwarden_rs.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/bitwarden_rs.conf
}

# vim:set ts=2 sw=2 et:
