# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: sqlwwx <wwx_2012@live.com>
# Contributors: Eduardo San Martin Morote http://posva.net/

pkgname=catimg
pkgver=2.7.0
pkgrel=1
pkgdesc="Print images in a terminal with 256 colors support"
arch=('x86_64')
url="https://github.com/posva/catimg"
license=('MIT')
depends=('glibc')
makedepends=('cmake' 'ninja')
source=("$pkgname-$pkgver.tar.gz::https://github.com/posva/catimg/archive/$pkgver.tar.gz")
sha512sums=('85b19183a2dc666aeb5655d6b66a40d325160d6ac8b5d38c00bad14ee79d69f2277ba0378af6ea84570a0d41c10d52950d658c0872dc1007e7ae17da7b6abdec')

build() {
	cd $pkgname-$pkgver
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release .
	ninja
}

package() {
	cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
