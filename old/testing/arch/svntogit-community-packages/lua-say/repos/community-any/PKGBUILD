# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=('lua-say' 'lua51-say' 'lua52-say' 'lua53-say')
pkgver=1.3
pkgrel=7
pkgdesc='Lua string hashing library, useful for internationalization'
arch=('any')
url='https://github.com/Olivine-Labs/say'
license=('MIT')
makedepends=('luarocks'
             # https://github.com/luarocks/luarocks/issues/1275
             'lua51'
             'lua52'
             'lua53'
             'lua')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Olivine-Labs/say/archive/v$pkgver-1.tar.gz")
sha256sums=('23e8cd378bb4ab1693279100a785acb2246418e3570b7de7d995b5847b3507ca')

build() {
    cd "say-$pkgver-1"

    for v in 5.1 5.2 5.3 5.4; do
        mkdir -p "$v/"
        luarocks make --pack-binary-rock --lua-version="$v" --deps-mode=none say-"$pkgver"-1.rockspec
        mv say-"$pkgver"-1.*.rock "$v/"
    done
}

package_lua51-say() {
    pkgdesc="$pkgdesc for Lua 5.1"

    cd "say-$pkgver-1"
    luarocks install --lua-version=5.1 --tree="$pkgdir/usr/" --deps-mode=none --no-manifest 5.1/*.rock
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua52-say() {
    pkgdesc="$pkgdesc for Lua 5.2"

    cd "say-$pkgver-1"
    luarocks install --lua-version=5.2 --tree="$pkgdir/usr/" --deps-mode=none --no-manifest 5.2/*.rock
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua53-say() {
    pkgdesc="$pkgdesc for Lua 5.3"

    cd "say-$pkgver-1"
    luarocks install --lua-version=5.3 --tree="$pkgdir/usr/" --deps-mode=none --no-manifest 5.3/*.rock
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua-say() {
    pkgdesc="$pkgdesc for Lua 5.4"

    cd "say-$pkgver-1"
    luarocks install --lua-version=5.4 --tree="$pkgdir/usr/" --deps-mode=none --no-manifest 5.4/*.rock
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
