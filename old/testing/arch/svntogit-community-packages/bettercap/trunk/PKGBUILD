# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>

pkgname=bettercap
pkgver=2.29
pkgrel=1
pkgdesc='Swiss army knife for network attacks and monitoring'
url='https://github.com/bettercap/bettercap'
arch=('x86_64')
license=('GPL3')
depends=('glibc' 'libpcap' 'libpcap.so' 'libnetfilter_queue' 'iproute2' 'libnfnetlink' 'libusb' 'libusb-1.0.so')
makedepends=('go' 'git')
optdepends=('bettercap-caplets: scripts (caplets) and proxy modules')
options=('!emptydirs')
source=(https://github.com/bettercap/bettercap/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('1a269b31d5aae5eb215efc5f0c531d2c7cec775fc43b1355fc57ed377c94b7b05e9f1cd3f68c2a2946a2324a78c371d59d8864ee703a56a8194188bf7a332513')
b2sums=('23af73462c2817d287262d3588a8cab7d7a138675d70a4ededad5b7fb308a17d293fc8a937ffbb4ebbcbad71249e2e7eab708af3a252025105d45b780bdc196f')

prepare() {
  cd ${pkgname}-${pkgver}
  find . -type f -exec sed 's|/usr/local|/usr|g' -i {} +
}

build() {
  cd ${pkgname}-${pkgver}
  export GO11MODULE=on
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath"
  go build
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 bettercap -t "${pkgdir}/usr/bin"
  install -Dm 644 bettercap.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
