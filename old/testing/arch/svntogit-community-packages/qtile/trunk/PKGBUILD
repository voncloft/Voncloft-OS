# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qtile
pkgver=0.16.1
pkgrel=2
pkgdesc="A full-featured, pure-Python tiling window manager"
arch=('x86_64')
url="http://www.qtile.org"
license=('MIT')
depends=('gdk-pixbuf2' 'pango' 'python-cffi' 'python-xcffib' 'python-cairocffi'
'python-dbus' 'python-gobject' 'python-setuptools')
makedepends=('jupyter_console' 'libpulse' 'python-ipykernel' 'python-iwlib'
'python-keyring' 'python-psutil' 'python-pytest' 'python-setuptools-scm' 'python-xdg')
optdepends=('alsa-utils: volume widget'
            'canto-daemon: canto widget'
            'cmus: cmus widget'
            'khal: khal_calendar widget'
            'libpulse: for pulse_volume and pulseaudio_ffi widget'
            'lm_sensors: sensors widget'
            'moc: moc widget'
            'python-iwlib: wlan widget'
            'python-keyring: imapwidget widget'
            'python-mpd2: mpd2widget widget'
            'python-psutil: graph, net and memory widget'
            'python-setproctitle: change process name to qtile'
            'python-xdg: launchbar widget')
checkdepends=('imagemagick' 'python-pytest' 'python-xvfbwrapper' 'xorg-xcalc'
'xorg-xclock' 'xorg-xeyes' 'xorg-server-xephyr' 'xorg-xrandr' 'xterm')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-0.16.1-xcffib_version_detection.patch::https://github.com/qtile/qtile/pull/1963/commits/7e37f56327dbee7343b5d5a31902a91468674cb0.patch")
sha512sums=('aea42f7f6799a88b586167a57f5b5ab6f1c5bb663dec352d15eb47ce0d37fb23c31d1631cab8178c71be5ae714ec12c35a350377383dba1749a98cd45c22515b'
            'f5745d53e6bfb10f6b4209431679e515a76a212e45ab5b30aa3c28200e1f9896a696d5d9ca6037a46fa2cb430a46d788bfa69e49aa238ad2e76685b071735748')
b2sums=('1f75f5dbcab6180dcf778e28108e70b6dd01105fa275aadf4a53d96ab84f3b759cb8553bcf3de2011b0990d61adc9269663afa32b83d1aa9c052b00ef9e89358'
        'af1fc26a550b3a293161e765c8ef1a77dbe92105b6b06f15a2c3da5eefa0f895214ee91bd7120e0ce1ed44a050d05b70c7b053363f2fde4a42542e7675c7c599')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix broken xcffib version detection:
  # https://github.com/qtile/qtile/issues/1962
  patch -Np1 -i "../${pkgname}-0.16.1-xcffib_version_detection.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTTHONPATH="build/lib/:${PYTHONPATH}"
  export LC_TYPE=en_US.UTF-8
  echo "building pango"
  python3 ./libqtile/pango_ffi_build.py
  echo "building xcursors"
  python3 ./libqtile/backend/x11/xcursors_ffi_build.py
  echo "building pulseaudio volume control"
  python3 ./libqtile/widget/pulseaudio_ffi.py
  # some tests are failing:
  # https://github.com/qtile/qtile/issues/1352
  pytest -v -k "not test_images_good \
                and not test_qtile_cmd \
                and not test_svg_scaling \
                and not test_pattern_rotate \
                and not test_pattern \
                and not test_from_path \
                and not test_init \
                and not test_get_cairo_surface"
}

package() {
  cd "${pkgname}-${pkgver}"
  # install
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}" \
    --prefix=/usr
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # config
  install -vDm 644 libqtile/resources/default_config.py \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # desktop file
  install -vDm 644 "resources/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/xsessions/"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {CHANGELOG,README.rst}
}
