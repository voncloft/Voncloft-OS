# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>

pkgname=cadaver
pkgver=0.23.3
pkgrel=12
pkgdesc="Command-line WebDAV client for Unix"
arch=('x86_64')
#url="http://www.webdav.org/cadaver"
url="https://packages.debian.org/jessie/cadaver"
license=('GPL')
depends=('neon')
validpgpkeys=('6D0C74985535060B23300BFE47F6969D0E44CF0C')
source=(http://http.debian.net/debian/pool/main/c/cadaver/cadaver_$pkgver.orig.tar.gz
        disable-sslv2.patch)
sha256sums=('fd4ce68a3230ba459a92bcb747fc6afa91e46d803c1d5ffe964b661793c13fca'
            'b86f04a16dfd2dc1a925a702301edd72147496ea03d8b583023cf90e162354e6')

prepare() {
  cd "$srcdir"/cadaver-$pkgver
  patch -Np1 -i ../disable-sslv2.patch
}

build() {
  cd "$srcdir"/cadaver-$pkgver
  ./configure --prefix=/usr --with-ssl
  make
}

package() {
  cd "$srcdir"/cadaver-$pkgver
  make prefix="$pkgdir"/usr install
}
