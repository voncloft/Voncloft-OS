# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=drumstick
pkgver=2.0.0
pkgrel=1
pkgdesc="MIDI libraries for Qt5/C++"
arch=('x86_64')
url="https://drumstick.sourceforge.io/"
license=('GPL2')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('alsa-lib' 'cmake' 'doxygen' 'docbook-xsl' 'fluidsynth' 'graphviz'
'libpulse' 'qt5-tools')
provides=('libdrumstick-alsa.so' 'libdrumstick-file.so' 'libdrumstick-rt.so'
'libdrumstick-widgets.so')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('09adaf7901ba57419222d3560e9212633ba5bfad37ebd113c8c56ddc83d5e55a0beccaa7c45b7eef3858f624cced15c516245ee970786813a1defcb20b247636')
b2sums=('c1a3f1290b8afd973f8ef2ab35515c41cb2cf5b618d67da652ab33c9015778870d8b1f1ca724b2c0d2f1dc5d743ba7b64b388b58e6f8d434a20da7ef00b33845')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

# # tests fail and seem to provide no further logs:
# # https://sourceforge.net/p/drumstick/bugs/24/
# check() {
#   cd "${pkgname}-${pkgver}"
#   make test -C build
# }

package() {
  depends+=('libasound.so' 'libfluidsynth.so' 'libpulse.so' 'libpulse-simple.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install -C build
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,ChangeLog,NEWS,README,TODO}
}
