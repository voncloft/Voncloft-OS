# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Max Liebkies <mail@maxliebkies.de>
# Contributor: Krzysztof Bogacki <krzysztof.bogacki@leancode.pl>

pkgbase=dotnet-core-2.2
pkgname=(
 dotnet-runtime-2.2
 dotnet-sdk-2.2
 aspnet-runtime-2.2
)
pkgver=2.2.7.sdk109
_runtimever=2.2.7
_sdkver=2.2.109
pkgrel=1
arch=(x86_64)
url=https://www.microsoft.com/net/core
license=(MIT)
makedepends=(
  clang
  cmake
  curl
  git
  icu
  inetutils
  krb5
  libunwind
  lldb
  llvm
  lttng-ust
  openssl-1.0
  zlib
)
options=(staticlibs)
source=(
  dotnet-source-build::git+https://github.com/dotnet/source-build.git#tag=2ea940c21cfb258ad514660d856b558655ea1f92
  dotnet-application-insights::git+https://github.com/Microsoft/ApplicationInsights-dotnet.git
  dotnet-arcade::git+https://github.com/dotnet/arcade.git
  dotnet-aspnet-razor::git+https://github.com/aspnet/Razor.git
  dotnet-cecil::git+https://github.com/mono/cecil.git
  dotnet-cli::git+https://github.com/dotnet/cli.git
  dotnet-cli-migrate::git+https://github.com/dotnet/cli-migrate.git
  dotnet-clicommandlineparser::git+https://github.com/dotnet/clicommandlineparser.git
  dotnet-common::git+https://github.com/aspnet/common.git
  dotnet-core-setup::git+https://github.com/dotnet/core-setup.git
  dotnet-coreclr::git+https://github.com/dotnet/coreclr.git
  dotnet-corefx::git+https://github.com/dotnet/corefx.git
  dotnet-fsharp::git+https://github.com/Microsoft/VisualFSharp.git
  dotnet-linker::git+https://github.com/mono/linker.git
  dotnet-msbuild::git+https://github.com/Microsoft/msbuild.git
  dotnet-newtonsoft-json::git+https://github.com/JamesNK/Newtonsoft.Json.git
  dotnet-nuget-client::git+https://github.com/NuGet/NuGet.Client.git
  dotnet-roslyn::git+https://github.com/dotnet/roslyn.git
  dotnet-roslyn-tools::git+https://github.com/dotnet/roslyn-tools.git
  dotnet-sdk::git+https://github.com/dotnet/sdk.git
  dotnet-standard::git+https://github.com/dotnet/standard.git
  dotnet-templating::git+https://github.com/dotnet/templating.git
  dotnet-vstest::git+https://github.com/Microsoft/vstest.git
  dotnet-websdk::git+https://github.com/aspnet/websdk.git
  dotnet-xliff-tasks::git+https://github.com/dotnet/xliff-tasks.git
  https://download.visualstudio.microsoft.com/download/pr/f0b6c052-2f5d-42b9-8ffa-870ea2a60d11/90c1d5b4a2548c1beaeacff0a39a459c/aspnetcore-runtime-2.2.7-linux-x64.tar.gz
  dotnet-coreclr-rid.patch
  dotnet-coreclr-asm.patch
  dotnet-coreclr-sysinfo.patch
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'e9872e53b834e2c3194710cc2b242bb17a00f1a8b1f42df6e4b87d2ea1ed935e'
            '192af878c513f7d27f670104aaef309073f65287809361739038d770b16415d3'
            'ecbd476481deef395d88496a5b6adfeb37660c45052523f31df6805ec85bdb6c'
            'b0c63c807cd58c170f55ba0621eb11761e6f6aa6a8530d8b0468c46f51e17698')

prepare() {
  cd dotnet-source-build

  for submodule in src/{application-insights,arcade,aspnet-razor,cli,cli-migrate,clicommandlineparser,common,core-setup,coreclr,corefx,fsharp,linker,msbuild,newtonsoft-json,nuget-client,roslyn,roslyn-tools,sdk,standard,templating,vstest,websdk,xliff-tasks}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../dotnet-${submodule#src/}
    git submodule update
  done

  cd src/linker

  for submodule in cecil; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../../../dotnet-${submodule}
    git submodule update
  done

  cd ../coreclr

  patch -Np1 -i "${srcdir}"/dotnet-coreclr-rid.patch
  patch -Np1 -i "${srcdir}"/dotnet-coreclr-asm.patch
  patch -Np1 -i "${srcdir}"/dotnet-coreclr-sysinfo.patch
}

build() {
  cd dotnet-source-build

  export CXXFLAGS="$CXXFLAGS -Wno-atomic-implicit-seq-cst"
  export PKG_CONFIG_PATH=/usr/lib/openssl-1.0/pkgconfig
  export SOURCE_BUILD_SKIP_SUBMODULE_CHECK=1

  ./build.sh
}

package_dotnet-runtime-2.2() {
  pkgdesc='The .NET Core runtime'
  depends=(
    dotnet-host
    glibc
    icu
    krb5
    libcurl.so
    libunwind
    openssl-1.0
    zlib
  )

  optdepends=('lttng-ust: CoreCLR tracing')

  cd dotnet-source-build/bin/x64/Release

  install -dm 755 "${pkgdir}"/usr/share/{dotnet,licenses}
  bsdtar -xf dotnet-sdk-${_sdkver}-linux-x64.tar.gz -C "${pkgdir}"/usr/share/dotnet/ --no-same-owner shared/Microsoft.NETCore.App
  ln -s dotnet-host "${pkgdir}"/usr/share/licenses/dotnet-runtime-2.2
}

package_dotnet-sdk-2.2() {
  pkgdesc='The .NET Core SDK'
  depends=(
    dotnet-runtime-2.2
    glibc
  )

  cd dotnet-source-build/bin/x64/Release

  install -dm 755 "${pkgdir}"/usr/share/{dotnet,licenses}
  bsdtar -xf dotnet-sdk-${_sdkver}-linux-x64.tar.gz -C "${pkgdir}"/usr/share/dotnet/ --no-same-owner sdk
  ln -s dotnet-host "${pkgdir}"/usr/share/licenses/dotnet-sdk-2.2
}

package_aspnet-runtime-2.2() {
  pkgdesc='The ASP.NET Core runtime'
  depends=(dotnet-runtime-2.2)

  install -dm 755 "${pkgdir}"/usr/share/{dotnet/shared,licenses}
  cp -dr --no-preserve='ownership' shared/Microsoft.AspNetCore.{All,App} "${pkgdir}"/usr/share/dotnet/shared/
  ln -s dotnet-host "${pkgdir}"/usr/share/licenses/aspnet-runtime-2.2
}

# vim: ts=2 sw=2 et:
