# Maintainer: David Runge <dvzrv@archlinux.org>

_name=ansible-vim
pkgname=vim-ansible
pkgver=3.0
pkgrel=1
pkgdesc="A vim plugin for syntax highlighting Ansible's common filetypes "
arch=('any')
url="https://github.com/pearofducks/ansible-vim"
license=('MIT')
groups=('vim-plugins')
depends=('python-jinja' 'vim')
makedepends=('ansible' 'python')
optdepends=('vim-ultisnips: for ansible snippets')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pearofducks/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('77890d945926b7042259dcdd031ed83447512e234e621622afa377ed0d6b183cc936a63d48e406e69a6cfa7e2ce0d3f6e2b0d68c6b5858d76c67861c65df0573')
b2sums=('9a294ff99cb3638d69178c5fcc155692ed56179cb576e73688ad2c9b9bdabc04dd603d531331c83d674d3c3371f984ab8dea9718766bcb4df2c6332a13c8b468')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # replace vendored jinja2.vim with python-jinja's jinja.vim
  # https://github.com/pearofducks/ansible-vim/issues/95
  sed -e 's/jinja2/jinja/g' \
      -i ftdetect/ansible.vim syntax/ansible.vim
}

build() {
  cd "$pkgname-$pkgver"
  # generating a reproducible UltiSnips snippet file:
  # https://github.com/pearofducks/ansible-vim/pull/105
  python UltiSnips/generate.py --style=dictionary
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644 ftdetect/*.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/ftdetect/"
  install -vDm 644 ftplugin/*.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/ftplugin/"
  install -vDm 644 indent/*.vim -t "${pkgdir}/usr/share/vim/vimfiles/indent/"
  install -vDm 644 syntax/ansible*.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/syntax/"
  install -vDm 644 ansible.snippets \
    -t "${pkgdir}/usr/share/vim/vimfiles/UltiSnips/"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
