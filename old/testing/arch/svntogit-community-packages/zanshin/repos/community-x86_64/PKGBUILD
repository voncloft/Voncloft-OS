# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=zanshin
pkgver=0.5.71
pkgrel=3
pkgdesc="To-do management application based on Akonadi"
arch=(x86_64)
url="https://zanshin.kde.org"
license=(GPL)
depends=(kdepim-runtime kontactinterface)
makedepends=(extra-cmake-modules kdoctools boost)
source=("https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig}
         zanshin-kontactinterface-20.08.patch::"https://invent.kde.org/pim/zanshin/-/commit/4850c089.patch")
sha256sums=('b0933d2d9a63bba372966da0ba9e313b85ab2d6aa513a43657da90c65a33612c'
            'SKIP'
            '089577115e1711bb3302a373c9c4c488aea8ed445c9f9a5439f6ddbd2e4a0956')
validpgpkeys=(17166A5275C5FA6F1A48FD78074BBBCB8DECC9E2  # Kevin Ottens <ervin+bluesystems@kde.org>
              2D1D5B0588357787DE9EE225EC94D18F7F05997E) # Jonathan Riddell <jr@jriddell.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../zanshin-kontactinterface-20.08.patch # Fix build with kontactinterface 20.08
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
