# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-code
pkgver=3.4.1
pkgrel=2
pkgdesc='The Pantheon Text Editor'
arch=(x86_64)
url=https://github.com/elementary/code
license=(GPL3)
groups=(pantheon)
depends=(
  editorconfig-core-c
  glib2
  gtk3
  gtksourceview3
  libgee
  libgit2-glib
  libgranite.so
  libpeas
  pango
)
makedepends=(
  cmake
  git
  gobject-introspection
  gtkspell3
  intltool
  libsoup
  meson
  vala
  vte3
  webkit2gtk
)
optdepends=(
  'gtkspell3: Spell Check plugin'
  'libsoup: Pastebin plugin'
  'vte3: Terminal plugin'
  'webkit2gtk: Browser Preview plugin'
)
_tag=5efce792e7020c436d5127345001d85b1575de5a
source=(pantheon-code::git+https://github.com/elementary/code.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pantheon-code

  git describe --tags
}

prepare() {
  # won't build with vala 0.50.1
  sed "/subdir('outline')/d" -i pantheon-code/plugins/meson.build
}

build() {
  arch-meson pantheon-code build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
