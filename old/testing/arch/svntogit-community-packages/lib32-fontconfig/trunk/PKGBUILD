# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-fontconfig
pkgver=2.13.91+48+gfcb0420
pkgrel=2
epoch=2
pkgdesc="Library for configuring and customizing font access (32-bit)"
url="https://www.freedesktop.org/wiki/Software/fontconfig/"
arch=(x86_64)
license=(custom)
makedepends=(git autoconf-archive gperf python-lxml python-six lib32-json-c
             lib32-expat lib32-freetype2 fontconfig)
checkdepends=(unzip)
_commit=fcb042028126d79ea5a5fa015b2b034b98656e73  # master
source=("git+https://gitlab.freedesktop.org/fontconfig/fontconfig.git#commit=$_commit"
        fontconfig-32.hook)
sha256sums=('SKIP'
            '6a0c171c0bce4ffd70fc5c8e275113d2866148591c952f35c69587e8b423c6a4')

# a nice page to test font matching:
# http://zipcon.net/~swhite/docs/computers/browsers/fonttest.html
# http://getemoji.com/

pkgver() {
  cd fontconfig
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd fontconfig
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd fontconfig

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  ./configure --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --localstatedir=/var \
    --disable-static \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/local/share/fonts
  make
}

check() {
  cd fontconfig
  make -k check
}

package() {
  depends=(fontconfig lib32-expat libfreetype.so)
  provides=(libfontconfig.so)
  install=fontconfig-32.install

  cd fontconfig
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/{etc,usr/{include,share}}
  mv "$pkgdir"/usr/bin/fc-cache{,-32}
  find "$pkgdir/usr/bin" -type f -not -name '*-32' -delete

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 ../*.hook
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}

# vim:set sw=2 et:
