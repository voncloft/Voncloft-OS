# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=hcxkeys
pkgver=6.0.0
pkgrel=1
pkgdesc='Set of tools to generate plainmasterkeys (rainbowtables) and hashes for hashcat and John the Ripper'
url='https://github.com/ZerBea/hcxkeys'
arch=('x86_64')
license=('MIT')
depends=('openssl' 'opencl-driver' 'opencl-icd-loader')
makedepends=('opencl-headers')
source=(https://github.com/ZerBea/hcxkeys/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/ZerBea/hcxkeys/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha512sums=('e3efd9ff6ea7fa7bfbb1fb4a1784938c6de96f25ce4a229be500960f8d9848dc9429a9274a6e13ddbc3f6085feaa85cad6598929cc66d652f7717695324defca'
            'SKIP')
b2sums=('2280da77f506133fe884c990450e237724ffa049a88c0a52b8f85cb60bb8386a89246e9ed44058293a87d824125a12da6652e827b8699411e7bfba7a9b5697ee'
        'SKIP')
validpgpkeys=('5920CE1C567948AFD2C0A9B7375516A45DB88630') # ZeroBeat <ZeroBeat@gmx.de>

build() {
  cd ${pkgname}-${pkgver}
  make PRODUCTION=1
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr PRODUCTION=1 install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
