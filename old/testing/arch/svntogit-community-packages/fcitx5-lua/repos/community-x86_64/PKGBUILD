# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fcitx5-lua
pkgver=5.0.2
pkgrel=1
pkgdesc="Lua support for Fcitx 5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-lua"
license=('GPL')
depends=('fcitx5' 'lua')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('893a4899d99d81c7c8d48cb7a3a016f3dfd3878cf4253062014eec428f21948add76ade6494b6fc4f8390c8acb056fa9fe0fe12a0863aef4701090b60c17ed77'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build() {
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

check() {
  cd $pkgname-$pkgver
  ninja test
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
