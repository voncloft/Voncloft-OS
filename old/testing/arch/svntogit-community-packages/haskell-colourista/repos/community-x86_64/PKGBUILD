# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: berberman <hatsue@typed.icu>

_hkgname=colourista
pkgname=haskell-colourista
pkgver=0.1.0.0
pkgrel=54
pkgdesc="Convenient interface for printing colourful messages based on the ansi-terminal library."
url="https://github.com/kowainik/colourista"
license=('MPL2')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('bfc95d2b09499c467f0af807ee2418efb572cab322a35c6021386f340dddf4c5')

prepare(){
  cd $_hkgname-$pkgver
  echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
  sed -i 's/\^>= 0.10/>= 0.10/' $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver    
    
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
