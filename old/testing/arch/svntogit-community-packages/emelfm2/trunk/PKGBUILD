# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: contrasutra

pkgname=emelfm2
pkgver=0.9.1
pkgrel=3
pkgdesc="A file manager that implements the popular two-pane design"
arch=('x86_64')
url="http://freshmeat.sourceforge.net/projects/emelfm2"
depends=('gtk2')
license=('GPL3')
source=("https://ftp.mirrorservice.org/sites/download.salixos.org/x86_64/extra-14.2/source/system/emelfm2/emelfm2-$pkgver.tar.bz2")
sha1sums=('62eb6299d0a33281feae2e6ec4ddab2194c36963')
# https://github.com/tom2tom/emelfm2

# WITH_SYSTEM_ICONS=1 added 0.9.1 ?

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  make PREFIX=/usr
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make PREFIX="${pkgdir}/usr" install install_i18n

  install -m 755 "${pkgdir}/usr/share/pixmaps/emelfm2/emelfm2_48.png" \
      "${pkgdir}/usr/share/pixmaps/emelfm2.png"
  sed -e '/Icon=/ s^emelfm2/emelfm2_48.png^emelfm2.png^' \
      -e '/Categories=/ s^;$^;Application;Utility;^' \
      -i "${pkgdir}/usr/share/applications/emelfm2.desktop"
}
