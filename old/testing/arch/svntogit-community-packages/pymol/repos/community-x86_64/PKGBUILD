# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: graysky
# Contributor: Tomasz Å»ok <tomasz dot zok at gmail dot com>

pkgname=pymol
pkgver=2.4.0
pkgrel=2
pkgdesc="Molecular visualization system on an Open Source foundation"
arch=('x86_64')
url="https://pymol.org/"
license=('custom')
depends=('freetype2' 'glew' 'glm' 'glut' 'netcdf' 'netcdf-cxx' 'python-numpy' 'tcsh')
makedepends=('desktop-file-utils' 'gendesk' 'libmmtf' 'msgpack-c')
optdepends=('python-pyqt5: Qt5 based UI')
source=("$pkgname-$pkgver.tar.gz::https://github.com/schrodinger/pymol-open-source/archive/v${pkgver}.tar.gz"
        ${pkgname}.png::"https://c.fsdn.com/allura/p/pymol/icon"
        "apbstools_tcltk8.6.patch")
sha512sums=('2b19294197d58a808bf7bb7fa9f829c2ec582ba47a4380783a2b1eedda2ce4118cd398e01885db8c449e7fd5e4c08a1c40508e679ef42d8e183a6a52b031c02a'
            '39917c1dc80cd6a59e8f6b3ea49a59491eaf23d14e32ac264f6d71f33007f686deb6a66e50e7f20ef9df5337038e3572b018633c361460d5ee0c1d8f0a8259d1'
            '90fce6d3c4364cd775b44bdd9013a08f26392339bec40ecd749220b3d11391381f5b159734bdb2e2ac675fbc016a6a1b9c4cec2fa8c9c0226609a5f09b610b9c')
changelog=ChangeLog

prepare() {
  # create desktop file
  gendesk -f -n --pkgname "${pkgname}" --pkgdesc "${pkgdesc}" \
          --name="PyMOL Molecular Graphics System" \
          --categories="Science;Chemistry"

  # suppress non-zero exit code that breaks makepkg
  sed -i '/sys.exit/ s,2,0,' "${srcdir}/${pkgname}-open-source-${pkgver}/setup.py"
  # fix FS#39526
  cd "${srcdir}/${pkgname}-open-source-${pkgver}"
  patch -p0 -i "${srcdir}/apbstools_tcltk8.6.patch"
}

build() {
  cd "${srcdir}/${pkgname}-open-source-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-open-source-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${srcdir}/pymol.desktop" "${pkgdir}/usr/share/applications/pymol.desktop"
  install -Dm644 "${srcdir}/pymol.png" "${pkgdir}/usr/share/pixmaps/pymol.png"
}

# vim:set ts=2 sw=2 et:
