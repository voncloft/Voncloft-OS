# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Controbutor: Baptiste Jonglez <archlinux at bitsofnetworks.org>
# Contributor: nl6720 <nl6720@gmail.com>
# Contributor: Andreas B. Wagner <AndreasBWagner@pointfree.net>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>

pkgname=yodl
pkgver=4.02.02
pkgrel=2
pkgdesc='Implements a pre-document language and tools to process it.'
arch=('x86_64')
url='https://fbb-git.gitlab.io/yodl/'
license=('GPL3')
depends=('bash')
makedepends=('icmake')
source=("https://gitlab.com/fbb-git/${pkgname}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('531b5b61f85ed556163b0cf76a30451212849e5e03e11819cc466603db0856737a9d3bc34ba11419befd7925ec1bac33f92b707c38acd2fa8047e3e1223d0a78')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}"
        CXXFLAGS+=' --std=c++2a'
        export CXXFLAGS
	./build programs
	./build macros
	./build man
	./build html
}
package() {
	cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}"
	./build install programs "${pkgdir}"
	./build install macros "${pkgdir}"
	./build install man "${pkgdir}"
	./build install manual "${pkgdir}"
}
