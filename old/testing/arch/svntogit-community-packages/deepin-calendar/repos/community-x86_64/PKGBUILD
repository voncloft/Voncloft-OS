# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-calendar
pkgver=5.7.0.23
pkgrel=1
pkgdesc='Calendar for Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-calendar"
license=('GPL3')
depends=('deepin-qt5integration' 'hicolor-icon-theme')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-calendar/archive/$pkgver.tar.gz"
        "dde-calendar-icon.patch")
sha512sums=('1ee3ea13d358c2bc6392f4cd8b50d68d1422899e02c30967675a7a7264163fc8e5892822850f97dcdbc6dba2445452324f69b6247680d0e0c4f4dadf3fb0a558'
            '40ffae29fcbd386512dac9b91c39219b420b349f1f5facc254467b9e5407091886c8d0ed0979d566ba79539c093464cbc7719da7268c9745bc30273f9d7db7e2')

prepare() {
  cd dde-calendar-$pkgver

  # Install the application icon
  # https://github.com/linuxdeepin/dde-calendar/pull/31
  patch -Np1 -i ../dde-calendar-icon.patch

  sed -i '/<QQueue>/a #include <QMouseEvent>' src/widget/dayWidget/daymonthview.cpp
  sed -i '/<QStylePainter>/a #include <QMouseEvent>' src/widget/schcedulesearchview.cpp
  sed -i '/include <QJsonObject>/a #include <QMouseEvent>' src/view/draginfographicsview.cpp
  sed -i '/include <QPainter>/a #include <QMouseEvent>' schedule-plugin/src/widget/itemwidget.h schedule-plugin/src/widget/modifyscheduleitem.h

  # Not included in https://github.com/linuxdeepin/dde-calendar/pull/30 yet
  sed -i '/include <QPainter>/a #include <QPainterPath>' src/widget/schcedulesearchview.cpp src/widget/dayWidget/daymonthview.cpp src/widget/weekWidget/weekheadview.cpp \
                                                         src/customWidget/customframe.cpp src/widget/yearWidget/yearview.cpp schedule-plugin/src/widget/modifyscheduleitem.h \
                                                         schedule-plugin/src/widget/itemwidget.h
  sed -i '/include <QMessageBox>/a #include <QWheelEvent>' src/widget/yearWidget/yearwindow.cpp
}

build() {
  cd dde-calendar-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd dde-calendar-$pkgver
  DESTDIR="$pkgdir" ninja install
}
