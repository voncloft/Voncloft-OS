# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Zamprogno <stefano dot zamprogno at gmail dot com>
# Contributor: Chuck Yang <Chuck.Yang@gmail.com>

pkgname=lshw
pkgver=B.02.19.2
pkgrel=1
pkgdesc="A small tool to provide detailed information on the hardware configuration of the machine."
url="https://ezix.org/project/wiki/HardwareLiSter"
license=('GPL')
arch=('x86_64')
depends=('gcc-libs' 'hwids')
optdepends=('gtk2')
makedepends=('gcc' 'gtk2' 'sqlite')
source=(https://ezix.org/software/files/lshw-$pkgver.tar.gz)
sha256sums=('9bb347ac87142339a366a1759ac845e3dbb337ec000aa1b99b50ac6758a80f80')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  sed -i 's|/usr/bin/gtk-lshw|/usr/sbin/gtk-lshw|' src/gui/integration/gtk-lshw.desktop
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  make SBINDIR=/usr/bin
  make SBINDIR=/usr/bin gui
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir"/ SBINDIR=/usr/bin install
  make DESTDIR="$pkgdir"/ SBINDIR=/usr/bin install-gui
  install -Dm0644 src/gui/integration/gtk-lshw.desktop "$pkgdir"/usr/share/applications/gtk-lshw.desktop
  install -Dm0644 src/gui/integration/gtk-lshw.pam "$pkgdir"/usr/share/doc/$pkgname/gtk-lshw.pam
  install -Dm0644 src/gui/integration/console.apps "$pkgdir"/usr/share/doc/$pkgname/console.apps
  rm -f "$pkgdir"/usr/share/lshw/{pci,usb}.ids
}
