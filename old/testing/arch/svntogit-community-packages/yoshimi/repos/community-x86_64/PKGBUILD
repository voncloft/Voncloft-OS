# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=1.7.4
pkgrel=2
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=('x86_64')
url="https://yoshimi.github.io/"
license=('GPL2')
groups=('pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'zlib')
makedepends=('alsa-lib' 'cmake' 'fftw' 'fltk' 'jack' 'lv2' 'mxml' 'ncurses'
'readline')
checkdepends=('kxstudio-lv2-extensions' 'lv2lint')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'readline: for standalone application'
            'ncurses: for standalone application')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-1.7.4-symbol_visibility.patch::https://github.com/Yoshimi/yoshimi/pull/127/commits/422411854164c40c26813274e4a27eb97e5ce6b6.patch"
)
sha512sums=('d04a8460cb47f0c24de9e0456ca930c6a452c08cb06d9a13c07110cab5aff49ebde50170184b801bb3b6e8f645ed89652f365319236db7e836c1aea6697a57eb'
            'ad42245bea852986caf59cd2b2cea69d6fa92aa8bcab5626bcd4f49f880e6f87f54cbbc5ff65c6b15c56dcbe9586d4866d4be1d11793b60151ecf06189c4b2d1')
b2sums=('d27f168195dd1f084ddc765382ff8591780e2fdbba8555855d74b6e5ac112fa3c1031f6c9adc50108e44640e47d494455e17fd937c8913f87b33fee571f78df3'
        '5674898a2220c287a3d8503651a27ecc78ab8403049dc9a409b2b44f8f85e2b936958d8783fda617fe15f8a519358da1ef2e4d68c07dde6f26506e20fb93bf18')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../"${pkgname}-1.7.4-symbol_visibility.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_INSTALL_DATAROOTDIR='/usr/share' \
        -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build\
        src
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  cp -v "build/LV2_Plugin/${pkgname}_lv2.so" "src/LV2_Plugin/"
  lv2lint -Mpack -I src/LV2_Plugin/ "http://yoshimi.sourceforge.net/lv2_plugin"
  rm -v "src/LV2_Plugin/${pkgname}_lv2.so"
}

package() {
  depends+=('libasound.so' 'libfftw3f.so' 'libfltk.so' 'libfltk_images.so'
  'libjack.so' 'libmxml.so' 'libncursesw.so' 'libreadline.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" VERBOSE=1 -C build install
  install -vDm 644 {Changelog,Dependencies,README.txt,Yoshimi_Helpers} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
