# Maintainer: David Runge <dave@sleepmap.de>
pkgname=realtime-privileges
pkgver=2
pkgrel=2
pkgdesc="Realtime privileges for users"
arch=('any')
url="https://wiki.archlinux.org/index.php/Realtime_process_management"
license=('GPL')
groups=('pro-audio' 'realtime')
install="${pkgname}.install"
changelog=ChangeLog
source=("99-${pkgname}.conf"
        "sysusers-${pkgname}.conf"
        "40-${pkgname}.rules"
        'ChangeLog')
sha512sums=('4d812d89561519fe79613d03b531408122a3ae729eba631187848c3480d6235b483102e8c647e6eb2f579293f462b0dae7f4f04d56e085063e24d69f3ed96b4a'
            '2f4d048a3e3c6de75bb7432092e1943143507110ff33aa9f7f2ca4ed3f028536c8f910735f5218f0fa6cc89e8eae70699bcce18c0dadc07e518fc3cf33611518'
            '9b37478e352bd27b1e8f37996306054d71fa9a0afd589bfb6c77dc59eaced25517ecc5ecaaa7aa22cb0c9a865e2579e1e279d7cbe27986c661b4eb0c48bf1b1a'
            '11f115c77867763424074234dbe4f4e87d83e0423ed1ebf43bb279bebda8ee64f8c44589f508f72ecce4e5f4b0824188f9d30cb36a799a60f35745b534f61f06')

package() {
  # realtime group installed through sysusers.d
  install -vDm 644 "sysusers-${pkgname}.conf" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  # limits for realtime group
  install -vDm 644 "99-${pkgname}.conf" -t "${pkgdir}/etc/security/limits.d/"
  # udev rules, giving access to devices to realtime group
  install -vDm 644 "40-${pkgname}.rules" -t "${pkgdir}/usr/lib/udev/rules.d/"
  install -vDm 644 ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
