# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: lillian rose winter <hi@neko.vg>
# Contributor: Evan McCarthy <evan@mccarthy.mn>

pkgname=libretls
pkgver=3.3.1
pkgrel=2
pkgdesc="libtls for OpenSSL"
arch=('x86_64')
url="https://git.causal.agency/${pkgname}"
license=('ISC' 'BSD' 'MIT')
depends=('openssl')
source=("https://causal.agency/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('f23a29ef321eff099b8fbabdecc3a3378cfb9eca24b2bf02062554e9779c4d33')
b2sums=('b935cae7bdd2e2338099dbc830e2773198d5d4ed12df80b89291d0eb8ae68549e36413c79ba1373d8bce8fd857c4a291fbd9bce30c819cc182bd5b68ac7b9527')

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix=/usr
    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install
    sed -n '1,/*\//p' tls.c | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
