# Contributor: Martin Wimpress <code@flexion.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <barthalion@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=galculator
pkgname=(galculator galculator-gtk2)
pkgver=2.1.4
pkgrel=5
pkgdesc='GTK+ based scientific calculator'
arch=('x86_64')
url='http://galculator.sourceforge.net/'
license=('GPL')
depends=('gtk2' 'gtk3')
makedepends=('intltool')
source=("http://galculator.mnim.org/downloads/${pkgbase}-${pkgver}.tar.bz2")
sha256sums=('01cfafe6606e7ec45facb708ef85efd6c1e8bb41001a999d28212a825ef778ae')

build() {
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  # GTK3 version
  cd "${srcdir}/${pkgbase}-${pkgver}"
  [ -d gtk3 ] || mkdir gtk3
  cd gtk3
  ../configure \
    --prefix=/usr
  make

  # GTK2 version
  cd "${srcdir}/${pkgbase}-${pkgver}"
  [ -d gtk2 ] || mkdir gtk2
  cd gtk2
  ../configure \
    --prefix=/usr \
    --disable-gtk3
  make
}

package_galculator() {
  depends=('gtk3')

  cd "${pkgbase}-${pkgver}/gtk3"
  make DESTDIR="$pkgdir" install
}

package_galculator-gtk2() {
  pkgdesc+=' (GTK2 version)'
  depends=('gtk2')
  conflicts=('galculator')

  cd "${pkgbase}-${pkgver}/gtk2"
  make DESTDIR="$pkgdir" install
}
