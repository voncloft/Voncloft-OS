# Maintainer: Laurent Carlier <lordheavym@gmail.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: TheHoff <forums>

pkgname=devil
pkgver=1.8.0
pkgrel=4
pkgdesc="Library for reading several different image formats"
arch=('x86_64')
url="http://openil.sourceforge.net/"
depends=('libpng' 'jasper' 'lcms2' 'libjpeg-turbo')
makedepends=('cmake')
options=('!docs' '!emptydirs')
license=('GPL')
source=(https://downloads.sourceforge.net/openil/DevIL-$pkgver.tar.gz
        soversion.patch)
sha256sums=('0075973ee7dd89f0507873e2580ac78336452d29d34a07134b208f44e2feb709'
            'f6ef4b428fd68f0fb837af548d20ffa5ca96f3e8e4f57faca8f5a90dc48b0377')

prepare() {
  cd "${srcdir}/DevIL"

  # Add solib version number to CMakeLists.txt
  # https://github.com/DentonW/DevIL/pull/50
  patch -Np1 -i "${srcdir}/soversion.patch"
}

build() {
  cd "${srcdir}/DevIL/DevIL"

  rm -rf build && mkdir build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make  
}

package() {
  cd "${srcdir}/DevIL/DevIL/build"

  make DESTDIR="$pkgdir" install
}
