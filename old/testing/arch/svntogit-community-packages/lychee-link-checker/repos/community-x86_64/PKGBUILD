# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=lychee
pkgname=lychee-link-checker
pkgver=0.5.0
pkgrel=1
pkgdesc='Fast, async, resource-friendly link checker written in Rust'
arch=('x86_64')
url=https://github.com/lycheeverse/lychee
license=('Apache' 'MIT')
replaces=('lychee-rs')
depends=('gcc-libs' 'rust')
source=("$url/archive/v$pkgver/$_name.v$pkgver.tar.gz")
b2sums=('88e485f9f3190a9ed100ac5b2be1517ba1d53cac3cb31eb93337a77436766ed53dd22d6ae54d1668b85e5637b6141aa0ce6b57bea564b5a27bafa92714703049')

build() {
  cd $_name-$pkgver
  cargo build --release --locked
}

check() {
  cd $_name-$pkgver
  cargo test --release --locked
}

package() {
  cd $_name-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/$_name
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
