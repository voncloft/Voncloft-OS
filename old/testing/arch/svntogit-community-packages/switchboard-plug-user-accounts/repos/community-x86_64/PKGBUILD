# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-user-accounts
pkgver=2.3.2
pkgrel=1
pkgdesc='Switchboard User Accounts Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-useraccounts
license=(GPL3)
groups=(pantheon)
depends=(
  accountsservice
  cairo
  gdk-pixbuf2
  glib2
  gnome-desktop
  gtk3
  libgee
  libgranite.so
  libpwquality
  libswitchboard-2.0.so
  polkit
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
)
_tag=3f82ded6cd32dd28a661db3566f046def345ed13
source=(switchboard-plug-user-accounts::git+https://github.com/elementary/switchboard-plug-useraccounts.git#tag=${_tag})
sha256sums=('SKIP')

pkgver() {
  cd switchboard-plug-user-accounts

  git describe --tags
}

build() {
  arch-meson switchboard-plug-user-accounts build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
