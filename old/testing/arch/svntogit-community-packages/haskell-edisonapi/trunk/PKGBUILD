# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=EdisonAPI
pkgname=haskell-edisonapi
pkgver=1.3.1
pkgrel=20
pkgdesc="A library of efficient, purely-functional data structures (API)"
url="http://rwd.rdockins.name/edison/home/"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        ghc-8.8.patch)
sha512sums=('677161da64856421c834856ee2f5ef7f59880883433d5c5c4061f0ab2faa0cb39c4eb83061820b77dab852acc4cce5dc75740fe454b15dbc2e67e6e84510ce42'
            '918e459664e6dfbdc7c2a5c578319207187a9500589099aaa41f64f0d957d8d5e42e0bf7ed35b28dc8b66b26cc059d46a0dda0938054f76190b6f56ecc3f97f2')

prepare() {
    cd $_hkgname-$pkgver
    patch -p2 -i ../ghc-8.8.patch
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYRIGHT"
}
