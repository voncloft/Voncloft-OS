# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: <karolina.lindqvist@kramnet.se>

pkgname=('algol68g' 'algol68g-doc')
pkgver=2.8.5
pkgrel=4
pkgdesc='Algol 68 compiler and interpreter'
arch=('x86_64')
url='https://jmvdveer.home.xs4all.nl/algol.html'
license=('GPL')
depends=('gsl' 'plotutils')
optdepends=('postgresql-libs: for postgresql support')
source=("https://jmvdveer.home.xs4all.nl/algol68g-$pkgver.tar.gz"
        'plotutils.patch'
        'https://ftp.tw.freebsd.org/distfiles/a68g-doc.pdf')
sha256sums=('0f757c64a8342fe38ec501bde68b61d26d051dffd45742ca58b7288a99c7e2d8'
            '50afcc7812d117e5f0a2f5240c9cd58f8bfcc04b3da0634b9aa0ab06183ed0aa'
            '064b6761207d937aa704d245fc871a072f5a33ea23a346cb136ec8b2078ea321')

prepare() {
  cd "$pkgname-$pkgver"

  patch -p1 -i ../plotutils.patch
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package_algol68g() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" \
    docdir="/usr/share/doc/$pkgname" install
}

package_algol68g-doc() {
  install -Dm644 a68g-doc.pdf \
    "$pkgdir/usr/share/doc/algol68g/learning_algol.pdf"
}

# vim: ts=2 sw=2 et:
