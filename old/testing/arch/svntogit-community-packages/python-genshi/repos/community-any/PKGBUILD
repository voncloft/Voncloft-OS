# Maintainer : Eli Schwartz <eschwartz@archlinux.org>

pkgname=python-genshi
pkgver=0.7.4
pkgrel=2
pkgdesc="Python toolkit for stream-based generation of output for the web."
arch=('any')
url="https://genshi.edgewall.org/"
license=("BSD")
depends=('python')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/edgewall/genshi/archive/${pkgver}.tar.gz")
sha256sums=('864d7dc47635950809639b13ad5d604984702fca335565636284cc6af478f454')

build() {
    cd "${srcdir}"/genshi-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/genshi-${pkgver}

    python setup.py test
}

package() {
    cd "${srcdir}"/genshi-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
