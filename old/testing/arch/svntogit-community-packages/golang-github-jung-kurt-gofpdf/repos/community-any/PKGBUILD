# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-jung-kurt-gofpdf
pkgver=2.4.0
pkgrel=2
pkgdesc='A PDF document generator with high level support for text, drawing and images'
arch=('any')
url="https://github.com/jung-kurt/gofpdf"
license=('MIT')
depends=('go')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jung-kurt/gofpdf/archive/v$pkgver.tar.gz")
sha512sums=('7c780fbe8c118cdb32411861af37e46d5b8c5c36dde89422c83a14181186b9de6e62167219ad468bb7ff8e3324c2912aee08395f851d6815950d73aa9a62ef45')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/jung-kurt
  cp -a "$srcdir"/gofpdf-$pkgver "$srcdir"/build/src/github.com/jung-kurt/gofpdf
  cd "$srcdir"/build/src/github.com/jung-kurt/gofpdf
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/jung-kurt
  cp -a "$srcdir"/gofpdf-$pkgver "$pkgdir"/usr/share/gocode/src/github.com/jung-kurt/gofpdf

  install -Dm644 gofpdf-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
