# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=jupyter-server
pkgver=1.3.0
pkgrel=1
pkgdesc="Provides the backend for Jupyter web applications like Jupyter notebook, JupyterLab, and Voila"
arch=(any)
url="https://jupyter.org/"
license=(custom)
depends=(python-jupyter_client jupyter-nbformat jupyter-nbconvert python-terminado python-prometheus_client python-traitlets python-tornado
         python-jinja python-send2trash python-anyio)
makedepends=(python-setuptools)
source=(https://pypi.io/packages/source/j/${pkgname/-/_}/${pkgname/-/_}-$pkgver.tar.gz)
sha256sums=('b9d32d102df25f66ec3c1fe508c62cd0c856123452d973741da84fee4be01912')

build() {
  cd ${pkgname/-/_}-$pkgver
  python setup.py build 
}

package() {
  cd ${pkgname/-/_}-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 COPYING.md -t "$pkgdir"/usr/share/licenses/$pkgname
}
