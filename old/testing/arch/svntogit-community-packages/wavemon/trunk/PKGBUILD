# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Beej Jorgensen <beej@beej.us>

pkgname=wavemon
pkgver=0.9.3
pkgrel=1
pkgdesc='Ncurses-based monitoring application for wireless network devices'
url='https://github.com/uoaerg/wavemon'
arch=('x86_64')
license=('GPL3')
depends=('glibc' 'ncurses' 'libncursesw.so' 'libcap' 'libcap.so' 'libnl')
source=(https://github.com/uoaerg/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('0e4f6db1dfd1915a3c0e1c9708eea501d7180e80df891d0c5a59bb72d48777731c67a256d2d7d6c748ade8e1a3efad9004f5e13971e0b4e16eefdd4f8c6fe1c1')
b2sums=('383e5462e6f3abf86b3604499764060927321018f9dfaef9b742d933e832e568249e41f348788184e125324f81672f2f2b2ff9ca970c215f95a80ec140e0ca11')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e '/^CFLAGS=/d' -i configure.ac
  sed -r 's|(/share)|\1/doc|g' -i Makefile.in
  sed -r 's|\?=|=|g' -i Makefile.in
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
