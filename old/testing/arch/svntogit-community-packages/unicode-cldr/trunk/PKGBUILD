# Maintainer: Felix Yan <felixonmars@archlinux.org>

shopt -s extglob

pkgbase=unicode-cldr
pkgname=(unicode-cldr unicode-cldr-annotations)
pkgver=38.0
pkgrel=2
pkgdesc="Unicode Common Locale Data Repository"
arch=('any')
url="http://cldr.unicode.org/"
license=('custom')
source=("https://unicode.org/Public/cldr/${pkgver%.*}/cldr-common-$pkgver.zip")
sha512sums=('76a2cdfbe74bd25825dd0a91f81d3651d6baab374b7cffd2346d9194b30d2bf16940e448ede5b1c5f470ad2ff7df082a957ec6eade336b9baaafd40870648b1e')

package_unicode-cldr-annotations() {
  pkgdesc="Unicode Common Locale Data Repository (annotations only)"

  cd common
  install -dm755 "$pkgdir"/usr/share/unicode/cldr/common
  cp -r annotations "$pkgdir"/usr/share/unicode/cldr/common/
  install -Dm644 ../unicode-license.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}

package_unicode-cldr() {
  depends=('unicode-cldr-annotations')

  cd common
  install -dm755 "$pkgdir"/usr/share/unicode/cldr/common
  cp -r !(annotations) "$pkgdir"/usr/share/unicode/cldr/common/

  install -Dm644 ../unicode-license.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
