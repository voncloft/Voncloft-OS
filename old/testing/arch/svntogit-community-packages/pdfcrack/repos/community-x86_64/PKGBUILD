# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=pdfcrack
pkgver=0.19
pkgrel=1
pkgdesc='Password recovery tool for PDF-files'
url='http://pdfcrack.sourceforge.net/'
arch=('x86_64')
license=('GPL2')
depends=('glibc')
source=(https://downloads.sourceforge.net/pdfcrack/pdfcrack-${pkgver}.tar.gz{,.asc})
sha512sums=('e58819e7d90db9a66ebf9509c0b8aace7f8e7dbf5f8083edc70c3f2ca28cf16476bbdbaa77f7c563dcf824e440ca0df03e91d6aaf2a0435ec8f349f4e19ee626'
            'SKIP')
b2sums=('ddbbd813a8b76fd0b643ac5a9f63f8d82f76d2514e43dd0d4a4e4cc4feffb4b01598fc5438aef77f98b48c3df1f04139aa8db76d77eb3ed9d67a36a3b8ef57fb'
        'SKIP')
validpgpkeys=(
  '987AEFC56E70D68440C0D45149241DE2BA57F699' # Henning Norén <henning.noren@gmail.com>
  'AC49CA5B95335D6DAF85BAC563EFAA4606037EB3' # Henning Norén <henning.noren@gmail.com>
  '4A524F4C6BD20FF789116E0443001F2E87BE0FD0' # Henning Norén <henning.noren@gmail.com>
)

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 pdfcrack -t "${pkgdir}/usr/bin"
  install -Dm 644 README changelog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 pdfcrack.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
