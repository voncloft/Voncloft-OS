# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=distrho-ports
pkgver=2021.01.15
pkgrel=1
pkgdesc="Linux audio plugins and LV2 ports"
arch=('x86_64')
url="https://github.com/distrho/distrho-ports"
license=('GPL2' 'LGPL3')
groups=('lv2-plugins' 'pro-audio' 'vst-plugins' 'vst3-plugins')
depends=('gcc-libs' 'glibc' 'libglvnd' 'libx11' 'libxext')
makedepends=('alsa-lib' 'freetype2' 'git' 'libxcursor' 'lv2' 'meson')
checkdepends=('kxstudio-lv2-extensions' 'lv2lint')
optdepends=('lv2-host: for LV2 plugins'
            'vst-host: for VST plugins'
            'vst3-host: for VST3 plugins')
source=("git+https://github.com/DISTRHO/${pkgname}#tag=${pkgver//./-}?signed")
md5sums=('SKIP')
validpgpkeys=('62B11043D2F6EB6672D93103CDBAA37ABC74FBA0') # falkTX <falktx@falktx.com>

prepare() {
  cd "$pkgname"
}

build() {
  cd "$pkgname"
  meson --prefix='/usr' \
        --buildtype plain \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        -Dintrospection=enabled \
        build
  ninja -C build
}

check() {
  local _legacy_plugin _legacy_plugins _legacy_urls _plugins _urls _plugin
  _plugins=('CHOW')
  declare -A _urls=(
    ["CHOW"]="https://github.com/jatinchowdhury18/CHOW"
  )
  _legacy_plugins=('Dexed' 'EasySSP' 'HiReSam' 'JuceOPL' 'KlangFalter'
  'LUFSMeter' 'LUFSMeterMulti' 'Luftikus' 'Obxd' 'PitchedDelay' 'ReFine'
  'StereoSourceSeparation' 'TAL-Dub-3' 'TAL-Filter-2' 'TAL-Filter'
  'TAL-NoiseMaker' 'TAL-Reverb-2' 'TAL-Reverb-3' 'TAL-Reverb' 'TAL-Vocoder-2'
  'Temper' 'TheFunction' 'ThePilgrim' 'Wolpertinger' 'drowaudio-distortion'
  'drowaudio-distortionshaper' 'drowaudio-flanger' 'drowaudio-reverb'
  'drowaudio-tremolo' 'drumsynth' 'eqinox' 'vex')
  declare -A _legacy_urls=(
    ["Dexed"]="https://github.com/asb2m10/dexed"
    ["EasySSP"]="http://au.tomatl.org/essp"
    ["HiReSam"]="https://github.com/klangfreund/SpectrumAnalyser"
    ["JuceOPL"]="https://bsutherland.github.io/JuceOPLVSTi/"
    ["KlangFalter"]="https://github.com/HiFi-LoFi/KlangFalter"
    ["LUFSMeter"]="http://www.klangfreund.com/lufsmeter"
    ["LUFSMeterMulti"]="http://www.klangfreund.com/lufsmetermultichannel"
    ["Luftikus"]="https://code.google.com/p/lkjb-plugins/luftikus"
    ["Obxd"]="https://obxd.wordpress.com"
    ["PitchedDelay"]="http://code.google.com/p/lkjb-dc12/"
    ["ReFine"]="https://github.com/lkjbdsp/lkjb-plugins#ReFine"
    ["StereoSourceSeparation"]="https://github.com/laixinyuan/StereoSourceSepartion"
    ["TAL-Dub-3"]="urn:juce:TalDub3"
    ["TAL-Filter-2"]="urn:juce:TalFilter2"
    ["TAL-Filter"]="urn:juce:TalFilter"
    ["TAL-NoiseMaker"]="http://kunz.corrupt.ch/products/tal-noisemaker"
    ["TAL-Reverb-2"]="urn:juce:TalReverb2"
    ["TAL-Reverb-3"]="urn:juce:TalReverb3"
    ["TAL-Reverb"]="urn:juce:TalReverb"
    ["TAL-Vocoder"]="http://kunz.corrupt.ch/products/tal-vocoder"
    ["Temper"]="https://github.com/creativeintent/temper"
    ["TheFunction"]="http://arcticanaudio.com/plugins/thefunction"
    ["ThePilgrim"]="http://arcticanaudio.com/plugins/thepilgrim"
    ["Wolpertinger"]="http://tumbetoene.tuxfamily.org"
    ["drowaudio-distortion"]="http://www.drowaudio.co.uk/audio/audio_plugins/distortion"
    ["drowaudio-distortionshaper"]="http://www.drowaudio.co.uk/audio/audio_plugins/distortion_shaper"
    ["drowaudio-flanger"]="http://www.drowaudio.co.uk/audio/audio_plugins/flanger"
    ["drowaudio-reverb"]="http://www.drowaudio.co.uk/audio/audio_plugins/reverb"
    ["drowaudio-tremolo"]="http://www.drowaudio.co.uk/audio/audio_plugins/tremolo"
    ["drumsynth"]="urn:juced:DrumSynth"
    ["eqinox"]="urn:juced:EQinox"
    ["vex"]="urn:juce:Vex"
  )
  cd "$pkgname"
  echo "Linting plugins: ${_legacy_plugins[*]}"
  for _legacy_plugin in "${_legacy_plugins[@]}"; do
    echo "Linting ${_plugin}: ${_legacy_urls[${_legacy_plugin}]}"
    if [ -n "${_legacy_urls[${_legacy_plugin}]}" ]; then
      # ignore lv2_generate_ttl symbol as it can not be removed easily:
      # https://github.com/DISTRHO/DISTRHO-Ports/issues/58
      lv2lint -Mpack -s lv2_generate_ttl -I "build/ports-legacy/${_legacy_plugin}.lv2/" "${_legacy_urls[${_legacy_plugin}]}"
    fi
  done
  for _plugin in "${_plugins[@]}"; do
    echo "Linting ${_plugin}: ${_urls[${_plugin}]}"
    if [ -n "${_urls[${_plugin}]}" ]; then
      # ignore lv2_generate_ttl symbol as it can not be removed easily:
      # https://github.com/DISTRHO/DISTRHO-Ports/issues/58
      lv2lint -Mpack -s lv2_generate_ttl -I "build/ports/${_plugin}.lv2/" "${_urls[${_plugin}]}"
    fi
  done
  ninja -C build test
}

package() {
  depends+=('libasound.so' 'libfreetype.so')
  cd "$pkgname"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
