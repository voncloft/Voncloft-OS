# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-geoip2
pkgver=4.1.0
_testdatacommit=cbaa463dc6950ababbf678ca85fb3833b81c76d3
pkgrel=3
pkgdesc="MaxMind GeoIP2 API"
url="https://github.com/maxmind/GeoIP2-python"
license=('Apache')
arch=('any')
depends=('python-aiohttp' 'python-maxminddb' 'python-requests')
makedepends=('python-setuptools')
checkdepends=('python-mocket')
source=("$pkgname-$pkgver.tar.gz::https://github.com/maxmind/GeoIP2-python/archive/v$pkgver.tar.gz"
        https://github.com/maxmind/MaxMind-DB/archive/$_testdatacommit.tar.gz)
sha512sums=('a82236b5097d555cfdba2885129202d4d42151997aad2e80e5aab7f96231642451a2e82f0176ca8e6ea033a69cfca5b3f82bf3de6b0cb9601ac926c3447fa680'
            '337552c1d6524ba31c1d3df55c0b6137ca2d5115529b8c1a619c82baa198886c274bac4228e456273851bddc46ca983ad2be6329b8f19d2769b745fb2c0f96b1')

prepare() {
  cd GeoIP2-python-$pkgver
  rmdir tests/data
  ln -s "$srcdir"/MaxMind-DB-$_testdatacommit tests/data
}

build() {
  cd GeoIP2-python-$pkgver
  python setup.py build
}

check() {
  cd GeoIP2-python-$pkgver
  python setup.py test
}

package() {
  cd GeoIP2-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
