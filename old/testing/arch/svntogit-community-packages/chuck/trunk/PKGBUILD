# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: tardo <tardo@nagi-fanboi.net>

pkgname=chuck
pkgver=1.4.0.1
pkgrel=1
pkgdesc='Concurrent, on-the-fly audio programming language'
arch=(x86_64)
url='https://chuck.cs.princeton.edu/'
license=(GPL)
depends=(jack)
groups=(pro-audio)
source=("https://chuck.cs.princeton.edu/release/files/$pkgname-$pkgver.tgz")
sha256sums=('11a20c34b385e132bf43d5ae6a562c652f631828cc6b1562a4c029bc9a850ed4')

build() {
  # Other build alternatives are: linux-alsa linux-oss osx win32
  make -C "$pkgname-$pkgver/src" linux-jack
}

package() {
  cd "$pkgname-$pkgver"
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -r examples "$pkgdir/usr/share/doc/$pkgname"
  install -Dm755 "src/$pkgname" "$pkgdir/usr/bin/$pkgname"
}

# getver: chuck.cs.princeton.edu/release
# vim: ts=2 sw=2 et:
