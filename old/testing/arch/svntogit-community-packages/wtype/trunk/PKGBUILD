# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Carlo Teubner <carlo@cteubner.net>

pkgname=wtype
pkgver=0.3
pkgrel=2
pkgdesc='xdotool type for wayland'
arch=('x86_64')
url=https://github.com/atx/wtype
license=('MIT')
depends=('libxkbcommon' 'wayland')
makedepends=('meson')
source=("$url/archive/v$pkgver/wtype-v$pkgver.tar.gz")
b2sums=('cbf6daa8e47ab7e7f07b750e136a0845821eca217a66a0baf4e65c2aa18ebc2db6ef7301f7547848056440f578570527904134be7da8f86fd838ae086e78aae9')

build() {
  cd wtype-$pkgver
  arch-meson build
  ninja -C build
}

package() {
  cd wtype-$pkgver
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
