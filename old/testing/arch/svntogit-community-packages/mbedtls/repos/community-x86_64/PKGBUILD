# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Mihai Militaru <mihai militaru at xmpp dot ro>
# Contributor: carstene1ns <arch carsten-teibes.de>

pkgname=mbedtls
pkgver=2.25.0
pkgrel=1
pkgdesc="Portable cryptographic and SSL/TLS library, aka polarssl"
arch=('x86_64')
url="https://tls.mbed.org"
license=('Apache')
depends=('glibc' 'sh')
checkdepends=('python')
makedepends=('cmake')
provides=('polarssl' 'libmbedcrypto.so' 'libmbedtls.so' 'libmbedx509.so')
replaces=('polarssl')
conflicts=('polarssl')
options=('staticlibs')
source=(https://github.com/ARMmbed/mbedtls/archive/${pkgname}-${pkgver}.tar.gz)
sha256sums=('ea2049c2dd4868693998d5a9780e198194be5aea1706ff4a9d4f882f18c0a101')
b2sums=('27a326ba08c341ed47ce280401a4b73606138aa8946fd8d8df5d084496eecc6eadf4eedd65270725a360d4b171c86cefeccb6b1515847587f0f11ed15b7dda9b')

prepare() {
  cd "$pkgname-$pkgname-$pkgver"
  # enable flags for non-embedded systems
  sed -i 's|//\(#define MBEDTLS_THREADING_C\)|\1|' include/mbedtls/config.h
  sed -i 's|//\(#define MBEDTLS_THREADING_PTHREAD\)|\1|' include/mbedtls/config.h

  # broken in 2.25.0
  sed -i 's/thread_id, -ret/thread_id, (unsigned int)(-ret)/' programs/ssl/ssl_pthread_server.c
  sed -i 's/thread_id, ret/thread_id, (unsigned int)ret/' programs/ssl/ssl_pthread_server.c
  sed -i 's/ -ret );/ (unsigned int)(-ret) );/' programs/ssl/ssl_pthread_server.c
  sed -i 's/ -ret, error_buf/ (unsigned int)(-ret), error_buf/' programs/ssl/ssl_pthread_server.c
  sed -i 's/04x\\n", ret /04x\\n", (unsigned int)ret /' programs/ssl/ssl_pthread_server.c
}

build() {
  cd "$pkgname-$pkgname-$pkgver"
  cmake \
    -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DUSE_SHARED_MBEDTLS_LIBRARY=ON \
	-DCMAKE_INSTALL_RPATH=
  make -C build
}

check() {
  cd "$pkgname-$pkgname-$pkgver"
  make -C build test
  # make check?
}

package() {
  cd "$pkgname-$pkgname-$pkgver"
  make -C build DESTDIR="$pkgdir" install

  # rename generic utils
  local _prog _baseprog
  for _prog in "$pkgdir"/usr/bin/*; do
	_baseprog=$(basename "$_prog")
    mv -v "$_prog" "${_prog//$_baseprog/mbedtls_$_baseprog}"
  done

  # fixup static lib permissions
  chmod 644 "$pkgdir"/usr/lib/*.a
}
