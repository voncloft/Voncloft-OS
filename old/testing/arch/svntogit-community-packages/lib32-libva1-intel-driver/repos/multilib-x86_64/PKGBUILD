# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=lib32-libva1-intel-driver
pkgver=1.8.3
pkgrel=1
pkgdesc='VA-API implementation for Intel G45 and HD Graphics family'
arch=('x86_64')
url='https://01.org/linuxmedia/vaapi'
license=('MIT')
depends=('lib32-libva1' 'lib32-libdrm' 'libva1-intel-driver')
makedepends=('git')
source=("git+https://github.com/intel/intel-vaapi-driver.git#tag=${pkgver}")
sha512sums=('SKIP')

prepare() {
  cd intel-vaapi-driver

  # Only relevant if intel-gpu-tools is installed,
  # since then the shaders will be recompiled
  sed -i '1s/python$/&2/' src/shaders/gpp.py

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd intel-vaapi-driver

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/libva1/pkgconfig:/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32/libva1'
  make
}

package() {
  make DESTDIR="${pkgdir}" -C intel-vaapi-driver install
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libva1-intel-driver "${pkgdir}"/usr/share/licenses/lib32-libva1-intel-driver
}

# vim: ts=2 sw=2 et:
