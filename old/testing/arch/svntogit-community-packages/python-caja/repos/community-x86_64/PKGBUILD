# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=python-caja
pkgver=1.24.0
pkgrel=2
pkgdesc="Python binding for Caja, to allow Caja property page and menu item extensions to be written in Python"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('caja' 'python-gobject')
makedepends=('intltool')
conflicts=('python2-caja')
replaces=('python2-caja')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/python-caja-${pkgver}.tar.xz")
sha256sums=('efe1910938f016e72b8806d5b222e5074d998da42cdad1bc1ef93f460c0ee6f2')

prepare() {
    	cd "python-caja-${pkgver}"
}

build() {
    	cd "python-caja-${pkgver}"
    	PYTHON=python3 ./configure \
        	--prefix=/usr

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

   	 make
}

package() {
    	cd "python-caja-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
