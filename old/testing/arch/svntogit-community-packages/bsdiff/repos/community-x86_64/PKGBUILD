# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=bsdiff
pkgver=4.3
pkgrel=10
pkgdesc="bsdiff and bspatch are tools for building and applying patches to binary files."
url="https://www.daemonology.net/bsdiff/"
license=('BSD')
arch=('x86_64')
depends=('bzip2')
source=("https://www.daemonology.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
	Makefile.patch
        "https://security.freebsd.org/patches/SA-16:25/bspatch.patch")
md5sums=('e6d812394f0e0ecc8d5df255aa1db22a'
         'f176d14d39c5784a2c824f6797544f4b'
         '30e8ec771baea61bd9c25adb385b06fc')

prepare() {
  cd "$srcdir/${pkgname}-${pkgver}"
  patch -p0 < ../Makefile.patch
  sed -i 's|usr.bin/bsdiff/bspatch/||' ../bspatch.patch
  patch -p0 < ../bspatch.patch
}

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make PREFIX="$pkgdir/usr" install
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  head -n 26 bsdiff.c >"$pkgdir/usr/share/licenses/$pkgname/license"
}
