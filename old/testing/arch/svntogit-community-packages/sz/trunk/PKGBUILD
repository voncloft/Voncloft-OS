# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=sz
pkgver=2.1.10
pkgrel=2
pkgdesc="Modular parametrizable lossy compressor framework for scientific data"
arch=(x86_64)
url="https://szcompressor.org/"
license=(BSD)
depends=(glibc gcc-libs)
makedepends=(cmake gcc-fortran hdf5 netcdf python python-numpy swig)
source=(https://github.com/szcompressor/SZ/releases/download/v${pkgver}/sz-${pkgver}.tar.gz)
sha256sums=('3aba7619bdb5412218f162696f946c9d3a3df5acf128ddc685b21e45c11f6ae3')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_FORTRAN=ON \
    -DBUILD_HDF5_FILTER=ON \
    -DBUILD_NETCDF_READER=ON \
    -DBUILD_PYTHON_WRAPPER=ON
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/copyright-and-BSD-license.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
  rm "${pkgdir}"/usr/bin/test*
}
