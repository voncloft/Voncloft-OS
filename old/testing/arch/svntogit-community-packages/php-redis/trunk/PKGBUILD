# Maintainer: David Runge <dvzrv@archlinux.org>

_name=redis
_upstream=phpredis
pkgbase=php-redis
pkgname=('php-redis' 'php7-redis')
pkgver=5.3.3
pkgrel=1
pkgdesc="An API for communicating with the Redis key-value store"
arch=('x86_64')
url="https://github.com/phpredis/phpredis/"
license=('PHP')
depends=('glibc')
makedepends=('liblzf' 'php-igbinary' 'php7-igbinary')
checkdepends=('lsof' 'redis')
optdepends=('redis: use a local redis instance')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${_upstream}/${_upstream}/archive/${pkgver}.tar.gz")
sha512sums=('1d004dc36c82cbb6a1537103d35b5b9ba29a0eec3ec4aa8c6af71f0b40fdcb7671501d395c8925f200cc81efd1a862ee9aca05624e0c79051714aa8b0abbe3ec')
b2sums=('2ce3b911f91236bfb22d01a83eddaac64864a553ea8ea707579cc0ea493178856c077efbe4a70e947720903a23748b170beb7e51514699858ee2527cc094db29')

prepare() {
  mv -v "${_upstream}-${pkgver}" "$pkgbase-$pkgver"
  # tempfile is non-standard, Debian only
  sed -e 's/tempfile/mktemp/g' -i  "$pkgname-$pkgver/tests/mkring.sh"
  # the kill after shutdown of redis makes it exit with status code 1
  sed -e '/kill -9/d' -i "$pkgname-$pkgver/tests/mkring.sh"
  # disable the extension by default
  echo -e "; this extension requires igbinary to be activated as well\n;extension=${_name}" > "$pkgname-$pkgver/${_name}.ini"

  cp -av "$pkgbase-$pkgver" "${pkgname[1]}-$pkgver"

  (
    cd "$pkgbase-$pkgver"
    phpize
  )
  (
    cd "${pkgname[1]}-$pkgver"
    phpize7
  )
}

build() {
  (
    cd "$pkgname-$pkgver"
    ./configure --prefix=/usr \
                --enable-redis-igbinary \
                --enable-redis-lzf \
                --with-liblzf=/usr/lib/
    make
  )
  (
    cd "${pkgname[1]}-$pkgver"
    ./configure --prefix=/usr \
                --enable-redis-igbinary \
                --enable-redis-lzf \
                --with-liblzf=/usr/lib/
    make
  )
}

check() {
  # tests are partly broken:
  # https://github.com/phpredis/phpredis/issues/1593
  (
    export TEST_PHP_EXECUTABLE=/usr/bin/php
    export TEST_PHP_ARGS="-d extension=igbinary -d extension=${srcdir}/${pkgbase}-${pkgver}/modules/redis.so"
    cd "$pkgname-$pkgver"
    tests/mkring.sh start
    $TEST_PHP_EXECUTABLE $TEST_PHP_ARGS tests/TestRedis.php --class Redis
    $TEST_PHP_EXECUTABLE $TEST_PHP_ARGS tests/TestRedis.php --class RedisArray
    tests/mkring.sh stop
  )
  (
    export TEST_PHP_EXECUTABLE=/usr/bin/php7
    export TEST_PHP_ARGS="-d extension=igbinary -d extension=${srcdir}/${pkgname[1]}-${pkgver}/modules/redis.so"
    cd "${pkgname[1]}-$pkgver"
    tests/mkring.sh start
    $TEST_PHP_EXECUTABLE $TEST_PHP_ARGS tests/TestRedis.php --class Redis
    $TEST_PHP_EXECUTABLE $TEST_PHP_ARGS tests/TestRedis.php --class RedisArray
    tests/mkring.sh stop
  )
}

package_php-redis() {
  depends+=('liblzf.so' 'php-igbinary')
  backup=("etc/php/conf.d/${_name}.ini")
  cd "$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install
  install -vDm 644 "${_name}.ini" -t "${pkgdir}/etc/php/conf.d/"
  install -vDm 644 {{README,arrays,cluster}.markdown,CREDITS} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

package_php7-redis() {
  depends+=('liblzf.so' 'php7-igbinary')
  backup=("etc/php7/conf.d/${_name}.ini")
  cd "$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install
  install -vDm 644 "${_name}.ini" -t "${pkgdir}/etc/php7/conf.d/"
  install -vDm 644 {{README,arrays,cluster}.markdown,CREDITS} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
