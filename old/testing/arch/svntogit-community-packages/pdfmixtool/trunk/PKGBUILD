# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Marco Scarpetta <marcoscarpetta02@gmail.com>

pkgname=pdfmixtool
pkgver=0.6
pkgrel=1
pkgdesc='Application to split, merge, rotate and mix PDF files'
arch=(x86_64)
url='https://scarpetta.eu/pdfmixtool/'
license=(GPL3)
depends=(hicolor-icon-theme podofo qpdf qt5-base)
makedepends=(cmake qt5-tools)
source=("https://gitlab.com/scarpetta/pdfmixtool/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha256sums=('a1d43d63e0981724434bb0db62ee91185d17e45402dbec8df1b2e34893ffc782')

prepare() {
  [[ -d build ]] || mkdir build
}

build() {
  cd build
  cmake -G "Unix Makefiles" ../$pkgname-v$pkgver/ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
