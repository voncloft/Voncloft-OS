# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=liblscp
pkgver=0.6.0
pkgrel=3
pkgdesc="LinuxSampler Control Protocol C++ library"
arch=('x86_64')
url="https://www.linuxsampler.org/"
license=('LGPL2.1')
depends=('glibc')
provides=('liblscp.so')
source=("https://download.linuxsampler.org/packages/${pkgname}-${pkgver}.tar.gz")
sha512sums=('64c5cb51ac7821d65a8769415deb68cefa24684e1d4fa592a68c7e45cf2af02ffa41a2f2300ffc98a6d1d6d880279aa1b61499ac0fe59177c60fc0a2075ada15')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

