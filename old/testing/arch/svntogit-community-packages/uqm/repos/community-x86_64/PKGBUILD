# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Maintainer : Daenyth <Daenyth+Arch _AT_ gmail _DOT_ com>
# Contributor : wizzomafizzo <wizzomafizzo@gmail.com>
# Contributor: tochiro@no.spam.mail.berlios.de
# Contributor: sh__

pkgname=uqm
pkgver=0.7.0
pkgrel=6
pkgdesc="The Ur-Quan Masters, a port of Star Control 2"
arch=('x86_64')
url="http://sc2.sourceforge.net/"
license=('GPL')
depends=('libmikmod' 'libvorbis' 'sdl_image' 'libgl' 'glu')
makedepends=('imagemagick' 'mesa' 'namcap')
source=(https://downloads.sourceforge.net/sourceforge/sc2/uqm-${pkgver}-source.tgz
        https://downloads.sourceforge.net/sourceforge/sc2/uqm-${pkgver}-content.uqm
        config.state
        uqm
        uqm.desktop
        uqm.png)
noextract=(uqm-${pkgver}-content.uqm)
md5sums=('f9018ea0493d7dac6a9e1006b00af7df'
         '2f36dcb15274dbbcb5e266f2ed84d5b2'
         'ca497b754edf56aab8a5791f1d2c974a'
         '07e64632fce6323a14ab558d0214b885'
         '44c83b9b9d102b99210af0036749a318'
         '06d0a72aa18e7edce6cd3c7a3a747c44')

prepare() {
  cd $pkgname-$pkgver
  cp ../config.state .
  sed -e "/INPUT_install_prefix/ s|replaceme|$pkgdir/usr|" \
      -i config.state    
  ./build.sh uqm reprocess_config

  # fix build (Fedora)
  cd src/uqm/comm/
  for d in */ ; do
    cd $d
    mv strings.h ustrings.h
    cd ..
  done
  find . -type f -name '*.c' | xargs sed -i s/strings.h/ustrings.h/g
  find . -type f -name '*.h' | xargs sed -i s/STRINGS/USTRINGS/g
}

build() {
  cd $pkgname-$pkgver
  ./build.sh uqm
}
  
package() {
  cd $pkgname-$pkgver
  ./build.sh uqm install

  install -Dm644 $srcdir/$pkgname-$pkgver-content.uqm \
     $pkgdir/usr/share/uqm/content/packages/$pkgname-$pkgver-content.uqm

  # stolen from fedora!
  install -Dm644 $srcdir/uqm.desktop \
     $pkgdir/usr/share/applications/uqm.desktop
  install -Dm644 $srcdir/uqm.png \
     $pkgdir/usr/share/pixmaps/uqm.png

  rm $pkgdir/usr/bin/uqm
  install -Dm755 $srcdir/uqm $pkgdir/usr/bin/uqm
}
