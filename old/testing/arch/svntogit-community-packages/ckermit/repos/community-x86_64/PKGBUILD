# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Brian Bidulock <bidulock@openss7.org>
# Contributor: Ryan Corder <ryanc@greengrey.org>
# Contributor: Samuel Tardieu <sam@rfc1149.net>

pkgname=ckermit
pkgver=9.0.302
pkgrel=7
pkgdesc="Portable scriptable network and serial communication software"
arch=('x86_64')
license=('custom')
depends=('ncurses')
url="http://www.columbia.edu/kermit/ck90.html"
#source=('ftp://kermit.columbia.edu/kermit/archives/cku302.tar.gz' 'lockdir.patch')
source=('ftp://ftp.kermitproject.org/kermit/archives/cku302.tar.gz'
	'lockdir.patch')
sha512sums=('2cfc264e5262d29618fdf218fd3407fd9049b16110972cdddb03d114703b7a0ea13c4b0a354f50c2a9f70d4727732fe4d1920b5c656f969bc56c1eb9dc3c43fa'
            'daa67abcbd408486d7bdb790b7756d939ee0d8a99b36c09f781bebf3407a19e35c1367a19e1b98efa3c30d4b649a07b45ab4e154a4fea16aed8d59cfee754fc5')

prepare() {
  cd "$srcdir"
  chmod -R og-rwx ./
  patch -p1 < lockdir.patch
}

build() {
  cd "$srcdir"
  make linux
  echo "#!/usr/bin/ckermit" > _tmp.ini
  cat ckermit.ini >> _tmp.ini
}

package() {
  cd "$srcdir"
  install -Dm0755 wermit "$pkgdir"/usr/bin/ckermit
  ln -s /usr/bin/ckermit "$pkgdir"/usr/bin/ckermit-sshsub
  install -Dm0644 ckuker.nr "$pkgdir"/usr//share/man/man1/ckermit.1
  install -Dm0644 COPYING.TXT "$pkgdir"/usr/share/licenses/$pkgname/License.txt
  install -Dm0755 _tmp.ini "$pkgdir"/usr/bin/ckermit.ini
}
