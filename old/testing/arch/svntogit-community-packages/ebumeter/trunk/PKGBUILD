# Maintainer: David Runge <dave@sleepmap.de>

pkgname=ebumeter
pkgver=0.4.2
pkgrel=4
pkgdesc="Loudness measurement according to EBU-R128"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/ebumeter-doc/quickguide.html"
license=('GPL3')
groups=('pro-audio')
depends=('clxclient' 'clthreads' 'jack' 'zita-resampler')
makedepends=('gendesk')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/ebumeter-${pkgver}.tar.bz2")
sha512sums=('5fdbac7afa5e78678384a8b8733f421381f310350e8712232684e0f36a09a41d843541bbf2a08b676025edfc494b6958ab7965e6ea670f8e19744b25c6a4f7d6')

prepare() {
  cd "$pkgname-$pkgver"
  sed -e '/native/d' \
      -i source/Makefile
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name ${pkgname} \
          --categories "AudioVideo;Audio"
}

build() {
  cd "$pkgname-$pkgver"
  make -C source PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver"
  make -C source PREFIX=/usr DESTDIR="$pkgdir/" install
  # XDG desktop integration
  install -vDm 644 "${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 {AUTHORS,README,doc/*.pdf} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 doc/*.{html,png} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/html"
}
