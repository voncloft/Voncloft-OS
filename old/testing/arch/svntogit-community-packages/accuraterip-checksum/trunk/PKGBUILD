# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Samantha Baldwin <fuhsaz 'plus' aur 'at' cryptic 'dot' li>
pkgname=accuraterip-checksum
pkgver=1.5
pkgrel=1
pkgdesc="A C99 commandline program to compute the AccurateRip checksum of singletrack WAV files"
arch=('x86_64')
url="https://github.com/leo-bogert/accuraterip-checksum"
license=('GPL')
depends=('libsndfile')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/leo-bogert/${pkgname}/archive/version${pkgver}.tar.gz")
sha512sums=('d3f74197ea7b6f59f9c3f1eddfe37322953beea79fa977b513e74bff853a1df16123dc2ebf013b1a25e35b02bfcc472564e8ba3916187b22cea353c768bb681a')

build() {
  cd "${srcdir}/${pkgname}-version${pkgver}"
  cc -lsndfile "${pkgname}.c" -o "${pkgname}" $LDFLAGS $CFLAGS
}

package() {
  cd "${srcdir}/${pkgname}-version${pkgver}"
  install -D -m755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}
