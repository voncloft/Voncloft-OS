# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=cuetools
pkgver=1.4.1
pkgrel=4
pkgdesc="Cue and toc file parsers and utilities"
arch=('x86_64')
url="https://github.com/svend/cuetools"
#url="http://developer.berlios.de/projects/cuetools/"
license=('GPL')
depends=('glibc')
optdepends=('id3v2: mp3 support in cuetag (does not require mutagen)'
            'mutagen-tools: mp3 support in cuetag (does not require id3v2)')
source=("$pkgname-$pkgver.tar.gz::https://github.com/svend/cuetools/archive/${pkgver}.tar.gz")
sha512sums=('dc81e6ec756f9d040dd913e8f340be136d68619caa24bdec21364f94b4fc0ffcbcf21325c7fd53c9230d6d70a2856b45a32c5ca7f41ec76fa47298bf226d959e')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  aclocal
  autoheader
  automake --force-missing --add-missing
  autoconf
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
