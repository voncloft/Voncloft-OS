# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=flask-security
pkgname=python-flask-security-too
pkgver=3.3.3
pkgrel=3
pkgdesc='Quick and simple security for Flask applications'
url='https://github.com/jwag956/flask-security'
arch=('any')
license=('MIT')
depends=('python' 'python-flask' 'python-flask-login' 'python-flask-mail' 'python-flask-principal'
         'python-flask-wtf' 'python-flask-babelex' 'python-itsdangerous' 'python-passlib')
makedepends=('python-flask' 'python-flask-login' 'python-flask-mail' 'python-flask-principal'
             'python-flask-wtf' 'python-flask-babelex' 'python-itsdangerous' 'python-passlib'
             'python-sphinx' 'python-setuptools' 'python-pallets-sphinx-themes' 'python-sphinx-issues')
#checkdepends=('python-pytest' 'python-pytest-runner' 'python-pytest-cov' 'python-pytest-cache'
              #'python-mock' 'python-sqlalchemy' 'python-flask-sqlalchemy' 'python-bcrypt' 'python-pony')
provides=('python-flask-security')
conflicts=('python-flask-security')
source=(https://github.com/jwag956/flask-security/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('f60a242de35a65a67221b7c738935054fb6a2fc33fae6294bc66b42791729e170b65213ec8f1f860d91fe64b44de290d075853ebde512e75dac235cb582a6633')
b2sums=('e57c4e61aad6e45244efac1cae886c23bd0dcf1ff706ee687b0b123ca7b50e064ba148153864c22cd5eecdaea556ddc301c9a973b45578eea7c058dca53bd406')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -e 's/ --translations//' \
      -e 's/ --pep8//' \
      -e 's/ --black//' \
      -e 's/ --flake8//' \
      -e 's/ --flakes//' \
      -i pytest.ini
}

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
  sphinx-build -b text docs docs/_build/text
  sphinx-build -b man docs docs/_build/man
}

# TODO: test marks not compatible with latest pytest
#check() {
  #cd ${_pkgname}-${pkgver}
  #PYTHONPATH=. py.test
#}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.rst CHANGES.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/flask-security.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  install -Dm 644 docs/_build/man/flask-security.1 "${pkgdir}/usr/share/man/man1/python-flask-security.1"
}

# vim: ts=2 sw=2 et:
