# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Erkin Batu Altunba≈ü <erkinbatu@gmail.com>

pkgname=ponysay
pkgver=3.0.3
pkgrel=3
pkgdesc="cowsay reimplemention for ponies"
url="http://erkin.github.com/ponysay/"
arch=('any')
license=('GPL3')
depends=('python' 'bash')
makedepends=('git')
source=("ponysay-$pkgver.tar.gz::https://github.com/erkin/ponysay/tarball/$pkgver"
        fix-do-not-compare-literal.patch)
sha512sums=('ee4340b3a4902465217fcf0e0a31acf2771697ebfff9ce1897d5e0062782efd36ceb91672a6eb45a9b70aeb7f50f48e2ebd7671dab3cf5af1ec4043f2767edd7'
            '372e26a1293d05a37821a5edcbfc2ebb41d751ba4226fa503c07e815c1bb32b6c000293c584babd96e48614bf3b9c4321deb8b28587a37a56783a434615ddeb5')

prepare() {
  cd "$srcdir/erkin-ponysay-"*

  patch -Np1 -i "$srcdir"/fix-do-not-compare-literal.patch
}

package() {
  cd "$srcdir/erkin-ponysay-"*

  python3 setup.py --prefix=/usr --dest-dir="$pkgdir" --freedom=partial --everything --with-pdf=/usr/share/doc/ponysay --without-pdf-compression install
}
