# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=kmplayer
pkgver=0.12.0.b
_pkgver=0.12.0b
pkgrel=4
pkgdesc="Video player plugin for Konqueror and basic MPlayer/Xine/ffmpeg/ffserver/VDR frontend for KDE"
arch=(x86_64)
license=(GPL LGPL FDL)
url="https://kmplayer.kde.org"
depends=(kmediaplayer kdelibs4support phonon-qt5 hicolor-icon-theme)
makedepends=(extra-cmake-modules kdesignerplugin kdoctools kinit)
optdepends=('mplayer: mplayer backend')
source=("https://download.kde.org/stable/$pkgname/${_pkgver%.*}/$pkgname-$_pkgver.tar.bz2"
        kmplayer-desktop.patch::"https://invent.kde.org/multimedia/kmplayer/-/commit/2126d76b.patch"
        kmplayer-qt5.9.patch::"https://invent.kde.org/multimedia/kmplayer/-/commit/3def6507.patch"
        kmplayer-qt5.11.patch::"https://invent.kde.org/multimedia/kmplayer/-/commit/7dafda63.patch")
sha256sums=('5e98cea9bd958860d90003d37037172a95ed8b699133e8f1e6033147aaefed73'
            '345e03ef0cb2ebb59cfd11b048226aebbccde592d064f404d6eff8c47319363c'
            '69b3a54e53a652e20beb01b27c8d953e8f3e6e85813d422e1c606fb08cfd3e4e'
            'f0b2e4cada02712d195f8a88ae5a6ec68ba9b4ab5cbaaaf448e82837dde9b3e9')

prepare() {
  sed -e 's/org.kde.//g' -i kmplayer-desktop.patch
  patch -d $pkgname-$_pkgver -p1 -i ../kmplayer-desktop.patch # Fix launching from desktop file
  patch -d $pkgname-$_pkgver -p1 -i ../kmplayer-qt5.9.patch # Fix build with Qt 5.9
  patch -d $pkgname-$_pkgver -p1 -i ../kmplayer-qt5.11.patch # Fix build with Qt 5.11
}

build() {
  cmake -B build -S $pkgname-$_pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
