# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libsemigroups
pkgver=1.3.6
pkgrel=1
pkgdesc="C++ library for semigroups and monoids"
url="https://libsemigroups.github.io/libsemigroups/"
arch=(x86_64)
license=(GPL3)
depends=(fmt)
makedepends=(eigen)
source=(https://github.com/libsemigroups/libsemigroups/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('6b9189bb2b7ca54b9c892fc50e46fd063c48894e84d2241ab0391a5976afdbff')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --enable-fmt \
              --with-external-fmt \
              --with-external-eigen
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
