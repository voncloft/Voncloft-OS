# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ľubomír 'The_K' Kučera <lubomir-dot-kucera-dot-jr-at-gmail-dot-com>

pkgname=bully
pkgver=1.1.12.g04185d7
pkgrel=2
_gitcommit=04185d7b1d1a7909f47f23fbd85691401c7fabd7
pkgdesc='Retrieve WPA/WPA2 passphrase from a WPS enabled access point'
url='https://github.com/aanarchyy/bully'
arch=('x86_64')
license=('GPL3')
depends=('glibc' 'libpcap' 'aircrack-ng')
optdepends=('pixiewps: pixiewps support')
makedepends=('git')
source=(${pkgname}::"git+https://github.com/aanarchyy/bully#commit=${_gitcommit}")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  git describe --tags --always|sed 's/^v//;s/-/./g'
}

build() {
  cd ${pkgname}
  make -C src
}

package() {
  cd ${pkgname}
  make -C src install DESTDIR="${pkgdir}" prefix=/usr
}

# vim: ts=2 sw=2 et:
