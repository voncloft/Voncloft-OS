# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-asyncio
pkgver=0.14.0
pkgrel=3
pkgdesc="Pytest support for asyncio."
arch=('any')
license=('Apache')
url="https://github.com/pytest-dev/pytest-asyncio/"
depends=('python-pytest')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'python-async_generator' 'python-hypothesis')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-asyncio/archive/v$pkgver.tar.gz")
sha512sums=('d1fadff152a0c94f80a8272a0dd293bd9d9300ad9e79c69556b5901c1a8ce00545f7715e503a383e3f4f35c8090d8f9ec33d16739b85378b59b1786a50441fca')

prepare() {
  cd pytest-asyncio-$pkgver
  # Don't treat DeprecationWarnings as errors
  sed -i '/filterwarnings = error/d' setup.cfg
}

build() {
  cd pytest-asyncio-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-asyncio-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH:$PWD/tests" py.test
}

package() {
  cd pytest-asyncio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
