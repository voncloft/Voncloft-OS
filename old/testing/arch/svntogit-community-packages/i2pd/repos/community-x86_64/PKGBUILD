# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Contributor: robertfoster
# Contributor: kurych
# Contributor: redfish
# Contributor: atommixz
# Contributor: denn
# Contributor: post-factum
# Contributor: wrdcrrtmnstr
# Contributor: r4sas

pkgname=i2pd
pkgver=2.35.0
pkgrel=4
pkgdesc='A full-featured C++ implementation of the I2P router'
arch=('x86_64')
url='https://i2pd.website/'
license=('BSD')
depends=('boost-libs' 'libminiupnpc.so' 'openssl' 'zlib')
makedepends=('cmake' 'boost')
provides=('i2p-router')
backup=('etc/i2pd/i2pd.conf'
        'etc/i2pd/tunnels.conf')
source=("https://github.com/PurpleI2P/i2pd/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '010-i2pd-use-arch-flags-on-tests.patch'
        '020-i2pd-config.patch'
        '030-i2pd-do-not-override-config.patch'
        '040-i2pd-systemd-service-hardening.patch'
        '050-i2pd-tunnels-d-readme.patch'
        '060-i2pd-2.35.0-fix-avx-detection.patch'
        'i2pd.sysusers'
        'i2pd.tmpfiles')
sha256sums=('d041fd4e7a88ac168e76f66fdab40174ad093cdc13451cdbd0dd1216e5581f8a'
            '0064503a9124b764d01db862ba3c2ff97bc5961d41359970df2d6ce9842a5ab5'
            '452550678ea5702a6492eb58e8d0452b91dc5d0aaa112cf04542df74a3dc0dfc'
            '45cfc3035c5b5cfc92cfffec1fe7d7efc4fed3229195cdb640ec4a6c405af149'
            '578ed9767890e970bd93b44e3be85c2595c4149ed65e4cc1c79ea12e2cb6982e'
            'cfcb6b07b67aff3e3af12767f4649d88b9320dc71907b6c01b465e5c138cdaa3'
            '3434318864ff7adc55ddcb3b6d625642dc443ac7249469011bbbfb598fb99689'
            '88b2e709228049ba11f37863f87de75ab6cde295104852871384337cfdc906a3'
            'fe8cc2ec83cb5b5c2b2ec8cce9a989e0cb6fd347e00b84e03a17b12efd152fac')

prepare() {
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/010-i2pd-use-arch-flags-on-tests.patch"
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/020-i2pd-config.patch"
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/030-i2pd-do-not-override-config.patch"
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/040-i2pd-systemd-service-hardening.patch"
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/050-i2pd-tunnels-d-readme.patch"
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/060-i2pd-2.35.0-fix-avx-detection.patch"
}

build() {
    cmake \
        -B "${pkgname}-${pkgver}/build" \
        -S "${pkgname}-${pkgver}/build" \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DBUILD_SHARED_LIBS:BOOL='ON' \
        -DWITH_UPNP:BOOL='ON' \
        -Wno-dev
    make -C "${pkgname}-${pkgver}/build"
}

check() {
    make -C "${pkgname}-${pkgver}/tests"
}

package() {
    cd "${pkgname}-${pkgver}"
    
    make -C build DESTDIR="$pkgdir" install
    
    # config
    install -D -m644 contrib/i2pd.conf    -t "${pkgdir}/etc/i2pd"
    install -D -m644 contrib/tunnels.conf -t "${pkgdir}/etc/i2pd"
    install -d -m755 "${pkgdir}/etc/i2pd/tunnels.d"
    
    # certificates
    local _file
    while read -r -d '' _file
    do
        install -D -m644 "$_file" "${pkgdir}/usr/share/i2pd/certificates/${_file#contrib/certificates/}"
    done < <(find contrib/certificates -type f -print0)
    
    # systemd
    install -D -m644 contrib/i2pd.service   -t "${pkgdir}/usr/lib/systemd/system"
    install -D -m644 "${srcdir}/i2pd.sysusers" "${pkgdir}/usr/lib/sysusers.d/i2pd.conf"
    install -D -m644 "${srcdir}/i2pd.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/i2pd.conf"
    
    # tunnels.d examples
    install -D -m644 contrib/tunnels.d/README         -t "${pkgdir}/usr/share/doc/i2pd/tunnels.d"
    install -D -m644 contrib/tunnels.d/IRC-Ilita.conf -t "${pkgdir}/usr/share/doc/i2pd/tunnels.d"
    install -D -m644 contrib/tunnels.d/IRC-Irc2P.conf -t "${pkgdir}/usr/share/doc/i2pd/tunnels.d"
    
    # headers
    install -D -m644 libi2pd{,_client}/*.h -t "${pkgdir}/usr/include/i2pd"
    
    # man page
    install -D -m644 debian/i2pd.1 -t "${pkgdir}/usr/share/man/man1"
    
    # license
    install -D -m644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
