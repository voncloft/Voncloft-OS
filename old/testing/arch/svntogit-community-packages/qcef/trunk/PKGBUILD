# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qcef
pkgver=1.1.4.6
_cefcommit=059a0c9cef4e289a50dc7a2f4c91fe69db95035e
pkgrel=1
pkgdesc="Qt5 binding of CEF"
arch=('x86_64')
url="https://github.com/linuxdeepin/qcef"
license=('GPL')
depends=('gconf' 'gtk2' 'libxss' 'nss' 'libpulse' 'qt5-webchannel' 'qt5-x11extras')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qcef/archive/$pkgver.tar.gz"
        "cef-$_cefcommit.tar.gz::https://github.com/linuxdeepin/cef-binary/archive/$_cefcommit.tar.gz")
        # http://opensource.spotify.com/cefbuilds/cef_binary_${_cefver}_linux64_minimal.tar.bz2
        # https://raw.githubusercontent.com/cztomczak/cefpython/master/patches/issue231.patch)
sha512sums=('a1be97eeac72c28229d745e363343a7a99ecdc6ffcca3ae663a9286b537916782ba3750b737123a329ce34be3c154955db6a322bc999fde0764987a4a88a513b'
            '20c931fb6df3cadb13373fb9cd6f7ac9daf468edafb4b7fa603f1c25e1d5bcb8550bf783273207e1724b56e99b063af8f20ff18c4017fbf73b9f8d84de854481')

prepare() {
  mkdir -p build

  # TODO: build cef instead of using binary distribution
  rmdir qcef-$pkgver/cef
  mv cef-binary-$_cefcommit qcef-$pkgver/cef

  # mv cef_binary_${_cefver}_linux64_minimal qcef-$pkgver/cef
  # cd qcef-$pkgver/cef
  # tar czvf cef.x86_64.tar.gz Release Resources
}

build() {
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ../qcef-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  chmod +s "$pkgdir"/usr/lib/qcef/chrome-sandbox
  rm -rf "$pkgdir"/build
}
