# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-terminal
pkgver=5.5.2
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=(x86_64)
url=https://github.com/elementary/terminal
license=(GPL3)
depends=(
  glib2
  gtk3
  libgranite.so
  pango
  vte3
)
makedepends=(
  appstream
  git
  intltool
  meson
  vala
)
groups=(pantheon)
source=(pantheon-terminal::git+https://github.com/elementary/terminal.git#tag=5c383d7f4cd31f3fad446ecaf84708ed81485608)
sha256sums=(SKIP)

pkgver() {
  cd pantheon-terminal

  git describe --tags
}

build() {
  arch-meson pantheon-terminal build \
    -Dubuntu-bionic-patched-vte=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
