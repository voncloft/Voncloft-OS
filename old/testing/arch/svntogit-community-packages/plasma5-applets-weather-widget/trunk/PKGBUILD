# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Martin Kostoln√Ω <clearmartin at zoho dot com>

pkgname=plasma5-applets-weather-widget
pkgver=1.6.10
pkgrel=3
pkgdesc="Plasma applet for displaying weather information from yr.no server"
arch=(x86_64)
url="https://kde-apps.org/content/show.php/Weather+Widget?content=169572"
license=(GPL)
depends=(plasma-workspace qt5-graphicaleffects qt5-xmlpatterns)
makedepends=(extra-cmake-modules)
source=($pkgname-$pkgver.tar.gz::"https://github.com/kotelnik/plasma-applet-weather-widget/archive/v$pkgver.tar.gz")
sha256sums=('9557441285121be52bfe27fa2bb214e865132b8581953faf88f1fae4ae1e87f2')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../plasma-applet-weather-widget-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
