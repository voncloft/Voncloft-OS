# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=text-icu
pkgname=haskell-text-icu
pkgver=0.7.0.1
pkgrel=36
pkgdesc="Bindings to the ICU library"
url="https://github.com/bos/text-icu"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'icu')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        icu68.patch)
sha512sums=('54551ce0d629bffcca32fc855bfcdf2e24282359ce3a32578a98de4f4ad15378fa7df98c59d73323549a2ac873b10a10202bda02f5be83fbe1ab945aac5e50a9'
            '315e366a2d428df2a1ff0064fea15c26cce4581754e9259c832b0f4ad71a50b96e02e9d76548e93f5d7682930d0826d02766db17105110bcf06add542aa67a77')

prepare() {
    cd $_hkgname-$pkgver
    patch -Np1 -i ../icu68.patch
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
