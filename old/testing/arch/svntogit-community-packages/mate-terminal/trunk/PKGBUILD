# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-terminal
pkgver=1.24.1
pkgrel=1
pkgdesc="The MATE Terminal Emulator"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('libsm' 'gettext' 'mate-desktop' 'vte3')
makedepends=('itstool' 'python')
groups=('mate-extra')
conflicts=('mate-terminal-gtk3')
replaces=('mate-terminal-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('550d38f223d21ab12d39b00af6cd75f083d3790c38d53051537df2ac6a87be62')

build() {
	cd "${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
