# Maintainer: David Runge <dave@sleepmap.de>
pkgname=ssr
pkgver=0.5.0
pkgrel=3
pkgdesc="A tool for real-time spatial audio reproduction"
arch=('x86_64')
url="http://spatialaudio.net/ssr/"
license=('GPL3')
groups=('pro-audio')
depends=('fftw' 'glu' 'jack' 'qt5-base')
makedepends=('asio' 'ecasound' 'gendesk' 'help2man' 'qt5-tools')
source=("https://github.com/SoundScapeRenderer/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('89234be15d4607278a7e91935300bd60aec3a4b21f006a54059ccbdba2e3eac48e2869f58172dd4569d188aad45b04f14516c49b157f44c04482656abd103dfe')
_name="SoundScape Renderer"

prepare() {
  declare -A descriptions=(
    ["${pkgname}"]="${_name}: ${pkgdesc}"
    ["${pkgname}-aap"]="${_name} (Ambisonics Amplitude Panner): ${pkgdesc}"
    ["${pkgname}-binaural"]="${_name} (Binaural): ${pkgdesc}"
    ["${pkgname}-brs"]="${_name} (Binaural Room Synthesis): ${pkgdesc}"
    ["${pkgname}-dca"]="${_name} (Distance-coded Ambisonics): ${pkgdesc}"
    ["${pkgname}-generic"]="${_name} (Generic): ${pkgdesc}"
    ["${pkgname}-vbap"]="${_name} (Vector Based Amplitude Panning): ${pkgdesc}"
    ["${pkgname}-wfs"]="${_name} (Wave Field Synthesis): ${pkgdesc}"
  )
  cd "$pkgname-$pkgver"
  autoreconf -vfi
  for name in "${!descriptions[@]}"; do
    gendesk -n \
            --pkgname ${name} \
            --name ${name} \
            --pkgdesc "${descriptions[$name]}" \
            --genericname "Spatial Audio Renderer" \
            --categories "AudioVideo;Audio"
  done
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --disable-vrpn
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 *.desktop -t "${pkgdir}/usr/share/applications"
}
