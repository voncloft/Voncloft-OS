# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-wsgiproxy2
pkgver=0.4.6
pkgrel=5
pkgdesc="A WSGI Proxy with various http client backends"
arch=('any')
license=('MIT')
url="https://github.com/gawel/WSGIProxy2/"
depends=('python-webob' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-requests' 'python-webtest' 'python-waitress')
source=("https://github.com/gawel/WSGIProxy2/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('9a3c8e96cafe4451943f63bbf47cc0f6ffc7a6916cd8e42f7d1aa53ccab59a74f772c55c1a1f456e97e72c5d15f1601c6501e2c0650bb54c81acd52696580023')

build() {
  cd WSGIProxy2-$pkgver
  python setup.py build
}

check() {
  cd WSGIProxy2-$pkgver
  nosetests3
}

package() {
  cd WSGIProxy2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
