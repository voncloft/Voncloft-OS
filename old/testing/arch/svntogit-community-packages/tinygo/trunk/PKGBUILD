# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=tinygo
pkgver=0.16.0
pkgrel=2
pkgdesc='Go compiler for small places. Microcontrollers, WebAssembly, and command-line tools. Based on LLVM.'
arch=(x86_64)
url='https://github.com/tinygo-org/tinygo'
license=(BSD)
options=(!strip)
depends=(llvm-libs clang lld)
makedepends=(go llvm git)
# use git as we need to fetch submodules
source=(git+https://github.com/tinygo-org/tinygo#tag=v$pkgver
        git+https://github.com/NordicSemiconductor/nrfx
        git+https://github.com/ARM-software/CMSIS
        git+https://github.com/avr-rust/avr-mcu
        git+https://github.com/tinygo-org/cmsis-svd
        git+https://github.com/llvm-mirror/compiler-rt#branch=release_80
        git+https://github.com/CraneStation/wasi-libc
        git+https://github.com/keith-packard/picolibc
        disable_static_llvm.patch)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'b6b846b6ca21c200dc842f669422454146b5358f83a89a4946c1e54ed6953190')

prepare() {
  cd tinygo

  git submodule init
  git config -f .gitmodules 'submodule.lib/nrfx.url' "$srcdir/nrfx"
  git config -f .gitmodules 'submodule.lib/CMSIS.url' "$srcdir/CMSIS"
  git config -f .gitmodules 'submodule.lib/avr.url' "$srcdir/avr-mcu"
  git config -f .gitmodules 'submodule.lib/cmsis-svd.url' "$srcdir/cmsis-svd"
  git config -f .gitmodules 'submodule.lib/compiler-rt.url' "$srcdir/compiler-rt"
  git config -f .gitmodules 'submodule.lib/wasi-libc.url' "$srcdir/wasi-libc"
  git config -f .gitmodules 'submodule.lib/picolibc.url' "$srcdir/picolibc"

  git submodule sync
  git submodule update

  patch -p1 < ../disable_static_llvm.patch
}

build() {
  cd tinygo
  make build/release
}

check() {
  cd tinygo
  # it reqreires a lot of dependencies. todo enable the tests
  # make test tinygo-test smoketest
}

package() {
  cd tinygo

  install -d "$pkgdir"/usr/bin "$pkgdir"/usr/lib/tinygo
  mv build/release/tinygo/* "$pkgdir"/usr/lib/tinygo
  ln -s /usr/lib/tinygo/bin/tinygo "$pkgdir"/usr/bin/tinygo
}
