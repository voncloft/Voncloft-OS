# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=lib32-rust
pkgver=1.37.0
pkgrel=2

_llvm_ver=8.0.1

pkgdesc='32-bit libraries for rust'
url='https://www.rust-lang.org/'
arch=('x86_64')
license=('MIT' 'Apache')

makedepends=("llvm=$_llvm_ver" 'libffi' 'perl' 'python' 'curl' 'cmake')
depends=('lib32-gcc-libs' "rust=1:$pkgver-$pkgrel")

source=("https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"{,.asc}
        "https://github.com/llvm/llvm-project/releases/download/llvmorg-$_llvm_ver/compiler-rt-$_llvm_ver.src.tar.xz"{,.sig}
        config.toml)

sha256sums=('120e7020d065499cc6b28759ff04153bfdc2ac9b5adeb252331a4eb87cbe38c3'
            'SKIP'
            '11828fb4823387d820c6715b25f6b2405e60837d12a7469e7a8882911c721837'
            'SKIP'
            'b7dee253d4e55a34ce07e0f2b9f15197285458565a38eabf9d4d49870f133e68')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE'  # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>
              '474E22316ABF4785A88C6E8EA2C794A986419D8A') # Tom Stellard <tstellar@redhat.com>

prepare() {
  cd "rustc-$pkgver-src"

  cp "$srcdir"/config.toml config.toml
}

build() {
  cd "rustc-$pkgver-src"

  export RUST_BACKTRACE=1
  export RUST_COMPILER_RT_ROOT="$srcdir/compiler-rt-$_llvm_ver.src"

  python ./x.py build -j"$(nproc)"
}

package() {
  cd "rustc-$pkgver-src"

  local target="$pkgdir/$(rustc --print sysroot)/lib/rustlib"
  install -d "$target"
  cp -r \
    build/x86_64-unknown-linux-gnu/stage2/lib/rustlib/i686-unknown-linux-gnu \
    "$target"

  for license in APACHE MIT; do install -Dm644 "LICENSE-$license" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done
}

# vim:set ts=2 sw=2 et:
