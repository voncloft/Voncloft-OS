# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=stalonetray
pkgver=0.8.3
pkgrel=4
pkgdesc="STAnd-aLONE sysTRAY. It has minimal build and run-time dependencies: the Xlib only."
arch=('x86_64')
url="https://github.com/kolbusa/stalonetray"
license=('GPL')
depends=('libx11' 'libxpm')
makedepends=('libxslt' 'docbook-xsl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kolbusa/stalonetray/archive/v$pkgver.tar.gz")
sha512sums=('5aee6a0bc85f90ba5ff187da8d9c510d301b794ebdfe36abce2643a0426dc4495da0e5c696c61af201e95e4e05b6414e90d8fcd5c047417ad8f7b2aba6affae0')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  XSLVER=$(pacman -Q docbook-xsl | cut -d' ' -f2 | cut -d- -f1)
  sed -i "s|/usr/share/sgml/docbook/xsl-stylesheets/manpages/docbook.xsl|/usr/share/xml/docbook/xsl-stylesheets-$XSLVER/manpages/docbook.xsl|g" Makefile.am
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  test -x configure || aclocal && autoheader && autoconf && automake --add-missing
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 stalonetrayrc.sample.in "$pkgdir"/etc/stalonetrayrc # FIXME
}
