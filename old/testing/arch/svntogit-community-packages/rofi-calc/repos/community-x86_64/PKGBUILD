# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=rofi-calc
pkgver=2.0.0
pkgrel=1
pkgdesc="Do calculations in rofi"
arch=('x86_64')
url="https://github.com/svenstaro/rofi-calc"
license=(MIT)
depends=('rofi' 'libqalculate')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/svenstaro/rofi-calc/archive/v${pkgver}.tar.gz)
sha512sums=('298f9983dbd140f5aee5b3799b165500eae100f3f5744ba07914b962faf01661dee0f5f4c9c12a07a4e210b6115ac6fb377d9fa9a7e64ef5a19600e9f55cd478')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  autoreconf -i
  mkdir build
}

build() {
  cd "$srcdir/$pkgname-$pkgver/build"

  ../configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"

  make DESTDIR="$pkgdir/" install

  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
