# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Andrew Sun <adsun701 at gmail dot com>
# Contributor: Manuel Hüsers <manuel dot huesers at uni-ol dot de>
# Contributor: fstirlitz <felix dot von dot s at posteo dot de>

pkgname=libxmlbird
pkgver=1.2.12
pkgrel=1
pkgdesc='XML parsing library written in Vala'
arch=(x86_64)
url='https://birdfont.org/xmlbird.php'
license=(LGPL3)
depends=(glib2)
makedepends=(python vala)
source=(https://birdfont.org/xmlbird-releases/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('0450d632c652c77511b5ee98415013e0043690b37245ada078a4dc11e3cee497'
            'SKIP')
validpgpkeys=('FB3BEFA59A6FF7F0E0682B68BCD31D4CCCEB9DD4') # Johan Mattsson

build() {
  cd $pkgname-$pkgver
  ./configure --prefix /usr --libdir lib --cflags "$CFLAGS" --ldflags "$LDFLAGS"
  ./build.py
}

package() {
  cd $pkgname-$pkgver
  ./install.py --dest "$pkgdir"
}
