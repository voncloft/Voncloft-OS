# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Olaf Leidinger <oleid@mescharet.de>
# Contributor: fabien Cellier <fabien.cellier@gmail.com>

pkgname=pocl
pkgver=1.6
pkgrel=1
pkgdesc="Portable OpenCL is an open-source implementation of OpenCL which can be easily adapted for new targets"
arch=('x86_64')
url="http://portablecl.org/"
license=('GPL')
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz"
        llvm11.patch)
depends=('clang' 'hwloc' 'opencl-icd-loader')
makedepends=('llvm' 'cmake' 'opencl-headers' 'ocl-icd' 'ninja')
sha512sums=('ecd3a3867a62a2a555183741fa4586eeefae00514d66d15a79b9094ff0a0248b213f62ab23b3e61bdb1042e760cd34b07e546f97b304ac86c3f861a9161df1b1'
            'ef149678347c88ee3c1bfef62c0b35667b2f613cc08a90f237b6f9d1a5f3d616fe5d6513bbda36ead5969fc6d32cace7d02a3c4c6230ab5144db94a0af56db30')

build() {
  cd "$pkgname-$pkgver"
  cmake \
      -GNinja \
      -Bbuild \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_BUILD_TYPE=Release \
      -DKERNELLIB_HOST_CPU_VARIANTS=distro
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build check
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir"/ ninja -C build install
}
