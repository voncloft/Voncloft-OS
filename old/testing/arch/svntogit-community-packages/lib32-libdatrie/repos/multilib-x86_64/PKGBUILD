# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Chaiwat Suttipongsakul <cwt114@gmail.com>

pkgname=lib32-libdatrie
pkgver=0.2.12
pkgrel=1
pkgdesc='Implementation of double-array structure for representing trie, as proposed by Junichi Aoe.'
url='https://linux.thai.net/projects/datrie'
license=('LGPL')
arch=('x86_64')
depends=('lib32-glibc' 'libdatrie')
makedepends=('gcc-multilib')
source=("https://linux.thai.net/pub/thailinux/software/libthai/libdatrie-${pkgver}.tar.xz")
sha256sums=('452dcc4d3a96c01f80f7c291b42be11863cd1554ff78b93e110becce6e00b149')

build() {
  cd libdatrie-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-static
  make
}

package() {
  cd libdatrie-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
