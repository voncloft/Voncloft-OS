# Maintainer: David Runge <dvzrv@archlinux.org>

# needs rebuild on minor soname bumps

pkgname=libiio
pkgver=0.21
pkgrel=4
pkgdesc="Interface to the Linux Industrial Input/Output (IIO) Subsystem"
arch=('x86_64')
url="https://github.com/analogdevicesinc/libiio"
license=('LGPL2.1')
depends=('gcc-libs' 'glibc' 'libaio' 'libserialport' 'libxml2')
makedepends=('cmake' 'libusb' 'python' 'python-setuptools')
optdepends=('python: Python bindings')
provides=('libiio.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/analogdevicesinc/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('5bbacd6a1141654f4825f00c1c2b27223e6cc324c80d7bcbb49e29997a83d585e45c0c52331ca10ee881c2f340b535984c3d667b61d8377a77e5c0f5cb1d2dc0')

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_INSTALL_SBINDIR='bin' \
        -DCMAKE_INSTALL_LIBDIR='lib' \
        -DCMAKE_BUILD_TYPE='None' \
        -DPYTHON_BINDINGS=ON \
        -DSYSTEMD_UNIT_INSTALL_DIR='/usr/lib/systemd/system' \
        -DUDEV_RULES_INSTALL_DIR='/usr/lib/udev/rules.d' \
        -DWITH_MAN=ON \
        -DWITH_SYSTEMD=ON \
        -Wno-dev \
        -B build \
        -S .
  make -C build VERBOSE=1
}

package() {
  depends+=('libusb-1.0.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" -C build install
  install -vDm 644 {CONTRIBUTING,Contributors,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
