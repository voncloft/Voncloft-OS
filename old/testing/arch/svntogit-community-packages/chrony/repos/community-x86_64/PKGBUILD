# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>
# Contributor: cdhotfire <cdhotfire@gmail.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Elisamuel Resto <ryuji@simplysam.us>

pkgname=chrony
pkgver=3.5.1
pkgrel=1
pkgdesc='Lightweight NTP client and server'
url='https://chrony.tuxfamily.org/'
arch=('x86_64')
license=('GPL2')
depends=('glibc' 'nss' 'readline' 'libcap' 'libcap.so' 'libedit' 'libseccomp' 'libseccomp.so' 'nettle')
makedepends=('asciidoctor')
options=('!emptydirs')
backup=('etc/chrony.conf')
source=(https://download.tuxfamily.org/chrony/${pkgname}-${pkgver}.tar.gz
        ${pkgname}-${pkgver}.tar.gz.asc::https://download.tuxfamily.org/chrony/${pkgname}-${pkgver}-tar-gz-asc.txt
        chrony.sysusers
        chrony.tmpfiles
        fix-util-unit-test-for-NTP-era-split.patch)
sha256sums=('1ba82f70db85d414cd7420c39858e3ceca4b9eb8b028cbe869512c3a14a2dca7'
            'SKIP'
            '56aa817638c8f959169d514f1e7fc727ef31e7d3beda4fca5165292c7602ee42'
            'c0b52787f68f3ef365eef64f53f22b4fb0046a91f0e38923f9d16f1ca19c3f3a'
            '69fad197b790f1fdb273b9d1905c3025dd38abf82fa93ffe675d096bf0f3d45b')
validpgpkeys=('8B1F4A9ADA73D401E3085A0B5FF06F29BA1E013B') # Miroslav Lichvar <mlichvar@redhat.com> has a dsa1024 key *in the wild*

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 < ../fix-util-unit-test-for-NTP-era-split.patch
  sed -i \
    -e 's|pool pool.ntp.org|pool 2.arch.pool.ntp.org|g' \
    -e 's|server foo.example.net|server 0.arch.pool.ntp.org|g' \
    -e 's|server bar.example.net|server 1.arch.pool.ntp.org|g' \
    -e 's|server baz.example.net|server 3.arch.pool.ntp.org|g' \
    examples/chrony.conf.example*
  sed 's|/usr/sbin|/usr/bin|g' -i examples/*.service
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --enable-scfilter \
    --enable-ntp-signd \
    --with-user=chrony \
    --with-sendmail=/usr/bin/sendmail \
    --with-hwclockfile=/etc/adjtime \
    --sbindir=/usr/bin \
    --with-pidfile=/run/chrony/chronyd.pid
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install install-docs

  install -Dm 644 "${srcdir}/chrony.sysusers" "${pkgdir}/usr/lib/sysusers.d/chrony.conf"
  install -Dm 644 "${srcdir}/chrony.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/chrony.conf"
  install -Dm 644 examples/{chronyd,chrony-wait}.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 examples/chrony.logrotate "${pkgdir}/etc/logrotate.d/chrony"
  install -Dm 644 README NEWS FAQ -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"

  install -Dm 644 examples/chrony.conf.example3 "${pkgdir}/etc/chrony.conf"
  sed -i \
    -e '/^! pool /s/^! //' \
    -e '/^! leapsectz right\/UTC/s/^! //' \
    -e '/^! makestep 1.0 3/s/^! //' \
    -e '/^! rtcsync/s/^! //' \
    "${pkgdir}/etc/chrony.conf"

  rmdir "${pkgdir}"{/var/lib/chrony,/var/lib}
}

# vim: ts=2 sw=2 et:
