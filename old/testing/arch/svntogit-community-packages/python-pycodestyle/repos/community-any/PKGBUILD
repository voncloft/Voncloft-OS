# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgbase=python-pycodestyle
pkgname=('python-pycodestyle' 'python2-pycodestyle')
pkgver=2.6.0
pkgrel=3
pkgdesc="Python style guide checker"
arch=('any')
url="https://github.com/PyCQA/pycodestyle"
license=('custom:Expat')
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/PyCQA/pycodestyle/archive/$pkgver.tar.gz")
sha512sums=('251b2526edc9759aa07b43c23c2785ebf2b7c4cca7e70444b8f79ff72cfb842ca49b929c01faba307534e4f213247597739ba777181fca6048d6cfae67cd25b6')

prepare() {
  cp -a pycodestyle-$pkgver{,-py2}

  sed -i '1s/env python$/python2/' pycodestyle-$pkgver-py2/pycodestyle.py
}

build() {
  cd "$srcdir"/pycodestyle-$pkgver
  python setup.py build

  cd "$srcdir"/pycodestyle-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pycodestyle-$pkgver
  python -m unittest discover

  cd "$srcdir"/pycodestyle-$pkgver-py2
  python2 -m unittest discover
}

package_python-pycodestyle() {
  depends=('python-setuptools')
  replaces=('pep8-python3' 'pep8')

  cd pycodestyle-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir/"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pycodestyle() {
  depends=('python2-setuptools')
  replaces=('pep8-python2' 'python2-pep8')

  cd pycodestyle-$pkgver-py2
  python2 setup.py install -O1 --prefix=/usr --root="$pkgdir/"
  mv "$pkgdir"/usr/bin/pycodestyle{,2}

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
