# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=entr
pkgver=4.6
pkgrel=1
pkgdesc='Run arbitrary commands when files change'
arch=('x86_64')
url='http://eradman.com/entrproject'
license=('MIT')
validpgpkeys=(519151D83E83D40A232B4D615C418B8631BC7C26) # Eric Radman <ericshane@eradman.com>
source=("$url/code/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('41e8c2311c7833a778193b13d1e0b1bb18ece54f5e20b8b5b0b4a2308ba1cc1a8b8378700dbac83ee68822b5f847a3fa9c47822d22a22d45da792d2408f48378'
            'SKIP')

build() {
  cd $pkgname-$pkgver

  export PREFIX='/usr'

  ./configure

  make
}

check() {
  cd $pkgname-$pkgver

  make test
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
