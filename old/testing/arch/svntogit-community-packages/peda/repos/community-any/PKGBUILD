# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Christian Rebischke <echo Q2hyaXMuUmViaXNjaGtlQGdtYWlsLmNvbQo= | base64 -d>

pkgname=peda
pkgver=1.1
pkgrel=4
pkgdesc='Python Exploit Development Assistance for GDB'
url='https://github.com/longld/peda'
arch=('any')
license=('custom:Creative Commons')
depends=('gdb' 'binutils' 'nasm' 'python-six')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/longld/peda/archive/v${pkgver}.tar.gz)
sha512sums=('fab97c2edf36605632639b38ec995a1fd06a7c309b2a50d4bd30dae257cc359eee3751e0792f39b408dc5b62a206e339ee4a4688bfcf60c5daeb8c032a16aaaa')

prepare() {
  cd ${pkgname}-${pkgver}
  rm lib/six.py
  sed '/git clone/d' -i README.md
  sed 's|~/peda/peda.py|/usr/share/peda/peda.py|g' -i README.md
}

build() {
  cd ${pkgname}-${pkgver}
  echo 'Compiling python objects...'
  python -m compileall . >/dev/null
  python -O -m compileall . >/dev/null
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 644 *.py -t "${pkgdir}/usr/share/${pkgname}"
  install -Dm 644 __pycache__/* -t "${pkgdir}/usr/share/${pkgname}/__pycache__"
  install -Dm 644 lib/*.py -t "${pkgdir}/usr/share/${pkgname}/lib"
  install -Dm 644 lib/__pycache__/* -t "${pkgdir}/usr/share/${pkgname}/lib/__pycache__"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim: ts=2 sw=2 et:
