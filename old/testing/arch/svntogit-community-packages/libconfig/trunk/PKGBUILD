# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: scippio <scippio@seznam.cz>

pkgname=libconfig
pkgver=1.7.2
pkgrel=3
pkgdesc="C/C++ Configuration File Library"
arch=('x86_64')
url="https://github.com/hyperrealm/libconfig"
license=('LGPL')
depends=('gcc-libs')
makedepends=('texinfo')
provides=('libconfig++.so' 'libconfig.so')
options=('!emptydirs' 'zipman')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hyperrealm/libconfig/archive/v$pkgver.tar.gz")
sha512sums=('9df57355c2d08381b4a0a6366f0db3633fbe8f73c2bb8c370c040b0bae96ce89ee4ac6c17a5a247fed855d890fa383e5b70cb5573fc9cfc62194d5b94e161cee')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  rm "$pkgdir/usr/share/info/dir"
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
# vim:set ts=2 sw=2 et:
