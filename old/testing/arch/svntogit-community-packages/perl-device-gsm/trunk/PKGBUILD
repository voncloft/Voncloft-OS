# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-device-gsm
pkgver=1.61
pkgrel=4
pkgdesc="Perl extension to interface GSM phones / modems"
arch=('any')
url="https://search.cpan.org/dist/Device-Gsm/"
license=('PerlArtistic')
depends=('perl-device-modem')
provides=('perl-device-gsm-charset' 'perl-device-gsm-networks'
	  'perl-device-gsm-pdu' 'perl-sms-token' 'perl-sms-token-da'
	  'perl-sms-token-dcs' 'perl-sms-token-mr' 'perl-sms-token-oa'
	  'perl-sms-token-pdutype' 'perl-sms-token-pid' 'perl-sms-token-sca'
	  'perl-sms-token-scts' 'perl-sms-token-ud' 'perl-sms-token-vp')
options=(!emptydirs)
source=("https://search.cpan.org/CPAN/authors/id/C/CO/COSIMO/Device-Gsm-$pkgver.tar.gz")
sha256sums=('3061ff08dd1303de46ac82fa8442a688824a9286aaaf6963ebd6743a5ca407c2')

build() {
  cd  "$srcdir"/Device-Gsm-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/Device-Gsm-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
