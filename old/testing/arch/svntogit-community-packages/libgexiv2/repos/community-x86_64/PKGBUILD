# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgexiv2
pkgver=0.12.1
pkgrel=4
pkgdesc='GObject-based wrapper around the Exiv2 library'
url='https://wiki.gnome.org/Projects/gexiv2'
arch=(x86_64)
license=(GPL2)
depends=(exiv2 glib2)
makedepends=(git gobject-introspection meson python-gobject vala)
source=("git+https://gitlab.gnome.org/GNOME/gexiv2#commit=a904953bb912ce383227a1a464bbc76b837046c7") # tag: gexiv2-0.12.1
sha256sums=('SKIP')

build() {
  arch-meson gexiv2 build -Denable-gtk-doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
