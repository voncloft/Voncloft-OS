# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=zfp
pkgver=0.5.5
pkgrel=3
pkgdesc="Compressed numerical arrays that support high-speed random access"
arch=(x86_64)
url="https://computing.llnl.gov/projects/floating-point-compression"
license=(BSD)
depends=(glibc gcc-libs)
makedepends=(cmake gcc-fortran cython python python-numpy)
source=(https://github.com/LLNL/zfp/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('6a7f4934489087d9c117a4af327fd6495ea757924f4df467b9c537abf8bd86c4')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_CFP=ON \
    -DBUILD_ZFPY=ON \
    -DBUILD_ZFORP=ON
  make -C build
}

check() {
  cd build
  LD_LIBRARY_PATH="$(pwd)"/lib/ ctest -v --output-on-failure
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -D build/bin/zfp -t "${pkgdir}"/usr/bin
  install -Dm644 ${pkgname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
