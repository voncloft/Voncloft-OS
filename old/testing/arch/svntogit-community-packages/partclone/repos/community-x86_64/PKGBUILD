# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contrinutor: Dan Serban
# Contributor: Franz Burgmann
# Contributor: Todd Partridge (Gen2ly)
# Contributor: Ivan Sichmann Freitas

pkgname=partclone
pkgver=0.3.17
pkgrel=1
pkgdesc="Utilities to save and restore used blocks on a partition"
arch=('x86_64')
url="https://partclone.org"
license=('GPL')
depends=('progsreiserfs' 'ntfs-3g' 'nilfs-utils')
source=(https://partclone.nchc.org.tw/download/unstable/$pkgname-$pkgver.tar.gz)
md5sums=('faab4ae8a7629b77ed11f61bc50b753a')

build() {
  cd $pkgname-$pkgver

  ./autogen
  ./configure --prefix=/usr --enable-extfs --enable-reiserfs --enable-fat \
    --enable-hfsp --enable-btrfs --enable-ncursesw --enable-ntfs \
    --enable-exfat --enable-f2fs --enable-minix --enable-nilfs2 --enable-xfs \
    --sbindir=/usr/bin

  make
}

package() {
  cd $pkgname-$pkgver

  make PREFIX=/usr DESTDIR="$pkgdir" install
}
