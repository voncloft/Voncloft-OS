# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jackmeter
pkgver=0.4
pkgrel=2
pkgdesc="A basic console based DPM (Digital Peak Meter) for JACK"
arch=('x86_64')
url="https://www.aelius.com/njh/jackmeter/"
license=('GPL2')
groups=('pro-audio')
depends=('glibc' 'libjack.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/njh/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('29a80230eaada881ed96d8db3c176f4db75d68120ff95a5d3f20f02c3093315557532d4fd0995bbd80e133c68f27ac2920dce3a943e4f89d3fd3cbea87775dc4')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,NEWS,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
