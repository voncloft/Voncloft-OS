# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=texvc
pkgver=1.28
pkgrel=3
_hash=1097ee7
pkgdesc="tex to HTML/MathML/png converter used by mediawiki"
arch=(x86_64)
url="https://www.mediawiki.org/wiki/Extension:Math"
license=("GPL")
depends=('imagemagick' 'ghostscript' 'texlive-core' 'texlive-latexextra')
makedepends=('ocaml' 'git')
#source=(mediawiki-math-$pkgver::git+https://git.wikimedia.org/git/mediawiki/extensions/Math.git#branch=REL${pkgver/./_})
source=("mediawiki-math-${pkgver}.tar.gz::https://codeload.github.com/wikimedia/mediawiki-extensions-Math/legacy.tar.gz/REL${pkgver/./_}")
sha256sums=('554d61a1d4ea383687dbdebd5c8b715cd846e9ca3f3e32e124a5559f7e0a5b76')

prepare() {
  cd "$srcdir"
  mv wikimedia-mediawiki-extensions-Math-${_hash} mediawiki-math-$pkgver
}

build() {
  cd "$srcdir"/mediawiki-math-$pkgver/math
  make
}

package() {
  cd "$srcdir"/mediawiki-math-$pkgver/math
  install -d -m 0755 "$pkgdir"/usr/bin
  install -m 0755 texvc "$pkgdir"/usr/bin
}
