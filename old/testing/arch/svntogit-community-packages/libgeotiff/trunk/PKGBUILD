# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=libgeotiff
pkgver=1.6.0
pkgrel=2
pkgdesc="A TIFF based interchange format for georeferenced raster imagery"
arch=('x86_64')
url="https://github.com/OSGeo/libgeotiff"
license=('custom')
depends=('libtiff' 'proj' 'libjpeg-turbo')
changelog=$pkgname.changelog
source=(https://github.com/OSGeo/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('9311017e5284cffb86f2c7b7a9df1fb5ebcdc61c30468fb2e6bca36e4272ebca')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/libgeotiff/LICENSE
}
