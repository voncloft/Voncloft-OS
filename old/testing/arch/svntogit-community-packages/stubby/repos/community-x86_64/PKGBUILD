# Maintainer: Bruno Pagani <archange@archlinux.org> 

pkgname=stubby
pkgver=0.3.0
pkgrel=1
pkgdesc="DNS Privacy stub resolver"
arch=(x86_64)
url="https://dnsprivacy.org/wiki/display/DP/DNS+Privacy+Daemon+-+Stubby"
license=(BSD)
backup=(etc/stubby/stubby.yml)
depends=(getdns libsystemd libyaml)
makedepends=(cmake)
source=(https://github.com/getdnsapi/stubby/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        stubby-fix-destdir-part1.patch::https://github.com/getdnsapi/stubby/commit/2fec82b1669bb35062ff9029c4b42fb4998fbd44.patch
        stubby-fix-destdir-part2.patch::https://github.com/getdnsapi/stubby/commit/c4841747a8042dfe2ea43c3c58d07a87e5a5a0ed.patch)
sha256sums=('b37a0e0ec2b7cfcdcb596066a6fd6109e91a2766b17a42c47d3703d9be41d000'
            'd8ee3dd5834c214cb2ac5eeafb91af60ceb4fa4bc948d6008198b822ff92848a'
            'fa5739135f2cbdfa3fe53793f9cf23a487167d882a3df31830f3c87b4e8ecd55')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's/^# appdata_dir:/appdata_dir:/g' -e 's:/etc/unbound/getdns-root.key:/etc/trusted-key.key:g' -i stubby.yml.example
  patch -p1 -i ../stubby-fix-destdir-part1.patch
  patch -p1 -i ../stubby-fix-destdir-part2.patch
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_BUILD_TYPE=Release
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  rmdir "${pkgdir}"/var{/run,}
  cd ${pkgname}-${pkgver}
  install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
  install -Dm644 systemd/${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
}
