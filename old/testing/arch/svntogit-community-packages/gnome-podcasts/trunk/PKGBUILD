# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-podcasts
pkgver=0.4.8
pkgrel=2
pkgdesc="Podcast application for GNOME"
arch=(x86_64)
url="https://gitlab.gnome.org/World/podcasts"
license=(GPL3)
depends=(gst-plugins-bad gst-plugins-base gst-plugins-good libhandy0)
makedepends=(git meson rust)
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs')
source=(https://gitlab.gnome.org/World/podcasts/-/archive/$pkgver/podcasts-$pkgver.tar.gz)
sha256sums=('f29c0057dfcdd6ab1745533cc6826f07adddbe3b5b9aad2757421d1259b8303c')

build() {
  cd podcasts-$pkgver
  arch-meson . build
  meson compile -C build
}

package() {
  cd podcasts-$pkgver
  DESTDIR="$pkgdir" meson install -C build
}
