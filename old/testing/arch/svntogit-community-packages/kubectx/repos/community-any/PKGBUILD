# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ian Beringer <ian@ianberinger.com>

pkgname=kubectx
pkgver=0.9.1
pkgrel=3
pkgdesc='Utility to manage and switch between kubectl contexts and Kubernetes namespaces'
url='https://github.com/ahmetb/kubectx'
arch=('any')
license=('Apache')
depends=('bash' 'kubectl')
provides=('kubectx' 'kubens')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('f9872bb2a0a354d6e4b07715dea434482fb61f34914354d0d458e723d87b41e09eb94fb302185631700aad09a29268e00925016fa1b2d4c80f30402f35b6e6e4')
b2sums=('b6d79d5cc7a1a4b55e76f9402d97629bf5ca5e97ab8c42d492640108019e8cc7011cb27422e327e6abbed9a59f684c634378871d7c7145618280a0588bc228ec')

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm 755 -p kubectx kubens -t "${pkgdir}/usr/bin"

  # completion
  install -Dm 644 -p completion/kubectx.bash "${pkgdir}/usr/share/bash-completion/completions/kubectx"
  install -Dm 644 -p completion/kubens.bash "${pkgdir}/usr/share/bash-completion/completions/kubens"
  install -Dm 644 -p completion/kubectx.zsh "${pkgdir}/usr/share/zsh/site-functions/_kubectx"
  install -Dm 644 -p completion/kubens.zsh "${pkgdir}/usr/share/zsh/site-functions/_kubens"
  install -Dm 644 -p completion/kubectx.fish "${pkgdir}/usr/share/fish/vendor_completions.d/kubectx.fish"
  install -Dm 644 -p completion/kubens.fish "${pkgdir}/usr/share/fish/vendor_completions.d/kubens.fish"
}

# vim: ts=2 sw=2 et:
