# Maintainer: AndyRTR <andyrtr@archlinux.org>

_pkgbasename=libxshmfence
pkgname=lib32-$_pkgbasename
pkgver=1.3
pkgrel=2
pkgdesc="a library that exposes a event API on top of Linux futexes (32-bit)"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('GPL')
#groups=()
depends=('lib32-glibc' ${_pkgbasename})
makedepends=('xorg-util-macros' 'xorgproto' 'gcc-multilib')
source=(${url}/releases/individual/lib/${_pkgbasename}-${pkgver}.tar.bz2{,.sig})
sha512sums=('c446e055f8fac62b9aa266132289a4cfc030282147974c45ce96d1768a98d1afb997470e58e4a68513174c404cbf373bdde2f0cd4b34abdbce1d89dd0b6fe2b7'
            'SKIP')
validpgpkeys=('C383B778255613DFDB409D91DB221A6900000011') # Keith Packard <keithp@keithp.com>
validpgpkeys+=('3BB639E56F861FA2E86505690FDD682D974CA72A') # "Matt Turner <mattst88@gmail.com>"

build() {
  cd $_pkgbasename-$pkgver

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  # fix some libtools errors
  autoreconf -vfi
	
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

check() {
  cd $_pkgbasename-$pkgver
  make -k check
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir/" install

  rm -r ${pkgdir}/usr/include
	
  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s ${_pkgbasename} "${pkgdir}/usr/share/licenses/${pkgname}"
}

