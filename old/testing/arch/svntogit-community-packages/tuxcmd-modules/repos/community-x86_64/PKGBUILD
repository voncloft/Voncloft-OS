# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=tuxcmd-modules
pkgver=0.6.70
pkgrel=6
pkgdesc='Modules for Tux Commander'
arch=('x86_64')
url='http://tuxcmd.sourceforge.net/'
license=('GPL')
depends=('tuxcmd')
makedepends=('sharutils')
optdepends=('gvfs: for gvfs module')
options=('!docs' '!emptydirs')
source=("https://downloads.sourceforge.net/project/tuxcmd/tuxcmd/tuxcmd-dev-${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        tuxcmd-module-gvfs_include-glib.patch
		tuxcmd-module-libarchive_libarchive-3.0.0a.patch
		tuxcmd-module-ziparchive-gcc-4.7-2.patch
		tuxcmd-module-ziparchive-gcc-4.7.patch)
sha512sums=('a89f33441380ca1cb6cc396ddc05ff969ce45edf204c4f4c6402b6b077f5dfb166073e862593efd782171e31f3dbc81a38e6a9e8898eb1ef46459109adff1f62'
            '43314fc5da886e451a494b7f486a253e70c0185e7a7c41c243ad955ac28e69fc696a9ded551b60712b5465220bd0e41e8f6d8417dc79a255a5d0231f017ba0af'
            '1d2c3c3d4f76aeb150619139ed2e359b3b90b4670b3ca35388ead2357ebebb442fc9ca438868749fcd6e40f9198cc53f8f5bfa24e996bb1d446f6f0d1ba210d5'
            '8a3256cd31c33f327b10212fbaed49a7130a5cb343a1a108dbcf83c40dcfd91ea46e73161ae6ee5143583cbd4b6b67683703bbd9d7eba5702820de915d849c11'
            '692d5fedd32944c203b9e02f53c4f6d778fe06f253e3abbfc49abd18e4532ce39b3088eb6efc04c399864c977435d411293cb114c1222285907c7326fbd2323c')

prepare() {
	cd ${pkgname}-${pkgver}
	patch -p1 -i ../tuxcmd-module-gvfs_include-glib.patch
	patch -p1 -i ../tuxcmd-module-libarchive_libarchive-3.0.0a.patch
	patch -p1 -i ../tuxcmd-module-ziparchive-gcc-4.7.patch
	patch -p1 -i ../tuxcmd-module-ziparchive-gcc-4.7-2.patch


	sed -i 's|$(INSTALL) \(.*\) $(DESTDIR)/lib$(LIB_SUFFIX)/tuxcmd/|$(INSTALL) -D \1 $(DESTDIR)/lib/tuxcmd/\1|' */Makefile
}

build() {
	cd ${pkgname}-${pkgver}

	make
}

package() {
	cd ${pkgname}-${pkgver}

	make DESTDIR=${pkgdir}/usr install
}
