# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgbase=luasocket
pkgname=(lua-socket lua51-socket lua52-socket lua53-socket)
pkgver=20200329
_commit=5b18e475f38fcf28429b1cc4b17baee3b9793a62
pkgrel=1
pkgdesc='Networking support library for the Lua language'
arch=('x86_64')
url='https://github.com/diegonehab/luasocket'
license=('MIT')
#options=('debug')
makedepends=('lua' 'lua51' 'lua52' 'lua53' 'git')
source=("git+https://github.com/diegonehab/luasocket.git#commit=${_commit}")
md5sums=('SKIP')

prepare() {
  cp -a luasocket luasocket-51
  cp -a luasocket luasocket-52
  cp -a luasocket luasocket-53
}

build() {
  cd "$srcdir"/luasocket-51
  MYCFLAGS="$CFLAGS" MYLDFLAGS=$LDFLAGS make LUAV=5.1 linux

  cd "$srcdir"/luasocket-52
  MYCFLAGS="$CFLAGS" MYLDFLAGS=$LDFLAGS make LUAV=5.2 linux

  cd "$srcdir"/luasocket-53
  MYCFLAGS="$CFLAGS" MYLDFLAGS=$LDFLAGS make LUAV=5.3 linux

  cd "$srcdir"/luasocket
  MYCFLAGS="$CFLAGS" MYLDFLAGS=$LDFLAGS make LUAV=5.4 linux
}

package_lua51-socket() {
  replaces=('luasocket')
  conflicts=('luasocket')

  cd luasocket-51
  make DESTDIR="$pkgdir" LUAV=5.1 prefix=/usr install-unix
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua52-socket() {
  cd luasocket-52
  make DESTDIR="$pkgdir" LUAV=5.2 prefix=/usr install-unix
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua53-socket() {
  cd luasocket-53
  make DESTDIR="$pkgdir" LUAV=5.3 prefix=/usr install-unix
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua-socket() {
  cd luasocket
  make DESTDIR="$pkgdir" LUAV=5.4 prefix=/usr install-unix
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
