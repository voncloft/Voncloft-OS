# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=generic-data
pkgname=haskell-generic-data
pkgver=0.9.2.0
pkgrel=36
pkgdesc="Deriving instances with GHC.Generics and related utilities"
url="https://github.com/Lysxia/generic-data"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ap-normalize' 'haskell-base-orphans' 'haskell-contravariant'
         'haskell-show-combinators')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-doctest' 'haskell-generic-lens' 'haskell-glob'
             'haskell-inspection-testing' 'haskell-one-liner' 'haskell-quickcheck'
             'haskell-show-combinators' 'haskell-tasty' 'haskell-tasty-hunit'
             'haskell-unordered-containers')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('b670a5ff77f9c918b13a5580a48e6b6e9583e34eefb4d348680bd3fb27c0ab85b028ffbc6010be09180021f6af32ceea8b8e7ade529d97cf59b43fb1655f2297')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
