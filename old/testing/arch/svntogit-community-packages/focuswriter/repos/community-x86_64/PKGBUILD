# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Graeme Gott <graeme@gottcode.org>

pkgname=focuswriter
pkgver=1.7.6
pkgrel=2
pkgdesc='Simple, distraction-free word processor'
arch=(x86_64)
url="https://gottcode.org/focuswriter/"
license=(GPL3)
depends=(hicolor-icon-theme hunspell qt5-multimedia)
makedepends=(qt5-tools)
source=("https://gottcode.org/$pkgname/$pkgname-$pkgver-src.tar.bz2")
sha256sums=('34f576a49c9fe64aedad66e02efafc29c840b6c3ddc98a42d87dfda499710541')

prepare() {
  cd $pkgname-$pkgver
  sed -i '/INSTALLS +=/ s/ pixmap//' focuswriter.pro
}

build() {
  cd $pkgname-$pkgver
  qmake PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
