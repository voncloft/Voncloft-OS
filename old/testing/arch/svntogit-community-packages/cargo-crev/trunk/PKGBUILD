# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-crev
pkgver=0.18.1
pkgrel=1
pkgdesc="Scalable, social, Code REView and recommendation system that we desperately need"
url="https://github.com/dpc/crev"
depends=('openssl' 'curl')
makedepends=('cargo' 'clang')
arch=('x86_64')
license=('MPL' 'Apache' 'MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dpc/crev/archive/v${pkgver}.tar.gz")
sha512sums=('45766cb4ba69f2c7efede1cf86aa14f5ee417970bc8b9c53a8ac45295b42ce24da3fdea6263c7933d05f9d82fdcb5f994fc5ec666b48975ef50cbbf131d7eb8e')
b2sums=('153671eed2c5b45bc9aed5c328701d0aa1bf926970a6821c580742191eaa92dcf5e4d4fdbddd93fa72ac9a47e72b4d898d92027dac294a8be11ca3ab9d7434c3')

build() {
  cd "${pkgname}-${pkgver}/cargo-crev"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
