# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=vco-plugins
pkgver=0.3.0
pkgrel=6
pkgdesc="LADSPA anti-aliased oscillator plugins"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/ladspa/"
license=('GPL2')
groups=('ladspa-plugins' 'pro-audio')
depends=('gcc-libs')
makedepends=('ladspa')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/VCO-plugins-${pkgver}.tar.bz2")
sha512sums=('44e066acdda38137dcc6a6b504a8453f8d0d25c90ba091e71a48554b47e60365a1e6b0bad694ec7340d399951b8b0592507be153370d1b4924d399310cf106c5')

prepare() {
  mv -v "VCO-plugins-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  # libs
  install -vDm 755 blvco.so "${pkgdir}/usr/lib/ladspa/blvco.so"
  ln -sv blvco.so "${pkgdir}/usr/lib/ladspa/vco_sawpulse.so"
  # docs
  install -vDm 644 {AUTHORS,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
