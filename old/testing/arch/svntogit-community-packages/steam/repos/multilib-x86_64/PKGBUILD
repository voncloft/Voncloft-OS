# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: K900 <k0009000@gmail.com>

pkgname=steam
pkgver=1.0.0.68
pkgrel=1
pkgdesc="Valve's digital software delivery system"
url='https://steampowered.com/'
arch=('x86_64')
license=('custom')
depends=('bash' 'desktop-file-utils' 'hicolor-icon-theme' 'curl' 'dbus'
         'freetype2' 'gdk-pixbuf2' 'ttf-font' 'zenity' 'lsb-release' 'nss' 'usbutils'
         'xorg-xrandr' 'vulkan-driver' 'vulkan-icd-loader' 'lsof' 'python')
depends_x86_64=('lib32-libgl' 'lib32-gcc-libs' 'lib32-libx11' 'lib32-libxss'
                'lib32-alsa-plugins' 'lib32-libgpg-error'
                'lib32-nss' 'lib32-vulkan-driver' 'lib32-vulkan-icd-loader')
optdepends=('steam-native-runtime: steam native runtime support')
source=(https://repo.steampowered.com/${pkgname}/pool/${pkgname}/s/${pkgname}/${pkgname}_${pkgver}{.tar.gz,.dsc}
        steam-runtime.sh)
sha512sums=('73c541e77e9e294b0ea1b991b73980b5a74f83516f95b15f93fce49459b49124d167d59261ed7522adf7b42d2a8e58d4ace12f8fe06f4cdd49347a742fd3afc0'
            '5bb9a009d4bb60483ebf6a906ab10b290828c6c5bd31bfba883ff4d96507a6450ba84e9e2447ccea76788071f3236fdd6e6bacb885313f78885c0daeda6b6f09'
            '5e75c019e9fe8c67d686c4e3343dac1180a69a4bdb7d39b333415c63201eef9b98da5619dbf6fd8daa6884e65bc7f8afc9e52778682425e5a75987d527eae6f0')
b2sums=('69c437322e5ff132052946524b8485815b92c4cfafd52428a430b6209c14b65ffd5b4bb74563c102338679b15eabe5cfb79d5692ada9b9cc694ca115ae41592e'
        '33a5e7559508636fb88314833672aea175d861b78a92716c20968a4ff33f7ef6d806e6768be8e21b63b0ef100d93a7f46aa5a830baf99feed78e424faa369068'
        'c6bac99336b7c30fec7cdbaf9e949555c687dd9dff50bcae136134d6314f4b841f5fc66ddb2caac1b003690b926fd4afbdc11da143b4674db4b75f27709fdd23')
validpgpkeys=('BA1816EF8E75005FCF5E27A1F24AEA9FB05498B7') # linux@steampowered.com

prepare() {
  cd ${pkgname}-launcher
  # apply roundups for udev rules
  sed -r 's|("0666")|"0660", TAG+="uaccess"|g' -i subprojects/steam-devices/60-steam-input.rules
  sed -r 's|("misc")|\1, OPTIONS+="static_node=uinput"|g' -i subprojects/steam-devices/60-steam-input.rules
  sed -r 's|(, TAG\+="uaccess")|, MODE="0660"\1|g' -i subprojects/steam-devices/60-steam-vr.rules

  # separated runtime/native desktop files
  sed -r 's|(Name=Steam)|\1 (Runtime)|' -i steam.desktop
  sed -r 's|(/usr/bin/steam)|\1-runtime|' -i steam.desktop
}

package() {
  cd ${pkgname}-launcher
  make DESTDIR="${pkgdir}" install

  install -Dm 755 "${srcdir}/steam-runtime.sh" "${pkgdir}/usr/bin/steam-runtime"
  install -d "${pkgdir}/usr/lib/steam"
  mv "${pkgdir}/usr/bin/steam" "${pkgdir}/usr/lib/steam/steam"
  ln -sf /usr/bin/steam-runtime "${pkgdir}/usr/bin/steam"

  install -Dm 644 COPYING steam_subscriber_agreement.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 debian/changelog -t "${pkgdir}/usr/share/doc/${pkgname}"

  # blank steamdeps because apt-get
  ln -sf /usr/bin/true "${pkgdir}/usr/bin/steamdeps"

  install -Dm 644 subprojects/steam-devices/60-steam-input.rules \
    "${pkgdir}/usr/lib/udev/rules.d/70-steam-input.rules"
  install -Dm 644 subprojects/steam-devices/60-steam-vr.rules \
    "${pkgdir}/usr/lib/udev/rules.d/70-steam-vr.rules"
}

# vim: ts=2 sw=2 et:
