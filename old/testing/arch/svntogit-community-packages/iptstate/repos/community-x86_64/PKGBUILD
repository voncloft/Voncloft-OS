# Maintainer: SÃ©bastien Luttringer
# Contributor: Andrea Zucchelli <zukka77@gmail.com>

pkgname=iptstate
pkgver=2.2.6
pkgrel=4
pkgdesc='top-like interface to netfilter connection-tracking table'
arch=('x86_64')
url='https://www.phildev.net/iptstate/'
license=('custom')
depends=('gcc-libs' 'ncurses' 'libnetfilter_conntrack')
validpgpkeys=('121BDA2D4ACB63616B367A0E58E11BB1E414D9AD') # Phil Dibowitz
source=("https://github.com/jaymzh/iptstate/releases/download/v$pkgver/iptstate-$pkgver.tar.bz2"{,.asc})
md5sums=('ec96c93b43976960d2e2ba3306cd09e6'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  install -D -m755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -D -m644 $pkgname.8 "$pkgdir/usr/share/man/man8/$pkgname.8"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
# vim:set ts=2 sw=2 et:
