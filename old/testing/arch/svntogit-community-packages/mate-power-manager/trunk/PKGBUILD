# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-power-manager
pkgver=1.24.1
pkgrel=1
pkgdesc="Power management tool for the MATE desktop"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('dbus-glib' 'gettext' 'libcanberra' 'libnotify' 'upower')
makedepends=('itstool' 'mate-panel' 'python')
optdepends=('mate-panel: Set brightness and inhibit power management from the panel')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
groups=('mate-extra')
conflicts=('mate-power-manager-gtk3')
replaces=('mate-power-manager-gtk3')
sha256sums=('0a08795a67cd7f523487ab0e9fb3b2bcf4f514f01a231c10248d68583a20598d')

build() {
    	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--libexecdir=/usr/lib/${pkgname} \
        	--sysconfdir=/etc \
        	--localstatedir=/var \
        	--sbindir=/usr/bin \
        	--enable-applets \
        	--disable-strict \
        	--without-keyring
    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
