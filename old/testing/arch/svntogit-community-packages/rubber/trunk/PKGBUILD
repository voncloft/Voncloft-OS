# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Baptiste Grenier <baptiste@bapt.name>

pkgname=rubber
pkgver=1.5.1
pkgrel=3
pkgdesc="A wrapper for LaTeX and friends"
arch=('any')
url="https://launchpad.net/rubber/"
license=('GPL')
depends=('python')
makedepends=('texinfo' 'texlive-plainextra')
source=("https://launchpad.net/rubber/trunk/$pkgver/+download/rubber-$pkgver.tar.gz")
sha512sums=('ba22eeddc24b4f9abf0bf087604a4261add648877ec558b766555de09538cd20c21c8cfd9731c1421540bb2479501dd6ed066346d250c6bc981529e31efacc40')

build() {
  cd "${srcdir}/rubber-$pkgver"
  python setup.py build
}

package() {
  cd "${srcdir}/rubber-$pkgver"
  python setup.py install --root="$pkgdir" --mandir=/usr/share/man --infodir=/usr/share/info

  mkdir -p "$pkgdir"/usr/share/bash-completion/completions/
  echo "complete -f -X '!*.tex' rubber" >"$pkgdir"/usr/share/bash-completion/completions/rubber
}
