# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Mort Yao <soi@mort.ninja>
# Contributor: Alex Merry <dev@randomguy3.me.uk>

pkgname=polyml
pkgver=5.8.1
pkgrel=1
pkgdesc='Standard ML compiler originally written in Poly'
arch=(x86_64)
url='https://polyml.org/'
license=(LGPL)
conflicts=(mesa-demos)
options=(!libtool)
depends=(gmp libffi)
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('fa0507b44470b4e07a061ef6a8896efe42710d8436e15721d743572ad2f7753b')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --enable-shared \
    --prefix=/usr \
    --with-gmp \
    --with-system-libffi
  make
}

check() {
  make -C $pkgname-$pkgver -k check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}

# getver: polyml.org
# vim: ts=2 sw=2 et:
