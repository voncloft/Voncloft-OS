# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-whitenoise
pkgver=5.2.0
pkgrel=2
pkgdesc='Radically simplified static file serving for WSGI applications'
arch=('any')
license=('MIT')
url='http://whitenoise.evans.io'
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-brotli' 'python-django' 'python-pytest' 'python-requests')
source=("$pkgname-$pkgver.tar.gz::https://github.com/evansd/whitenoise/archive/v$pkgver.tar.gz")
sha512sums=('e68f89c6af86ffaee8931196a5714a47057b4d750b75053042007229ef390b287144a2d10376dbf232168277fc95ba4a40c128acd349242d041e6f5f39e92055')

build() {
  cd whitenoise-$pkgver
  python setup.py build
}

check() {
  cd whitenoise-$pkgver
  DJANGO_SETTINGS_MODULE=tests.django_settings pytest
}

package() {
  cd whitenoise-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
