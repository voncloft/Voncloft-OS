# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=m4
pkgver=1.4.18
pkgrel=3
pkgdesc="The GNU macro processor"
arch=('x86_64')
url="https://www.gnu.org/software/m4"
license=('GPL3')
groups=('base-devel')
depends=('glibc' 'bash')
source=(https://ftp.gnu.org/gnu/m4/$pkgname-$pkgver.tar.xz{,.sig}
        m4-1.4.18-glibc-change-work-around.patch)
md5sums=('730bb15d96fffe47e148d1e09235af82'
         'SKIP'
         '01968e8f3a5be62de1c849668db8042b')
validpgpkeys=('71C2CC22B1C4602927D2F3AAA7A16B4A2527436A')   # Eric Blake


prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i $srcdir/m4-1.4.18-glibc-change-work-around.patch
}

build() {
  cd $pkgname-$pkgver  
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make prefix=${pkgdir}/usr install
}
