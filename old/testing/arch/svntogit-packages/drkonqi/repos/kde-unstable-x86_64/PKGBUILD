# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=drkonqi
pkgver=5.20.90
pkgrel=1
pkgdesc='The KDE crash handler'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(GPL2)
depends=(kidletime kio syntax-highlighting gdb)
makedepends=(extra-cmake-modules)
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('fc19c2617a060a052b0a561bee2100e2ed098ee2f389223e38b8ab71cd0f5f4f'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
