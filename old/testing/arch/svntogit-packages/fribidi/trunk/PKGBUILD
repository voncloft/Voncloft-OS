# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Nezmer <Nezmer@gmail.com>

pkgname=fribidi
pkgver=1.0.10
pkgrel=1
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm"
url="https://github.com/fribidi/fribidi/"
arch=(x86_64)
license=(LGPL)
depends=(glibc)
makedepends=(git meson)
provides=(libfribidi.so)
_commit=5464c284034da9c058269377b7f5013bb089f553  # tags/v1.0.10^0
source=("git+https://github.com/fribidi/fribidi#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd fribidi
  git describe --tags | sed -e 's/-/+/g' -e 's/^v//'
}

prepare() {
  cd fribidi
}

build() {
  arch-meson fribidi build \
    -D docs=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
