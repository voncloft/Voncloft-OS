# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=net-tools
pkgver=2.10
pkgrel=1
pkgdesc="Configuration tools for Linux networking"
arch=('x86_64')
license=('GPL2')
url="http://net-tools.sourceforge.net/"
depends=('glibc')
source=("https://downloads.sourceforge.net/project/net-tools/$pkgname-$pkgver.tar.xz")
options=(!makeflags)
sha1sums=('4080baab0486dc882c3b293d5559c27251ae4268')

build() {
  cd ${pkgname}-${pkgver}
  export BINDIR='/usr/bin' SBINDIR='/usr/bin'
  yes "" | make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # the following is provided by yp-tools
  rm "${pkgdir}"/usr/bin/{nis,yp}domainname
  # hostname is provided by inetutils
  rm "${pkgdir}"/usr/bin/{hostname,dnsdomainname,domainname}
  rm -r "${pkgdir}"/usr/share/man/man1
}
