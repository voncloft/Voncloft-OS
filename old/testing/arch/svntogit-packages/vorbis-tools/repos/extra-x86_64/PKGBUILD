# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=vorbis-tools
pkgver=1.4.2
pkgrel=1
pkgdesc='Extra tools for Ogg-Vorbis'
arch=('x86_64')
url='https://www.xiph.org/vorbis/'
license=('GPL2')
depends=('libao' 'libvorbis' 'curl' 'flac')
source=("https://downloads.xiph.org/releases/vorbis/${pkgname}-${pkgver}.tar.gz")
sha256sums=('db7774ec2bf2c939b139452183669be84fda5774d6400fc57fde37f77624f0b0')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --without-speex \
    --enable-vcut
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
