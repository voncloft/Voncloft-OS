# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgname=glib-perl
pkgver=1.329.3
_pkgver=1.3293
pkgrel=1
pkgdesc="Perl wrappers for glib 2.x, including GObject"
arch=(x86_64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
options=('!emptydirs')
depends=('glib2')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig')
source=(https://downloads.sourceforge.net/sourceforge/gtk2-perl/Glib-${_pkgver}.tar.gz)
sha256sums=('7316a0c1e7cc5cb3db7211214f45d7bdc2354365a680ac4bd3ac8bf06d1cb500')

build() {
  cd Glib-$_pkgver
  PERL_USE_UNSAFE_INC=1 \
  perl Makefile.PL INSTALLDIRS=vendor
  PERL_USE_UNSAFE_INC=1 \
  make
}

check() {
  cd Glib-$_pkgver
  make test
}

package() {
  cd Glib-$_pkgver
  make DESTDIR="${pkgdir}" install
}
