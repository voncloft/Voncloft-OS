# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Gour <Gour <gour@mail.inet.hr>

pkgname=libisoburn
pkgver=1.5.2 #.pl01
pkgrel=2
pkgdesc="frontend for libraries libburn and libisofs"
url="https://dev.lovelyhq.com/libburnia"
arch=('x86_64')
license=('GPL2')
depends=('libburn' 'libisofs' 'readline')
optdepends=('tk:      for xorriso-tcltk frontend')
            #'bwidget: for xorriso-tcltk frontend') # AUR
provides=('xorriso' 'xorriso-tcltk')
source=(http://files.libburnia-project.org/releases/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('cc720bc9511d8e0b09365e2c8b0e40817986be308cd96ca2705c807c955590ec'
            'SKIP')
validpgpkeys=('44BC9FD0D688EB007C4DD029E9CBDFC0ABC0A854') # Thomas Schmitt <scdbackup@gmx.net>

build() {
  cd "${pkgname}"-${pkgver/.pl01/}
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}"-${pkgver/.pl01/}
  make DESTDIR="${pkgdir}" install
}
