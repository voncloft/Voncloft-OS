# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=vulkan-validation-layers
pkgver=1.2.162
pkgrel=2
pkgdesc="Vulkan Validation Layers"
arch=(x86_64)
url="https://www.khronos.org/vulkan/"
license=('custom')
depends=(gcc-libs vulkan-icd-loader vulkan-headers libx11)
makedepends=(cmake python-lxml libx11 libxrandr wayland git)
options=(!strip)
source=(https://github.com/KhronosGroup/Vulkan-ValidationLayers/archive/v${pkgver}.tar.gz)
#source=("git+https://github.com/KhronosGroup/Vulkan-ValidationLayers.git#commit=35d091dad71304dab79758fdae4bda12f692d735")
sha256sums=('83619b81a86e733383a71ea54f0f5e64e194bf596ee8ec4e36217bb0b29b8917')

#pkgver() {
#  cd "${srcdir}/Vulkan-ValidationLayers"
#
#  echo 1.1.97.0+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
#}

build() {
  cd "${srcdir}"/Vulkan-ValidationLayers*

  rm -rf build ; mkdir build ; cd build
  ../scripts/update_deps.py
  cmake -C helper.cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_DATADIR=/share \
    -DCMAKE_INSTALL_INCLUDEDIR="/usr/include/" \
    -DBUILD_LAYER_SUPPORT_FILES=ON \
    -DCMAKE_SKIP_RPATH=True \
    -DBUILD_TESTS=Off \
    -DBUILD_WSI_XCB_SUPPORT=On \
    -DBUILD_WSI_XLIB_SUPPORT=On \
    -DBUILD_WSI_WAYLAND_SUPPORT=On \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    ..
  make
}

package() {
  groups=('vulkan-devel')
  cd "${srcdir}"/Vulkan-ValidationLayers*/build
  
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}"/usr/share/{licenses/,doc/}${pkgname}/
  cp -r ../docs/* "${pkgdir}"/usr/share/doc/${pkgname}/
  install -m644 ../LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}
