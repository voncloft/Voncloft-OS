# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-log-log4perl
pkgver=1.54
pkgrel=1
pkgdesc='Log4j implementation for Perl'
arch=('any')
url="https://metacpan.org/release/Log-Log4perl"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/E/ET/ETJ/Log-Log4perl-$pkgver.tar.gz")
sha512sums=('689d389551a99108cbe83dbe56f08cb7d1a53483b9d19401be0c8d1aad050514ddbe02fe7f18f9649a5b113dc9a9c7d8aafc16066d5180cdd114b8293a2f98dc')

build() {
  cd Log-Log4perl-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Log-Log4perl-$pkgver
  make test
}

package() {
  cd Log-Log4perl-$pkgver
  make DESTDIR="$pkgdir" install
}
