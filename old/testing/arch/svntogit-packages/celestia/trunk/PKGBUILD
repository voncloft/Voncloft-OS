# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor : Damir Perisa <damir.perisa@bluewin.ch>

pkgname=celestia
pkgver=1.6.2.1
pkgrel=1
pkgdesc="Real-time space simulation"
arch=('x86_64')
license=('GPL')
url="https://celestia.space/"
depends=('gtk2' 'libtheora' 'lua53' 'gtkglext' 'glut' 'libxmu' 'glu' 'libjpeg' 'mesa')
options=('!makeflags')
source=("https://github.com/CelestiaProject/Celestia/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha1sums=('fbd404e0a132f0b555cf2e061e36bd384fb6038c')

prepare() {
    cd Celestia-${pkgver}
    autoreconf -vi
}

build() {
    cd Celestia-${pkgver}
    ./configure --prefix=/usr \
                --with-lua=/usr \
                --datadir=/usr/share \
                --with-gtk \
                --disable-rpath \
                --with-lua
    make
}

package() {
    cd Celestia-${pkgver}

    make DESTDIR="${pkgdir}" MKDIR_P='mkdir -p' install
}

