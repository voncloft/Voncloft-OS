# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=xorg-xinit
pkgver=1.4.1
pkgrel=3
pkgdesc="X.Org initialisation program "
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('libx11' 'xorg-xauth' 'xorg-xrdb' 'xorg-xmodmap')
makedepends=('xorg-util-macros')
optdepends=('xorg-twm' 'xterm')
backup=('etc/X11/xinit/xserverrc'
        'etc/X11/xinit/xinitrc')
source=(${url}/releases/individual/app/xinit-${pkgver}.tar.bz2{,.sig}
        06_move_serverauthfile_into_tmp.diff
        fs25361.patch
        fs46369.patch
        use_uname_-n_instead_of_hostname.diff
        xserverrc)
options=(!emptydirs)
sha512sums=('6cbc5d025a891c419f3f4493381b2fca57a67d78df866d2f16a83426f86bad6eca7f240fac12b25cbcc63df0fec41f625407184e044898602d66483715315340'
            'SKIP'
            '99216b2d50052a0bafede9a2db1744a8b0313ccbc02c609502ddacb8684fc56c6f2656e6521c848880033b25005bb14a1bce0d6fefade85141ed56aad07dadf3'
            '12a89cbb26902e135bb21f945c8de86526ff879c9f20a2601157f6a39899f021ed2970cee9e4fbcd4c13af6fe78e7902dd1f7ce1928fc914d681453bf848c0f8'
            '8a36bff3c472763a9a46b9a36b8b4a15f03e6fb0387b12efba27f15dc500faa2a3f92c0f7e217a0b00da7c78682c7af6357cc6b88b550ece9bc89c477412b7e0'
            '860aa77462e56d399ca0cc829e8a60f4df6d62eca5ac41bc52dd9e3783b7cf6e6e33ce27e76a86ae393058f53a0cb44624058cdd1733fcbfc96ab800882649f7'
            'f86d96d76bcb340021e7904925f0029f8662e4dfc32489198b3a8695dca069da496539e2287249c763fe9c4d8d5d591fd18fe49a0bee822cbbd0eb712efbb89b')
validpgpkeys=('7C2608F8E5F9E78D7A3F26FF177544B49AF313FB') #  "Hans de Goede <jwrdegoede@fedoraproject.org>"
validpgpkeys+=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

prepare() {
  cd xinit-${pkgver}
  patch -Np1 -i ../06_move_serverauthfile_into_tmp.diff
  patch -Np1 -i ../fs25361.patch
  patch -Np1 -i ../fs46369.patch
  # FS#67679
  patch -Np1 -i ../use_uname_-n_instead_of_hostname.diff
  sed -i -e 's/XSLASHGLOB.sh/XSLASHGLOB/' xinitrc.cpp
}

build() {
  cd xinit-${pkgver}
  autoreconf -fi
  ./configure --prefix=/usr --with-xinitdir=/etc/X11/xinit
  make
}

package() {
  cd xinit-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m644 "${srcdir}/xserverrc" "${pkgdir}/etc/X11/xinit/xserverrc"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
