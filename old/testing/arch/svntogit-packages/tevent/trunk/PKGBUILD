# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Marco A Rojas <marquicus at gmail dot com>
# Contributor: Ng Oon-Ee <ngoonee.talk@gmail.com>
# Contributor: Thomas Burdick <thomas.burdick@gmail.com>

pkgname=tevent
pkgver=0.10.2
pkgrel=3
epoch=1
pkgdesc='Event system based on the talloc memory management library'
url="https://tevent.samba.org/"
arch=('x86_64')
source=(https://samba.org/ftp/tevent/${pkgname}-${pkgver}.tar{.gz,.asc})
license=('GPL3')
depends=('talloc')
makedepends=('python')
optdepends=('python: for python bindings')
provides=(libtevent.so)
validpgpkeys=('9147A339719518EE9011BCB54793916113084025') # Samba Library Distribution Key <samba-bugs@samba.org>
sha512sums=('1da8f28898f35daab515892b880d1de601062cc3e2b2570cd62e6913df17b0195a05acb7b484c628cddc0eb8b0cde893105ede3feb32bc5764e7d25684f332ba'
            'SKIP')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --bundled-libraries=NONE \
    --builtin-libraries=replace
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
