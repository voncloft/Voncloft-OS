# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=alsa-topology-conf
pkgver=1.2.4
pkgrel=2
pkgdesc="ALSA topology configuration files"
url="https://alsa-project.org/"
arch=('any')
license=('BSD')
source=("https://www.alsa-project.org/files/pub/lib/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('e5b367a23f42ed2c2a83f3dd9df264b4e054f5ba7c4be98520418598f0b06a11627498a8a6ef943522b209951645f83bbbbfb32db7c9c8260aa5db08358970cb'
            'SKIP')
b2sums=('57df107b255e598e2fb79b56dc34d84130a023050db42e7925082ce5efb7048658ed6a8cd1854d2d9e23e8a833dbe90d7aa11ca7c1874f39caf392b292147ab7'
        'SKIP')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

package() {
  cd "${pkgname}-${pkgver}"
  for dir in topology/*; do
    install -vDm 644 "${dir}"/*.conf -t "$pkgdir/usr/share/alsa/topology/$(basename $dir)"
  done
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
