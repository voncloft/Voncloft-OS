# Maintainer: kevin <kevin@archlinux.org>
# Contributor: Eric Johnson <eric@coding-zone.com>

pkgname=perl-mime-lite
pkgver=3.031
pkgrel=3
pkgdesc="Perl module that provides lightweight MIME generator"
arch=('any')
url="https://search.cpan.org/dist/MIME-Lite/"
license=('GPL' 'PerlArtistic')
depends=('perl-email-date-format' 'perl-mailtools' 'perl-mime-types' 'perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/MIME-Lite-${pkgver}.tar.gz")
md5sums=('4ff72d8dd5811b8538825fca4b0f133e')

build() {
  cd MIME-Lite-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd MIME-Lite-${pkgver}
  make test
}

package() {
  cd MIME-Lite-${pkgver}
  make DESTDIR="${pkgdir}" install
}
