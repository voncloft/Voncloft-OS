# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=procinfo-ng
pkgver=2.0.304
pkgrel=8
pkgdesc="Displays useful information from /proc"
arch=('x86_64')
url="http://procinfo-ng.sourceforge.net/"
license=('GPL2' 'LGPL2.1')
depends=('ncurses' 'perl')
provides=('procinfo')
conflicts=('procinfo')
replaces=('procinfo')
source=(https://downloads.sourceforge.net/project/procinfo-ng/procinfo-ng/${pkgver}/${pkgname}-${pkgver}.tar.bz2
        https://sources.archlinux.org/other/procinfo-ng/procinfo-18.tar.gz)
sha512sums=('db336d2ff3bb3b31ac83a51a9d273fae8d16d36bd805f548eda06cb501699473fd8e4b4f9b722165fbf685d76c3fc09bfea9e516eabc37a888e2fae7679c08cf'
            '12c4d7983f5d6531be6d4df39f5f1e1ee2ea2394a6eb8fb8df0573f0085499e4b7d43355bfa549a24ddf1ecb09bbfa7b038cef9c483620c74ad5cedc219e35fa')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # install tools from old procinfo package
  cd "${srcdir}/procinfo-18"
  install -m755 lsdev.pl "${pkgdir}/usr/bin/lsdev"
  install -m755 socklist.pl "${pkgdir}/usr/bin/socklist"
  install -m644 lsdev.8 "${pkgdir}/usr/share/man/man8/lsdev.8"
  install -m644 socklist.8 "${pkgdir}/usr/share/man/man8/socklist.8"
}
