# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=vpnc
_vpnc_commit=1bd238527af94123788bcc529bde085d6be7227e
_vpncscripts_commit=c0122e891f7e033f35f047dad963702199d5cb9e
pkgver=0.5.3.r468.r81
pkgrel=2
epoch=1
pkgdesc='VPN client for cisco3000 VPN Concentrators'
url='https://github.com/streambinder/vpnc'
arch=('x86_64')
license=('GPL')
depends=('libgcrypt' 'openssl' 'iproute2')
optdepends=('perl: for pcf2vpnc')
makedepends=('git')
optdepends=('openresolv: Let vpnc manage resolv.conf')
backup=('etc/vpnc/default.conf')
source=("vpnc::git+https://github.com/streambinder/vpnc#commit=${_vpnc_commit}"
        "vpnc-scripts::git://git.infradead.org/users/dwmw2/vpnc-scripts.git#commit=${_vpncscripts_commit}"
        vpnc.conf
        vpnc@.service
        vpnc-systemd-resolved.patch)
sha512sums=('SKIP'
            'SKIP'
            'ac70712192c01ff638a9badc5cff7105bee5c4fed5d3a3b728e9597661952d156041c82fe1e544e2bab602d193d4105d3689c79c46d964623f6ce38dd89f0ea7'
            'cafcab676986c1a2e49441f01d61997f1c6b54bbb68661b9af007d4816f8e76eee6b7ac2dfab55b55965fa407e8331c663cf11aa79384c30b0c9049c1477b791'
            '3383783a0e4848ccff2247c5c7c99bcfeb3f7a7756d000fdb4627a73c82dd7ac8106183160d07dc77339df70efc4819ea89327e9da1d7c885d675d1dc84d4df1')

pkgver() {
  cd ${pkgname}
  printf "%s.r%s.r%s" "$(cat VERSION)" \
    "$(git -C ../vpnc rev-list --count HEAD)" \
    "$(git -C ../vpnc-scripts rev-list --count HEAD)"
}

prepare() {
  cd vpnc-scripts
  patch -Np1 < ../vpnc-systemd-resolved.patch

  cd ../${pkgname}
  # Build hybrid support
  sed 's|^#OPENSSL|OPENSSL|g' -i Makefile
  # fix resolvconf location for community/openresolv
  sed 's|/sbin/resolvconf|/usr/bin/resolvconf|g' -i ../vpnc-scripts/vpnc-script
  ln -sf ../../vpnc-scripts/vpnc-script src
  ln -sf ../../vpnc.conf src
}

build() {
  make -C ${pkgname}
}

check() {
  make -C ${pkgname} test
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" PREFIX=/usr SBINDIR=/usr/bin install
  install -Dm 644 ../vpnc@.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
