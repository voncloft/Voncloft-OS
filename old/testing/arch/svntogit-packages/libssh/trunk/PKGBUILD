# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: ice-man <icemanf@gmail.com>
# Contributor: sergeantspoon <sergeantspoon@archlinux.us>

pkgbase=libssh
pkgname=(libssh libssh-docs)
pkgver=0.9.5
pkgrel=1
pkgdesc='Library for accessing ssh client services through C libraries'
url='https://www.libssh.org/'
license=('LGPL')
arch=('x86_64')
depends=('zlib' 'openssl')
makedepends=('cmake' 'cmocka' 'doxygen' 'python' 'openssh')
provides=('libssh.so')
source=("https://www.libssh.org/files/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('acffef2da98e761fc1fd9c4fddde0f3af60ab44c4f5af05cd1b2d60a3fa08718'
            'SKIP')
validpgpkeys=('8DFF53E18F2ABC8D8F3C92237EE0FC4DCC014E3D') # Andreas Schneider <asn@cryptomilk.org>

prepare() {
  # disable the test. It is confused by our clean container setup.
  # 'extra-x86-build' uses user 'nobody' that has a record in /etc/passwd file
  # but $HOME envvar is set to '/build'. The test expects that $HOME corresponds to passwd file.
  sed 's/cmocka_unit_test(torture_path_expand_tilde_unix),//' -i libssh-${pkgver}/tests/unittests/torture_misc.c

  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_GSSAPI=OFF \
    -DUNIT_TESTING=ON
  make
  make docs
}

check() {
  cd build
  make test
}

package_libssh() {
  cd build
  make DESTDIR="$pkgdir" install
}

package_libssh-docs() {
  pkgdesc="Documentation for libssh"
  depends=()
  provides=()

  mkdir -p "$pkgdir"/usr/share/doc/libssh
  cp -r build/doc/html "$pkgdir"/usr/share/doc/libssh
#  cp -r build/doc/man "$pkgdir"/usr/share
#  rm "$pkgdir"/usr/share/man/man3/{bug,deprecated}.*
}
