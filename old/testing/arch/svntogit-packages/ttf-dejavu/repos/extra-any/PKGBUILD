# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=ttf-dejavu
pkgver=2.37+18+g9b5d1b2f
pkgrel=2
pkgdesc="Font family based on the Bitstream Vera Fonts with a wider range of characters"
arch=('any')
url="https://dejavu-fonts.github.io/"
license=('custom')
makedepends=('git' 'fontforge' 'perl-font-ttf')
provides=('ttf-font')
_commit=9b5d1b2ffeec20c7b46aa89c0223d783c02762cf  # master
source=("git+https://github.com/dejavu-fonts/dejavu-fonts.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd dejavu-fonts
  git describe --tags | sed 's/^version_//;s/_/./;s/-/+/g'
}

build() {
  cd "${srcdir}"/dejavu-fonts
  make  full-ttf
}

package() {
  install -dm755 "${pkgdir}"/etc/fonts/conf.avail
  install -dm755 "${pkgdir}"/etc/fonts/conf.d
  install -dm755 "${pkgdir}"/usr/share/fonts/TTF

  cd "${srcdir}"/dejavu-fonts
  install -m644 build/*.ttf "${pkgdir}"/usr/share/fonts/TTF/
  install -m644 fontconfig/*.conf "${pkgdir}"/etc/fonts/conf.avail/

  pushd "${pkgdir}"/etc/fonts/conf.avail
  for config in *; do
    ln -sf ../conf.avail/${config} ../conf.d/${config}
  done
  popd

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
