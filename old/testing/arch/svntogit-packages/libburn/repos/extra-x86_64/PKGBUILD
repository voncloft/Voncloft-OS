# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=libburn
pkgver=1.5.2 # .pl01
pkgrel=2
pkgdesc="Library for reading, mastering and writing optical discs"
arch=('x86_64')
url="https://dev.lovelyhq.com/libburnia"
license=('GPL')
depends=('glibc')
# https://dev.lovelyhq.com/libburnia/web/wikis/Releases
source=(http://files.libburnia-project.org/releases/${pkgname}-${pkgver}.tar.gz{,.sig})
options=('!emptydirs')
sha256sums=('7b32db1719d7f6516cce82a9d00dfddfb3581725db732ea87d41ea8ef0ce5227'
            'SKIP')
validpgpkeys=('44BC9FD0D688EB007C4DD029E9CBDFC0ABC0A854') # Thomas Schmitt <scdbackup@gmx.net>

build() {
  cd "${pkgname}"-${pkgver/.pl01/}
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${pkgname}"-${pkgver/.pl01/}
  make DESTDIR="${pkgdir}" install
}
