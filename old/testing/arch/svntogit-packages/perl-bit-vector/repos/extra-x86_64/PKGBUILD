# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=perl-bit-vector
pkgver=7.4
pkgrel=11
pkgdesc='Efficient bit vector, set of integers and "big int" math library'
arch=('x86_64')
license=('GPL' 'LGPL' 'PerlArtistic')
url="https://search.cpan.org/dist/Bit-Vector/"
depends=('perl-carp-clan')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/S/ST/STBEY/Bit-Vector-${pkgver}.tar.gz)
md5sums=('bf67f144e5be5327ed79d4c69e6e0086')

build() {
  cd Bit-Vector-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Bit-Vector-$pkgver
  make test
}

package() {
  cd Bit-Vector-$pkgver
  make DESTDIR="${pkgdir}" install
}
