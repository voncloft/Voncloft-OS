# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Bartłomiej Piotrowski
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=ristretto
pkgver=0.10.0
pkgrel=3
pkgdesc='Fast and lightweight picture-viewer for Xfce4'
arch=('x86_64')
url='https://docs.xfce.org/apps/ristretto/start'
license=('GPL')
depends=('libxfce4ui' 'libexif' 'file')
makedepends=('intltool')
optdepends=('librsvg: SVG support'
            'tumbler: thumbnailing support')
groups=('xfce4-goodies')
source=("https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2")
sha256sums=('16225dd74245eb6e0f82b9c72c6112f161bb8d8b11f3fc77277b7bc3432d4769')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: git.xfce.org/apps/ristretto/plain/NEWS
# vim:set ts=2 sw=2 et:
