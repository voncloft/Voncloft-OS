# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-font-util
pkgver=1.3.2
pkgrel=2
pkgdesc='X.Org font utilities'
url='https://xorg.freedesktop.org/'
arch=('x86_64')
license=('custom')
makedepends=('xorg-util-macros')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E')
source=("${url}/releases/individual/font/font-util-${pkgver}.tar.bz2"{,.sig})
sha512sums=('7cac529b12ae71185b89c72c1569b9826f52eeaecc1c76010338e7c42c4078ae339f18220b580bbd68fb5dc09df6ecf169a47c32e6104d8ee53bd443fa21d167'
            'SKIP')

replaces=('font-util')
provides=('font-util')
conflicts=('font-util')
groups=('xorg-fonts' 'xorg')

build() {
	cd "${srcdir}/font-util-${pkgver}"

	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--with-mapdir=/usr/share/fonts/util \
		--with-fontrootdir=/usr/share/fonts \

	make
}

package() {
	cd "${srcdir}/font-util-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
