# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-notifyd
pkgver=0.6.2
pkgrel=2
pkgdesc="Notification daemon for the Xfce desktop"
arch=('x86_64')
url="https://docs.xfce.org/apps/notifyd/start"
license=('GPL2')
groups=('xfce4-goodies')
depends=('libxfce4ui' 'libnotify' 'hicolor-icon-theme')
makedepends=('intltool' 'python' 'xfce4-panel')
provides=('notification-daemon')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('19ab84c6665c7819998f2269322d53f462c30963ce26042df23ae525e7d16545')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
