# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=kdiff3
pkgver=1.8.5
pkgrel=1
pkgdesc="A file comparator/merge tool"
arch=(x86_64)
url="https://kde.org/applications/development/kdiff3/"
license=(GPL)
depends=(kparts hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('bc92f7d04eb123f9c56dbe3047af67bf715240fa99487ac1d1ccf2a45e0854c1'
            'SKIP')
validpgpkeys=(EE56D710885D36FB1E4D21F3F442B36D614B06BC) # Michael Reeves <reeves.87@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
