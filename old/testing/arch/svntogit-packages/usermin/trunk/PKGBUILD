# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=usermin
pkgver=1.610
pkgrel=1
pkgdesc="a web interface that can be used to easily perform tasks like reading mail, setting up SSH or configuring mail forwarding"
arch=(x86_64)
license=('custom:usermin')
url="http://www.webmin.com/"
depends=('perl' 'webmin')
makedepends=('perl-net-ssleay')
backup=('etc/usermin/miniserv.conf' 'etc/usermin/miniserv.users' 'etc/usermin/config' 'etc/usermin/usermin.cats' \
'etc/usermin/at/config' \
'etc/usermin/man/config' \
'etc/usermin/ssh/config' \
'etc/usermin/chfn/config' \
'etc/usermin/cron/config' \
'etc/usermin/file/config' \
'etc/usermin/plan/config' \
'etc/usermin/proc/config' \
'etc/usermin/spam/config' \
'etc/usermin/htaccess/config' \
'etc/usermin/cshrc/config' \
'etc/usermin/gnupg/config' \
'etc/usermin/mysql/config' \
'etc/usermin/quota/config' \
'etc/usermin/shell/config' \
'etc/usermin/theme/config' \
'etc/usermin/mailbox/config' \
'etc/usermin/schedule/config' \
'etc/usermin/config' \
'etc/usermin/commands/config' \
'etc/usermin/htaccess-htpasswd/config' \
'etc/usermin/forward/config' \
'etc/usermin/telnet/config' \
'etc/usermin/tunnel/config' \
'etc/usermin/updown/config' \
'etc/usermin/procmail/config' \
'etc/usermin/fetchmail/config' \
'etc/usermin/changepass/config' \
'etc/usermin/language/config' \
'etc/usermin/postgresql/config' \
'etc/usermin/usermount/config' \
)
install=usermin.install
source=(http://downloads.sourceforge.net/sourceforge/webadmin/$pkgname-$pkgver.tar.gz 
	usermin.service
	ftp://ftp.archlinux.org/other/usermin/usermin-config.tar.bz2) 

package() {
  cd "$srcdir"/$pkgname-$pkgver
    
  # remove modules we do not support

  #remove config files from other distros
  rm -f $(find . ! -name 'config-generic-linux' ! -name 'config-\*-linux' ! -name 'config-lib.pl' -name 'config-*')
  
  # remove caldera theme
  rm -rf caldera

  # remove init-scripts from other distros
  rm usermin-init
  rm usermin-daemon

  # setting perl path
  (find . -name '*.cgi' ; find . -name '*.pl') | perl perlpath.pl /usr/bin/perl -
  
  # create dirs
  mkdir -p "$pkgdir"/opt/usermin
  mkdir -p "$pkgdir"/var/log/usermin
  mkdir -p "$pkgdir"/etc/usermin
  
  # install pam stuff
  install -D -m 644 usermin-pam "$pkgdir"/etc/pam.d/usermin
  
  # remove other distros and add only Archlinux don't change next line else it will not work!
  rm os_list.txt
  echo 'Archlinux			Any version	generic-linux	*	-d "/etc/pacman.d"' >> os_list.txt
  
  # copy stuff to right dirs
  cd "$srcdir"/$pkgname-$pkgver
  cp -rp * "$pkgdir"/opt/usermin
  cd "$srcdir"/usermin-config
  cp -rfp * "$pkgdir"/opt/usermin

  # define parameters for setup.sh
  config_dir="$pkgdir"/etc/usermin
  var_dir="$pkgdir"/var/log/usermin
  perl=/usr/bin/perl
  autoos=1
  port=20000
  login=root
  crypt="XXX"
  ssl=1
  atboot=0
  nostart=1
  nochown=1
  autothird=1
  nouninstall=1
  noperlpath=1
  atbootyn=n
  tempdir="$pkgdir"/tmp
  export config_dir var_dir perl autoos port tempdir ssl nochown autothird nouninstall nostart noperlpath atbootyn login crypt
  
  # Fix setup.sh
  sed -i -e 's:read atbootyn::g' -e 's:exit 13::g' "$pkgdir"/opt/usermin/setup.sh
  "$pkgdir"/opt/usermin/setup.sh
  
  # Fixup the config files to use their real locations
  sed -i 's:^pidfile=.*$:pidfile=/var/run/usermin.pid:' "$pkgdir"/etc/usermin/miniserv.conf
  find "$pkgdir"/etc/usermin -type f -exec sed -i "s:$pkgdir::g" {} \; 
  
  # make it only accessible by localhost
  echo 'allow=127.0.0.1' >> "$pkgdir"/etc/usermin/miniserv.conf
  
  # enable user and password login
  sed -i -e '/localauth/d' "$pkgdir"/etc/usermin/miniserv.conf

  #install systemd service file
  install -D -m 644 "$srcdir"/usermin.service "$pkgdir"/usr/lib/systemd/system/usermin.service

  # delete temp dir
  rm -r "$pkgdir"/tmp
  #install license
  install -m 644 -D "$srcdir"/$pkgname-$pkgver/LICENCE "$pkgdir"/usr/share/licenses/usermin/LICENCE
}
md5sums=('d73f0696e2644034f1ec8202b9f020be'
         'a5bd5e5ee779f8a3cf3d776ca77ee36e'
         'ec37a79f948f778224550930d2a7ca07')
