# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libpipewire02
pkgver=0.2.7
pkgrel=1
pkgdesc="User space API to deal with multimedia pipelines (${pkgver:0:3})"
url="https://pipewire.org"
license=(LGPL2.1)
arch=(x86_64)
depends=(alsa-lib dbus)
makedepends=(git meson)
provides=(libpipewire-${pkgver:0:3}.so)
_commit=14c11c0fe4d366bad4cfecdee97b6652ff9ed63d  # tags/0.2.7
source=("git+https://github.com/PipeWire/pipewire#commit=$_commit")
sha256sums=('SKIP')


pkgver() {
  cd pipewire
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd pipewire
}

build() {
  arch-meson pipewire build \
    -D gstreamer=disabled \
    -D systemd=false
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  rm -rf "$pkgdir"/{etc,usr/bin}
}
