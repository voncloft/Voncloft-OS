# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=opencl-clhpp
pkgver=2.0.13
pkgrel=1
pkgdesc='OpenCL C++ header files'
arch=('any')
url='https://github.com/KhronosGroup/OpenCL-CLHPP'
license=('custom')
makedepends=('cmake' 'doxygen' 'graphviz')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/KhronosGroup/OpenCL-CLHPP/archive/v${pkgver}.tar.gz")
sha256sums=('8ff0d0cd94d728edd30c876db546bf13e370ee7863629b4b9b5e2ef8e130d23c')
depends=('opencl-headers')

build() {
  cd "${srcdir}"/OpenCL-CLHPP*/

  cmake -B build ./ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_DOCS=On \
    -DBUILD_EXAMPLES=Off \
    -DBUILD_TESTS=Off
  make -C build docs
}

package() {
  cd "${srcdir}"/OpenCL-CLHPP*/

  DESTDIR="$pkgdir" make -C build install
  
  install -dm755 "${pkgdir}"/usr/share/doc/"${pkgname}"
  cp -r build/docs/html/* "${pkgdir}"/usr/share/doc/"${pkgname}"/
  install -D -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
