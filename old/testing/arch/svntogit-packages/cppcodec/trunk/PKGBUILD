# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=cppcodec
pkgver=0.2
pkgrel=3
pkgdesc='Header-only C++11 library to encode/decode base64, base64url, base32, base32hex and hex'
url='https://github.com/tplgy/cppcodec'
arch=('any')
license=('MIT')
makedepends=('cmake' 'catch2')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('50c9c81cdb12560c87e513e1fd22c1ad24ea37b7d20a0e3044d43fb887f4c6494c69468e4d0811cd2fc1ae8fdb01b01cfb9f3cfdd8611d4bb0221cbd38cbead3')
b2sums=('248487326d3fafb104d469d16cddab86fe63dec6eecb74af6211c262a42ff8806ee97619459f9dca648107748e30268206e84d64efba47a9ea1c398eb4c06919')

build() {
  cd ${pkgname}-${pkgver}
  cmake \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -B build
  make -C build
}

check() {
  cd ${pkgname}-${pkgver}
  make -C build test
}

package() {
  cd ${pkgname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 example/* -t "${pkgdir}/usr/share/doc/${pkgname}/example"
}

# vim: ts=2 sw=2 et:
