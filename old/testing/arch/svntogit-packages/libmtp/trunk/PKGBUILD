# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: damir <damir@archlinux.org>
# Contributor: Kevin Edmonds <edmondskevin@hotmail.com>

pkgname=libmtp
pkgver=1.1.18
pkgrel=1
pkgdesc="Library implementation of the Media Transfer Protocol"
arch=(x86_64)
url="http://libmtp.sourceforge.net"
license=(LGPL)
depends=(libusb)
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('7280fe50c044c818a06667f45eabca884deab3193caa8682e0b581e847a281f0'
            'SKIP')
validpgpkeys=(7C4AFD61D8AAE7570796A5172209D6902F969C95) # Marcus Mei√üner <meissner@suse.de>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-udev=/usr/lib/udev
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
