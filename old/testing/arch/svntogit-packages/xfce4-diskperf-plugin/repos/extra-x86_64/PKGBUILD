# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-diskperf-plugin
pkgver=2.6.3
pkgrel=1
pkgdesc="Plugin for the Xfce4 panel displaying instant disk/partition performance"
arch=('x86_64')
license=('BSD')
url="https://docs.xfce.org/panel-plugins/xfce4-diskperf-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('intltool')
source=(https://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('73b1ea2deb2403287337e8521d21345fb2f88445f9030732fa28f1bfa5d51c59')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
