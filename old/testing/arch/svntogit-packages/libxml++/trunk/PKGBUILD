# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgbase=libxml++
pkgname=(libxml++ libxml++-docs)
pkgver=3.2.2
pkgrel=1
pkgdesc="C++ bindings to libxml2"
url="https://libxmlplusplus.github.io/libxmlplusplus/"
arch=(x86_64)
license=(LGPL)
depends=(libxml2 glibmm)
makedepends=(glibmm-docs git meson mm-common)
options=(!emptydirs)
_commit=ba03d1ea29c1b6a8757be9b56637f19d1385ad3e  # tags/3.2.2^0
source=("git+https://github.com/libxmlplusplus/libxmlplusplus#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libxmlplusplus
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd libxmlplusplus
}

build() {
  arch-meson libxmlplusplus build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

# Do not remove the space before the () or commitpkg will
# accidentally to run this function on the system (!!!) 
package_libxml++ () {
  provides=(libxml++-3.0.so)

  DESTDIR="$pkgdir" meson install -C build

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_libxml++-docs() {
  pkgdesc+=" (documentation)"
  depends=(glibmm-docs)

  mv -t "$pkgdir" docs/*
}
