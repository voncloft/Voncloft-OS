# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xedit
pkgver=1.2.2
pkgrel=3
pkgdesc="Simple text editor for X"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw' 'libxmu' 'libxt' 'libx11')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xedit-${pkgver}.tar.bz2{,.sig})
sha512sums=('24d8e23a2ad1a2e81f6606928bb032f25b70b76da83b65341367bc4111d011abe89f9e30402d057854f2ccdaa9c1ef0d7c55bf29b81ab2d050a8f448cf3eeb5d'
            'SKIP')
validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A') # "Matt Turner <mattst88@gmail.com>"

build() {
  cd "${srcdir}/xedit-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xedit-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
