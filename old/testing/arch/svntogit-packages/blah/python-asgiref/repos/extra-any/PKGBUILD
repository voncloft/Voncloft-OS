# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-asgiref
pkgver=3.3.1
pkgrel=1
pkgdesc="Reference ASGI adapters and channel layers"
arch=(any)
url="http://github.com/django/asgiref"
license=('BSD')
makedepends=('python-setuptools')
checkdepends=(python-pytest python-pytest-asyncio)
depends=('python')
source=("https://pypi.io/packages/source/a/asgiref/asgiref-${pkgver}.tar.gz")
sha256sums=('7162a3cb30ab0609f1a4c95938fd73e8604f63bdba516a7f7d64b83ff09478f0')

build() {
    cd "asgiref-${pkgver}"
    python setup.py build
}

check() {
    cd "asgiref-${pkgver}"
    PYTHONPATH=. pytest .
}

package() {
    cd "asgiref-${pkgver}"
    python setup.py install --root=${pkgdir} --optimize=1

    install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
