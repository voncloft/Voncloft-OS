# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-themes-standard
pkgver=3.22.3+16+gc092fc89
pkgrel=1
pkgdesc="Standard Themes for GNOME Applications"
url="https://gitlab.gnome.org/GNOME/gnome-themes-extra"
arch=(x86_64)
license=(GPL2)
depends=(cantarell-fonts)
makedepends=(intltool gtk2 gtk3 gnome-common git)
optdepends=('gtk-engines: HighContrast GTK2 theme')
groups=(gnome)
options=(!emptydirs)
_commit=c092fc89acc581d145e63aae77e186284a56af71  # master~13
source=("git+https://gitlab.gnome.org/GNOME/gnome-themes-extra.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gnome-themes-extra
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gnome-themes-extra
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd gnome-themes-extra
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd gnome-themes-extra
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
