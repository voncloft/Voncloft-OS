# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=traceroute
pkgver=2.1.0
pkgrel=5
pkgdesc="Tracks the route taken by packets over an IP network"
arch=('x86_64')
url="http://traceroute.sourceforge.net/"
license=('GPL2')
depends=('glibc')
source=(https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
        reproducible-build.patch)
sha1sums=('bc5c6c8022187511be5665b3818d919be5987dcc'
          '49c777b929516dd72de8d518cfa97e7b88058750')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../reproducible-build.patch
}

build() {
  cd ${pkgname}-${pkgver}
  make CFLAGS="$CFLAGS" CPPFLAGS="$CPPFLAGS" LDFLAGS="$LDFLAGS"
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix=/usr DESTDIR="${pkgdir}" install
}
