# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-accounts-signon
pkgver=2.1
pkgrel=2
pkgdesc="A mission control plugin for Telepathy, integrating with libaccounts and libsignon to provide IM accounts and authentication"
arch=(x86_64)
url='https://gitlab.com/accounts-sso/telepathy-accounts-signon'
license=(LGPL)
depends=(telepathy-mission-control libaccounts-glib libsignon-glib)
makedepends=(meson)
source=($pkgname-$pkgver.tar.gz::"https://gitlab.com/accounts-sso/$pkgname/repository/archive.tar.gz?ref=$pkgver")
sha256sums=('4a39e8e45bc0aaf74b6c6188ce6d6388abe7552632201a80bc570b10c0555682')

prepare() {
  mkdir -p build
  mv $pkgname-$pkgver-* $pkgname-$pkgver
}

build() {
  cd build
  meson ../$pkgname-$pkgver \
    --prefix /usr
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
