# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libbytesize
pkgver=2.5
pkgrel=1
pkgdesc='A tiny library providing a C "class" for working with arbitrary big sizes in bytes'
arch=('x86_64')
url="https://github.com/rhinstaller/libbytesize"
license=('LGPL')
depends=('mpfr' 'pcre2')
makedepends=('python')
optdepends=('python: for bscalc command')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rhinstaller/libbytesize/archive/$pkgver.tar.gz")
sha512sums=('3ce78c39fa400de1083eac8fb7b528b5febf4b55aa9e86ac56a1828d4f90c490590074070f8e17d3b584271877d2b4022065f08a4566428c9c960524f444d33c')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
