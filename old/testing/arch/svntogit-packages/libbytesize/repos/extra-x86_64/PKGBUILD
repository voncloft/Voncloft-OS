# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libbytesize
pkgver=2.4
pkgrel=4
pkgdesc='A tiny library providing a C "class" for working with arbitrary big sizes in bytes'
arch=('x86_64')
url="https://github.com/rhinstaller/libbytesize"
license=('LGPL')
depends=('mpfr' 'pcre2')
makedepends=('python')
optdepends=('python: for bscalc command')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rhinstaller/libbytesize/archive/$pkgver.tar.gz")
sha512sums=('5192e3bad346758b4f66ae2c40170a2be792c78287f9784ec4a7c4419f1754098ef6fa463d568cb5e028a60945f85cbee8ef80af590a1d36c7cbe82a6f20fbab')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
