# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=iputils
# Commit date + git rev-parse --short origin/master
_rev=23c3782a
pkgver=20200821
pkgrel=1
pkgdesc="Network monitoring tools, including ping"
arch=('x86_64')
license=('GPL')
url="http://www.skbuff.net/iputils/"
depends=('libcap' 'libidn2')
makedepends=('perl-sgmls' 'git' 'docbook-xsl' 'meson' 'systemd')
conflicts=('netkit-base' 'arping' 'netkit-tftpd')
replaces=('netkit-base')
source=("git+https://github.com/iputils/iputils.git#commit=${_rev}")
sha1sums=('SKIP')

build() {
  mkdir -p build
  cd build

  arch-meson ../$pkgname -DBUILD_RARPD=true -DBUILD_TFTPD=true
  ninja
}

package() {
  cd build

  DESTDIR="$pkgdir" ninja install
}
