# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-todo
pkgver=3.28.1+33+gc03a4c1
pkgrel=1
pkgdesc="Task manager for GNOME"
url="https://wiki.gnome.org/Apps/Todo"
arch=(x86_64)
license=(GPL)
depends=(evolution-data-server libpeas python)
makedepends=(gobject-introspection appstream-glib git meson gtk-doc)
groups=(gnome-extra)
_commit=c03a4c1dd82ce523d71d74875118f9d24e7ed066  # gnome-3-28
source=("git+https://gitlab.gnome.org/GNOME/gnome-todo.git#commit=$_commit"
        gnome-todo-eds-libecal-2.0.patch)
sha256sums=('SKIP'
            '09d429f8dfe38b8712ce5ecc5e8cf7bc6e190810af5d5dca8e5119fe8ac2b573')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_TODO_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  
  # From https://src.fedoraproject.org/rpms/gnome-todo/tree/bed44b8530f3c79589982e03b430b3a125e9bceb
  git apply -3 ../gnome-todo-eds-libecal-2.0.patch
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
