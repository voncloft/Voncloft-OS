# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ignacio Galmarino <igalmarino@gmail.com>

pkgname=openresolv
pkgver=3.12.0
pkgrel=1
pkgdesc="resolv.conf management framework (resolvconf)"
arch=(any)
url="https://roy.marples.name/projects/openresolv"
license=('custom:BSD')
backup=(etc/resolvconf.conf)
provides=(resolvconf)
depends=(bash)
makedepends=(systemd)
source=("https://roy.marples.name/downloads/openresolv/${pkgname}-${pkgver}.tar.xz"
        fs33801.patch)
sha256sums=('42b30508e857a228535c631eaac936862d86eca68c14b5c0bf387ba176b91b97'
            '466678996689463af748bcdce7ea6f9f24f59004fa4db61fdc04da9f5fff2a6a')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fs33801.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/resolvconf --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE
}
