# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  Hyacinthe Cartiaux <hyacinthe.cartiaux at free.fr>
# Contributor: Bartlomiej Piotrowski <nospam at bpiotrowski dot pl>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=discount
pkgver=2.2.7
pkgrel=1
pkgdesc='A Markdown implementation written in C'
arch=(x86_64)
url="https://www.pell.portland.or.us/~orc/Code/discount/"
license=('custom:BSD')
source=($pkgname-$pkgver.tar.gz::"https://github.com/Orc/discount/archive/v$pkgver.tar.gz")
sha256sums=('72c1325ddfc40871d6810f1e272cf2d45b361f26357eb38f170fd04d737bb9f2')
depends=(glibc)
conflicts=(markdown)
provides=(markdown)
replaces=(markdown)

prepare() {
  sed -e 's|FLAGS=\"$AC_CFLAGS -shared\"|FLAGS="$AC_CFLAGS $AC_LDFLAGS -shared"|g' -i $pkgname-$pkgver/configure.inc # Honor system LDFLAGS in libmarkdown
}

build() {
  cd $pkgname-$pkgver
  ./configure.sh --prefix=/usr --enable-all-features --with-fenced-code --shared
  make -j1
}

package() {
  cd $pkgname-$pkgver
  # fix ldconfig call
  sed -e 's|/sbin/ldconfig|/sbin/ldconfig -n|' -i librarian.sh

  make DESTDIR="$pkgdir" install.everything
  install -Dm644 COPYRIGHT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
