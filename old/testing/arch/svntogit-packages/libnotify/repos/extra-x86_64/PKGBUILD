# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libnotify
pkgver=0.7.9
pkgrel=1
pkgdesc="Library for sending desktop notifications"
arch=(x86_64)
url="https://developer.gnome.org/notification-spec/"
license=(LGPL)
depends=(gdk-pixbuf2)
makedepends=(gtk3 gobject-introspection git meson docbook-xsl xmlto gtk-doc)
provides=(libnotify.so)
_commit=98a4bf483a69c6436311bcb9834d9d93235c96b7  # tags/0.7.9^0
source=("git+https://gitlab.gnome.org/GNOME/libnotify.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
