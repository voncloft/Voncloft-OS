# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xwininfo
pkgver=1.1.5
pkgrel=2
pkgdesc="Command-line utility to print information about windows on an X server"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxcb')
makedepends=('xorg-util-macros' 'libx11')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xwininfo-${pkgver}.tar.bz2{,.sig})
sha512sums=('a3268bbeeeeafac94e589fdd47554a965910d40df7cc3d9c36c7c24fa4f5dd49615484d5916ccb41efca9cd4cf9d8981a4c60953b921220b3f4f57f9cce0e1a6'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd xwininfo-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xwininfo-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
