# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=dmidecode
pkgver=3.3
pkgrel=1
pkgdesc="Desktop Management Interface table related utilities"
arch=('x86_64')
url="https://www.nongnu.org/dmidecode"
license=('GPL')
depends=('glibc')
source=("https://download.savannah.nongnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('82c737a780614c38a783e8055340d295e332fb12c7f418b5d21a0797d3fb1455'
            'SKIP')
validpgpkeys=('90DFD6523C57373D81F63D19865688D038F02FC8') # Jean Delvare <khali@linux-fr.org>

build() {
  cd ${pkgname}-${pkgver}

  sed -i "s:sbin:bin:g" Makefile
  make prefix=/usr CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}"
}

package() {
  cd ${pkgname}-${pkgver}

  make prefix=/usr DESTDIR="${pkgdir}" install
}
