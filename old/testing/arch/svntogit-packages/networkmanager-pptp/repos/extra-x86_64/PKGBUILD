# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-pptp
pkgver=1.2.9dev+10+gb41b0d0
pkgrel=3
pkgdesc="NetworkManager VPN plugin for PPTP"
url="https://wiki.gnome.org/Projects/NetworkManager"
arch=(x86_64)
license=(GPL)
_pppver=2.4.8
depends=(libnm libsecret "ppp=$_pppver" pptpclient)
makedepends=(libnma intltool python git)
optdepends=('libnma: GUI support')
_commit=b41b0d05337542b01240e6b9fafd4ca680671571  # master
source=("git+https://gitlab.gnome.org/GNOME/NetworkManager-pptp.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd NetworkManager-pptp
  git describe --tags | sed 's/-dev/dev/;s/-/+/g'
}

prepare() {
  cd NetworkManager-pptp
  intltoolize --automake --copy
  autoreconf -fvi
}

build() {
  cd NetworkManager-pptp
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --disable-static \
    --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd NetworkManager-pptp
  make DESTDIR="$pkgdir" install dbusservicedir=/usr/share/dbus-1/system.d
}

# vim:set sw=2 et:
