# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=freecell-solver
pkgver=6.2.0
pkgrel=1
pkgdesc="A program that automatically solves layouts of Freecell and similar variants of Card Solitaire"
arch=(x86_64)
url="https://fc-solve.shlomifish.org"
license=(MIT)
depends=(bash)
makedepends=(cmake gperf perl-moo perl-path-tiny perl-template-toolkit python-random2 python-six python-pysol_cards rinutils)
optdepends=('python-random2: for the Python interface' 'python-six: for the Python interface'
            'python-pysol_cards: for the Python interface')
source=(https://fc-solve.shlomifish.org/downloads/fc-solve/$pkgname-$pkgver.tar.xz)
sha256sums=('2267758cc00ec7e7f0c47e61f398032afb4bb5386d1e54d5164ca815547f7423')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DFCS_WITH_TEST_SUITE=OFF \
    -DBUILD_STATIC_LIBRARY=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/COPYING.html "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.html
}

