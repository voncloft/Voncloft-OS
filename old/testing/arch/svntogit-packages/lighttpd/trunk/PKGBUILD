# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=lighttpd
pkgver=1.4.59
pkgrel=2
pkgdesc='A secure, fast, compliant and very flexible web-server'
license=('custom')
arch=('x86_64')
url="https://www.lighttpd.net/"
depends=('pcre' 'util-linux' 'systemd')
makedepends=('mariadb-libs' 'lua' 'libxml2' 'e2fsprogs' 'sqlite' 'gdbm' 'pkgconfig')
optdepends=('libxml2: mod_webdav'
            'lua: mod_cml/mod_magnet'
            'mariadb-libs: mod_mysql_vhost/mod_authn_mysql'
            'sqlite: mod_webdav')
backup=('etc/lighttpd/lighttpd.conf' 'etc/logrotate.d/lighttpd')
options=('emptydirs')
source=("https://download.lighttpd.net/lighttpd/releases-1.4.x/lighttpd-${pkgver}.tar.xz"{,.asc}
        'lighttpd.logrotate.d' 'lighttpd.conf' 'lighttpd.tmpfiles' 'lighttpd.service')
sha256sums=('fb953db273daef08edb6e202556cae8a3d07eed6081c96bd9903db957d1084d5'
            'SKIP'
            '41f6c0042bb61021553779f861910e335834f6c15e4411756cdc6233b31076fe'
            'fece4581bebf39768571962dedce176b2b5f487c0abb5c1cfb35395de216c01f'
            'd8a185145a7c08b4fd8c8e6c12dae3e176389dd9b1c66e239757b2ba5108c871'
            '0c88403364e732c06090f6807105dedbac25aa82add0499d28007f8aaa780b78')
validpgpkeys=('C7CA1E9E29DC77F5480894B2E0E7D0171E95BAD7'
              '649D0DD767FF206202A76C5158F14A786FE198C8'
              'EAAF41A8BE3BB8D001CACD136DE62CA242909B84')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	./configure --prefix=/usr \
		--sbindir=/usr/bin \
		--libdir=/usr/lib/lighttpd/ \
		--sysconfdir=/etc/lighttpd \
		--with-mysql \
		--with-ldap \
		--with-attr \
		--with-openssl \
		--with-kerberos5 \
		--without-fam \
		--with-webdav-props \
		--with-webdav-locks \
		--with-gdbm \
		--with-lua
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgver"
	make check
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	install -D -m644 "$srcdir/lighttpd.logrotate.d" "$pkgdir/etc/logrotate.d/lighttpd"
	install -D -m644 "$srcdir/lighttpd.conf" "$pkgdir/etc/lighttpd/lighttpd.conf"
	install -d -m700 -o http -g http "$pkgdir"/var/{log,cache}/lighttpd/
	install -D -m644 "${srcdir}/lighttpd.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/lighttpd.conf"
	install -D -m644 "${srcdir}/lighttpd.service" "${pkgdir}/usr/lib/systemd/system/lighttpd.service"

	pushd doc/config >/dev/null
	find . -type f ! -name 'Makefile*' -exec install -D -m644 {} "${pkgdir}"/usr/share/doc/lighttpd/config/{} \;
	popd >/dev/null

	install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
