# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xfd
pkgver=1.1.3
pkgrel=2
pkgdesc="Displays all the characters in a font using either the X11 core protocol or libXft2"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw' 'fontconfig' 'libxft' 'libxrender' 'libxmu' 'libxkbfile')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xfd-${pkgver}.tar.bz2{,.sig})
sha512sums=('68e2d4ce5e24215353b6f3231191d69639c4dd90a703e03ccf152bbfe4012da071c387ba289f918fb0336a3be9ee49dc05cbec7273fa85f5e476ead7e10795f0'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd xfd-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xfd-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
