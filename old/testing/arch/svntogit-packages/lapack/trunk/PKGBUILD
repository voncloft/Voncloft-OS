# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Jason Taylor <jftaylor21@gmail.com>

pkgbase=lapack
pkgname=(lapack blas cblas lapacke lapack-doc)
pkgver=3.9.0
pkgrel=3
url="https://www.netlib.org/lapack"
pkgdesc="Linear Algebra PACKage"
makedepends=(gcc-fortran cmake python doxygen)
arch=(x86_64)
license=(custom)
source=($pkgbase-$pkgver.tar.gz::"https://github.com/Reference-LAPACK/lapack/archive/v$pkgver.tar.gz" LICENSE.blas
        lapacke-missing-symbols.patch::"https://github.com/Reference-LAPACK/lapack/commit/87536aa3.patch")
sha256sums=('106087f1bb5f46afdfba7f569d0cbe23dacb9a07cd24733765a0e89dbe1ad573'
            '2420cd1331c5754071966389b26e6196c5cb086a2ade0080953af62e0c6d4107'
            '327d62ee39eeb967ce48b4ed0b95e78c6210f688b5c3f944810aafe68d2dadfa')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../lapacke-missing-symbols.patch # https://github.com/Reference-LAPACK/lapack/issues/365
}

build() {
  install -d build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_Fortran_COMPILER=gfortran \
    -DLAPACKE_WITH_TMG=ON \
    -DCBLAS=ON \
    -DBUILD_DEPRECATED=ON
  make

# Build man pages
  cd ../$pkgname-$pkgver
  doxygen DOCS/Doxyfile_man
}

package_lapack() {
  depends=(blas)
  
  cd build
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/lib/{libblas.*,libcblas.*,liblapacke.*}
  rm -r "$pkgdir"/usr/lib/pkgconfig/{blas.*,cblas.*,lapacke.*}
  rm -r "$pkgdir"/usr/lib/cmake/{cblas*,lapacke*}
  rm -r "$pkgdir"/usr/include
}

package_blas() {
  pkgdesc="Basic Linear Algebra Subprograms"
  depends=(gcc-libs)

  cd build/BLAS
  make DESTDIR="$pkgdir" install
  
  install -m755 -d "$pkgdir/usr/share/licenses/blas"
  install -m644 "$srcdir/LICENSE.blas" \
    "$pkgdir/usr/share/licenses/blas/"
}

package_cblas() {
  pkgdesc="C interface to BLAS"
  depends=(blas)

  cd build/CBLAS
  make DESTDIR="$pkgdir" install
}

package_lapacke() {
  pkgdesc="C interface to LAPACK"
  depends=(lapack)

  cd build/LAPACKE
  make DESTDIR="$pkgdir" install
}

package_lapack-doc() {
  pkgdesc="Man pages for BLAS/LAPACK"

  mkdir -p "$pkgdir"/usr/share
  cp -r lapack-$pkgver/DOCS/man "$pkgdir"/usr/share
}
