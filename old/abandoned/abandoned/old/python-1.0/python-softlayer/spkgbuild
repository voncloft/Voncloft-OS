# Maintainer: Felix Yan <felixonmars@archlinux.org>

name=python-softlayer
version=5.9.2
release=2
pkgdesc="A library for SoftLayer's API"
url='https://github.com/softlayer/softlayer-python'
depends=('python-six' 'python-ptable' 'python-click' 'python-requests' 'python-prompt_toolkit'
         'python-pygments' 'python-urllib3')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-mock' 'python-testtools')
source=("$name-$version.tar.gz::https://github.com/softlayer/softlayer-python/archive/v$version.tar.gz")
sha512sums=('9ae24ad3c5505268efa216d7cd286954c83de5760452a68c1c666a1ba0719789ddc5f999c2a1be6f90915bac74f4269bda924b5799ca41a72be82f827e12965c')

prepare() {
  cd softlayer-python-$version
  sed -i 's/==/>=/' setup.py
}

build() {
  cd softlayer-python-$version
  python setup.py build
}

check() {
  cd softlayer-python-$version
  python setup.py pytest --addopts '-k "not test_edit_perms_on_bad"'
}

package() {
  cd softlayer-python-$version
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$name/LICENSE

  # It's only present when tests are enabled, so adding -f
  rm -rf "$pkgdir"/usr/lib/python3.9/site-packages/tests
}

# vim:set ts=2 sw=2 et:
