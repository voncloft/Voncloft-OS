# Maintainer: David Runge <dvzrv@archlinux.org>

_name=tree-format
name=python-tree-format
version=0.1.2
release=3
pkgdesc="Python library for printing trees on the console"
url="https://github.com/jml/tree-format"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-testtools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${version}.tar.gz")
sha512sums=('386b1d5852d70623514c6284cb2d4b7abe10c7b3b39ba2aca321d3590a065ad7cf1390fd518f9792bfbb9c13730beaf3504c399e25d39494e95af564a0f5b639')

prepare() {
  mv -v "${_name}-${version}" "$name-$version"
}

build() {
  cd "$name-$version"
  python setup.py build
}

check() {
  cd "$name-$version"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$name-$version"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${name}"
}
