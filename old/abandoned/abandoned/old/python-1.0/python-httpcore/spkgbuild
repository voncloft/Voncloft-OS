# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_name=httpcore
name=python-httpcore
version=0.12.0
release=3
pkgdesc="A minimal HTTP client"
url="https://github.com/encode/${_name}"
depends=('python-h11' 'python-sniffio')
optdepends=('python-h2: for HTTP/2 support')
makedepends=('python-setuptools' 'python-h2')
checkdepends=('python-anyio' 'python-curio' 'python-pproxy' 'python-pytest-trio' 'uvicorn')
source=("${name}-${version}.tar.gz::${url}/archive/${version}.tar.gz")
sha512sums=('173008b3a81957c1151c628356a399967872d2b184fce3226aa607977644aee12d0cd8dbceef87cf6271c45f283e444183ef7b547a4f00c5d1b234e76697ec14')
b2sums=('13a60577be926116a1b94c6397b0d35b35534204175dcb1d19743880dd57346d84e9d1cbe3b91dcc3b6d8442ec4cc2bcef1eeeda1b9b6ad121030b8f23124738')

prepare() {
    cd "${srcdir}"/${_name}-${version}

    # do not run coverage in unittests!
    sed -i '/^addopts/d' setup.cfg
}

build() {
    cd "${srcdir}"/${_name}-${version}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_name}-${version}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_name}-${version}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${name}/LICENSE.md
}
