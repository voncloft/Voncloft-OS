# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=partd
name=python-${_pkg}
version=1.1.0
release=3
pkgdesc="Concurrent appendable key-value storage"
url="https://github.com/dask/partd"
makedepends=(python-setuptools)
depends=(
    python
    python-locket
    python-toolz
)
optdepends=(
    python-blosc
    python-numpy
    python-pandas
    python-pyzmq
)
checkdepends=(
    python-pytest
    python-blosc
    python-numpy
    python-pandas
    python-pyzmq
)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${version}.tar.gz)
sha256sums=('6e258bf0810701407ad1410d63d1a15cfd7b773fd9efe555dac6bb82cc8832b0')

build() {
  cd ${_pkg}-${version}
  python setup.py build
}

check() {
  cd ${_pkg}-${version}
  PYTHONPATH="${PWD}"/build/lib pytest
}

package() {
  cd ${_pkg}-${version}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  # Install license file
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${name}/
}
