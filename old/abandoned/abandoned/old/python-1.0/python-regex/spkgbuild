# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nikola MilinkoviÄ‡ <nikmil@gmail.com>
# Submitter: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_pkgbase=regex
name=python-regex
version=2020.11.13
release=2
pkgdesc="Alternative python regular expression module."
url="https://bitbucket.org/mrabarnett/mrab-regex"
depends=('python')
makedepends=('python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/r/${_pkgbase}/${_pkgbase}-${version}.tar.gz")
sha256sums=('83d6b356e116ca119db8e7c6fc2983289d87b27b3fac238cfe5dca529d884562')
b2sums=('2ab9db519ae6569ee9f8a3b1d52c6ecf23b5d216a70e24f1357af88dbf93ca3d6d4eef17e267311faec47bbae6ea4041c6e5526c7cfa0ffeed3509d9c9336410')

build() {
  cd "regex-${version}"

  python setup.py build
}

check() {
  cd "regex-${version}"

  cd build/lib.linux-${CARCH}-3*/
  python -m unittest regex/test_regex.py
}

package() {
  cd "regex-${version}"

  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${name}/LICENSE.txt
}
