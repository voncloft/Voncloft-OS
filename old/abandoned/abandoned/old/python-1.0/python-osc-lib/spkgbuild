# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

name=python-osc-lib
version=2.2.1
release=3
pkgdesc="OpenStackClient Library"
url="https://docs.openstack.org/osc-lib/latest/"
depends=('python-six' 'python-pbr' 'python-babel' 'python-cliff' 'python-keystoneauth1'
         'python-os-client-config' 'python-oslo-i18n' 'python-oslo-utils' 'python-stevedore'
         'python-simplejson' 'python-openstacksdk')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-requests-mock')
source=("$name-$version.tar.gz::https://github.com/openstack/osc-lib/archive/$version.tar.gz")
sha512sums=('01f71ca4ed0f817a68f034acb04f1794afedde38714e93d398ed919464c0c4b6a274d27e8848adc81dcbd4d60787c5f360a77ce7d5510b8339d557d94a1d2003')

export PBR_VERSION=$version

prepare() {
  # https://storyboard.openstack.org/#!/story/2003142
  sed -i 's/issubclass/isinstance/' osc-lib-$version/osc_lib/utils/__init__.py

  sed -i 's/assertItemsEqual/assertCountEqual/' osc-lib-$version/osc_lib/tests/{cli,utils}/*.py
}

build() {
  cd osc-lib-$version
  python setup.py build
}

check() {
  cd osc-lib-$version
  stestr run
}

package() {
  cd osc-lib-$version
  python setup.py install --root="$pkgdir" --optimize=1
}
