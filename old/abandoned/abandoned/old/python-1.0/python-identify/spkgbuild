# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_name=identify
name=python-identify
version=1.5.13
release=1
pkgdesc="File identification library for Python"
url="https://github.com/chriskuehl/identify"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-editdistance' 'python-pytest')
source=("${_name}-${version}.tar.gz::https://github.com/chriskuehl/${_name}/archive/v${version}.tar.gz")
sha512sums=('f6f1e0c6c424c1ac2913048d09614b948c0c2cbe99bf3acbba1486a120c3f3950bd2c8906fc6cf92ad60c78a2e9052c9d23a4644b70e6a27e5da046724845078')
b2sums=('4578890fe7622f356c054f3e9baa8d6f4eae586134144e33582d916247a7ece0e1816c807aac742643cd3d0265fe27b0443560d5c87eae3e29c3d17169ba68db')

prepare() {
  mv -v "${_name}-${version}" "$name-$version"
}

build() {
  cd "$name-$version"
  python setup.py build
}

check() {
  cd "$name-$version"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$name-$version"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${name}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${name}"
}
