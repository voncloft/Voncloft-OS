# Maintainer: David Runge <dvzrv@archlinux.org>

_name=tabulate
name=python-tabulate
version=0.8.7
release=3
pkgdesc="Pretty-print tabular data in Python, a library and a command-line utility."
url="https://bitbucket.org/astanin/python-tabulate"
depends=('python-setuptools')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-numpy' 'python-pandas' 'python-wcwidth')
optdepends=('python-numpy: NumPy array support'
            'python-pandas: pandas.DataFrame support'
            'python-wcwidth: wide-character support')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${version}.tar.gz")
sha512sums=('963d12970227671fc80d26feacfafd37404034eba82778cee2450b8dbc7c723a7137209b601bc2adc13883543bff0d6b887ed2e7b91934a7f2ca4704ce5fd340')

build() {
  cd "${_name}-$version"
  python setup.py build
}

check() {
  cd "${_name}-$version"
  export PYTHONPATH="${PYTHONPATH}:${PWD}"
  python setup.py test
}

package_python-tabulate() {
  cd "${_name}-$version"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${name}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${name}/"
}
