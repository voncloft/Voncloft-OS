# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Tobias Roettger <toroettg@gmail.com>

name=python-ordered-set
version=4.0.2
release=3
pkgdesc='A MutableSet that remembers its order, so that every entry has an index'
url="https://github.com/LuminosoInsight/ordered-set"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/o/ordered-set/ordered-set-$version.tar.gz")
md5sums=('5d88f3870c32d4868b28c8fe833f7e74')

build() {
  cd ordered-set-$version
  python setup.py build
}

check() {
  cd ordered-set-$version
  pytest --doctest-modules test.py ordered_set.py README.md --doctest-glob=README.md --ignore=setup.py
}

package() {
  cd ordered-set-$version
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 MIT-LICENSE -t "$pkgdir"/usr/share/licenses/$name/
}

# vim:set ts=2 sw=2 et:
