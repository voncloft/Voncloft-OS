# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_name=asyncpg
name=python-$_name
version=0.21.0
release=2
pkgdesc='A fast PostgreSQL Database Client Library for Python/asyncio'
url='https://github.com/MagicStack/asyncpg'
depends=('python')
makedepends=('git' 'python-setuptools' 'cython')
checkdepends=('python-pytest-runner' 'python-uvloop' 'postgresql')
source=("git+$url.git#tag=v$version"
        'git+https://github.com/MagicStack/py-pgproto.git')
sha512sums=('SKIP'
            'SKIP')

prepare() {
  cd $_name

  git submodule init
  git config submodule.asyncpg/pgproto.url "$srcdir"/py-pgproto
  git submodule update

  sed -i "s|'Cython==.*'|'Cython'|" setup.py
}

build() {
  cd $_name

  python setup.py build_py build_ext
}

check() {
  cd $_name

  python setup.py pytest
}

package() {
  cd $_name

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$name/LICENSE
}

# vim:set ts=2 sw=2 et:
