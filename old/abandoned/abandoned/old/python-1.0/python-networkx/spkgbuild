# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>

name=python-networkx
version=2.5
release=3
pkgdesc='Python package for the creation, manipulation, and study of the structure, dynamics, and functions of complex networks.'
url="https://networkx.github.io"
depends=('python-decorator')
optdepends=('python-numpy: Provides sparse matrix representation of graphs and many numerical scientific tools.'
            'python-scipy: Provides flexible drawing of graphs.'
            'python-pyparsing: Required for pydot, GML file reading.'
            'python-yaml: Required for YAML format reading and writing.')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://github.com/networkx/networkx/archive/networkx-$version.tar.gz")
sha512sums=('8195e56d9b80e03a4d0b4472beb907813d49ca93564a30eeca5516a3fed43273cf753555d50607a08d146504a9c7d0430341c0570cce8dcd226e5bd68321284b')

build() {
  cd networkx-networkx-$version
  python setup.py build
}

check() {
  cd networkx-networkx-$version
  python setup.py pytest
}

package() {
  cd networkx-networkx-$version
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$name/LICENSE.txt
  mv "$pkgdir"/usr/share/doc/networkx-$version "$pkgdir"/usr/share/doc/$name
}
