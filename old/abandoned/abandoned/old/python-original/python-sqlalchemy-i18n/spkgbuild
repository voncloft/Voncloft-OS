# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=sqlalchemy-i18n
pkgname=python-sqlalchemy-i18n
pkgver=1.0.3
pkgrel=4
pkgdesc='Internationalization extension for SQLAlchemy models'
url='https://github.com/kvesteri/sqlalchemy-i18n'
arch=('any')
license=('BSD')
depends=('python' 'python-sqlalchemy' 'python-sqlalchemy-utils' 'python-six')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/kvesteri/sqlalchemy-i18n/archive/${pkgver}.tar.gz)
sha512sums=('08f5d0d00228fd9f39214b039f6b9260b99f5f940a3067daac695ce0c7e2909a0336abe081d3c272a8cb744e2f049ab108dea502875a52b987c1f52d27c52db8')

build() {
  cd ${_name}-${pkgver}
  python setup.py build
}

package() {
  cd ${_name}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
