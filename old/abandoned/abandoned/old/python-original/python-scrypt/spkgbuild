# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=python-scrypt
pkgver=0.8.17
pkgrel=3
pkgdesc='Bindings for the scrypt key derivation function library'
url='https://github.com/holgern/py-scrypt'
arch=('x86_64')
license=('BSD')
depends=('python' 'glibc' 'openssl')
makedepends=('python-setuptools')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5d233642e997e01deaf7f38c71f697ca5e2fa75bc7cb57e211303c9d6af3219cfdd1fe2d93db54a47ed6a04f50c247746598a34e6d2ff2aff8c9fae5da2ed146')
b2sums=('0286f6c51f4862fdc440e3e2728fe8fd6ad0d3c244691fed1c4569bf1a08c9ba1340b5e317e676dfec3b5acd0d8938e8fb333b38e47f766a6b090556c244edaa')

build() {
  cd py-scrypt-${pkgver}
  python setup.py build
}

check() {
  cd py-scrypt-${pkgver}
  python setup.py test
}

package() {
  cd py-scrypt-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  rm -r "${pkgdir}"/usr/lib/python*/site-packages/scrypt/tests
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
