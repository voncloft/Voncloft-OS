# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=pytest-regressions
pkgname=python-$_pkgname
pkgver=2.0.2
pkgrel=3
pkgdesc='Pytest plugin for regression testing'
arch=('any')
url='https://github.com/ESSS/pytest-regressions'
license=('MIT')
depends=('python-pytest' 'python-pytest-datadir' 'python-yaml')
makedepends=('python-setuptools-scm')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('47d11cb55321a5d3173d750c90e6c524f0ea520fdfb20086390faf53afa6c8cf1f5fe1e4319864e7b8bd8c26ad3ab6b6469c5a4eacb61b9471d397b4a2ab6941')

build() {
  cd $_pkgname-$pkgver

  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
