# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-js2py
pkgver=0.70
_commit=55d6bf39ba75e4e5bea2ec2df9a1314148323046
pkgrel=3
pkgdesc="JavaScript to Python Translator & JavaScript interpreter written in 100% pure Python"
url="https://github.com/PiotrDabkowski/Js2Py"
license=('MIT')
arch=('any')
depends=('python-tzlocal' 'python-six' 'python-pyjsparser')
makedepends=('python-setuptools' 'python-tzlocal'  'python-six' 'python-pyjsparser' )
checkdepends=('npm' 'python-numpy')
source=("$pkgname-$_commit.tar.gz::https://github.com/PiotrDabkowski/Js2Py/archive/$_commit.tar.gz")
sha512sums=('d61e28398f8ebb166e1faf718d68d32cb99ea226396341d9f8c58160cc17424ed56cb23d1ff33de5989eb11114446e332c3e5a8dd8a94a8d653007af59a7d900')

prepare() {
  mv Js2Py-{$_commit,$pkgver}
}

build() {
  cd Js2Py-$pkgver
  python setup.py build
}

check() {
  cd Js2Py-$pkgver
  python simple_test.py
}

package() {
  cd Js2Py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
