# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-cbor
pkgver=1.0.0
pkgrel=6
# The commit that bumps cbor/VERSION.py to 1.0.0
_commit=b3af679e7cf3e12d50acb83c3c591fc5db9a658d
pkgdesc='CBOR rfc7049 for Python'
arch=(x86_64)
url='https://github.com/brianolson/cbor_py'
license=(Apache)
makedepends=(python-setuptools)
depends=(python)
checkdepends=(python-pytest)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/brianolson/cbor_py/archive/$_commit.tar.gz")
sha256sums=('d9211361e37363dfbf54fcb21d23e610aa25679609044844305c2ee5b80b78d9')

prepare() {
  cd cbor_py-$_commit
  sed -i 's#logger.warn(#logger.warning(#' cbor/tests/*.py
}

build() {
  cd cbor_py-$_commit
  python setup.py build
}

check() {
  cd cbor_py-$_commit
  pyver=$(python -c "import sys; print('{}.{}'.format(*sys.version_info[:2]))")
  # test_sortkeys is broken https://github.com/brianolson/cbor_py/issues/6
  PYTHONPATH=build/lib.linux-$CARCH-$pyver pytest -k 'not test_sortkeys'
}

package() {
  cd cbor_py-$_commit
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
