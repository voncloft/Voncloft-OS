# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-pdoc
pkgver=0.3.2
pkgrel=7
pkgdesc='A simple program and library to auto generate API documentation for Python modules.'
arch=('any')
url='https://github.com/BurntSushi/pdoc'
license=('custom:Public Domain')
depends=('python-mako' 'python-markdown')
source=("$pkgname-$pkgver.tar.gz::https://github.com/BurntSushi/pdoc/archive/$pkgver.tar.gz")
sha512sums=('435d7279f67107043b7763b2fb571b777fbec578f9acf2cc96463587fe55e753ca2dc3b7925e7d595da8fd0d228991fd9b6191e614dca401316df320179cbb76')

prepare() {
  sed -i 's/markdown < 2.5/markdown/' pdoc-$pkgver/setup.py
}

build() {
  cd pdoc-$pkgver
  python setup.py build
}

package() {
  cd pdoc-$pkgver
  python setup.py install --root="$pkgdir" -O1

  install -Dm644 "$pkgdir"/usr/share/pdoc/UNLICENSE "$pkgdir"/usr/share/licenses/$pkgname/UNLICENSE
  rm -r "$pkgdir"/usr/share
}
