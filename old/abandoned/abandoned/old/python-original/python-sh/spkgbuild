# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=python-sh
pkgver=1.14.1
pkgrel=3
pkgdesc='Subprocess interface that allows you to call any program as if it were a function'
arch=('any')
url='https://pypi.python.org/pypi/sh'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('lsof' 'python-pytest')
source=("https://pypi.io/packages/source/s/sh/sh-$pkgver.tar.gz")
sha512sums=('9a4d9b841e81ae465b036f3fbf5e8ca277b090ffada660b17e9c2c457e09f7dbb0038b687fc0c0f4a6d575a55deb451c572a60f92f8c8d18ea65d6d902abb9b4')
b2sums=('e4439a54d4bf2c106ae5217b55d6fa19f2bc369fb63e8515954a017fd2e98eca88fce07ea3efa70b3c427826bec657aa75c057a09c88de76c1595ef89465ee32')

build() {
  cd sh-$pkgver
  python setup.py build
}

check() {
  cd sh-$pkgver
  export PYTHONPATH="build:${PYTHONPATH}"
  # test_no_fd_leak: AssertionError: 27 != 25
  pytest test.py --deselect test.py::MiscTests::test_no_fd_leak
}

package() {
  cd sh-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -vDm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/python-sh/LICENSE
  install -vDm 644 {README.rst,CHANGELOG.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
