# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-rtree
pkgver=0.9.7
pkgrel=2
pkgdesc="Rtree: spatial index for Python GIS"
url="https://toblerity.github.com/rtree/"
license=('LGPL')
arch=('any')
depends=('python' 'spatialindex')
makedepends=('python-setuptools' 'python-wheel')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-numpy')
source=("https://github.com/Toblerity/rtree/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f3add9440ddbc3b40366a3826c4206d03d46aa4e0e5cf90e5838347def7f9b52662458cb964a5f27cfd589637b2b1616dca62cbd89e6095601cf0f1881e2090b')

build() {
  cd rtree-$pkgver
  python setup.py build
}

check() {
  cd rtree-$pkgver
  python setup.py pytest
}

package() {
  cd rtree-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
