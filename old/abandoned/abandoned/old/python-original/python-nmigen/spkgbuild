# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=nmigen
pkgname=python-$_pkgname
pkgver=0.2
pkgrel=3
pkgdesc='A refreshed Python toolbox for building complex digital hardware'
arch=('any')
url='https://github.com/nmigen/nmigen'
license=('BSD')
depends=('python')
optdepends=('python-pyvcd: for nmigen.pysim'
            'python-jinja: for nmigen.build')
makedepends=('python-setuptools-scm')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('0fa6de5ae445a7f9f9d1e1703d9a1f3b9e7b5842a3e46f92454bc4493e780d24051159ddaf4528fa061c135670e587fee2eb8ba565602fd0d2ab94f3493dc147')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
