# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=precis_i18n
pkgname=python-precis_i18n
pkgver=1.0.2
pkgrel=3
pkgdesc='PRECIS framework (RFC 8264, RFC 8265, RFC 8266)'
url='https://github.com/byllyfish/precis_i18n'
arch=('any')
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=(https://github.com/byllyfish/precis_i18n/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('bbbc52ae11fc9f0da3f4723d52205c4103f6f96929f10926436b1bff3aee22ce9ff2f7bdf9df6c9f82a4b99aeea94a9ad5df85b9c9f03bccfa5109c35eb181d7')
b2sums=('0a3cbca0541ec80bbf44cbed76bbd2eaf2fc5d94a32109cc528476009b6906d4b985c0e2be4f739cae36946b8bdb7508ed259a7329ec65240caa3ff24cb12771')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python setup.py test
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst CHANGELOG.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
