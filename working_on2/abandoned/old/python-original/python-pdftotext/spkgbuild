# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Shane Stone <shanewstone at gmail>

_name=pdftotext
pkgname=python-pdftotext
pkgver=2.1.5
pkgrel=2
pkgdesc='Simple PDF text extraction'
arch=('x86_64')
url=https://github.com/jalan/pdftotext
license=('MIT')
depends=('poppler' 'python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('98aeb8b07a4127e1a30223bd933ef080bbd29aa88f801717ca6c5618380b8aa6')
b2sums=('35f5eeee4d278d15fb79a1efcd0576ab3a6188e2d440e4606c57335666877fed3fa3364beee9891e39b9dcd9fc7bd299e944a5342bd1267326a0b43a91e27376')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  mkdir -p temp
  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --skip-build --root=temp
  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$sitepackages" python -m unittest discover tests
}

package() {
  cd $_name-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
