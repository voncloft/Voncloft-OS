# Maintainer: Baptiste Jonglez <archlinux at bitsofnetworks dot org>

pkgname=python-sphinx-testing
pkgver=1.0.1
pkgrel=4
pkgdesc="Testing utility classes and functions for Sphinx extensions"
arch=('any')
url="https://github.com/sphinx-doc/sphinx-testing"
license=('BSD')
makedepends=('python-setuptools')
depends=('python-six' 'python-sphinx')
checkdepends=('python-mock' 'python-sphinx')
source=("https://pypi.io/packages/source/s/sphinx-testing/sphinx-testing-$pkgver.tar.gz")
sha256sums=('ef661775b5722d7b00f67fc229104317d35637a4fb4434bf2c005afdf1da4d09')

build() {
  cd "$srcdir/sphinx-testing-$pkgver"
  python setup.py build
}

check() {
  export PYTHONPATH="$srcdir/sphinx-testing-$pkgver/src"
  cd "$srcdir/sphinx-testing-$pkgver/tests"
  python -m unittest discover
}

package() {
  cd "$srcdir/sphinx-testing-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
