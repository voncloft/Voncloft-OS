# Maintainer: David Runge <dvzrv@archlinux.org>

_name=authheaders
pkgname=python-authheaders
pkgver=0.13.0
pkgrel=3
pkgdesc="Library for the generation of email authentication headers"
arch=('any')
url="https://github.com/ValiMail/authentication-headers"
license=('BSD' 'MPL2' 'ZPL')
depends=('publicsuffix-list' 'python-authres' 'python-dkim' 'python-dnspython'
'python-publicsuffix2')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc}
        psddmarc.csv::https://www.psddmarc.org/psddmarc-participants.csv)
sha512sums=('dfaccd9dedea8bebc22b831c481c733590e11a3483e42b45f0b6329bdaa98eec0644faf8da8b92c56c2d48a43eec9c52f8ea9a3c67ae841c46a7085cf0572177'
            'SKIP'
            'f9bfa3ad0090c08b796c5a8cb1d16f2ec4069632f89469f3594c3770778ec66bcada09f1c813badc992ddad9d19fc25fb63c83fe70243c0d260de07f44219498')
validpgpkeys=('E7729BFFBE85400FEEEE23B178D7DEFB9AD59AF1') # Donald Scott Kitterman <scott@kitterman.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  local python_stdlib_basepath="$(python -c "from sysconfig import get_path; print(get_path('stdlib'))")"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # use the system provided publicsuffix list (PSL)
  ln -svf /usr/share/publicsuffix/public_suffix_list.dat \
    "${pkgdir}/$python_stdlib_basepath/site-packages/authheaders/public_suffix_list.txt"
  # the psddmarc-participants.csv seemingly needs to be embedded:
  # https://github.com/ValiMail/authentication-headers/issues/4
  install -vDm 644 ../psddmarc.csv -t "${pkgdir}/$python_stdlib_basepath/site-packages/authheaders/"
  install -vDm 644 {CHANGES,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
