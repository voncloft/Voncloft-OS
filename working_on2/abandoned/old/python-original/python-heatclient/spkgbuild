# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-heatclient
pkgver=2.3.0
pkgrel=1
pkgdesc="OpenStack Orchestration API Client Library"
arch=('any')
url="https://docs.openstack.org/python-heatclient/latest/"
license=('Apache')
depends=('python-babel' 'python-pbr' 'python-cliff' 'python-iso8601' 'python-osc-lib'
         'python-prettytable' 'python-oslo-i18n' 'python-oslo-serialization' 'python-oslo-utils'
         'python-keystoneauth1' 'python-swiftclient' 'python-yaml' 'python-requests' 'python-six')
checkdepends=('python-stestr' 'python-testscenarios' 'python-requests-mock')
source=("https://github.com/openstack/python-heatclient/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-python3.9.patch::https://github.com/openstack/python-heatclient/commit/2b6f6ac284e4e749a8f6cf627cdc00c54a05f422.patch)
sha512sums=('22ed0b5ae6b0ae504dbdacd3a8eaf1fbbcfe556128f79295b8c24efffaeb560772e8bedae75828561b4fb4fb75fe1934625b4fd60dc967a3f71a0909c96ce6db'
            '2e0628ec2deab33f0489978021e4997c4395b889dfaa500f924424a99b490fa077b5ff70e5efbe9c4846c644a2d52841d528755e138b3049c9dd1c8c8bfecddf')

export PBR_VERSION=$pkgver

prepare() {
  patch -d python-heatclient-$pkgver -p1 < $pkgname-python3.9.patch || :
}

build() {
  cd python-heatclient-$pkgver
  python setup.py build
}

check() {
  cd python-heatclient-$pkgver
  stestr run
}

package() {
  cd python-heatclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
