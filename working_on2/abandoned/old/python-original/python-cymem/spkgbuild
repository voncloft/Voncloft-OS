# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cymem
pkgver=2.0.5
pkgrel=1
pkgdesc="Cython memory pool for RAII-style memory management"
url="https://github.com/explosion/cymem"
license=('MIT')
arch=('x86_64')
depends=('cython' 'python')
makedepends=('python-pip' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
source=("https://github.com/explosion/cymem/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('3bb1fa6a8dee6ffb60a244682a3d869b057bbe44b49c03e3ef79703a1076c50f9850f6e06c232ff317afcf213f77c7fb4561004a10056f806d5795b44c626ffe')

build() {
  cd cymem-$pkgver
  python setup.py build
}

check() {
  cd cymem-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.9" pytest
}

package() {
  cd cymem-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
