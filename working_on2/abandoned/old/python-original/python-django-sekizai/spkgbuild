# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-sekizai
pkgname=python-django-sekizai
pkgver=2.0.0
pkgrel=5
pkgdesc="Django Template Blocks with extra functionality"
arch=('any')
url="https://github.com/divio/django-sekizai"
license=('BSD')
depends=('python-django' 'python-django-classy-tags')
makedepends=('python-setuptools')
# test files are missing in pypi sdist tarball
# https://github.com/divio/django-sekizai/issues/87
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/divio/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('edac88decc17e98eb30e399cb6e3a2b21941c920163ef8c8d412fec28e5137e470401b65189a9f0cbb2adbce240a4c8c2cb03e0f9735d1de2d5445fcf32f6e03')
b2sums=('a1760cdcb234400af4bc5c0bc69699439a9082cda49731992ad57cf162beba7d88e4e06d826a6fcf7e34af11f5ac42e6c7f83ce058cb0af0566c50a5cc3a9efc')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  python setup.py test
}

package() {
  local site_packages="$(python -c "import site; print(site.getsitepackages()[0])")"
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # remove conflicting test files
  # https://github.com/divio/django-sekizai/issues/97
  rm -rvf "${pkgdir}/${site_packages}/tests"
}
