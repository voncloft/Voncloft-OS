# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flask-bootstrap
pkgver=3.3.7.1
pkgrel=3
pkgdesc="Ready-to-use Twitter-bootstrap for use in Flask"
url="https://github.com/mbr/flask-bootstrap"
license=('Apache')
arch=('any')
depends=('python-flask' 'python-dominate' 'python-visitor')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-requests' 'python-flask-appconfig' 'python-flask-nav'
              'python-flask-wtf' 'python-flask-debug')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mbr/flask-bootstrap/archive/$pkgver.tar.gz")
sha512sums=('010c71f69a95cb51afb449d1b43cb345ad23815ea89b368281f721a2af13cf2c143d67b586114898450d8e7fec413f45ff042cfca3dc1183cb0f50566e39fdc3')

build() {
  cd flask-bootstrap-$pkgver
  python setup.py build
}

check() {
  cd flask-bootstrap-$pkgver
  python setup.py pytest
}

package() {
  cd flask-bootstrap-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
