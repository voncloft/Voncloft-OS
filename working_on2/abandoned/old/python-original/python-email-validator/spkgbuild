# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-email-validator
pkgver=1.1.2
pkgrel=3
pkgdesc="A robust email syntax and deliverability validation library for Python 2.x/3.x."
arch=('any')
url="https://github.com/JoshData/python-email-validator"
license=('custom:CC0')
depends=('python-dnspython' 'python-idna')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/JoshData/python-email-validator/archive/v$pkgver.tar.gz")
sha512sums=('b40f8ecd65a9a5945a1a84b174398fc495afccd27ad5fcd7b4a03ecd26bcff6dd6ac3b5f75ac200cf2647b92d4defb25afe1dda28f82ed86bd8c2bd82708d0aa')

build() {
  cd python-email-validator-$pkgver
  python setup.py build
}

check() {
  cd python-email-validator-$pkgver
  python -m pytest
}

package() {
  cd python-email-validator-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 "LICENSE" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
