# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-annotation
pkgver=4.7.0
pkgrel=4
pkgdesc="Object annotation mechanism"
arch=('any')
url="https://github.com/zopefoundation/zope.annotation"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface' 'python-zope-component'
         'python-zope-location' 'python-zope-proxy')
checkdepends=('python-zope-configuration' 'python-zope-testing' 'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.annotation/archive/$pkgver.tar.gz")
sha512sums=('e9fb4bafb1e144967c2aab56d55f1b86b355ce59d740388339a7d38956b66b3d01c1cb4f80903721722e94fd87824b5760b0772b1109b08bd40864a6249e4ef3')

build() {
  cd zope.annotation-$pkgver
  python setup.py build
}

check() {
  cd zope.annotation-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.annotation-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
