# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=bowler
pkgname=python-bowler
pkgver=0.8.0
pkgrel=5
pkgdesc="Safe code refactoring for modern Python."
arch=('any')
url="https://github.com/facebookincubator/${_pkgname}"
license=('MIT')
depends=('python-attrs' 'python-click' 'python-fissix' 'python-setuptools' 'python-sh')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('f3e0ba2e6d62d84e9dc34c07fd4ae6453c8de480e14cdd67a5665cfeedbf8cb2')
b2sums=('766dcb5c1d13aae41f73bf4fed320cc5ccbfc117656deef513a98e39134e03def963e27564093aa98b6ec2e94b7adb67ca6fb06ddfd778a01f3f5f77959a68b1')

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py test
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
