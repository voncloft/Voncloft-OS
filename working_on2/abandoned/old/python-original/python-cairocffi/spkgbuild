# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Florijan Hamzic <florijanh@gmail.com>

pkgname=python-cairocffi
pkgver=1.2.0
pkgrel=3
pkgdesc="cairocffi is a CFFI-based drop-in replacement for Pycairo, a set of Python bindings and object-oriented API for cairo."
arch=('any')
url="https://cairocffi.readthedocs.io/"
license=('BSD')
depends=('python-cffi' 'cairo' 'python-xcffib')
makedepends=('python-setuptools' 'gdk-pixbuf2')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-pytest-flake8'
              'python-pytest-isort' 'python-numpy')
optdepends=('gdk-pixbuf2: for cairocffi.pixbuf')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Kozea/cairocffi/archive/v${pkgver}.tar.gz")
sha512sums=('06741670d0e10be6d762fa4d85c8dfb4d26827f5e2c5863ca6301ea3feeacb52bb0f2c86fcf095894f99523a22624142e05ce6035ad07ec07555b18a38f7a79f')


build() {
  cd cairocffi-$pkgver
  python setup.py build
}

check() {
  cd cairocffi-$pkgver
  python setup.py pytest
}

package() {
  cd cairocffi-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
