# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=spyder-kernels
name=python-${_pkg}
version=1.10.1
release=1
pkgdesc="Jupyter Kernels for the Spyder console"
url="https://github.com/spyder-ide/spyder-kernels/"
makedepends=(python python-setuptools)
depends=(
    python-cloudpickle
    python-ipykernel
    python-jupyter_client
    python-pyzmq
    python-wurlitzer
)
checkdepends=(
    cython
    python-dask
    python-distributed
    python-flaky
    python-matplotlib
    python-mock
    python-numpy
    python-pandas
    python-pillow
    python-pytest
    python-scipy
    python-xarray
)
# No tests in pypi tarballs
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${version}.tar.gz)
source=(${url}/archive/v${version}/${name}-${version}.tar.gz)
sha256sums=('57cb546279b0f2487322ecebba1bf107b542cc7744c7cf9facecd6fe6715d649')

build() {
  cd ${_pkg}-${version}
  python setup.py build
}

check() {
  cd ${_pkg}-${version}
  pytest spyder_kernels
}

package() {
  cd ${_pkg}-${version}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${name}/
}
