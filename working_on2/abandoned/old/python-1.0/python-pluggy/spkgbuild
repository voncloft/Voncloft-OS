# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pluggy
name=(python-pluggy python2-pluggy)
version=0.13.1
release=4
pkgdesc="Plugin and hook calling mechanisms for python"
url="https://github.com/pytest-dev/pluggy"
makedepends=('python-setuptools-scm' 'python2-setuptools-scm' 'python-importlib-metadata'
             'python2-importlib-metadata')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$version.tar.gz::https://github.com/pytest-dev/pluggy/archive/$version.tar.gz")
sha512sums=('3269f7ad7801a70ba8aa3182c568f70e2ee88a90b6f6335be631f9737dd42fac3fb1b27c5b6fe6422de128ddcc58736f0c41ed60b7c0177126dd676b98ece301')

prepare() {
  cp -a pluggy-$version{,-py2}

  export SETUPTOOLS_SCM_PRETEND_VERSION=$version
}

build() {
  cd "$srcdir"/pluggy-$version
  python setup.py build

  cd "$srcdir"/pluggy-$version-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pluggy-$version
  python setup.py pytest

  cd "$srcdir"/pluggy-$version-py2
  python2 setup.py pytest
}

package_python-pluggy() {
  depends=('python-importlib-metadata')

  cd pluggy-$version
  python setup.py install -O1 --root "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$name/LICENSE
}

package_python2-pluggy() {
  depends=('python2-importlib-metadata')

  cd pluggy-$version-py2
  python2 setup.py install -O1 --root "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$name/LICENSE
}
