# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=spur
name=python-${_pkg}
version=0.3.22
release=3
pkgdesc="Run commands and manipulate files locally or over SSH using the same interface"
url="https://github.com/mwilliamson/spur.py"
makedepends=(python-setuptools)
depends=(python python-paramiko)
checkdepends=(python-nose)
# No tests in pypi tarballs
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${version}.tar.gz)
source=(${url}/archive/${version}/${name}-${version}.tar.gz)
sha256sums=('9b4c16675d9eda3c98b56d8886a877d8d2d310530b9396e90209ecef41e06c72')

build() {
  cd ${_pkg}.py-${version}
  python setup.py build
}

check() {
  cd ${_pkg}.py-${version}
  # https://github.com/mwilliamson/spur.py/issues/85
  nosetests || echo "Tests failed"
}

package() {
  cd ${_pkg}.py-${version}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  # Install license file
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${name}/
}
