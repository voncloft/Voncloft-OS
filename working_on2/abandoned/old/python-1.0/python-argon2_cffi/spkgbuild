# Maintainer: Balló György <ballogyor+arch at gmail dot com>

_name=argon2_cffi
name=python-argon2_cffi
version=20.1.0
release=3
pkgdesc="CFFI-based Argon2 Bindings for Python"
url="https://github.com/hynek/argon2_cffi"
depends=(argon2 python-cffi python-six)
makedepends=(git python-setuptools)
_commit=b4a022c86db8acb06c789a39fad1c66c2428279e  # tags/20.1.0^0
source=("git+https://github.com/hynek/argon2_cffi#commit=$_commit"
        "git+https://github.com/p-h-c/phc-winner-argon2")
sha256sums=('SKIP'
            'SKIP')

version() {
  cd $_name
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_name
  git submodule init
  git config --local submodule.extras/libargon2.url "$srcdir/phc-winner-argon2"
  git submodule update
  cd ..
}

build() {
  cd $_name
  python3 setup.py build
}

package() {
  cd $_name
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$name/LICENSE"
}
