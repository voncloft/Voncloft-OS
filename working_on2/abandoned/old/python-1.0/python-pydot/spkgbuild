# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contrinutor: shining <shiningxc@gmail.com>
# Contributor: phillid <dbphillipsnz _at_thingy_that_swirly_a_symbol gmaildott comm>

name=python-pydot
version=1.4.1
release=5
pkgdesc="Python interface to Graphviz's Dot"
url="https://github.com/erocarrera/pydot"
depends=('python-pyparsing' 'graphviz')
makedepends=('python-setuptools')
checkdepends=('python-chardet')
source=("$name-$version.tar.gz::https://github.com/erocarrera/pydot/archive/v$version.tar.gz")
sha512sums=('b5f16b59908586df61cd415cb1a3e242fb69c39674a82b171862beff1f3727f47cba39bdd806901cb56bc8a6888783a53d112b1b70044e124ea992789d77f938')

build() {
  cd pydot-$version
  python setup.py build
}

check() {
  # https://github.com/pydot/pydot/issues/204

  cd "$srcdir"/pydot-$version/test
  PYTHONPATH="$PWD/../build/lib:$PYTHONPATH" python pydot_unittest.py || echo "Tests failed"
}

package() {
  cd pydot-$version
  python setup.py install -O1 --root="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$name/LICENSE
}
