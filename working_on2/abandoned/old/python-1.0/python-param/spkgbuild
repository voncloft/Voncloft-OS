# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=param
name=python-${_pkg}
version=1.10.0
release=1
pkgdesc="Make your Python code clearer and more reliable by declaring Parameters"
url="https://param.holoviz.org/"
makedepends=(python-setuptools)
checkdepends=(python-nose python-numpy python-pandas ipython)
# No tests in Pypi tarballs
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${version}.tar.gz)
source=(https://github.com/holoviz/param/archive/v${version}/${_pkg}-${version}.tar.gz)
sha256sums=('197e058ab2d35a08e029a0cdbe13e43045e35c607af99857858752e14959e541')

build() {
    cd ${_pkg}-${version}
    python setup.py build
}

check() {
    cd ${_pkg}-${version}
    PARAM_TEST_NUMPY=1 PARAM_TEST_PANDAS=1 PARAM_TEST_PANDAS=1 nosetests
}

package() {
    cd ${_pkg}-${version}
    python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
    install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${name}
}
