# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pipenv-to-requirements
pkgver=0.9.0
pkgrel=3
pkgdesc="Generate requirements[-dev].txt from Pipfile using pipenv"
url="https://github.com/gsemet/pipenv-to-requirements"
license=('MIT')
arch=('any')
depends=('python-pbr' 'python-pipenv')
checkdepends=('python-pyfakefs' 'python-pytest-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gsemet/pipenv-to-requirements/archive/$pkgver.tar.gz")
sha512sums=('f89be89e120cc3d9a543c8efc1ff4c517f3d47145133a7db2f842dcbf168099588a2d281b7fb69b6ddf63439fbdebafd683b258a349cc7ad50efd454495caf14')

export PBR_VERSION=$pkgver

build() {
  cd pipenv-to-requirements-$pkgver
  python setup.py build
}

check() {
  cd pipenv-to-requirements-$pkgver
  pytest
}

package() {
  cd pipenv-to-requirements-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
