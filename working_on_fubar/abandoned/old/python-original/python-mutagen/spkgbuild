# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

_name=mutagen
pkgname=python-mutagen
pkgver=1.45.1
pkgrel=3
arch=('any')
pkgdesc="An audio metadata tag reader and writer (python library)"
url="https://github.com/quodlibet/mutagen"
license=('GPL2')
depends=('python-setuptools')
checkdepends=('python-pytest' 'python-hypothesis' 'python-pyflakes'
'python-pycodestyle')
source=("https://github.com/quodlibet/${_name}/releases/download/release-$pkgver/${_name}-$pkgver.tar.gz"{,.sig})
sha512sums=('c5244efa01b9de5ec22ceca39e55f34b933ca4a5bec0f2088fd3adca657f0230cbe85cd5e156db1d90fb44d9339bc94dc3bdcd3e49f1b0d46bc3d71ece6b45d8'
            'SKIP')
b2sums=('a1e29698d96b4c98f16e18dd8b3f902e41dcc4409d01da861a726a6a9fbd7f116e07b4e1872b8b3857f0eec080d18bac736de741635746f5c71416a3a8ef6e78'
        'SKIP')
validpgpkeys=('0EBF782C5D53F7E5FB02A66746BD761F7A49B0EC') # Christoph Reiter <reiter.christoph@gmail.com>

prepare() {
  mv -v "${_name}-$pkgver" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgbase}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgbase}-${pkgver}"
  pytest -v --ignore tests/quality/test_flake8.py
}

package() {
  cd "${pkgbase}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --prefix=/usr \
                          --root="${pkgdir}"
  install -vDm 644 {NEWS,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
