# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pyls-black
pkgname=python-${_pkg}
pkgver=0.4.6
pkgrel=1
pkgdesc="Black plugin for the Python Language Server"
arch=(any)
url="https://github.com/rupert/pyls-black"
license=(MIT)
depends=(python-black python-language-server python-toml)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
# PyPi tarballs do not have the tests
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/v${pkgver}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('c591f57b28852e095dafb87cdd3d16fc3dfe188ec199b728f34569d472d63150')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  PYTHONPATH="${PWD}/build/lib" pytest -v --deselect tests/test_plugin.py::test_entry_point
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
