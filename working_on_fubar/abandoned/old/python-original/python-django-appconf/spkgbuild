# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-appconf
pkgname=python-django-appconf
pkgver=1.0.4
pkgrel=4
pkgdesc="An app to handle configuration defaults of packaged Django apps gracefully"
arch=('any')
url="https://github.com/django-compressor/django-appconf"
license=('BSD')
depends=('python-django' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('0d0a206d43d4c6d7f3dbe5fb6423f5f012dae42bae7459f3ee06501f9c126e73f69a30da5c34c84014e582a7b42f253ebd37ece8b8477963cb5d2357ca095639')
b2sums=('21af3c3913aafe6a7670256bb10e80d0ac286fdeea646f91e8696470be3c1dcfb0b6d04e626f80784de02693583bf0b7a16da658175f8a1d81200a5c837831ee')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # removing pre-populated __pycache__ directory
  # https://github.com/django-compressor/django-appconf/issues/72
  rm -rf tests/__pycache__
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="${PWD}:build:${PYTHONPATH}"
  DJANGO_SETTINGS_MODULE=tests.test_settings coverage run /usr/bin/django-admin.py test -v2 tests
  rm -rf tests/__pycache__
}

package() {
  cd "$pkgname-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 {AUTHORS,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
