# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pydle
pkgver=0.9.4
pkgrel=2
pkgdesc="A compact, flexible and standards-abiding IRC library"
url="https://github.com/Shizmob/pydle"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
optdepends=('python-pure-sasl: for SASL support')
checkdepends=('python-pytest' 'python-pure-sasl')
source=("https://github.com/Shizmob/pydle/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('3b61b603aca1f6da6cc8674fe07f3f14a6d14a7db27c33d376c6e5275feb1a00b4e6a8c19d1a52a67e3dae1440292a8cbac13fc0a73d87b5205d8ecde03ec61c')

build() {
  cd pydle-$pkgver
  python setup.py build
}

check() {
  cd pydle-$pkgver
  # https://github.com/Shizmob/pydle/issues/78
  pytest || echo "Tests failed"
}

package() {
  cd pydle-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
