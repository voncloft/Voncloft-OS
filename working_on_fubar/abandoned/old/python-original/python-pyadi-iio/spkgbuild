# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=pyadi-iio
pkgname=python-$_pkgname
pkgver=0.0.4
pkgrel=2
pkgdesc='Python interfaces for ADI hardware with IIO drivers'
arch=('any')
url='https://github.com/analogdevicesinc/pyadi-iio'
license=('custom')
depends=('libiio' 'python-numpy')
makedepends=('python-setuptools' 'python-sphinx' 'python-sphinx_rtd_theme')
checkdepends=('python-pytest-runner' 'python-scapy')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('bf1c0eab452c2891d16802a028e2291fc4493c872ea256956e6061f15b4233de5100b9a23651e5ce36f0ae245b7caa19a0a47b1ffd8eb2dc9fa4ed0011a202b4')

build() {
  cd $_pkgname-$pkgver

  python setup.py build

  cd doc

  make html
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  # Install documentation
  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  cp -r -a --no-preserve=ownership doc/build/html "$pkgdir"/usr/share/doc/$pkgname

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
