# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Fernando Jim√©nez Solano <fjim@sdfeu.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=python-pyacoustid
pkgver=1.2.0
pkgrel=2
pkgdesc='Bindings for Chromaprint acoustic fingerprinting and the Acoustid API'
arch=(any)
url=https://github.com/beetbox/pyacoustid
license=(MIT)
depends=(
  chromaprint
  python
)
makedepends=(
  git
  python-setuptools
)
source=(git+https://github.com/beetbox/pyacoustid.git#tag=dd69cfd1b68ac15bf84597b6ab5ab0aae3226a83)
sha256sums=(SKIP)

pkgver() {
  cd pyacoustid

  git describe --tags | sed 's/^v//'
}

build() {
  cd pyacoustid

  python setup.py build
}

package() {
  cd pyacoustid

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-pyacoustid/
}

# vim: ts=2 sw=2 et:
