# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sebastien Binet <binet@lblbox>

pkgname=python-ptrace
pkgver=0.9.7
pkgrel=3
pkgdesc='Python binding of ptrace library to trace processes and syscalls'
url='https://github.com/haypo/python-ptrace'
arch=('any')
license=('GPL2')
depends=('python' 'python-six')
makedepends=('python-sphinx' 'python-setuptools')
source=(https://github.com/haypo/python-ptrace/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('2893da4bc63015c1173f70657a4132018d7397e3d7ef2bc9532903fdc3386ee25ce92c8d0b38c2fd31f0089bc67b43354b178fdc27bd25916c8c2704cf8fcce7')
b2sums=('c171104b1858eaad396514ed248b6b34aea4dd0a65028743e17c2eebb8d02e3f9dcfae7e22d56de0e468bae8ad1e110110c2d0eb5b695e057233ec5d975bd7c6')

build() {
  echo "Building python..."
  cd ${pkgbase}-${pkgver}
  python setup.py build
  make -j1 -C doc man text SPHINXBUILD=sphinx-build
}

check() {
  echo "Checking python..."
  cd ${pkgbase}-${pkgver}
  python runtests.py || echo "Warning: tests only run in an env with ptrace allowed"
}

package() {
  cd ${pkgbase}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 doc/build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a examples "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/build/man/${pkgbase}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
