# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-invoke
pkgver=1.5.0
pkgrel=1
pkgdesc="Pythonic task execution"
url="https://github.com/pyinvoke/invoke"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-mock' 'python-pytest-relaxed')
source=("https://pypi.io/packages/source/i/invoke/invoke-$pkgver.tar.gz")
sha512sums=('ac6b1939907ba34930836f567454aeea63373aa510644964eb18e1e684f30d4053c75a73506d0e947a38ca206daf847ce927ec2df15e73932278c2c73c3e20bd')

prepare() {
  find invoke-$pkgver -type f -name '*.pyc' -delete
}

build() {
  cd "$srcdir"/invoke-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/invoke-$pkgver
  python setup.py pytest || echo "Tests failed"
}

package() {
  cd invoke-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
