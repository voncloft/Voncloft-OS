# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-keystoneclient
pkgver=4.2.0
pkgrel=1
pkgdesc="Client Library for OpenStack Identity"
arch=('any')
url="https://docs.openstack.org/python-keystoneclient/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-iso8601' 'python-prettytable' 'python-debtcollector'
         'python-keystoneauth1' 'python-oslo-utils' 'python-oslo-i18n'
         'python-oslo-serialization' 'python-oslo-config' 'python-requests' 'python-stevedore')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-requests-mock' 'python-pycodestyle' 'python-lxml'
              'python-testresources')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-keystoneclient/archive/$pkgver.tar.gz")
sha512sums=('1dcf97f4f0601077a786ff5ce5678f5bd2022b27624652a42a355e35ae858071e60dac716b5b0e76265eed5d04b5091032450cff2aa25ac2b2b1cb3daa01e125')

export PBR_VERSION=$pkgver

build() {
  cd python-keystoneclient-$pkgver
  python setup.py build
}

check() {
  cd python-keystoneclient-$pkgver
  stestr run
}

package() {
  cd python-keystoneclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
