# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Cedric Girard <girard.cedric@gmail.com>

_pkgname=contextlib2
pkgbase=python-$_pkgname
pkgname=(python-$_pkgname python2-$_pkgname)
pkgver=0.6.0.post1
pkgrel=3
pkgdesc="Backport of the standard library's contextlib module to earlier Python versions"
arch=('any')
url='https://github.com/jazzband/contextlib2'
license=('PSF')
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgname-$pkgver::$url/archive/v$pkgver.tar.gz")
sha512sums=('8fd8073b4aab96d46a8d2aacfde9e9d3b1c517d9f05a86ae07e1528696c11313c9b92bee9673d8333f62bb3503a8ef3c488db7c09f3a13df5752e341e572f23f')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}
}

build() {
  echo "Building python-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver
  python setup.py build

  echo "Building python2-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver-py2
  python2 setup.py build
}

package_python-contextlib2() {
  depends=('python')
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-contextlib2() {
  depends=('python2')
  cd $_pkgname-$pkgver-py2

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
