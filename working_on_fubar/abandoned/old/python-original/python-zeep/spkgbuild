# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zeep
pkgver=4.0.0
pkgrel=3
pkgdesc="A fast and modern Python SOAP client"
arch=('any')
license=('MIT')
url="https://docs.python-zeep.org"
depends=('python-appdirs' 'python-attrs' 'python-cached-property' 'python-defusedxml'
         'python-isodate' 'python-lxml' 'python-requests' 'python-requests-toolbelt'
         'python-requests-file' 'python-pytz')
checkdepends=('python-pytest-runner' 'python-freezegun' 'python-mock' 'python-pretend'
              'python-pytest-cov' 'python-requests-mock' 'python-pytest-httpx'
              'python-pytest-asyncio' 'python-aiohttp' 'python-aioresponses' 'python-xmlsec')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mvantellingen/python-zeep/archive/$pkgver.tar.gz")
sha512sums=('48999c8b6144021d974ce40db0fdfe9702a9b2984c1ebd8b39c426a0dc58a50e3bffc439586d3bb6bfd7f69b42a32758bd09b0b4c1dfc061fd61d36d1e346e58')

prepare() {
  sed -i -e 's/==/>=/' -e '/flake8/d' -e '/isort/d' python-zeep-$pkgver/setup.py

  #sed -i 's/get_marker/get_closest_marker/' python-zeep-$pkgver/tests/conftest.py
}

build() {
  cd python-zeep-$pkgver
  python setup.py build
} 

check() {
  cd python-zeep-$pkgver
  python setup.py pytest
}

package() {
  cd python-zeep-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
