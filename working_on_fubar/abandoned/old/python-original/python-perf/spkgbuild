# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: j605

pkgbase='python-perf'
pkgname=('python-perf' 'python2-perf')
pkgver=1.6.0
pkgrel=5
pkgdesc='Toolkit to run Python benchmarks'
url='https://github.com/haypo/perf'
arch=('any')
license=('MIT')
makedepends=('python' 'python-setuptools' 'python-six'
             'python2' 'python2-setuptools' 'python2-six' 'python2-statistics')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/vstinner/perf/archive/${pkgver}.tar.gz)
sha256sums=('7af7b9cfd9d26548ab7127f8e51791357ecd78cda46aad5b2d9664a70fc58878')
sha512sums=('051aa421dafad336cf26ce839837e25fad33a8377a2c71ce1d0e4c7b768ccb36414531dac2a0f0890b7c1cb9dc79c66d32e42d11f0600f55e9d36dc5f62a8d06')

prepare() {
  cp -a pyperf-${pkgver}{,-py2}
  sed 's|pyperf=|pyperf2=|' -i pyperf-${pkgver}-py2/setup.py
}

build() {
  (cd pyperf-${pkgver}
    python setup.py build
  )
  (cd pyperf-${pkgver}-py2
    python2 setup.py build
  )
}

package_python-perf(){
  depends=('python' 'python-setuptools' 'python-six')
  cd pyperf-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python2-perf() {
  depends=('python2' 'python2-setuptools' 'python2-six' 'python2-statistics')
  cd pyperf-${pkgver}-py2
  python2 setup.py install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
