# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-jellyfish
pkgver=0.8.2
pkgrel=3
pkgdesc='A python library for doing approximate and phonetic matching of strings'
arch=(x86_64)
url='https://github.com/jamesturk/jellyfish'
license=(BSD)
depends=(python)
makedepends=(
  git
  python-setuptools
)
source=(
  git+https://github.com/jamesturk/jellyfish.git#tag=a5983092df14a45ab0cf0c3a8a071a3eb66f8e9a
  git+https://github.com/jamesturk/cjellyfish.git
)
sha256sums=(
  SKIP
  SKIP
)

pkgver() {
  cd jellyfish

  git describe --tags
}

prepare() {
  cd jellyfish

  for submodule in cjellyfish; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule}
    git submodule update ${submodule}
  done

}

build() {
  cd jellyfish

  python setup.py build
}

package() {
  cd jellyfish

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-jellyfish/
}

# vim: ts=2 sw=2 et:
