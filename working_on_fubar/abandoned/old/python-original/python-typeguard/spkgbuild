# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-typeguard
pkgver=2.10.0
pkgrel=3
pkgdesc="Run-time type checker for Python"
url="https://github.com/agronholm/typeguard"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-cov' 'python-typing_extensions')
source=("$pkgname-$pkgver.tar.gz::https://github.com/agronholm/typeguard/archive/$pkgver.tar.gz")
sha512sums=('1fb1b27369f279a467a0d524618c79f1369afa10579256826ab589158069f59676d496284079a15f1217272f76934ea162e5baaaac5474ff5dbd1de7764535f0')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd typeguard-$pkgver
  python setup.py build
}

check() {
  cd typeguard-$pkgver
  python -m pytest
}

package() {
  cd typeguard-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
