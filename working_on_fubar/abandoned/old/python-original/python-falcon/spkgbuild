# Maintainer: David Runge <dvzrv@archlinux.org>

_name=falcon
pkgname=python-falcon
pkgver=2.0.0
pkgrel=6
pkgdesc="Web API framework for building fast and reliable microservices, proxies, and app backends in Python"
arch=('x86_64')
url="https://falconframework.org/"
license=('Apache')
depends=('python')
makedepends=('cython' 'python-setuptools')
checkdepends=('python-msgpack' 'python-mujson' 'python-rapidjson'
'python-requests' 'python-pytest' 'python-pytest-runner' 'python-testtools'
'python-ujson' 'python-yaml')
optdepends=('python-django: for falcon-bench'
            'python-flask: for falcon-bench'
            'python-pecan: for falcon-bench'
            'python-bottle: for falcon-bench'
            'python-pprofile: for falcon-bench'
            'python-vmprof: for falcon-bench')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('1b821d96452208dcfbaa978ecc9340b0b531878aeb362a5b27668fea5f354315275afc7c2ba3012b2cf86c8e32b52d10c932bcf2cc91789a1b4f5a458da5078b')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -k 'not test_traverse_with_verbose and not test_imported_from_c_modules' \
         --ignore 'tests/test_media_handlers.py'
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
