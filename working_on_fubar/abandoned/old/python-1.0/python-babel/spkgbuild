# Maintainer: Morten Linderud <foxboron@archlinux.no>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor : Giedrius Slavinskas <giedrius25@gmail.com>

name=python-babel
version=2.9.0
_core=37
release=1
pkgdesc="A collection of tools for internationalizing Python applications"
url="http://babel.pocoo.org/"
depends=('python' 'python-pytz')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-runner' 'python-freezegun')
noextract=("cldr-core-$_core.zip")
source=("$name-$version.tar.gz::https://github.com/python-babel/babel/archive/v$version.tar.gz"
        "cldr-core-$_core.zip::http://unicode.org/Public/cldr/$_core/core.zip")
sha256sums=('7df55ebc7a75b6c544c381e1cc07151c2429f73f4ed01107bd8998b96cc83f42'
            'ba93f5ba256a61a6f8253397c6c4b1a9b9e77531f013cc7ffa7977b5f7e4da57')

prepare() {
  cp "$srcdir"/cldr-core-$_core.zip babel-$version/cldr/cldr-core-$_core.zip
}

build(){
  cd "$srcdir/babel-$version"
  python setup.py import_cldr
  python setup.py build
}

check(){
  cd "$srcdir/babel-$version"
  # the tests fail if running in the wrong timezone:
  # https://github.com/python-babel/babel/issues/757
  TZ=UTC python setup.py pytest
}

package_python-babel() {
  cd "$srcdir"/babel-${version}
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m0644 LICENSE "$pkgdir"/usr/share/licenses/$name/LICENSE
}
