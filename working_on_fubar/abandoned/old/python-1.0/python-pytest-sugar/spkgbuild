# Maintainer: Felix Yan <felixonmars@archlinux.org>

name=python-pytest-sugar
version=0.9.4
_commit=92ae9dee9f76af01a64d29de5ab07ef33fc5a15b
release=3
pkgdesc='A plugin for py.test that changes the default look and feel of py.test'
url='https://github.com/Frozenball/pytest-sugar'
depends=('python-pytest' 'python-termcolor')
makedepends=('python-setuptools')
source=("https://github.com/Frozenball/pytest-sugar/archive/$_commit/$name-$_commit.tar.gz")
sha512sums=('006772eeacd4cccec0fbe7dd615f4a24b30bda935a7c7a91551173e5c4053bc4316a9c22f859c29ed2314ba5a2e81d0956fe5f1ccdd5fb9ca70e68354d30371d')

prepare() {
  mv pytest-sugar-{$_commit,$version}
}

build() {
  cd pytest-sugar-$version
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-sugar-$version
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-sugar-$version
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$name/LICENSE
}

# vim:set ts=2 sw=2 et:
