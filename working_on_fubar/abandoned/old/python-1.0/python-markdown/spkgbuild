# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrew Antle <andrew dot antle at gmail dot com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Chaiwat Suttipongsakul <cwt at bashell dot com>

name=python-markdown
_pkgbasename=Markdown
version=3.3.3
release=3
pkgdesc="Python implementation of John Gruber's Markdown."
url='https://python-markdown.github.io/'
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-yaml')
source=("https://files.pythonhosted.org/packages/source/M/$_pkgbasename/$_pkgbasename-$version.tar.gz")
sha256sums=('5d9f2b5ca24bc4c7a390d22323ca4bad200368612b5aaa7796babf971d2b2f18')

build() {
  cd "$srcdir/$_pkgbasename-$version"
  python setup.py build
}

check() {
  cd "$srcdir/$_pkgbasename-$version"
  [[ $(python -c "import markdown; print(markdown.version)") == "$version" ]]
  [[ $(python -c "import markdown; print(markdown.markdown('*test*'))") == "<p><em>test</em></p>" ]]
  python -m unittest discover tests
}

package() {
  cd "$srcdir/$_pkgbasename-$version"
  python3 setup.py install --root="$pkgdir" --optimize=0
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/python-markdown/LICENSE"
}

