# Maintainer: David Runge <dvzrv@archlinux.org>

_name=orderedmultidict
name=python-orderedmultidict
version=1.0.1
release=6
pkgdesc="Ordered multivalue dictionary that retains method parity with dict."
url="https://github.com/gruns/orderedmultidict"
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('flake8' 'python-entrypoints' 'python-mccabe'
'python-pycodestyle' 'python-pyflakes')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${version}.tar.gz")
sha512sums=('5a826bb46dfe9d7e6e2159bf545612a35dd0f1bb5817c1fd78e0f3f4019f9144b36be94af08f87e881a310f593091df09702d001624c377f559a30f64b219c24')

prepare() {
  mv -v "${_name}-${version}" "${name}-${version}"
}

build() {
  cd "${name}-${version}"
  python setup.py build
}

check() {
  cd "${name}-${version}"
  python setup.py test
}

package() {
  cd "${name}-${version}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {API,README}.md -t "${pkgdir}/usr/share/doc/${name}"
}
