# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Anatol Pomozov

name=python-txaio
version=20.12.1
release=1
pkgdesc='Compatibility API between asyncio/Twisted/Trollius'
url="https://github.com/crossbario/txaio"
depends=('python-six')
makedepends=('python-setuptools')
# python-tests is used here:
# https://github.com/crossbario/txaio/blob/v20.1.1/test/_asyncio_test_utils.py#L35
checkdepends=('python-pytest' 'python-mock' 'python-twisted' 'python-tests')
source=("https://pypi.io/packages/source/t/txaio/txaio-$version.tar.gz")
sha512sums=('efde6115b8eccba94dce736fffe49f280ff49e9738f3299578cf41641c3b6f1deb5b1c0f0c8c6fbf85c8d8adbdc2d3f67aa256335d63a5977ac0bfacc98f909c')

prepare() {
  # This tests whether pip can install the sdist, and is completely broken
  # except in their boutique setup. They won't fix it.
  # https://github.com/crossbario/txaio/issues/77#issuecomment-246276723
  rm txaio-$version/test/test_packaging.py
}

build() {
  cd "$srcdir"/txaio-$version
  python setup.py build
}

check() {
  cd "$srcdir"/txaio-$version
  python -m pytest
}

package() {
  cd txaio-$version
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$name/LICENSE
}
