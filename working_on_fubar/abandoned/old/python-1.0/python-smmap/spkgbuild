# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>
# Contributor: Carl George < arch at cgtx dot us >

_name=smmap
name=python-smmap
version=4.0.0
release=1
epoch=1
pkgdesc="A pure git implementation of a sliding window memory map manager"
url="https://github.com/gitpython-developers/smmap"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.org/packages/source/s/${_name}/${_name}-${version}.tar.gz"{,.asc})
sha512sums=('fae59fd4e8ccc95cbc07be3ece208e862948c2b537850376b169acc15b9f0b1d94d412fde88647c245dda117dbbdab4db6426e1d95f3f9779bdd40c2d9c7d2cd'
            'SKIP')
b2sums=('2edd07925d9511879cb816f1e80d5fe7920ef91635f085b8d0ca81fa8d46bf0a1dfce7eab6f70250b93602a5a10e8170045f0720cf9fe3b26911d4e8b419f4cf'
        'SKIP')
validpgpkeys=('27C50E7F590947D7273A741E85194C08421980C9') # Sebastian Thiel (In Rust I trust!) <byronimo@gmail.com>

prepare() {
  mv -v "${_name}-${version}" "${name}-${version}"
}

build() {
  cd "${name}-${version}"
  python setup.py build
}

check() {
  cd "${name}-${version}"
  pytest -v
}

package() {
  cd "${name}-${version}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${name}/"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${name}/"
}
