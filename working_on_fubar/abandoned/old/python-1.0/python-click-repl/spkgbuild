# Maintainer: David Runge <dave@sleepmap.de>
_name=click-repl
name=python-click-repl
version=0.1.6
release=4
pkgdesc="Subcommand REPL for click apps"
url="https://github.com/click-contrib/click-repl"
depends=('python-click' 'python-prompt_toolkit' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${version}.tar.gz")
sha512sums=('50576166e8eb61098b8b4e1c7db43101c226d09785a497539830addfd52a2c35b2fa7865d3cbb20559dcf2fea99801f306dcb05c177886f021a1ada188501ced')

prepare() {
  mv -v "${_name}-$version" "$name-$version"
}

build() {
  cd "$name-$version"
  python setup.py build
}

## no tests in pypi source:
## https://github.com/click-contrib/click-repl/issues/49
#check() {
#  cd "$name-$version"
#  export PYTHONPATH="${PYTHONPATH}:${PWD}/build/lib/"
#  py.test
#}

package() {
  cd "$name-$version"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${name}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${name}/"
}
