# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Sebastien Binet <binet@cern.ch>

name=python-pytables
version=3.6.1
release=7
pkgdesc="A package for managing hierarchical datasets and designed to efficiently and easily cope with extremely large amounts of data"
url="https://www.pytables.org"
depends=(blosc lzo hdf5 python-numexpr)
makedepends=(python-setuptools cython)
source=("https://github.com/PyTables/PyTables/archive/v${version}/${name}-${version}.tar.gz")
sha256sums=('4cea86bab5bcb5423a07c7951b8c65e24b674e0dcec0e448d434829eff5f18d0')

build() {
  cd PyTables-${version}
  python setup.py build --blosc=/usr --lzo=/usr --hdf5=/usr --bzip2=/usr
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd PyTables-${version}/build/lib.linux-${CARCH}-${python_version}
  PYTHONPATH="." python -m tables.tests.test_all  
}

package() {
  cd PyTables-${version}
  python setup.py install --skip-build --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${name}/
}
